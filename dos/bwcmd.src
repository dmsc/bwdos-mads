*** BW-DOS Command Line Interpreter

CPACMDL	DTA	<(CPCAR-1),<(CPRUN-1)
	DTA	<(CPPRT-1),<(CPTYP-1),<(CPDIR-1)
	DTA	<(CPDIRS-1),<(CPBAS-1),<(CPTYP-1)

CPACMDH	DTA	>(CPCAR-1),>(CPRUN-1)
	DTA	>(CPPRT-1),>(CPTYP-1),>(CPDIR-1)
	DTA	>(CPDIRS-1),>(CPBAS-1),>(CPTYP-1)

***

CONVDC00	STA	DECIN
	LDA	#0
	STA	DECIN+1
CONVDC0	LDA	#0
	STA	DECIN+2

CONVDC	LDX	#7
COND0	STX	CONDI+1

	LDX	#24
	LDA	#0

COND1	ASL	DECIN
	ROL	DECIN+1
	ROL	DECIN+2
	ROL

	CMP	#10
	BCC	COND2
	SBC	#10

	INC	DECIN

COND2	DEX
	BNE	COND1

	ORA	#$30
CONDI	LDX	#0
COND3	STA	DECOUT,X

	DEX
	BMI	RET3

	LDA	DECIN
	ORA	DECIN+1
	ORA	DECIN+2
	BNE	COND0

	LDA	#$20
	BNE	COND3
***

CRNM2	INC	BUFOFF
	DTA	BIT3

CRNAME	LDA	#$20

CRNM00	LDY	BUFOFF
	LDX	LBUF,Y
	CPX	#$20
	BEQ	CRNM2

	STA	CRODDE+1

	LDA	#0
	STA	CROUTI+1

	CPX	#$9B
	BEQ	CRNM3

	LDA	LBUF+1,Y
	CMP	#$9B
	BEQ	CRNM3

	CMP	#':'
	BEQ	CRNM5

	LDA	LBUF+2,Y
	CMP	#':'
	BEQ	CRNM4

CRNM3	LDY	#PROMPT-LBUF
	JSR	CROUTI

CRNM4	LDY	BUFOFF
	JSR	CROUTI
	STY	BUFOFF

CRNMX	CPX	#3
RET3	RTS
*

CRNM5	INC	CROUTI+1
	JSR	CRNM4

	LDA	COMFNAM+1
	STA	COMFNAM

K31	LDA	#'1'
	STA	COMFNAM+1
	BNE	CRNMX
**

CRO1	STA	COMFNAM,X
	CPX	#27
	BCS	CRO1B
	INX

CRO1B	INY

	DTA	BIT3
CROUTI	LDX	#0

	LDA	LBUF,Y
	CMP	#$9B
	BEQ	CRO3
CRODDE	CMP	#$20
	BNE	CRO1

CRO3	LDA	#$9B
	STA	COMFNAM,X
	STX	CROUTI+1

	RTS

***

DTABLE	DTA	A(DOPEN-1)
	DTA	A(DCLOSE-1)
	DTA	A(DGET-1)
	DTA	A(DPUT-1)
	DTA	A(DSTAT-1)
	DTA	A(DSPEC-1)
*

RESET	CLD

	LDA	$47
	BNE	RESINI

	DEC	$47

	BIT	INCOMND
	BPL	RESINI

	JSR	RINIT

CMTAD	JMP	COMTAB

**

RESINI	JSR	UNI
	UC	TMEMLO,MEMLO,2
	UC	CMTAD+1,DOSVEC,2
	UCE	KFF,ECHOFLG,2

* INITIALIZE:
*     - LAST1 FCBIOCB (ANYTHING)
*     - SEC1CHG
*     - DCBs
*     - FCBs = $FF (VIA DEC)
*     - BUFDRIV - BUFTIME

*X=0
	TXA
RESI1	STA	SEC1CHG-1,X

	CPX	#151
	BCC	RESI1B
	DEC	FCBS-151,X

RESI1B	INX
	BNE	RESI1

*DRIVE# DO DCB (A=0, X=0)

	TAY
	DTA	BIT3
RESI3	LDY	#7

RESI4	INY
	TYA
	STA	DCBDRIV,X

	TXA
	CLC
	ADC	#26
	TAX

	CPY	#4
	BCC	RESI4
	BEQ	RESI3

*A=5*26=130 (->MI)

	LDY	#30

RESI5	LDX	HATABS,Y
	BNE	RESI6
	TYA

RESI6	CPX	#'D'
	BEQ	RESI7

	DEY
	DEY
	DEY
	BPL	RESI5

	TAY
	BMI	RET2

RESI7	LDX	#253	;-3

RESI8	LDA	RESITAB-253,X
	STA	HATABS,Y

	INY
	INX
	BNE	RESI8

RET2	RTS

**

RESITAB	DTA	C'D'
	DTA	A(DTABLE)
***

RINIT	JMP	(DOSINI)

*DEFAULT TD

INTDATE	DTA	DEFDAY,DEFMON,DEFYEA
	DTA	DEFHOU,DEFMIN,DEFSEC
***

SETTD	JSR	UNI
	UCE	DATER,INTDATE,6

GETTD	JSR	UNI
	UCE	INTDATE,DATER,6

RET	RTS

***

DADC7C	LDX	LDSA7C+1
	DTA	BIT3

DINC	LDX	#1
DADC	LDA	#0	;ADC
	BEQ	DIC2

DDEC	LDX	#1
DSBC	LDA	#$FF	;SBC

DIC2	STX	DIC8+1
	STA	DIC6+1
	ASL		;GETSTK keeps C flag

	LDX	#254
DIC3	JSR	GETSTK
	STA	DIC5-253,X
	STA	DIC7-253,X
	INX
	BNE	DIC3

DIC8	LDA	#0
	DEX
DIC4	INX

DIC6	EOR	#0
DIC5	ADC	$FFFF,X
DIC7	STA	$FFFF,X

	TXA
	BEQ	DIC4

	RTS
***

CPRUN	BNE	CPR1
CPRUN2	JMP	(RUNLOC)

CPR1	JSR	UNI
	UCE	NULY,RUNLOC,2

CPR2	LDA	COMFNAM+3,X
	INX

	SEC
	SBC	#$30
	CMP	#10
	BCC	CPR3
	SBC	#7
	CMP	#16
	BCS	CPRUN2

CPR3	ASL
	ASL
	ASL
	ASL
	LDY	#4

CPR4	ASL
	ROL	RUNLOC
	ROL	RUNLOC+1

	DEY
	BNE	CPR4
	BEQ	CPR2

***

GETSTK	STX	GST4+1
	TSX

	INC	$103,X
	LDA	$103,X
	BNE	GST2
	INC	$104,X

GST2	STA	GST3+1
	LDA	$104,X
	STA	GST3+2

GST3	LDA	$FF00

GST4	LDX	#0
	RTS
***

UNIUC2	JSR	GETSTK
	LDX	#3
	DTA	BIT3

UNIUC	LDX	#0

	PHA
	AND	#31
	STA	UNIUCL+2,X
	PLA

	LSR
	LSR
	LSR
	LSR
	LSR
	STA	UNIUC1+1

	JSR	GETSTK
	STA	UNIUCL+1,X

	TXA
	BEQ	UNIUC2
*

UNIUC1	LDX	#0

UNIUC3	DEX
UNIUCL	LDA	$FFFF,X
	STA	$FFFF,X

	TXA
	BNE	UNIUC3

	TYA
	BMI	UNIUCE
**

UNI	JSR	UNISCH

UNI1	JSR	GETSTK
	TAY

	ASL
	ASL
	PHP

	TYA
	AND	#31

	PLP
	BCS	UNIUC
	BMI	UNIUCS

	STA	UNI1CL+2

	JSR	GETSTK
	STA	UNI1CL+1

	TYA
	PHA

	JSR	UNIOBN
UNI1CL	JSR	UNIRTS
	JSR	UNISCH

	PLA
	BPL	UNI1
*

UNIOBN	LDA	#0
	PHA
UNIA	LDA	#0
	PLP
UNIRTS	RTS

**

UNIUCS	STA	UNIA+1
	STY	UNIUCS3+1

	JSR	GETSTK
	TAX
	JSR	GETSTK
	TAY

UNIUCS2	LDA	DLVOL,X
	STA	DLVOL,Y

	INX
	INY
	DEC	UNIA+1
	BNE	UNIUCS2

UNIUCS3	LDA	#0
	BPL	UNI
*

UNIUCE	LDX	#0
	RTS

***

CPCIO	JSR	GETSTK
	DTA	BIT3
CPC192	LDA	#192
	DTA	BIT3
CPC4	LDA	#4

CPCIO1	STA	$44
	ASL
	ASL
	TAX
	TAY

	BCS	CPCIO2
*AUXs

	JSR	GETSTK

	LSR
	STA	ICAX1,X
	LDA	#0
	ROR
	STA	ICAX2,X

* LOAD: ICCOM, ICBAL, ICBAH, ICBLL, ICBLH
CPCIO2	LDA	#$67
	STA	$43

CPCIO3	JSR	GETSTK
	STA	ICCOM,Y

CPCIO4	INY
	ASL	$43
	BCC	CPCIO4
	BNE	CPCIO3
*
	LDA	$44
	PHA

	LDA	#$20	;PUT SPACES BY DEFAULT
	JSR	CIOV

	PLA
	TAX

	TYA
	BPL	CPCIX
	CMP	#136
	BEQ	CPCIX
*ERR
	PHA

	TXA
	PHA

	AND	#28
	BEQ	CPCIO8
	ORA	#192

	JSR	CPCIO1
	DTA	12
*IGNORED ADR/LEN - REUSED
DIOAPUV	DTA	A(0,0)
***

CPCIO8	PLA
	BPL	CPCER

	PLA

CPCIX	TAY
	RTS
*

CPCER	JSR	CPC192
	DTA	11
	DTA	A(ERRTX,6)

	PLA
	JSR	CONVDC00

	JSR	CPC192
	DTA	11
	DTA	A(DECOUT+4,4)

	JSR	CPC192
	DTA	11
	DTA	A(ERRTX,1)

	JSR	XDIVIO1
***

CP	CLD

	JSR	DIOREP
*X=0
	STX	TDOVER
	STX	BUFOFF

	DEX
	STX	INCOMND
	TXS

	JSR	CPCIO
	DTA	4+192,12
*Ignored ADR/LEN - Reuse space
DIOANEW	DTA	A(EGET-1,EPUT-1)
***

	JSR	CPC192
	DTA	11
	DTA	A(PROMPT-1,4)

	JSR	CPCIO
	DTA	64,5
	DTA	A(LBUF,64)

	JSR	EXECUTE
	JMP	CP
***

CPCAR	LDA	$BFFD
	INC	$BFFD
	CMP	$BFFD
	STA	$BFFD
	BNE	CPERR

	LDX	$BFFC
	BNE	CPERR

	LDA	DWARM
	STA	8

	STX	INCOMND
	DEX
	STX	DWARM

	JMP	($BFFA)
***

CPBAS2	STA	BASICF
	PHP

	JSR	CPC192
	DTA	12
*Ignored ADR/LEN - Reuse space
DIO2T1	DTA	8*2,4*2
DIO2T2	DTA	$40,$50
***

* prevent garbage screen
	LDX	RTCLOK+2
WVBLNK	CPX	RTCLOK+2
	BEQ	WVBLNK
	STA	DMACTL

	LDA	PORTB
	ROR
	ROR
	PLP
	ROL
	ROL
	STA	PORTB

* search end of RAM, varies depending
* on cart or lower memory
	LDX	#$CF

CPBAS2B	TXA
	SEC
	SBC	#16
	TAX

	STX	$44

	EOR	($43),Y
	STA	($43),Y
	EOR	($43),Y

	BNE	CPBAS2B
* A=0
	INX
	STX	RAMSIZ
	STX	RAMTOP

	JSR	CPCIO
	DTA	128,12*2,3
	DTA	A(ENAME)
*Ignored LENGTH - Reuse space
DIOAPUT	DTA	A(0)
***

NULDW	LDX	#0
	STX	DWARM

***Fall trough to RTS - Ignore rest of code

UNISCH	STA	UNIA+1
	PHP
	PLA
	STA	UNIOBN+1
	RTS

***

CPBAS	LDA	COMFNAM+3
	CMP	#'O'
	BNE	CPERR

	LDA	COMFNAM+4

	CMP	#'F'
	BEQ	CPBAS2

	CLC
	EOR	#'N'
	BEQ	CPBAS2

CPERR	LDA	#$9C
CPER2	PHA
	JMP	CPCER
***

ETAB	DTA	A(0,0,0,0,0,0)

EGETP	JMP	(DIOAPUV)
EPUTP	JMP	(DIOAPUV+2)

***

EGEE	JSR	XDIVIO1
*X=0

**

EGET	TXA
	BNE	EGETP

	BIT	BATFLG
	BPL	EGE2

	JSR	EGETP

	CPY	#0
	BPL	EPU2

	RTS

**

EGE2	LDY	#1
	LDX	#5
	JSR	DIVHND

	CPY	#0
	BMI	EGEE

	LDX	#0
**

EPUT	CPX	#0
	BNE	EPUTP

	PHA
	JSR	EPUTP
	PLA

EPU2	BIT	ECHOFLG
	BMI	EPUTY

	PHA
	LDY	#0
	LDX	#7
	JSR	DIVHND

	CPY	#0
	BPL	EPUTX

	JSR	XDIVIO0

EPUTX	PLA
DIO2X0	LDY	#1

EPUTY	LDX	#0
	CPY	#0
DIO2EX	RTS


*DISABLE
DIORDIS	JSR	UNI
DIORA3	UC	DIOR2+1,HATABS+2,2
	UCE	DIOAPUT,ICPTL,2

DIO2X0EQ	BEQ	DIO2X0

**

DIVIO	JSR	XDIVIO

DIO2	LDY	#0
	LDA	DIO2T1,Y
	STA	DIO3
	LDA	DIO2T3,Y

	JSR	CPCIO1
DIO3	DTA	4*2,3
	DTA	A(COMFNAM,28)
	BMI	DIO2EX

	LDA	#0

DIO3B	LDY	DIO2+1
	LDX	DIO2T2,Y

	ORA	IOCB,X
	STA	ECHOFLG,Y

	LDA	#$FF
	STA	IOCB,X
**

DIOREP	LDA	HATABS+2	;HATABS E:
	CMP	#>ETAB
	BEQ	DIOR4

	JSR	UNI
DIORA1	UCE	HATABS+1,DIOR2+1,2

	LDX	#11
DIOR2	LDA	$FFFF,X	;ORIG.ADR.
DIOR3	STA	ETAB,X
	DEX
	BPL	DIOR2

	JSR	UNI
	UC	ETAB+4,DIOAPUV,4
	UC	ETAB+6,DIOAPUT,2
	UCE	DIOANEW,ETAB+4,4

	JSR	DINC
	DTA	A(DIOAPUV)
	JSR	DINC
	DTA	A(DIOAPUV+2)

DIOR4	LDA	ECHOFLG
	AND	ECHOFLG+1
	BMI	DIORDIS
*ENABLE
	JSR	UNI
DIORA2	UC	DIOR3+1,HATABS+2,2
	UCE	ETAB+6,ICPTL,2

	BEQ	DIO2X0EQ
***

CPPRT	BNE	DIVIO0

	LDA	COMFNAM
	CMP	PROMPT
	BNE	DIVIO0
**

XDIVIO0	LDY	#0
	DTA	BIT3
XDIVIO1	LDY	#1

XDIVIO	TYA
	AND	#1
	TAY
	STY	DIO2+1

	LDA	ECHOFLG,Y
	BMI	DIOREP

	LDX	#3
	JSR	DIVHND

	LDA	#$FF
	BMI	DIO3B

***

EXEBAT	LDA	#$20
	JSR	CRNM2

	LDY	#LBAT-LBUF
	JSR	CROUTI
*

DIVIO1	LDY	#1
	DTA	BIT3
DIVIO0	LDY	#0

	JSR	DIVIO
	BPL	EXEEX
	JMP	CPER2

**

EXECUTE	LDA	LBUF
	CMP	#'-'
	BEQ	EXEBAT

	CMP	#';'
	BEQ	EXEEX

	JSR	CRNAME
	BNE	EXE2

	JSR	UNI
	UCE	COMFNAM,PROMPT,2

EXEEX	RTS
*
EXE2	JSR	EXE2B

	.CB	'REN'
	.CB	'DEL'
	EOL
	.CB	'PROT'
	.CB	'UNPROT'

ERRTX	EOL
ENAME	DTA	C'Error'
	EOL
	EOL

DIO2T3	DTA	16+128,20+128

	.CB	'MD'
	.CB	'RD'
	.CB	'CD'
	.CB	'LOAD'
	.CB	'CAR'
	.CB	'RUN'
	.CB	'PRINT'
	.CB	'TYPE'
	.CB	'DIR'
	.CB	'DIRS'
	.CB	'BASIC'
	.CB	'MAN'

	DTA	0

	JSR	NULDW

	LDY	#LCOM-LBUF
	JSR	CROUTI

	CLC

EXELD	LDA	#4
	ROL
	STA	EXELD2

	JSR	CPC4
EXELD2	DTA	4*2,40
	DTA	A(COMFNAM,28)

EXE9RTS	RTS

**

EXE2B	LDX	#$FF
	STX	$43

EXE3	LDX	#0
	LDY	#0
	INC	$43

EXE4	JSR	GETSTK
	PHA

	EOR	COMFNAM+3,Y
	ASL
	BEQ	EXE5
	DEX

EXE5	INY
	PLA
	BEQ	EXE9RTS
	BPL	EXE4

	TXA
	BMI	EXE3

	LDA	COMFNAM+3,Y
	CMP	#$9B
	BNE	EXE3
*A=$9B
	JSR	CRNM00

	PLA
	PLA

	LDA	$43
	CMP	#13
	BEQ	EXELD
	BCS	EXE8
*C=0
	ADC	#32
	STA	EXE7B

	JSR	CPC4
	DTA	0
EXE7B	DTA	0
	DTA	A(COMFNAM,28)

	RTS
**

EXE8	TAY
	LDA	CPACMDH-14,Y
	PHA
	LDA	CPACMDL-14,Y
	PHA

	CPX	#3
	RTS
***

* Equals 6*2+1 after EOR-ing with constants bellow:
* 6*2+1 EOR at CPDIRS EOR at CPTYP

CPDIR	LDA	#<(CPDIRS-1)^(6*2)^(6*2+1)

; A is <(CPDIRS-1) when called from EXE8:
* <(CPDIRS-1) EOR 6*2 EOR at CPTYP

CPDIRS	EOR	#<(CPTYP-1)^(4*2)^<(CPDIRS-1)^(6*2)
	PHA

* JE TO? : < >

	LDA	COMFNAM-1,X
	CMP	#$38
	BEQ	CPDIRS2
	AND	#$F9
	CMP	#$38
	BNE	CPDIRS2

	LDY	#LHVE-LBUF
	JSR	CROUTI

CPDIRS2	PLA

; A is <(CPTYP-1) when called from EXE8:
* <(CPTYP-1) EOR 4*2

CPTYP	EOR	#<(CPTYP-1)^(4*2)

	STA	CPD2
	JSR	CPC4
CPD2	DTA	4*2,3
	DTA	A(COMFNAM,28)
*Ignored LENGTH above (28)- Reuse space

CPD3	JSR	CPCIO
	DTA	4+64,7
	DTA	A(LBUF,64)

	PHP

	JSR	UNI
	UCE	ICBLL+$10,CPD3L,2

	JSR	CPCIO
	DTA	64,11
	DTA	A(LBUF)
CPD3L	DTA	A(64)

	PLP
	BPL	CPD3

	RTS

**

DIVHND	STA	$45
	TXA
	PHA

	LDX	ECHOFLG,Y
	LDA	HATABS+1,X
	STA	$43
	LDA	HATABS+2,X
	STA	$44

	LDX	DIO2T2,Y

	PLA
	TAY

	LDA	($43),Y
	PHA
	DEY
	LDA	($43),Y
	PHA

	LDA	$45

CPDX	RTS

