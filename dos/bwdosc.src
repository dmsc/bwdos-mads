*************************************
*				*
*	B W  D O S  V 1.30  	*
*				*
*	     CAST 3		*
*				*
*************************************

** DOMYSLENE HODNOTY Z PRENOSU

STBUFF	EQU	*

STARTC	EQU	*+$300
	ORG	STARTC,STARTC+$3000

****

FGPUT	CPX	#8
	BCC	FGP2

	BIT	ZAJBEST+1
	BPL	FGP3

	CPX	#11
	BCS	FGP3

FGP2	STA	SRCHNAM,X
	INX

FGP3	RTS

**

FGETNM	LDY	#0
	STY	SRDIR3C+1
	INC	FGETNM+1

	LDA	#14
	STA	ZAJBEST+1

	LDX	#0
	BEQ	FGETN2

* "*"

FGETN2A	LDA	#'?'
	JSR	FGPUT
	BCC	FGETN2A

* PLATNY ZN.

FGETN1	JSR	FGPUT

FGETN2	INY
	LDA	($24),Y
	CMP	#'*'
	BEQ	FGETN2A

	CMP	#'?'
	BEQ	FGETN1
	CMP	#'_'
	BEQ	FGETN1

	CMP	#'.'
	BEQ	FGETN4

	CMP	#$30
	BCC	FGETN3
	CMP	#$3A
	BCC	FGETN1
	CMP	#$41
	BCC	FGETN3
	CMP	#$5B
	BCC	FGETN1

*NEPLATNY ZN.

FGETN3	SEC
	ROR	ZAJBEST+1

FGETN4	LDA	#$20
	JSR	FGPUT
	BCC	FGETN4

	ROR	ZAJBEST+1
	BCC	FGETN2
* /A?
	LDA	($24),Y
	CMP	#'/'
	BNE	FGETN5

	INY
	LDA	($24),Y
	DEY
	CMP	#'A'
	BNE	FGETN5

	ROR	AFLG

	INY
	INY

FGETN5	CPY	FGETNM+1
SRDIR2	STY	FGETNM+1
	PHP

	BEQ	FGTN6
	CLC
FGTN6	ROR	GODR2+1

	LDA	($24),Y
	PLP

	RTS

***

CWD	JSR	UNI
	UCALL	SRDDVOJ
	UCALL	GODIR
	UCSE	FCB1ST,DCBWD,2

***RTS VYNECHAN - NEVADI

SRDDVOJ	LDY	#2

SRDIR1	LDA	($24),Y
	CMP	#':'
	BEQ	SRDIR2

	DEY
	BNE	SRDIR1
*
SRDE2	JSR	ERRORS
	DTA	$96
***

SRCHDIR	JSR	SRDDVOJ
	STY	SRDIR3B+1

SRD2	LSR	AFLG

	JSR	UNI
	UCS	DCBWD,FCB1ST,2
	UCALLE	FGETNM

	BNE	SRDIR4B
*>?
	CMP	#'>'
	BNE	SRD2B

	JSR	UNI
	UCSE	DCBMAIN,FCB1ST,2

	BEQ	SRDIR4
*

SRD2BB	JSR	OPNDRD
	JSR	GDR9

	LDA	FCB1ST
	ORA	FCB1ST+1
	BEQ	SRDE2

	JSR	FGETNM
	BNE	SRDIR4B

SRD2B	CMP	#'<'
	BEQ	SRD2BB
**

SRDIR3	LSR	AFLG

SRDIR3B	LDY	#0
SRDIR3C	CPY	#0

SRDEX	RTS
**

SRDIR4A	JSR	GOTODIR

SRDIR4	JSR	FGETNM
	BEQ	SRDIR3

SRDIR4B	CMP	#'>'
	BEQ	SRDIR4A
	BNE	SRDIR3B
***

GODIR	JSR	SRD2

GODR2	LDA	#0
	BMI	UVOLNX

**

GOTODIR	LSR	AFLG
	JSR	OPNDRD

	JSR	SRCHIT0
	BMI	SRDE2

GDR9	JSR	UNI
	UCSE	DIRBUF+1,FCB1ST,2

UVOLNX	RTS

***

UVOLN	LDA	BUFDRIV,X
	BPL	UVOLNX

	AND	#127
	STA	BUFDRIV,X

	TAY

	JSR	ZAJADR

	LDA	BUFSECH,X
	PHA
	LDA	BUFSECL,X
	TAX
	PLA

	SEC
**

RWSECT	STY	$301

	STX	$30A
	STA	$30B
*1?
	DEX
	BNE	RWS2
	TAX
	BNE	RWS2

	TAY

RWS2	LDX	#'R'
	LDA	#64
	BCC	RWSC3

	LDX	WRTCMD
	ASL

RWSC3	STX	$302
	STA	$303

	LDA	SECLENS-1,Y
	STA	$308
	ASL
	ROL
	EOR	#1
	STA	$309

	JSR	UNI
	UC	K16+1,$306,1
	UC	K31+1,$300,1
	UC	B,$304,2
	UCALLE	SIO

	BMI	SIOERRJ
SIORTS	RTS

***

PRIDEL	LDA	FCBAUX1
K16	CMP	#16
	BCS	PRID3A

	LDY	#0
	DTA	BIT3

PRID3A	LDY	#2

	TYA
	PHA
	LDX	#254

PRID3BX	LDA	DCBDAT,Y
	STA	VTOCSEC-254,X
	INY
	INX
	BNE	PRID3BX

	JSR	PRID3T
	JSR	ZMENEN1

	JSR	DDEC
	DTA	A(VTOCSEC)

PRID3D	JSR	SETBPOS
	TAX
	AND	(B),Y
	BNE	PRID3C

	JSR	PRID3T
	BCC	PRID3D	;VZDY
** JE!

PRID3C	TXA
	EOR	(B),Y
	STA	(B),Y

	JSR	ZMENEN

	JSR	DDEC
	DTA	A(DCBFREE)

	CLC
	BCC	PRID3B
**

PRID3T	JSR	DINC
	DTA	A(VTOCSEC)

	LDA	VTOCSEC
	CMP	DCBMAIN+2
	LDA	VTOCSEC+1
	SBC	DCBMAIN+3
	BCC	SIORTS

	JSR	DDEC
	DTA	A(VTOCSEC)

	PLA
	PLA
	SEC

* ZACHOVAT C!

PRID3B	PLA
	TAY
	LDX	#254

PRID3E	LDA	VTOCSEC-254,X
	STA	DCBDAT,Y
	INY
	INX
	BNE	PRID3E

* A=(VTOCSEC+1)

	LDX	VTOCSEC
	BCC	ZAJIST0	;C=0

	DEY
	DEY
	BEQ	PRID3A

* DSK FULL

	LDY	#$A2
SIOERRJ	JMP	ERROR

**

ZAJIST1	LDA	#0
	LDX	#1

ZAJIST	SEC

ZAJIST0	STX	ZSL+1
	STA	ZSH+1

	PHP

	LDX	#2
ZAJ1	LDA	BUFTIME,X
	BEQ	ZAJ2
	DEC	BUFTIME,X

ZAJ2	DEX
	BPL	ZAJ1

*HLEDEJ V BUFS

	LDX	#2

ZAJ3	JSR	NASLI
	BNE	ZAJ4

	LDA	BUFSECL,X
	TAY
	CMP	ZSL+1
	BNE	ZAJ4

	LDA	BUFSECH,X
	CMP	ZSH+1
	BNE	ZAJ4
*1?
	DEY
	BNE	ZAJ3Z
	TAY
	BEQ	ZAJ9	;X=BUFF#
*JE

ZAJ3Z	JSR	ZAJADR

	PLP
	BCS	ZAJ3B

ZAJ30	LDY	MYSLEN
	LDA	#0

ZAJ3A	DEY
	STA	(B),Y
	BNE	ZAJ3A

ZMENEN	LDX	#0
	ASL	BUFDRIV,X
	SEC
	ROR	BUFDRIV,X

ZAJ3B	RTS

*NENI

ZAJ4	DEX
	BPL	ZAJ3

*PRIDEL BUFF

	STX	ZAJBEST+1
	INX
	STX	ZAJBUF+1

ZAJ5	LDA	BUFDRIV,X
	BEQ	ZAJBEST
	LDA	BUFTIME,X

ZAJBEST	CMP	#$FF
	BCS	ZAJ7

	STA	ZAJBEST+1
	STX	ZAJBUF+1

ZAJ7	INX
	CPX	#3
	BCC	ZAJ5
*UVOLNI

ZAJBUF	LDX	#0
	JSR	UVOLN

	LDX	ZAJBUF+1
*X=BUFF#

ZAJ9	JSR	ZAJADR

	LDA	FCBDRIV
	STA	BUFDRIV,X
	TAY

ZSL	LDA	#0
	STA	BUFSECL,X
ZSH	LDA	#0
	STA	BUFSECH,X

	PLP
	BCC	ZAJ30
*READ

*A=ZSH+1
*Y=FCBDRIV

	LDX	ZSL+1
	CLC
	JMP	RWSECT
***

ERA1	JSR	UNI
	UCS	FCBSECT,VTOCSEC,2
	UCALLE	SETBPOS

	ORA	(B),Y
	STA	(B),Y

	JSR	UNI
	UCALL	ZMENEN
	UCS	K02,DCBDIR,2

* Set last aloc to last deleted
	UCSE	VTOCSEC,DCBDAT,2

	JSR	DINC
	DTA	A(DCBFREE)
**

ZMENEN1	LDX	FCBDRIV
	SEC
	ROR	SEC1CHG-1,X

ZMERET	RTS

***

RENRUT	LDX	#10

RIT2	LDA	RENNAM,X
LOSLNAD	EQU	RIT2+1
LOSLN	EQU	RENNAM

	CMP	#'?'
	BEQ	RIT3
	STA	DIRBUF+6,X

RIT3	DEX
	BPL	RIT2

	RTS

***

PROTRUT	SEC
	DTA	BIT2
UNPRUT	CLC

	PHP
	LSR	DIRBUF
	PLP
	ROL	DIRBUF

NONE	RTS

***

RDPUTL	DTA	<RENRUT,<ERARUT
	DTA	<NONE,<PROTRUT
	DTA	<UNPRUT

RDPUTH	DTA	>RENRUT,>ERARUT
	DTA	>NONE,>PROTRUT
	DTA	>UNPRUT

**

REDEPRU	TYA
	AND	#7
	PHA
	TAY

	LDA	RDPUTL,Y
	STA	RDPUJ+1
	LDA	RDPUTH,Y
	STA	RDPUJ+2

	LDA	RDPUTST,Y
	STA	RDPU2+1

	JSR	OPEN014

	PLA
	BNE	RDPU1

	JSR	UNI
	UCS	SRCHNAM,DIRBUF,11
	UCALL	FGETNM
	UCS	SRCHNAM,RENNAM,11
	UCSE	DIRBUF,SRCHNAM,11

RDPU1	JSR	SRCHRDP
	BNE	ERANOT

RDPUJ	JSR	ZMERET
	JSR	SADIR

	JSR	SRCHRDP
	BEQ	RDPUJ

	RTS
***

SRCHRDP	JSR	SRCHCOK
	BNE	SRRDPX

RDPU2	LDA	#0
	AND	DIRBUF
	BNE	SRCHRDP

SRRDPX	RTS

***

DELDIR	LDA	#$24
	JSR	OPEN0

	LDX	#10
	LDA	#'?'
DLDR2	STA	SRCHNAM,X
	DEX
	BPL	DLDR2

	JSR	SRCHCOK
	BEQ	DLDRER

	JSR	OPEN014
	JSR	SRCHIT0

ERARUT	LDA	DIRBUF
	LSR
	BCC	ERAR2

	LDY	#$A4
	DTA	BIT3
ERANOT	LDY	#170
	DTA	BIT3
DLDRER	LDY	#$A7
	JMP	ERROR
*

ERAR2	LDA	#16
	STA	DIRBUF

	JSR	SADIR
**

ERASDT	JSR	UNI
	UCS	FCB1ST,FCBDIR1,2
	UCS	DIRBUF+1,FCB1ST,2
	UCALL	OPNINT12
	UCSE	K02,FCBMAPX,1

ERADT2	JSR	NXTDAT
	BEQ	ERADT3

	JSR	ERA1

ERADT3	LDY	MYSLEN
	DEY
	DEY
	CPY	FCBMAPX
	BNE	ERADT2

	JSR	UNI
	UCS	FCBMAP,FCBSECT,2
	UCALL	ERA1
	UCALLE	ZAJMAP

	LDY	#0
	LDA	(B),Y
	INY
	ORA	(B),Y
	BNE	ERADT2

ERASX	JSR	UNI
	UCS	FCBDIR1,FCB1ST,2
	UCALLE	OPNDRD

	JMP	LDDIR

****

SETBPOS	LDA	VTOCSEC
	LDX	VTOCSEC+1

	LDY	#3

SETBP1	PHA
	TXA
	LSR
	TAX
	PLA
	ROR

	DEY
	BNE	SETBP1

POSCMPA	JSR	POSCMP

	STA	SETBP2+1

	TXA
	CLC
	ADC	DCBMAP1
	TAX

	LDA	#0
	ADC	DCBMAP1+1

	JSR	ZAJIST

	LDA	VTOCSEC
	AND	#7
	TAY

	LDA	#0
	SEC

SETBP1B	ROR
	DEY
	BPL	SETBP1B

SETBP2	LDY	#0

	RTS
***

SRCHCOK	LDA	#128
	DTA	BIT3
SRCHIT	LDA	MYA1
	DTA	BIT3
SRCHIT0	LDA	#$20
	STA	SRIT3+1

	JSR	UNI
	UCSE	KFF,FCBDIRP,2

SRIT2	JSR	UNI
	UCSE	FCBPOS,SRCHTMP,2

	BIT	FCBPOS+1
	BMI	SRITEND

	JSR	LDDIRN
	BMI	SRITEND

DIRBADB	LDA	DIRBUF
	BEQ	SRITEND

	AND	#8
	BNE	SRIT3
*VOLNO
	BIT	FCBDIRP+1
	BPL	SRIT2

	JSR	SRIT6
	BEQ	SRIT2
*

SRIT3	LDA	#0
	BMI	SRIT3B

	EOR	DIRBUF
	AND	#32
	BNE	SRIT2
*FNM

SRIT3B	LDX	#11

SRIT4	LDA	SRCHNAM-1,X
	CMP	#'?'
	BEQ	SRIT5
	CMP	DIRBUF+5,X
	BNE	SRIT2

SRIT5	DEX
	BNE	SRIT4
	TXA

SRIT6	PHA

	JSR	UNI
	UCSE	SRCHTMP,FCBDIRP,2

	PLA

	RTS

**

SRITEND	LDA	#$FF

	BIT	FCBDIRP+1
	BMI	SRIT6

	TAX
	RTS

***

LOAD	STX	LOSIT3+1

	LDA	$2B
	STA	LDRUN+1

	LDY	#34
	JSR	LOSIT2
	BMI	LOSER2B

*PO XIO JE X=IOCB#
	LDA	$20
	STA	$340,X

	JSR	LOSAD
	BMI	LOSER2
	LDY	#$98
	BCC	LOSER2
*C=1
	ROR	LOS2T+1

LOS2	JSR	LOSAD
	BMI	LOSER2
	BCS	LOS2

LOS2T	LDA	#0
	BPL	LOS2B
	LSR	LOS2T+1

	JSR	UNI
	UCE	LOSADR,$2E0,2

LOS2B	JSR	LOSSGMD
	BPL	LOS2

*CHYBA VZDY PO XIO -> X=IOCB#

LOSER2	LDA	#$FF
	STA	$340,X

LOSER2B	TYA
	PHA

	LDY	#45
	JSR	LOSIT2

	PLA
	TAY
	CPY	#136
	BNE	LOSER3

	JSR	UNI
	UC	$2E0,TRUN,2
	UCE	TRUN,RUNLOC,2

	LDA	#$E0
	BNE	LOSOK

**

LOSAD	JSR	UNI
	UC	LOSADAD+1,LDSAAD,2
	UCALLE	LOSDVA

	BMI	LOSER3

LOSADAD	LDA	LOSADR
	AND	LOSADR+1
	CMP	#$FF

	TYA
LOSER3	RTS

***

LOSSGMD	JSR	UNI
	UC	POSCMPA+1,$2E2,2
	UC	LOSLNAD,LDSAAD,2
	UCALLE	LOSDVA

	BMI	LOSER3

	SEC
	LDX	#254
LOSS3	LDA	LOSLN-254,X
	SBC	LOSADR-254,X
	STA	LDSALEN-254,X
	INX
	BNE	LOSS3

	JSR	DINC
	DTA	A(LDSALEN)

	JSR	UNI
	UC	LOSADR,LDSAAD,2
	UCALLE	LOSIT

	BMI	LOSER3

	LDA	#$E2

LOSOK	JSR	LDRUN
	CLD
	LDY	#1
	RTS
***

LOSDVA	JSR	UNI
	UCSE	K02,LDSALEN,2

LOSIT	LDY	#46
LOSIT2	LDX	#0
	STX	RWFLG
LOSIT3	LDX	#0
	JMP	XIO

***

LDRUN	LDX	#0
	BMI	LOSER3

	STA	DORUN+1

DORUN	JMP	($2E0)

***

PERADT	EQU	ERASDT
PFGETNM	EQU	FGETNM
PCWD	EQU	CWD
PZAJIST	EQU	ZAJIST
PLOAD	EQU	LOAD
PGODR	EQU	GODIR
PZAJ1	EQU	ZAJIST1
PSRCOK	EQU	SRCHCOK
PSRDIR	EQU	SRCHDIR
PDLDR	EQU	DELDIR
PUVOLN	EQU	UVOLN
PZMENEN	EQU	ZMENEN
PREDEPR	EQU	REDEPRU
PPRIDEL	EQU	PRIDEL
PSRIT0	EQU	SRCHIT0
PSRIT	EQU	SRCHIT

MEMLO	EQU	*

***

