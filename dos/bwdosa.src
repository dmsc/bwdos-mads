ST00	EQU	$6E6
	ORG	ST00,$A800
	OUT	N

*************************************
*				*
*	B W  D O S  V 1.00  	*
*				*
*	     CAST 1		*
*				*
*************************************

*PRENOS

MEMLO	EQU	$1EF9
STBUFF	EQU	$1706
LDSA7C	EQU	$14AE
DOPEN	EQU	$FAE
DCLOSE	EQU	$FB1
DGET	EQU	$FB4
DPUT	EQU	$FB7
DSTAT	EQU	$FBA
DSPEC	EQU	$F95

* DEFINICE

BIT3	EPZ	$2C
BIT2	EPZ	$24

EOL	MACRO
	DFB	155
	MEND
*

UC	MACRO	A1,A2,A3
	DFB (A1)/256+64
	DFB (A1):L
	DFB (A2)/256+32*(A3)
	DFB (A2):L
	MEND

UCE	MACRO	A1,A2,A3
	DFB (A1)/256+192
	DFB (A1):L
	DFB (A2)/256+32*(A3)
	DFB (A2):L
	MEND

UCS	MACRO	A1,A2,A3
	DFB (A3)+32
	DFB (A1)-DLVOL
	DFB (A2)-DLVOL
	MEND

UCSE	MACRO	A1,A2,A3
	DFB (A3)+32+128
	DFB (A1)-DLVOL
	DFB (A2)-DLVOL
	MEND

UCALL	MACRO	A1
	DFB (A1):H
	DFB (A1):L
	MEND

UCALLE	MACRO	A1
	DFB (A1)/256+128
	DFB (A1):L
	MEND

* VSTUPNI INIT

	CLD

	LDX	#0
	LDY	#$20

VSTI1	LDA	ZACATEK+$3000,X
VSTI2	STA	ZACATEK,X
	INX
	BNE	VSTI1

	INC	VSTI1+$3002
	INC	VSTI2+$3002

	DEY
	BNE	VSTI1

	JMP	SRCHNAM

* HLAVICKA V 7. STRANCE

ZACATEK	DFB	'S'
	DFB	$32

PTXROL	DFB	0

	ASC	"BW"
	DFB	$11

GETTDA	DFW	GETTD
	DFW	SETTD
	DFW	CONVDC
	JMP	SIO
***

CPRUN	BNE	CPR1
CPRUN2	JMP	(RUNLOC)

CPR1	JSR UNI
	UCE NULY,RUNLOC,2

CPR2	LDA	COMFNAM+3,X
	INX
	SEC
	SBC	#'0'
	CMP	#10
	BCC	CPR3
	SBC	#7
	CMP	#16
	BCS	CPRUN2

CPR3	ASL
	ASL
	ASL
	ASL
	LDY	#4

CPR4	ASL
	ROL	RUNLOC
	ROL	RUNLOC+1

	DEY
	BNE	CPR4
	BEQ	CPR2

*** PRIKAZY CP

CMDS	ASC "RENAME"
	EOL
	ASC "ERASE"

ERRTX	EOL
ENAME	ASC "Error"

	EOL
	ASC "PROTECT"
	EOL
	ASC "UNPROTECT"
	EOL
	EOL
	EOL
	EOL
	EOL
	EOL
	ASC "CREDIR"
	EOL
	ASC "DELDIR"
	EOL
	ASC "CWD"
	EOL
	ASC "LOAD
	EOL

	ASC "CAR"
	EOL
	ASC "RUN"
	EOL
	ASC "PRINT"
	EOL
	ASC "TYPE"
	EOL
	ASC "DIR"
	EOL
	ASC "DIRS"
	EOL
	ASC "BASIC"
	EOL
	DFB 0
***

CPACMDL DFB CPCAR-1:L,CPRUN-1:L
 DFB CPPRT-1:L,CPTYP-1:L,CPDIR-1:L
 DFB CPDIRS-1:L,CPBAS-1:L

CPACMDH DFB CPCAR-1:H,CPRUN-1:H
 DFB CPPRT-1:H,CPTYP-1:H,CPDIR-1:H
 DFB CPDIRS-1:H,CPBAS-1:H

***

ETAB	ASC "            "
	ASC "   "

*DEFAULT TD

INTDATE	DFB 5,4,94
	DFB 16,17,18
***

SETTD	JSR UNI
	UCE DATER,INTDATE,6

GETTD	JSR UNI
	UCE INTDATE,DATER,6

RET	RTS

***

DTABLE	DFW DOPEN-1
	DFW DCLOSE-1
	DFW DGET-1
	DFW DPUT-1
	DFW DSTAT-1
	DFW DSPEC-1
*

RESET	CLD

	LDA	$47
	BNE	RESINI

	DEC	$47

	BIT	INCOMND
	BPL	RESINI

	JSR	RINIT

CMTAD	JMP	COMTAB

**

RESINI	JSR UNI
	UC TMEMLO,$2E7,2
	UC CMTAD+1,10,2
	UCE KFF,ECHOFLG,2

	TXA
	LDX	#19

RESI1	STA	SEC1CHG,X
	DEX
	BPL	RESI1

	TXA
	LDX	#105

RESI1B	STA	FCBS-1,X
	DEX
	BNE	RESI1B

	TXA
	LDX	#130

RESI2	STA	DCBDRIV-1,X
	DEX
	BNE	RESI2

	LDY	#1
	DFB	BIT3
RESI3	LDY	#8

RESI4	TYA
	STA	DCBDRIV,X

	TXA
	CLC
	ADC	#26
	TAX

	INY
	CPY	#5
	BCC	RESI4
	BEQ	RESI3

	LDX	#223

RESI5	LDA	$31A-223,X
	BEQ	RESI6
	CMP	#'D'
	BEQ	RESI6

	INX
	INX
	INX
	BNE	RESI5

***

RINIT	JMP	(12)

***

RESI6	LDA	#'D'
	STA	$31A-223,X
	LDA	#DTABLE:L
	STA	$31B-223,X
	LDA	#DTABLE:H
	STA	$31C-223,X

RET2	RTS

***

CONVDC00	STA	DECIN
	LDA	#0
	STA	DECIN+1
CONVDC0	LDA	#0
	STA	DECIN+2

CONVDC	LDX	#7
COND0	STX	CONDI+1

	LDX	#24
	LDA	#0

COND1	ASL	DECIN
	ROL	DECIN+1
	ROL	DECIN+2
	ROL

	CMP	#10
	BCC	COND2
	SBC	#10

	INC	DECIN

COND2	DEX
	BNE	COND1

	ORA	#$30
CONDI	LDX	#0
COND3	STA	DECOUT,X

	DEX
	BMI	RET2

	LDA	DECIN
	ORA	DECIN+1
	ORA	DECIN+2
	BNE	COND0

	LDA	#$20
	BNE	COND3
***

GETSTK	STX	GST4+1
	STY	GST5+1

	TSX
	INX
	INX
	INX

	INC	$100,X
	LDA	$100,X

	INX
	TAY
	BNE	GST2

	INC	$100,X

GST2	LDA	$100,X
	STA	GST3+2

GST3	LDA	$FF00,Y

GST4	LDX	#0
GST5	LDY	#0

	RTS
**

DADC7C	LDA	LDSA7C+1
	DFB BIT3

DINC	LDA	#1
DADC	CLC
	BCC	DIC2

DDEC	LDA	#1
DSBC	SEC

DIC2	STA	DIC6+1
	LDA	#$D2
	ROR
	STA	DIC6

	LDX	#254
DIC3	JSR	GETSTK
	STA	DIC5-253,X
	STA	DIC7-253,X
	INX
	BNE	DIC3

	LDA	DIC6
	ASL

	DEX
DIC4	INX

DIC5	LDA	$FFFF,X
DIC6	ADC	#0
DIC7	STA	$FFFF,X

	STX	DIC6+1
	TXA
	BEQ	DIC4

	RTS
***

UNIUC2	JSR	GETSTK
	LDX	#3
	DFB BIT3
UNIUC	LDX	#0

	PHA
	AND	#31
	STA	UNIUCL+2,X
	PLA

	ROL
	ROL
	ROL
	ROL
	AND	#7
	STA	UNIUC1+1

	JSR	GETSTK
	STA	UNIUCL+1,X

	TXA
	BEQ	UNIUC2
*

UNIUC1	LDX	#0

UNIUC3	DEX
UNIUCL	LDA	$FFFF,X
	STA	$FFFF,X

	TXA
	BNE	UNIUC3

	TYA
	BMI	UNIUCE
**

UNI	JSR	UNISCH

UNI1	JSR	GETSTK
	TAY

	ASL
	ASL
	PHP

	TYA
	AND	#31

	PLP
	BCS	UNIUC
	BMI	UNIUCS

	STA	UNI1CL+2

	JSR	GETSTK
	STA	UNI1CL+1

	TYA
	PHA

	JSR	UNIOBN
UNI1CL	JSR	UNIRTS
	JSR	UNISCH

	PLA
	BPL	UNI1
*

UNIOBN	LDA	#0
	PHA
UNIA	LDA	#0
	PLP
UNIRTS	RTS

**

UNIUCS	STA	UNIA+1
	STY	UNIUCS3+1

	JSR	GETSTK
	TAX
	JSR	GETSTK
	TAY

UNIUCS2	LDA	DLVOL,X
	STA	DLVOL,Y

	INX
	INY
	DEC	UNIA+1
	BNE	UNIUCS2

UNIUCS3	LDA	#0
	BPL	UNI
*

UNIUCE	LDX	#0
	RTS

***

CRNAME	LDA	#$20
CRNM00	STA	CRODDE+1

CRNM1	LDY	BUFOFF
	LDA	LBUF,Y
	CMP	#$20
	BNE	CRNM2

	INC	BUFOFF
	BNE	CRNM1
*

CRNM2	LDX	#0
	STX	CROUTI+1

	CMP	#$9B
	BEQ	CRNM3

	LDA	LBUF+1,Y
	CMP	#$9B
	BEQ	CRNM3

	CMP	#':'
	BEQ	CRNM5

	LDA	LBUF+2,Y
	CMP	#':'
	BEQ	CRNM4

CRNM3	LDY	#PROMPT-LBUF
	JSR	CROUTI

CRNM4	LDY	BUFOFF
	JSR	CROUTI
	STY	BUFOFF

CRNMX	CPX	#3
	RTS
*

CRNM5	INC	CROUTI+1
	JSR	CRNM4

	LDA	COMFNAM+1
	STA	COMFNAM

	LDA	#'1'
	STA	COMFNAM+1
	BNE	CRNMX
**

CRO1	STA	COMFNAM,X
	CPX	#27
	BCS	CRO1B
	INX

CRO1B	INY

	DFB BIT3
CROUTI	LDX	#0

CRO2	LDA	LBUF,Y
	CMP	#$9B
	BEQ	CRO3
CRODDE	CMP	#$20
	BNE	CRO1

CRO3	LDA	#$9B
	STA	COMFNAM,X
	STX	CROUTI+1

PRIHX	RTS

***

CPCIO	JSR	GETSTK
	DFB BIT3
CPC192	LDA	#192
	DFB BIT3
CPC4	LDA	#4

CPCIO1	STA	CPCIO6+1
	ASL
	ASL
	TAX
	TAY

	BCS	CPCIO2
*AUXs

	JSR	GETSTK

	ASL
	ROR	$34B,X
	LSR
	STA	$34A,X

CPCIO2	LDA	#$B3

CPCIO3	ASL
	STA	$43

	BCC	CPCIO4

	JSR	GETSTK
	STA	$342,Y

CPCIO4	INY

CPCIO5	LDA	$43
	BNE	CPCIO3

	JSR	$E456
	BPL	CPCIX
	CPY	#136
	BEQ	CPCIX
*ERR
	TYA
	PHA
CPCIO6	LDA	#0
	PHA

	AND	#28
	BEQ	CPCIO8
	ORA	#192

	JSR	CPCIO1
	DFB 12
	DFW COMFNAM,0

CPCIO8	PLA
	BPL	CPCER

	PLA
	TAY
CPCIX	TYA
	RTS
*

CPCER	JSR CPC192
	DFB 11
	DFW ERRTX,6

	PLA
	JSR	CONVDC00

	JSR CPC192
	DFB 11
	DFW DECOUT+4,4

	JSR CPC192
	DFB 11
	DFW ERRTX,1

	JSR	XDIVIO1
***

CP	CLD

	LDX	#$FF
	STX	INCOMND
	TXS

	INX
	STX	TDOVER
	STX	BUFOFF

	JSR	CPCIO
	DFB 4+192,12
	DFW COMFNAM,28

	JSR	CPC192
	DFB 11
	DFW PROMPT-1,4

	JSR	CPCIO
	DFB 64,5
	DFW LBUF,64

	JSR	EXECUTE
	JMP	CP
***

CPCAR	LDA	$BFFD
	INC	$BFFD
	CMP	$BFFD
	STA	$BFFD
	BNE	CPERR

	LDX	$BFFC
	BNE	CPERR

	LDA	DWARM
	STA	8

	STX	INCOMND
	DEX
	STX	DWARM

	JMP	($BFFA)
***

CPBAS2	TXA
	ASL
	PHA

	JSR	CPC192
	DFB 12
	DFW COMFNAM,28

	PLA
	STA	$3F8

	EOR	$D301
	AND	#2
	EOR	$D301
	STA	$D301

	LDY	#$FF
	STY	$43
	INY
	STY	$2E5

	LDX	#$CF

CPBAS2B	TXA
	SEC
	SBC	#16
	TAX

CPBAS3	STX	$44

	LDA	($43),Y
	EOR	#$FF
	STA	($43),Y
	CMP	($43),Y

	BNE	CPBAS2B

	INX
	STX	$2E4
	STX	$2E6
	STX	$6A

	JSR	CPCIO
	DFB 128,12,3
	DFW ENAME,4
***

NULDW	LDX	#0
	STX	DWARM

***RTS VYPUSTEN - NEVADI

UNISCH	STA	UNIA+1
	PHP
	PLA
	STA	UNIOBN+1
	RTS

***

CPBAS	LDA	COMFNAM+3
	EOR	#'O'
	BNE	CPERR
	TAX

	LDA	COMFNAM+4

	CMP	#'N'
	BEQ	CPBAS2
	INX
	CMP	#'F'
	BEQ	CPBAS2

CPERR	LDA	#$9C
CPER2	PHA
	JMP	CPCER
***

CPPRT	BNE	DIVIO0

	LDA	COMFNAM
	CMP	PROMPT
	BNE	DIVIO0
**

XDIVIO0	LDY	#0
	DFB BIT3
XDIVIO1	LDY	#1

XDIVIO	JSR	DIO0
	BMI	DIO0X

	LDX	#3
	JSR	DIVHND

	LDY	DIO2+1
	LDA	#$FF
	STA	ECHOFLG,Y

	INY
	JSR	DIO0
	BPL	XDIOX
*
	LDX	EOFFS+1

EOLD2L	LDA	#7
	STA	$31B,X
EOLD2H	LDA	#7
	STA	$31C,X

	LDX	#254
EOLDAD6	LDA	$FFFF,X
	STA	$346-254,X
	INX
	BNE	EOLDAD6

XDIOX	DEY

**

DIO0	TYA
	AND	#1
	TAY
	STY	DIO2+1

	LDA	ECHOFLG,Y

DIO0X	RTS

***

EXEBAT	INC	BUFOFF
	JSR	CRNAME

	LDY	#LBAT-LBUF
	JSR	CROUTI
*

DIVIO1	LDY	#1
	DFB BIT3
DIVIO0	LDY	#0

	JSR	DIVIO
	BMI	CPER2
	RTS
***

DIVIO	JSR	DIO0
	BMI	DIO2

	JSR	XDIVIO

DIO2	LDY	#0
	LDA	DIO2T,Y
	STA	DIO3
	LDA	DIO2T+4,Y

	JSR	CPCIO1
DIO3	DFB 4,3
	DFW COMFNAM,28
	BMI	DIO2EX

	LDY	DIO2+1
	LDX	DIO2T+2,Y

	LDA	$340,X
	STA	ECHOFLG,Y
	LDA	#$FF
	STA	$340,X

EOFFS	LDX	#0
	LDA	#ETAB:L
	STA	$31B,X
	LDA	#ETAB:H
	STA	$31C,X

	JSR UNI
	UCE ETAB+6,$346,2

	LDY	#1
DIO2EX	RTS
**

DIO2T	DFB 8,4,$40,$50
	DFB 16+128,20+128

**

EXECUTE	LDA	LBUF
	CMP	#'-'
	BEQ	EXEBAT

	CMP	#';'
	BEQ	EXEEX

	JSR	CRNAME
	BNE	EXE2

	JSR UNI
	UCE COMFNAM,PROMPT,2

EXEEX	RTS
*

EXE2	LDX	#$FF
	STX	$43

EXE3	LDY	#0
	STY	$44
	INC	$43

EXE4	INX
	LDA	CMDS,X
	BEQ	EXE9

EXE4B	CMP	COMFNAM+3,Y
	BEQ	EXE5
	DEC	$44

EXE5	INY
	CMP	#$9B
	BNE	EXE4

	LDY	$44
	BMI	EXE3
*A=$9B
	JSR	CRNM00

	LDA	$43
	CMP	#13
	BEQ	EXELD
	BCS	EXE8
*C=0
	ADC	#32
	STA	EXE7B

	JSR	CPC4
	DFB 0
EXE7B	DFB 0
	DFW COMFNAM,28

	RTS
**

EXE8	TAY
	LDA	CPACMDH-14,Y
	PHA
	LDA	CPACMDL-14,Y
	PHA

	CPX	#3
	RTS

***

EXE9	JSR	NULDW

	LDY	#LCOM-LBUF
	JSR	CROUTI

	CLC

EXELD	LDA	#8
	ROR
	STA	EXELD2

	JSR	CPC4
EXELD2	DFB	4,40
	DFW	COMFNAM,28

	RTS
***

CPDIR	SEC
	DFB BIT2
CPDIRS	CLC

	LDA	#12
	ROR
	PHA

* JE TO? : < >

	LDA	COMFNAM-1,X
	CMP	#$38
	BEQ	CPDIRS2
	AND	#$F9
	CMP	#$38
	BNE	CPDIRS2

CPDIRS1	LDY	#LHVE-LBUF
	JSR	CROUTI

CPDIRS2	PLA
	DFB BIT3

CPTYP	LDA	#4

	STA	CPD2
	JSR	CPC4
CPD2	DFB 4,3
	DFW COMFNAM,28

CPD3	JSR	CPCIO
	DFB 4+64,5
	DFW LBUF,64

	BMI	CPDX

	JSR	CPCIO
	DFB 64,9
	DFW LBUF,64

	JMP	CPD3

***

EGEE	JSR	XDIVIO1

	LDX	#0
**

EGET	TXA
	BNE	EGETP

	BIT	BATFLG
	BPL	EGE2

	JSR	EGETP

	CPY	#0
	BPL	EPU2

CPDX	RTS

**

EGE2	LDY	#1
	LDX	#5
	JSR	DIVHND

	CPY	#0
	BMI	EGEE

	LDX	#0
**

EPUT	CPX	#0
	BNE	EPUTP

	PHA
	JSR	EPUTP
	PLA

EPU2	BIT	ECHOFLG
	BMI	EPUTY

	LDY	#0
	LDX	#7
	JSR	DIVHND

	CPY	#0
	BPL	EPUTX

	JSR	XDIVIO0

EPUTX	LDA	DIH3+1
	LDY	#1

EPUTY	CPY	#0
	RTS
**

EGETP	JMP	RET
EPUTP	JMP	RET

***

DIVHND	STA	DIH3+1
	STX	$45

	LDX	ECHOFLG,Y
	LDA	$31B,X
	STA	$43
	LDA	$31C,X
	STA	$44

	LDX	DIO2T+2,Y

DIH2	LDY	$45

DIH4	LDA	($43),Y
	PHA
	DEY
	LDA	($43),Y
	PHA

DIH3	LDA	#0

	RTS

*** COMTAB

SEC1CHG	ASC "        "
BUFDRIV	ASC "   "
BUFSECL	ASC "   "
BUFSECH	ASC "   "
BUFTIME	ASC "   "

DWARM	DFB 255,128

DECOUT	ASC "        "

SIO	JMP	$E459

ECHOFLG	DFB	$FF
BATFLG	DFB	$FF

DECIN	DFB 0,0,0,0

WRTCMD	ASC "P"
	DFB	$FF

COMTAB	JMP	CP
	JMP	CRNAME
	DFW	DIVIO
	DFW	XDIVIO

BUFOFF	DFB	0,0,7

DATER	DFB 24,1,94
TIMER	DFB 15,30,0
ODATER	DFB 24,1,94
OTIMER	DFB 15,30,0
TDOVER	DFB 0

TRUN	DFW	RET,STBUFF
TMEMLO	DFW	MEMLO
INCOMND	DFB 0

COMFNAM	ASC "D1:STARTUP.BAT"
	EOL
	ASC "             "

RUNLOC	DFW	RET

** INIT

LBUF	JSR	RESINI

	LDA	#$FF
	STA	$47

	LDY	#1
	JSR	DIVIO
	BMI	LBERR

	JSR	NULDW
	STX	580

	LDA	9
	ORA	#1
	STA	9

	JMP	CP

*

LBERR	CLC
	RTS


*
	ASC "                 "
	ASC "                "

***

LBUFEND	EOL

PROMPT	ASC "D1:"
	EOL
LBAT	ASC ".BAT"
	EOL
LCOM	ASC ".COM"
	EOL
LHVE	ASC "*.*"

***

DLVOL	EOL
	ASC "Volume:"

DIRNAM	ASC "           "

DIBUF	ASC "                    "
	ASC "                    "
	ASC " "
DLDIR	EOL
	ASC "Directory:"


DLG3TX	ASC /DIR/
DLG3DT	ASC "<DIR>"
DLFRE	ASC "FREE SECTORS"

VTOCSEC	DFW 0
SRCHTMP	DFW 0
LOSADR	DFW 0
LDSALEN	DFW 0

	DFB	128
SECLENS	ASC "    "
POSTMP	ASC "    "

KFF	DFW $FFFF
DOSNAM	ASC "DOS       "
*!
RDPUTST	DFB 32,33
*!
NULY	DFB 0,0,0
K02	DFW 2
K1C	DFB $1C
K23	DFW 23
K01	DFW 1

*** INIT

SRCHNAM	LDX	#0

T2	LDA	$31A,X
	CMP	#'E'
	BEQ	T3

	INX
	INX
	INX
	BNE	T2
*

T3	STX	EOFFS+1

	LDA	$31B,X
	STA	EOLD2L+1
	STA	$43
	SEC
	SBC	#248
	STA	EOLDAD6+1

	LDA	$31C,X
	STA	EOLD2H+1
	STA	$44
	SBC	#0
	STA	EOLDAD6+2

	LDY	#14
T4	LDA	($43),Y
	STA	ETAB,Y
	DEY
	BPL	T4

	JSR UNI
	UC ETAB+4,EGETP+1,2
	UC ETAB+6,EPUTP+1,2
	UC T5T,ETAB+4,4

	UCE KRESET,12,2

	JSR	DINC
	DFW EGETP+1
	JSR	DINC
	DFW EPUTP+1

*CLR MEM

	LDA	#0
	LDY	#MEMLO:L

	LDX	#MEMLO:H

T5A	STX	T5+2
T5	STA	$FF00,Y
	INY
	BNE	T5

	INX
	CPX	$2E6
	BCC	T5A

*NADPIS

T6	TYA
	EOR	TNADP,Y
	BEQ	T8

	STY	T7+1

	STA	T6BUF
	JSR	CPC192
	DFB 11
	DFW T6BUF,1

T7	LDY	#0

	INY
	BNE	T6

T8	JMP	LBUF

**

T6BUF	DFB 0

**

KRESET	DFW	RESET

**

T5T	DFW EGET-1,EPUT-1

***

TNADP DFB 155,33,34,35,36,37,38,57
 DFB 54,41,72,92,33,73,65,92,48,49
 DFB 68,118,102,59,54,38,54,40,42
 DFB 59,32,33,133,63,0,1,2,3,12,102
 DFB 15,7,106,108,125,110,127,98,104
 DFB 123,16,30,18,114,118,119,99,116
 DFB 24,8,3,2,8,166,30,31,96,97,98
 DFB 99,100,101,102,103,27,57,47,49
 DFB 37,44,34,57,53,35,33,58,59,59
 DFB 108,204,120,121,122,123,124,23
 DFB 63,55,18,4,17,4,5,7,3,71,12,12
 DFB 25,75,45,47,44,58,51,81,23,93
 DFB 34,91,237,119

**

RENNAM	EQU	SRCHNAM+11
DIRBUF	EQU	RENNAM+11
FCBIOCB	EQU	DIRBUF+23
DCBDRIV	EQU	FCBIOCB+21
FCBS	EQU	DCBDRIV+130
START2	EQU	FCBS+105

****

PCOMTAB	EQU	COMTAB
PCONVDC0	EQU	CONVDC0
PCONDC00	EQU	CONVDC00
PCONV	EQU	CONVDC
PGETSTK	EQU	GETSTK
PUNI	EQU	UNI
PGETTDA	EQU	GETTDA
PDINC	EQU	DINC
PDADC	EQU	DADC
PDDEC	EQU	DDEC
PDSBC	EQU	DSBC
PDADC7C	EQU	DADC7C

****

SAVEAT	EQU	ST00+$3000

