ST00	EQU	$6E6
	ORG	ST00,$A800
	OUT	N

*************************************
*				*
*	B W  D O S  V 1.21  	*
*				*
*	     CAST 1		*
*				*
*************************************

*PRENOS

MEMLO	EQU	$1EEF
STBUFF	EQU	$1716
LDSA7C	EQU	$148D
DOPEN	EQU	$F9D
DCLOSE	EQU	$FA0
DGET	EQU	$FA3
DPUT	EQU	$FA6
DSTAT	EQU	$FA9
DSPEC	EQU	$F89

* DEFINICE

BIT3	EPZ	$2C
BIT2	EPZ	$24

EOL	MACRO
	DFB	155
	MEND
*

UC	MACRO	A1,A2,A3
	DFB (A1)/256+64
	DFB (A1):L
	DFB (A2)/256+32*(A3)
	DFB (A2):L
	MEND

UCE	MACRO	A1,A2,A3
	DFB (A1)/256+192
	DFB (A1):L
	DFB (A2)/256+32*(A3)
	DFB (A2):L
	MEND

UCS	MACRO	A1,A2,A3
	DFB (A3)+32
	DFB (A1)-DLVOL
	DFB (A2)-DLVOL
	MEND

UCSE	MACRO	A1,A2,A3
	DFB (A3)+32+128
	DFB (A1)-DLVOL
	DFB (A2)-DLVOL
	MEND

UCALL	MACRO	A1
	DFB (A1):H
	DFB (A1):L
	MEND

UCALLE	MACRO	A1
	DFB (A1)/256+128
	DFB (A1):L
	MEND

* VSTUPNI INIT

	CLD

	LDX	#0
	LDY	#$20

VSTI1	LDA	ZACATEK+$3000,X
VSTI2	STA	ZACATEK,X
	INX
	BNE	VSTI1

	INC	VSTI1+$3002
	INC	VSTI2+$3002

	DEY
	BNE	VSTI1

	JMP	SRCHNAM

* HLAVICKA V 7. STRANCE

ZACATEK	DFB	'S'
	DFB	$32

	DFB	0

	ASC	"BW"
	DFB	$12

GETTDA	DFW	GETTD
	DFW	SETTD
	DFW	CONVDC
	JMP	SIO

***

CPACMDL DFB CPCAR-1:L,CPRUN-1:L
 DFB CPPRT-1:L,CPTYP-1:L,CPDIR-1:L
 DFB CPDIRS-1:L,CPBAS-1:L

CPACMDH DFB CPCAR-1:H,CPRUN-1:H
 DFB CPPRT-1:H,CPTYP-1:H,CPDIR-1:H
 DFB CPDIRS-1:H,CPBAS-1:H

***

CONVDC00	STA	DECIN
	LDA	#0
	STA	DECIN+1
CONVDC0	LDA	#0
	STA	DECIN+2

CONVDC	LDX	#7
COND0	STX	CONDI+1

	LDX	#24
	LDA	#0

COND1	ASL	DECIN
	ROL	DECIN+1
	ROL	DECIN+2
	ROL

	CMP	#10
	BCC	COND2
	SBC	#10

	INC	DECIN

COND2	DEX
	BNE	COND1

	ORA	#$30
CONDI	LDX	#0
COND3	STA	DECOUT,X

	DEX
	BMI	RET3

	LDA	DECIN
	ORA	DECIN+1
	ORA	DECIN+2
	BNE	COND0

	LDA	#$20
	BNE	COND3
***

CRNM2	INC	BUFOFF
	DFB BIT3

CRNAME	LDA	#$20

CRNM00	LDY	BUFOFF
	LDX	LBUF,Y
	CPX	#$20
	BEQ	CRNM2

	STA	CRODDE+1

	LDA	#0
	STA	CROUTI+1

	CPX	#$9B
	BEQ	CRNM3

	LDA	LBUF+1,Y
	CMP	#$9B
	BEQ	CRNM3

	CMP	#':'
	BEQ	CRNM5

	LDA	LBUF+2,Y
	CMP	#':'
	BEQ	CRNM4

CRNM3	LDY	#PROMPT-LBUF
	JSR	CROUTI

CRNM4	LDY	BUFOFF
	JSR	CROUTI
	STY	BUFOFF

CRNMX	CPX	#3
RET3	RTS
*

CRNM5	INC	CROUTI+1
	JSR	CRNM4

	LDA	COMFNAM+1
	STA	COMFNAM

K31	LDA	#'1'
	STA	COMFNAM+1
	BNE	CRNMX
**

CRO1	STA	COMFNAM,X
	CPX	#27
	BCS	CRO1B
	INX

CRO1B	INY

	DFB BIT3
CROUTI	LDX	#0

	LDA	LBUF,Y
	CMP	#$9B
	BEQ	CRO3
CRODDE	CMP	#$20
	BNE	CRO1

CRO3	LDA	#$9B
	STA	COMFNAM,X
	STX	CROUTI+1

	RTS

***

DTABLE	DFW DOPEN-1
	DFW DCLOSE-1
	DFW DGET-1
	DFW DPUT-1
	DFW DSTAT-1
	DFW DSPEC-1
*

RESET	CLD

	LDA	$47
	BNE	RESINI

	DEC	$47

	BIT	INCOMND
	BPL	RESINI

	JSR	RINIT

CMTAD	JMP	COMTAB

**

RESINI	JSR UNI
	UC TMEMLO,$2E7,2
	UC CMTAD+1,10,2
	UCE KFF,ECHOFLG,2

*MAZE - LAST1 FCBIOCB (NEVADI)
*     - SEC1CHG
*     - DCBs
*     - FCBs = $FF
*     - BUFDRIV - BUFTIME

*X=0
	TXA
RESI1	STA	SEC1CHG-1,X

	CPX	#151
	BCC	RESI1B
	DEC	FCBS-151,X

RESI1B	INX
	BNE	RESI1

*DRIVE# DO DCB

	LDY	#1
	DFB	BIT3
RESI3	LDY	#8

RESI4	TYA
	STA	DCBDRIV,X

	TXA
	CLC
	ADC	#26
	TAX

	INY
	CPY	#5
	BCC	RESI4
	BEQ	RESI3

*A=5*26=130 (->MI)

	LDY	#30

RESI5	LDX	$31A,Y
	BNE	RESI6
	TYA

RESI6	CPX	#'D'
	BEQ	RESI7

	DEY
	DEY
	DEY
	BPL	RESI5

	TAY
	BMI	RET2

RESI7	LDX	#253	;-3

RESI8	LDA	RESITAB-253,X
	STA	$31A,Y

	INY
	INX
	BNE	RESI8

RET2	RTS

**

RESITAB	DFB 'D'
	DFW DTABLE
***

RINIT	JMP	(12)

*DEFAULT TD

INTDATE	DFB 22,7,95
	DFB 12,32,05
***

SETTD	JSR UNI
	UCE DATER,INTDATE,6

GETTD	JSR UNI
	UCE INTDATE,DATER,6

RET	RTS

***

DADC7C	LDA	LDSA7C+1
	DFB BIT3

DINC	LDA	#1
DADC	LDX	#$69	;ADC
	BNE	DIC2

DDEC	LDA	#1
DSBC	LDX	#$E9	;SBC

DIC2	STA	DIC6+1
	STX	DIC6

	LDX	#254
DIC3	JSR	GETSTK
	STA	DIC5-253,X
	STA	DIC7-253,X
	INX
	BNE	DIC3

	LDA	DIC6
	ASL

	DEX
DIC4	INX

DIC5	LDA	$FFFF,X
DIC6	ADC	#0
DIC7	STA	$FFFF,X

	STX	DIC6+1
	TXA
	BEQ	DIC4

	RTS
***

CPRUN	BNE	CPR1
CPRUN2	JMP	(RUNLOC)

CPR1	JSR UNI
	UCE NULY,RUNLOC,2

CPR2	LDA	COMFNAM+3,X
	INX

	SEC
	SBC	#$30
	CMP	#10
	BCC	CPR3
	SBC	#7
	CMP	#16
	BCS	CPRUN2

CPR3	ASL
	ASL
	ASL
	ASL
	LDY	#4

CPR4	ASL
	ROL	RUNLOC
	ROL	RUNLOC+1

	DEY
	BNE	CPR4
	BEQ	CPR2

***

GETSTK	STX	GST4+1
	STY	GST5+1

	TSX
	INX
	INX
	INX

	INC	$100,X
	LDA	$100,X

	INX
	TAY
	BNE	GST2

	INC	$100,X

GST2	LDA	$100,X
	STA	GST3+2

GST3	LDA	$FF00,Y

GST4	LDX	#0
GST5	LDY	#0

	RTS
***

UNIUC2	JSR	GETSTK
	LDX	#3
	DFB BIT3

UNIUC	LDX	#0

	PHA
	AND	#31
	STA	UNIUCL+2,X
	PLA

	LSR
	LSR
	LSR
	LSR
	LSR
	STA	UNIUC1+1

	JSR	GETSTK
	STA	UNIUCL+1,X

	TXA
	BEQ	UNIUC2
*

UNIUC1	LDX	#0

UNIUC3	DEX
UNIUCL	LDA	$FFFF,X
	STA	$FFFF,X

	TXA
	BNE	UNIUC3

	TYA
	BMI	UNIUCE
**

UNI	JSR	UNISCH

UNI1	JSR	GETSTK
	TAY

	ASL
	ASL
	PHP

	TYA
	AND	#31

	PLP
	BCS	UNIUC
	BMI	UNIUCS

	STA	UNI1CL+2

	JSR	GETSTK
	STA	UNI1CL+1

	TYA
	PHA

	JSR	UNIOBN
UNI1CL	JSR	UNIRTS
	JSR	UNISCH

	PLA
	BPL	UNI1
*

UNIOBN	LDA	#0
	PHA
UNIA	LDA	#0
	PLP
UNIRTS	RTS

**

UNIUCS	STA	UNIA+1
	STY	UNIUCS3+1

	JSR	GETSTK
	TAX
	JSR	GETSTK
	TAY

UNIUCS2	LDA	DLVOL,X
	STA	DLVOL,Y

	INX
	INY
	DEC	UNIA+1
	BNE	UNIUCS2

UNIUCS3	LDA	#0
	BPL	UNI
*

UNIUCE	LDX	#0
	RTS

***

CPCIO	JSR	GETSTK
	DFB BIT3
CPC192	LDA	#192
	DFB BIT3
CPC4	LDA	#4

CPCIO1	STA	$44
	ASL
	ASL
	TAX
	TAY

	BCS	CPCIO2
*AUXs

	JSR	GETSTK

	LSR
	STA	$34A,X
	LDA	#0
	ROR
	STA	$34B,X

CPCIO2	LDA	#$67
	STA	$43

CPCIO3	JSR	GETSTK
	STA	$342,Y

CPCIO4	INY
	ASL	$43
	BCC	CPCIO4
	BNE	CPCIO3
*
	LDA	$44
	PHA

	JSR	$E456

	PLA
	TAX

	TYA
	BPL	CPCIX
	CMP	#136
	BEQ	CPCIX
*ERR
	PHA

	TXA
	PHA

	AND	#28
	BEQ	CPCIO8
	ORA	#192

	JSR	CPCIO1
	DFB 12
*ADR/LEN LIBOVOLNE - VSUVKA
DIOAPUV	DFW 0,0
***

CPCIO8	PLA
	BPL	CPCER

	PLA

CPCIX	TAY
	RTS
*

CPCER	JSR CPC192
	DFB 11
	DFW ERRTX,6

	PLA
	JSR	CONVDC00

	JSR CPC192
	DFB 11
	DFW DECOUT+4,4

	JSR CPC192
	DFB 11
	DFW ERRTX,1

	JSR	XDIVIO1
***

CP	CLD

	JSR	DIOREP
*X=0
	STX	TDOVER
	STX	BUFOFF

	DEX
	STX	INCOMND
	TXS

	JSR	CPCIO
	DFB 4+192,12
*ADR/LEN LIBOVOLNE - VSUVKA
DIOANEW	DFW EGET-1,EPUT-1
***

	JSR	CPC192
	DFB 11
	DFW PROMPT-1,4

	JSR	CPCIO
	DFB 64,5
	DFW LBUF,64

	JSR	EXECUTE
	JMP	CP
***

CPCAR	LDA	$BFFD
	INC	$BFFD
	CMP	$BFFD
	STA	$BFFD
	BNE	CPERR

	LDX	$BFFC
	BNE	CPERR

	LDA	DWARM
	STA	8

	STX	INCOMND
	DEX
	STX	DWARM

	JMP	($BFFA)
***

CPBAS2	STA	$3F8
	PHP

	JSR	CPC192
	DFB 12
*ADR/LEN LIBOVOLNE - VSUVKA
DIO2T1	DFB 8*2,4*2
DIO2T2	DFB $40,$50
***

	LDA	$D301
	ROR
	ROR
	PLP
	ROL
	ROL
	STA	$D301

	LDY	#0
	STY	$2E5

	LDX	#$CF

CPBAS2B	TXA
	SEC
	SBC	#16
	TAX

	STX	$44

	LDA	($43),Y
	EOR	#$FF
	STA	($43),Y
	CMP	($43),Y

	BNE	CPBAS2B

	INX
	STX	$2E4
	STX	$2E6
	STX	$6A

	JSR	CPCIO
	DFB 128,12*2,3
	DFW ENAME
*DELKA LIBOVOLNA - VSUVKA
DIOAPUT	DFW 0
***

NULDW	LDX	#0
	STX	DWARM

***RTS VYPUSTEN - NEVADI

UNISCH	STA	UNIA+1
	PHP
	PLA
	STA	UNIOBN+1
	RTS

***

CPBAS	LDA	COMFNAM+3
	CMP	#'O'
	BNE	CPERR

	LDA	COMFNAM+4

	CMP	#'F'
	BEQ	CPBAS2

	CLC
	EOR	#'N'
	BEQ	CPBAS2

CPERR	LDA	#$9C
CPER2	PHA
	JMP	CPCER
***

ETAB	DFW	0,0,0,0,0,0

EGETP	JMP	(DIOAPUV)
EPUTP	JMP	(DIOAPUV+2)

***

EGEE	JSR	XDIVIO1
*X=0

**

EGET	TXA
	BNE	EGETP

	BIT	BATFLG
	BPL	EGE2

	JSR	EGETP

	CPY	#0
	BPL	EPU2

	RTS

**

EGE2	LDY	#1
	LDX	#5
	JSR	DIVHND

	CPY	#0
	BMI	EGEE

	LDX	#0
**

EPUT	CPX	#0
	BNE	EPUTP

	PHA
	JSR	EPUTP
	PLA

EPU2	BIT	ECHOFLG
	BMI	EPUTY

	LDY	#0
	LDX	#7
	JSR	DIVHND

	CPY	#0
	BPL	EPUTX

	JSR	XDIVIO0

EPUTX	LDA	DIH3+1
DIO2X0	LDY	#1

EPUTY	LDX	#0
	CPY	#0
DIO2EX	RTS


*DISABLE
DIORDIS	JSR	UNI
DIORA3	UC DIOR2+1,$31C,2
	UCE DIOAPUT,$346,2

DIO2X0EQ	BEQ	DIO2X0

**

DIVIO	JSR	XDIVIO

DIO2	LDY	#0
	LDA	DIO2T1,Y
	STA	DIO3
	LDA	DIO2T3,Y

	JSR	CPCIO1
DIO3	DFB 4*2,3
	DFW COMFNAM,28
	BMI	DIO2EX

	LDA	#0

DIO3B	LDY	DIO2+1
	LDX	DIO2T2,Y

	ORA	$340,X
	STA	ECHOFLG,Y

	LDA	#$FF
	STA	$340,X
**

DIOREP	LDA	$31C	;COMTAB E:
	CMP	#ETAB:H
	BEQ	DIOR4

	JSR	UNI
DIORA1	UCE $31B,DIOR2+1,2

	LDX	#11
DIOR2	LDA	$FFFF,X	;ORIG.ADR.
DIOR3	STA	ETAB,X
	DEX
	BPL	DIOR2

	JSR	UNI
	UC ETAB+4,DIOAPUV,4
	UC ETAB+6,DIOAPUT,2
	UCE DIOANEW,ETAB+4,4

	JSR	DINC
	DFW DIOAPUV
	JSR	DINC
	DFW DIOAPUV+2

DIOR4	LDA	ECHOFLG
	AND	ECHOFLG+1
	BMI	DIORDIS
*ENABLE
	JSR	UNI
DIORA2	UC DIOR3+1,$31C,2
	UCE ETAB+6,$346,2

	BEQ	DIO2X0EQ
***

CPPRT	BNE	DIVIO0

	LDA	COMFNAM
	CMP	PROMPT
	BNE	DIVIO0
**

XDIVIO0	LDY	#0
	DFB BIT3
XDIVIO1	LDY	#1

XDIVIO	TYA
	AND	#1
	TAY
	STY	DIO2+1

	LDA	ECHOFLG,Y
	BMI	DIOREP

	LDX	#3
	JSR	DIVHND

	LDA	#$FF
	BMI	DIO3B

***

EXEBAT	LDA	#$20
	JSR	CRNM2

	LDY	#LBAT-LBUF
	JSR	CROUTI
*

DIVIO1	LDY	#1
	DFB BIT3
DIVIO0	LDY	#0

	JSR	DIVIO
	BPL	EXEEX
	JMP	CPER2

**

EXECUTE	LDA	LBUF
	CMP	#'-'
	BEQ	EXEBAT

	CMP	#';'
	BEQ	EXEEX

	JSR	CRNAME
	BNE	EXE2

	JSR UNI
	UCE COMFNAM,PROMPT,2

EXEEX	RTS
*
EXE2	JSR	EXE2B

	ASC "RENAM"
	ASC /E/
	ASC "ERAS"
	ASC /E/
	EOL
	ASC "PROTEC"
	ASC /T/
	ASC "UNPROTEC"
	ASC /T/

ERRTX	EOL
ENAME	ASC "Error"
	EOL
	EOL

DIO2T3	DFB 16+128,20+128

	ASC "CREDI"
	ASC /R/
	ASC "DELDI"
	ASC /R/
	ASC "CW"
	ASC /D/
	ASC "LOA"
	ASC /D/
	ASC "CA"
	ASC /R/
	ASC "RU"
	ASC /N/
	ASC "PRIN"
	ASC /T/
	ASC "TYP"
	ASC /E/
	ASC "DI"
	ASC /R/
	ASC "DIR"
	ASC /S/
	ASC "BASI"
	ASC /C/

	DFB 0

	JSR	NULDW

	LDY	#LCOM-LBUF
	JSR	CROUTI

	CLC

EXELD	LDA	#4
	ROL
	STA	EXELD2

	JSR	CPC4
EXELD2	DFB	4*2,40
	DFW	COMFNAM,28

EXE9RTS	RTS

**

EXE2B	LDX	#$FF
	STX	$43

EXE3	LDX	#0
	LDY	#0
	INC	$43

EXE4	JSR	GETSTK
	PHA

	EOR	COMFNAM+3,Y
	ASL
	BEQ	EXE5
	DEX

EXE5	INY
	PLA
	BEQ	EXE9RTS
	BPL	EXE4

	TXA
	BMI	EXE3

	LDA	COMFNAM+3,Y
	CMP	#$9B
	BNE	EXE3
*A=$9B
	JSR	CRNM00

	PLA
	PLA

	LDA	$43
	CMP	#13
	BEQ	EXELD
	BCS	EXE8
*C=0
	ADC	#32
	STA	EXE7B

	JSR	CPC4
	DFB 0
EXE7B	DFB 0
	DFW COMFNAM,28

	RTS
**

EXE8	TAY
	LDA	CPACMDH-14,Y
	PHA
	LDA	CPACMDL-14,Y
	PHA

	CPX	#3
	RTS
***

CPDIR	LDA	#6*2+1
	DFB BIT3
CPDIRS	LDA	#6*2
	PHA

* JE TO? : < >

	LDA	COMFNAM-1,X
	CMP	#$38
	BEQ	CPDIRS2
	AND	#$F9
	CMP	#$38
	BNE	CPDIRS2

	LDY	#LHVE-LBUF
	JSR	CROUTI

CPDIRS2	PLA
	DFB BIT3

CPTYP	LDA	#4*2

	STA	CPD2
	JSR	CPC4
CPD2	DFB 4*2,3
	DFW COMFNAM,28
*            REZERVA ^ - DELKA LIBOV.

CPD3	JSR	CPCIO
	DFB 4+64,5
	DFW LBUF,64

	BMI	CPDX

	JSR	CPCIO
	DFB 64,9
	DFW LBUF,64

	JMP	CPD3

**

DIVHND	STA	DIH3+1
	STX	$45

	LDX	ECHOFLG,Y
	LDA	$31B,X
	STA	$43
	LDA	$31C,X
	STA	$44

	LDX	DIO2T2,Y

	LDY	$45

	LDA	($43),Y
	PHA
	DEY
	LDA	($43),Y
	PHA

DIH3	LDA	#0

CPDX	RTS

*** COMTAB

DWARM	DFB 255,128

DECOUT	ASC "        "

SIO	JMP	$E459

ECHOFLG	DFB	$FF
BATFLG	DFB	$FF

DECIN	DFB 0,0,0,0

WRTCMD	ASC "P"
	DFB	$FF

COMTAB	JMP	CP
	JMP	CRNAME
	DFW	DIVIO
	DFW	XDIVIO

BUFOFF	DFB	0,0,7

DATER	DFB 22,7,95
TIMER	DFB 12,32,05
ODATER	DFB 22,7,95
OTIMER	DFB 12,32,05
TDOVER	DFB 0

TRUN	DFW	RET,STBUFF
TMEMLO	DFW	MEMLO
INCOMND	DFB 0

COMFNAM	ASC "D1:STARTUP.BAT"
	EOL
	ASC "             "

RUNLOC	DFW	RET

** INIT

LBUF	JSR	RESINI

	LDA	#$FF
	STA	$47

	LDY	#1
	JSR	DIVIO
	BMI	LBERR

	JSR	NULDW
	STX	580

	LDA	9
	ORA	#1
	STA	9

	JMP	CP

*

LBERR	CLC
	RTS


*
	ASC "                 "
	ASC "                "

***

LBUFEND	EOL

PROMPT	ASC "D1:"
	EOL
LBAT	ASC ".BAT"
	EOL
LCOM	ASC ".COM"
	EOL
LHVE	ASC "*.*"

***

DLVOL	EOL
	ASC "Volume"
KDVOJT	ASC ":"

DIRNAM	ASC "           "

DIBUF	ASC "                    "
	ASC "                    "
	ASC " "
DLDIR	EOL
	ASC "Directory:"


DLG3TX	ASC /DIR/
DLG3DT	ASC "<DIR>"
DLFRE	ASC "FREE SECTORS"

VTOCSEC	DFW 0
SRCHTMP	DFW 0

	DFB	128
SECLENS	ASC "    "
POSTMP	ASC "    "

KFF	DFW $FFFF
DOSNAM	ASC "DOS       "
*!
RDPUTST	DFB 32,33
*!
NULY	DFB 0,0,0
K02	DFW 2
K1C	DFB $1C
K23	DFW 23
K01	DFW 1
KPOML	ASC "-"

*** INIT

SRCHNAM	LDX	#$1A

T2	LDA	$300,X
	CMP	#'E'
	BEQ	T3

	INX
	INX
	INX
	CPX	#$3B
	BCC	T2

	JMP	$E471
*

T3	INX
	STX	DIORA1+1
	STX	DIORA2+3
	STX	DIORA3+3
	INX
	STX	DIOREP+1

*RESET
	JSR	UNI
	UCE KRESET,12,2

*CLR MEM (X=0)

	TXA
	LDY	#MEMLO:L
	LDX	#MEMLO:H

T5A	STX	T5+2
T5	STA	$FF00,Y
	INY
	BNE	T5

	INX
	CPX	$2E6
	BCC	T5A

*NADPIS
	LDX	#0

T6	TXA
	EOR	TNADP,X
	BEQ	T8

	STX	T7+1

	STA	T6BUF
	JSR	CPC192
	DFB 11
	DFW T6BUF,1

T7	LDX	#0

	INX
	BNE	T6

T8	JMP	LBUF

**

KRESET	DFW	RESET

***

TNADP DFB 155,33,34,35,36,37,38,39
 DFB 54,55,42,73,91,32,74,64,67,49
 DFB 68,118,102,59,54,38,54,43,43,59
 DFB 32,33,133,63,8,98,11,3,102,96
 DFB 113,98,123,102,108,127,12,28,23
 DFB 22,5,17,18,27,126,92,68,94,24
 DFB 123,95,73,82,92,77,90,43,104,217
 DFB 99,100,101,102,19,32,32,57,107
 DFB 37,62,110,46,112,23,32,54,49,2
 DFB 55,37,61,121,42,41,51,58,44,62
 DFB 13,65,67,248,100

T6BUF	EQU	*

**

RENNAM	EQU	SRCHNAM+11
DIRBUF	EQU	RENNAM+11
FCBIOCB	EQU	DIRBUF+23
SEC1CHG	EQU	FCBIOCB+21
LDSALEN	EQU SEC1CHG+4

DCBDRIV	EQU	SEC1CHG+8
FCBS	EQU	DCBDRIV+130

BUFDRIV	EQU	FCBS+105
BUFSECL	EQU	BUFDRIV+3
BUFSECH	EQU	BUFSECL+3
BUFTIME	EQU	BUFSECH+3

START2	EQU	BUFTIME+3

****

PCOMTAB	EQU	COMTAB
PCONVDC0	EQU	CONVDC0
PCONDC00	EQU	CONVDC00
PCONV	EQU	CONVDC
PGETSTK	EQU	GETSTK
PUNI	EQU	UNI
PGETTDA	EQU	GETTDA
PDINC	EQU	DINC
PDADC	EQU	DADC
PDDEC	EQU	DDEC
PDSBC	EQU	DSBC
PDADC7C	EQU	DADC7C
PK31	EQU	K31+1

****

SAVEAT	EQU	ST00+$3000

