*************************************
*				*
*	B W  D O S  V 1.30  	*
*				*
*	     CAST 2		*
*				*
*************************************

*** DCB A FCB ADRESY

FCBDRIV	EQU	FCBIOCB+1
FCBSECT	EQU	FCBIOCB+2
FCBIX	EQU	FCBIOCB+4
FCBMAP	EQU	FCBIOCB+5
FCBMAPX	EQU	FCBIOCB+7
FCBPOS	EQU	FCBIOCB+8
FCB1ST	EQU	FCBIOCB+11
FCBLEN	EQU	FCBIOCB+13
FCBAUX1	EQU	FCBIOCB+16
FCBDIR1	EQU	FCBIOCB+17
FCBDIRP	EQU	FCBIOCB+19

DCBMAIN	EQU	DCBDRIV+1
DCBFREE	EQU	DCBDRIV+5
DCBMAPS	EQU	DCBDRIV+7
DCBMAP1	EQU	DCBDRIV+8
DCBDAT	EQU	DCBDRIV+10
DCBDIR	EQU	DCBDRIV+12
DCBVOL	EQU	DCBDRIV+14
DCBRND	EQU	DCBDRIV+23
DCBSEQ	EQU	DCBDRIV+22
DCBWD	EQU	DCBDRIV+24

* DEFINICE

RWFLG	EQU	$48
MYA1	EQU	$49
MYSLEN	EQU	$45
B	EQU	$43
CIOIOCB	EQU	$46

***

ZAJMAP	LDX	FCBMAP
	LDA	FCBMAP+1

	JSR	ZAJIST

	LDY	FCBMAPX
	RTS
***

XIO8GO	LDA	XIOADH-32,Y
	AND	#31
	PHA
	LDA	XIOADL-32,Y
	PHA

	LDX	CIOIOCB

	RTS

***

XIOADL	DTA	<(REDEPRU-1)
	DTA	<(REDEPRU-1),<(OPEN-1)
	DTA	<(REDEPRU-1),<(REDEPRU-1)
	DTA	<(POINT-1),<(NOTE-1)
	DTA	<(GETLEN-1)
	DTA	<(LOAD-1),<(STAT-1)
	DTA	<(CREDIR-1),<(DELDIR-1)
	DTA	<(CWD-1),<(CLOSE-1)
	DTA	<(BLOCK-1)
	DTA	<(CHKDSK-1),<(GET-1)
	DTA	<(PUT-1)

XIOADH	DTA	>(REDEPRU-1)+192+32
	DTA	>(REDEPRU-1)+192+32
	DTA	>(OPEN-1)+128
	DTA	>(REDEPRU-1)+192+32
	DTA	>(REDEPRU-1)+192+32
	DTA	>(POINT-1)+32
	DTA	>(NOTE-1)+32,>(GETLEN-1)+32
	DTA	>(LOAD-1)+32,>(STAT-1)+192
	DTA	>(CREDIR-1)+192+32
	DTA	>(DELDIR-1)+192+32
	DTA	>(CWD-1)+192+32,>(CLOSE-1)+64
	DTA	>(BLOCK-1),>(CHKDSK-1)+192+32
	DTA	>(GET-1),>(PUT-1)

***

DSPEC	LDY	ICCOMZ
	STA	CIODTA+1

	TYA
	AND	#$F0
	CMP	#$20
	BNE	DSPER
	AND	XIOADH-32,Y
	BNE	XIO01

DSPER	LDY	#$A8
	RTS
*
DOPEN	LDY	#34
	DTA	BIT3
DCLOSE	LDY	#45
	DTA	BIT3
DGET	LDY	#48
	DTA	BIT3
DPUT	LDY	#49
	DTA	BIT3
DSTAT	LDY	#41

XIO	STA	CIODTA+1

XIO01	CLD
	STX	CIOIOCB

	LDA	#1
	STA	XOK+1

	CPY	#40
	BNE	XIO2
*LOAD
	JSR	XIO8GO
	DTA	BIT3

XTOOOPN	LDY	#$A1
XJCD	JMP	CIODTA

**

XIO2	TSX
	STX	XSSAVE+1

	INX
	INX
	LDA	$100,X
	STA	RTAD+1

	STY	XIO8+1
*FCB
	LDA	XIOADH-32,Y
	AND	#192
	STA	XIO4+1

	LDX	CIOIOCB
	JSR	XIOSRCH
	BNE	XIO4

	BIT	XIO4+1
	BPL	XIO5

	LDY	#$81
	BNE	XJCD
*

XIO4	LDA	#0
	BPL	XNOTOP

	LDX	#$FF
	JSR	XIOSRCH
	BNE	XTOOOPN

	LDA	CIOIOCB
	STA	FCBS,Y
	LDA	ICDNOZ
	STA	FCBS+1,Y
*

XIO5	STY	XIOFCB+1
	LDX	#235

XIO5B	LDA	FCBS,Y
	STA	FCBIOCB-235,X

	INY
	INX
	BNE	XIO5B
*DCB
	LDY	#104

XIO6	LDA	DCBDRIV,Y
	CMP	FCBDRIV
	BEQ	XIO7

	TYA
	SEC
	SBC	#26
	TAY

	BCS	XIO6

*MUSI SE JEDNAT O OPEN (JINAK BY NEBYL
*NOVE ZADAN CHYBNY DRIVE), PROTO ERROR
*ZRUSI FCB

	JSR	ERRORS
* DFB $A0 VYNECHANO - OPCODE LDY #

XNOTOP	LDY	#$85

	LDA	XIO8+1
	CMP	#45	;CLOSE?
	BNE	XJCD
	JMP	XOK
**

XIO7	LDX	#230

XIO7B	LDA	DCBDRIV-230,X
	PHA
	LDA	DCBDRIV,Y
	STA	DCBDRIV-230,X
	PLA
	STA	DCBDRIV,Y

	INY
	INX
	BNE	XIO7B

	BIT	XIO4+1
	BPL	XIO8A

*ZAHAJENI OPERACE TYPU OPEN

	JSR	ZAJIST1

	LDY	#7
	LDA	(B),Y
	CMP	#$80
	BNE	BGNOPNE

	LDA	#31
	TAY
	INY
	CMP	(B),Y
	BCC	BGO30

BGNOPNE	JSR	ERRORS
	DTA	$94
**

BGO30	LDY	#22

BGNOPN3	LDA	DCBVOL-22,Y
	CMP	(B),Y
	BNE	BGNOPN4

	INY
	CPY	#30
	BCC	BGNOPN3

	LDY	#38
BGO31	LDA	DCBSEQ-38,Y
	CMP	(B),Y
	BNE	BGNOPN4

	INY
	CPY	#40
	BCC	BGO31
	BCS	XIO8A

*JINY DSK

BGNOPN4	LDA	#$99	;STA
	JSR	ENDCSUB

	INY
	LDA	(B),Y
	LDY	FCBDRIV
	STA	SECLENS-1,Y

	JSR	UNI
	UCS	DCBMAIN,DCBWD,2
	UCALLE	DRIVNEP

***

XIO8A	LDY	FCBDRIV
	LDA	SECLENS-1,Y
	STA	MYSLEN

XIO8	LDY	#0
	JSR	XIO8GO

	BIT	XIO4+1
	BVC	XIO9

*ZAKONCENI OPERACE TYPU CLOSE

	JSR	ODKLID
	STX	FCBIOCB
***

XIO9	LDY	XIOFCB+1
	LDX	#235

XIO9B	LDA	FCBIOCB-235,X
	STA	FCBS,Y

	INY
	INX
	BNE	XIO9B

XOK	LDY	#1
	BNE	CIODTA

***

ERRORS	JSR	GETSTK
	TAY

ERROR	LDX	#0
XSSAVE	EQU	ERROR

	TXS

	TYA
	PHA

*OPEN/CLOSE?

	LDA	XIO4+1
	BEQ	ERR1B

XIOFCB	LDY	#0
	LDA	#$FF
	STA	FCBS,Y

ERR1B	JSR	ODKLID
	JSR	DRIVNEP

	PLA
	TAY

CIODTA	LDA	#0
	LDX	CIOIOCB

	CPY	#0

	RTS
***

ODKLID	LDX	FCBDRIV
	LDA	SEC1CHG-1,X
	BPL	ENDCLO2

	LSR	SEC1CHG-1,X

	JSR	ZAJIST1
	JSR	ZMENEN

	INC	DCBSEQ
	LDA	#$B9	;LDA
	JSR	ENDCSUB

ENDCLO2	LDX	#3

ENDCLO3	DEX
	BMI	ENDCLX

	JSR	NASLI
	BNE	ENDCLO3
	BCC	ENDCLO3

	JSR	UVOLN
** PLATI VZDY
	BPL	ENDCLO2
***

NASLI	LDA	BUFDRIV,X
	EOR	FCBDRIV
	ASL
ENDCLX	RTS

***

DRIVNEP	LDX	#2

DRNP2	JSR	NASLI
	BNE	DRNP3
	STA	BUFDRIV,X

DRNP3	DEX
	BPL	DRNP2

*** RTS VYNECHAN - NEVADI

XIOSRCH	LDY	#84

XIOS3	TXA
	CMP	FCBS,Y
	BEQ	XIOS4

	TYA
	SEC
	SBC	#21
	TAY

	BCS	XIOS3

XIOS4	RTS

***

ENDCSUB	STA	ENDCL1X
	STA	BGO40BX

	LDY	#38

BGO40B	LDA	(B),Y
BGO40BX	STA	DCBSEQ-38,Y
	STA	(B),Y

	INY
	CPY	#40
	BCC	BGO40B

	LDY	#9

ENDCL1	LDA	(B),Y
ENDCL1X	LDA	DCBMAIN-9,Y
	STA	(B),Y

	INY
	CPY	#30
	BCC	ENDCL1

	RTS
***

CHKDSK	JSR	UNI
	UCE	ICBLLZ,CHKP2+1,2
*X JE 0
	LDY	#32
	JSR	CHKP
	LDY	#31
	JSR	CHKP

	LDY	#11
CHKD2	JSR	CHKP
	CPY	#15
	BCC	CHKD2

	LDY	#22
CHKD3	JSR	CHKP
	CPY	#30
	BCC	CHKD3

	LDY	#38
	JSR	CHKP
	JSR	CHKP

	LDA	#0
	DTA	BIT3

CHKP	LDA	(B),Y
	INY

CHKP2	STA	$FFFF,X
LOSADR	EQU	CHKP2+1

	INX

	RTS
***

NUMMOD	LDX	#253

NUMMD1	LDA	DECOUT+4
	CMP	#32
	BNE	NUMMD2

	LDA	DECOUT-248,X
	DTA	BIT3
NUMMD2	LDA	#$39

	ORA	#$30
	STA	DECOUT-248,X
	INX
	BNE	NUMMD1

	RTS
**

DLG01A	JSR	DLCLRS
	DTA	17+128

	JSR	UNI
	UCALL	NUMMOD
	UC	DECOUT+5,DIBUF,3
	UCSE	DLFRE,DIBUF+4,12

	BEQ	DLG1EQ
**

DLG02	JSR	UNI
	UC	DCBFREE,DECIN,2
	UCALLE	CONVDC0

DTYPE	LDX	#0
	BPL	DLG01A

	JSR	DLCLRS
	DTA	20+128

	JSR	UNI
	UC	DECOUT+2,DIBUF,6
	UCSE	DLFRE,DIBUF+7,12

	BEQ	DLG1EQ
**

DLG01BA	JSR	DLCLRS
	DTA	18

	JSR	UNI
	UCSE	DIRBUF+6,DIBUF+2,11

DIRBAD	LDA	DIRBUF
	LSR
	BCC	DLG01B

	LDX	#'*'
	STX	DIBUF

DLG01B	AND	#16
	BEQ	DLG01C

	JSR	UNI
	UCSE	DLG3TX,DIBUF+10,3

DLG01C	LDA	DIRBUF+3
	LDX	DIRBUF+4
	LDY	DIRBUF+5
	JSR	POSCMP

	CMP	#0
	BEQ	DLG01D
	INX
	BNE	DLG01D
	INY

DLG01D	STX	DECIN
	STY	DECIN+1

	JSR	UNI
	UCALL	CONVDC0
	UCALL	NUMMOD
	UCE	DECOUT+5,DIBUF+14,3

DLG1EQ	BEQ	DLG1
**

DLG01	JSR	UNI
	UCS	DIRNAM,SRCHNAM,11
	UCALLE	SRCHCOK
	BNE	DLG02
*
	BIT	DTYPE+1
	BPL	DLG01BA
*

	JSR	DLCLRS
	DTA	36

	JSR	UNI
	UCS	DIRBUF+6,DIBUF,8
	UCS	DIRBUF+14,DIBUF+9,3
	UCS	DLG3DT,DIBUF+13,5
	UCS	KPOML,DIBUF+22,1
	UCS	KPOML,DIBUF+25,1
	UCSE	KDVOJT,DIBUF+31,1

	LDA	DIRBUF
	AND	#32
	BNE	DLG03B

	JSR	UNI
	UC	DIRBUF+3,DECIN,3
	UCALL	CONVDC
	UCE	DECOUT+1,DIBUF+12,7

DLG03B	LDY	#251
*X JE 0

DLG03C	TXA
	PHA

	LDA	DIRBUF-234,Y
	JSR	CONVDC00
	JSR	NUMMOD

	PLA
	TAX
	LDA	DECOUT+6
	STA	DIBUF+20,X
	LDA	DECOUT+7
	STA	DIBUF+21,X

	INX
	INX
	INX

	INY
	BNE	DLG03C
**

DLG1	DEC	DLBL+1

DLBX	LDX	#0
	LDA	DIBUF,X

	INC	DLBX+1

	STA	CIODTA+1
	RTS

**

DLG0	LDA	#0
DLBL	EQU	DLG0

	ASL
	BNE	DLG1
	BCS	PTA4
	JMP	DLG01
***

GET	LDX	FCBAUX1
	CPX	#$16
	BEQ	DLG0
**
KPUT	EQU	<(PUT-1)
**
	LDA	#255-KPUT ;BUDE 0

* PUT-1:L EOR FF
* (ZARUCENY OBSAH A)

PUT	EOR	#255-KPUT ;BUDE FF
	STA	RWFLG

	AND	#12
	EOR	#7
	CMP	ICCOMZ
	BNE	GP1

RTAD	LDA	#0
	CMP	#$E4
	BCC	GP1

	LDY	ICBLHZ
	BNE	PTA3
*Y=0
	INY
	CPY	ICBLLZ
	BCS	GP1

PTA3	JSR	UNI
	UC	ICBALZ,LDSAAD,2
	UC	ICBLLZ,LDSALEN,2
	UCALLE	LDSA
	PHP

	JSR	UNI
	UC	LDSAAD,ICBALZ,2
	UCE	LDSALEN,ICBLLZ,2

	PLP
	BMI	PTA4

	JSR	DDEC
	DTA	A(ICBALZ)

* X=1
	LDA	(ICBALZ-1,X)

	INC	ICBLLZ

PTA1	STA	CIODTA+1

BLEX	JSR	TESTEOF
	BEQ	STAT3	;PAK A=0

	RTS
*

GP1	JSR	UNI
	UC	PTA1+1,LDSAAD,2
	UCSE	K01,LDSALEN,2
**

BLOCK	JSR	LDSA
	BPL	BLEX

*EOF

PTA4	LDA	#139	;136 EOR 3

STAT3	EOR	#3

	STA	XOK+1
	RTS
***

NXTDAT	JSR	ZAJMAP

	INY
	INY
	STY	FCBMAPX
	CPY	MYSLEN
	BNE	SETP6

*

NEXTMAP	JSR	ZAJMAP

	LDY	#0
	LDA	(B),Y
	TAX
	INY
	ORA	(B),Y
	BNE	NEXTM2
*
	JSR	PRIDEL

	LDY	#2
NEXTM3	LDA	FCBMAP-2,Y
	STA	(B),Y
	INY
	CPY	#4
	BCC	NEXTM3

	JSR	ZAJMAP

	LDY	#1

NEXTM4	LDA	VTOCSEC,Y
	STA	(B),Y
	DEY
	BPL	NEXTM4

	JSR	UNI
	UCALL	ZMENEN
	UCSE	VTOCSEC,FCBMAP,2

	BEQ	NEXTM1
*

NEXTM2	LDA	(B),Y
	STA	FCBMAP+1
	STX	FCBMAP

NEXTM1	JSR	ZAJMAP

	LDY	#4
	STY	FCBMAPX
**

SETP6	LDA	(B),Y
	STA	FCBSECT

	INY
	LDA	(B),Y
	STA	FCBSECT+1

	ORA	FCBSECT

	RTS

***

UPRAVLEN	SEC
	LDX	#253

UPRLN2	LDA	FCBLEN-253,X
	SBC	FCBPOS-253,X

	INX
	BNE	UPRLN2

	BCS	UPRLN3

	JSR	UNI
	UCSE	FCBPOS,FCBLEN,3

	CLC
UPRLN3	RTS

***

LDSA	LDA	FCBAUX1
	LDY	#$83

	BIT	RWFLG
	BPL	LDSA0A
	LSR
	LDY	#$87

LDSA0A	AND	#4
	BNE	LDSA0

	JMP	ERROR
***

CLOSE	LDA	FCBAUX1
	CMP	#$16
	BNE	CLOS1

	LSR	DLOPF

CLOS1	AND	#8
	BEQ	UPRLN3

	JSR	UNI
	UCS	FCBLEN,RENNAM,3
	UCS	FCBDIR1,FCB1ST,2
	UCALL	OPNDRD
	UCALL	LDDIR
	UCSE	RENNAM,DIRBUF+3,3
**

SADIR	LDA	#$FF
	DTA	BIT3
LDDIR	LDA	#0

	PHA

	JSR	UNI
	UCS	FCBDIRP,POSTMP,2
	UCS	NULY,POSTMP+2,1
	UCALLE	SETPOS

	PLA

	DTA	BIT3
LDDIRN	LDA	#0
	DTA	BIT3
SADIRN	LDA	#$FF
	STA	RWFLG

	JSR	UNI
	UC	DIRBAD+1,LDSAAD,2
	UCSE	K23,LDSALEN,2
**

LDSA0	LDA	MYSLEN
	SEC
	SBC	FCBIX
	BNE	LDSA2

	LDA	#$FF

LDSA2	STA	LDSA7C+1

	LDA	LDSALEN+1
	BNE	LDSA4
	LDA	LDSALEN
	BEQ	LDSARET

	JSR	LDSAUPL

LDSA4	JSR	TESTEOF
	BEQ	LDSAEOF

	JSR	LDSAUPL

LDSA5	LDA	FCBSECT
	ORA	FCBSECT+1
	BNE	LDSA6

	LDA	FCBAUX1
	AND	#8
	BNE	LDSA5B

STPER	JSR	ERRORS
	DTA	$A6
**

LDSAEOF	DEX
LDSARET	RTS

**

LDSA5B	JSR	UNI
	UCALL	PRIDEL
	UCS	VTOCSEC,FCBSECT,2
	UCALLE	ZAJMAP

	LDA	VTOCSEC
	STA	(B),Y
	INY
	LDA	VTOCSEC+1
	STA	(B),Y

	JSR	ZMENEN

* JSR ZMENEN NELZE SPOJIT DO BLOKU
* POD UNI - UNI ZNICI REG. Y NA VSTUPU
* DO PODPROGRAMU ZMENEN

LDSA6	LDX	FCBSECT
	LDA	FCBSECT+1

	JSR	UNI
	UCALL	ZAJIST
	UCE	LDSAAD,LDSA8+1,2
*X JE 0
	LDY	FCBIX

	BIT	RWFLG
	BMI	LDSA8

LDSA7	LDA	(B),Y
LDSA7B	STA	$FFFF,X
LDSAAD	EQU	LDSA7B+1

	INY
	INX
	CPX	LDSA7C+1
	BNE	LDSA7
	BEQ	LDSA9
***

POINT	LDY	#253

POI2	LDA	ICAX3,X
	STA	POSTMP-253,Y
	INX
	INY
	BNE	POI2
**

SETPOS	LDA	POSTMP+2
	BMI	STPER

	JSR	UNI
	UCS	K02,FCBMAPX,1
	UCS	FCB1ST,FCBMAP,2
	UCS	POSTMP,FCBPOS,3
	UCALLE	UPRAVLEN

	BCS	SETP3B

	LDA	FCBAUX1
	AND	#8
	BEQ	STPER

SETP3B	LDA	POSTMP
	LDX	POSTMP+1
	LDY	POSTMP+2

	JSR	POSCMP
	STA	FCBIX

	STX	POSTMP
	STY	POSTMP+1
*VPRED

SETP4	JSR	NXTDAT

	LDA	POSTMP
	ORA	POSTMP+1
	BEQ	POSCXX

	JSR	DDEC
	DTA	A(POSTMP)

	BCS	SETP4
***

LDSA8	LDA	$FFFF,X
	STA	(B),Y

	INY
	INX
LDSA7C	CPX	#0
	BNE	LDSA8

	JSR	ZMENEN
*

LDSA9	LDX	LDSA7C+1
	JSR	DSBC
	DTA	A(LDSALEN)

	JSR	DADC7C
	DTA	A(LDSAAD)

	JSR	DADC7C
	DTA	A(FCBPOS)
	BCC	LDSA11
	INC	FCBPOS+2

LDSA11	JSR	UPRAVLEN

	LDA	FCBIX
	CLC
	ADC	LDSA7C+1
	CMP	MYSLEN
	BNE	LDSA12

	JSR	NXTDAT
	LDA	#0

LDSA12	STA	FCBIX
	JMP	LDSA0
***

TESTEOF	LDX	#253
	SEC
LDSA3	LDA	FCBLEN-253,X
	SBC	FCBPOS-253,X
	STA	POSTMP-253,X
	INX
	BNE	LDSA3

	BIT	RWFLG
	BMI	LDSA3A

	ORA	POSTMP+1
	BEQ	LDSA3B

LDSA3A	LDA	#$FF
POSCXX	RTS

***

DLCLRS	JSR	GETSTK

DLCLR	STA	DLBL+1
	ASL
	LSR
	TAX

	LDA	#$9B
	TAY

DLCL1	STA	DIBUF-1,X
	LDA	#32

	DEX
	BNE	DLCL1

	STX	DLBX+1

***RTS VYNECHAN - NEVADI

LDSA3B	LDA	POSTMP
	RTS
***

LDSAUPL	CMP	LDSA7C+1
	BCS	POSCXX
	STA	LDSA7C+1

***RTS VYNECHAN - NEVADI

POSCMP	BIT	MYSLEN
	BPL	POSCMPX

	ASL
	PHA
	TXA
	ROL
	TAX
	TYA
	ROL
	TAY
	PLA
	LSR

POSCMPX	RTS

***

OPNDRD	LDA	#$14
	DTA	BIT3
OPNINT12	LDA	#12

OPNINT	STA	FCBAUX1
	PHA

	JSR	UNI
	UCS	NULY,POSTMP,3
	UCALLE	SETPOS

	PLA
	CMP	#16
	BCC	OPNINT2

	JSR	UNI
	UCS	K23,FCBLEN,1
	UCALL	LDDIRN
	UCSE	DIRBUF+3,FCBLEN,3

OPNINT2	RTS

***

OPENT	JSR	UNI
	UCS	SRCHNAM,DIRBUF+6,11
	UCS	NULY,DIRBUF+3,3
	UCE	DATER,DIRBUF+17,6

	BIT	TDOVER
	BPL	OPNT2

	JSR	UNI
	UCE	ODATER,DIRBUF+17,6

OPNT2	LDA	MYA1
	STA	FCBAUX1
	AND	#$20
	ORA	#8
	STA	DIRBUF

	RTS
**

OPNDL	BIT	DLOPF
	BMI	DLTOO

	LDA	#$16
	STA	MYA1

	JSR	UNI
	UCALL	OPN3
	UCSE	SRCHNAM,DIRNAM,11

	STX	DLBL+1

	LDA	ICAX2Z
	STA	DTYPE+1
	BPL	OPNDL2

	JSR	DLCLRS
	DTA	42
	STY	DIBUF+40

	JSR	UNI
	UCS	DLVOL,DIBUF,8
	UCS	DCBVOL,DIBUF+12,8
	UCS	DLDIR,DIBUF+20,11
	UCSE	DIRBUF+6,DIBUF+32,8

OPNDL2	SEC
	ROR	DLOPF

	RTS
**

CREDIR	LDA	#$28
	LDX	#9	;ORA
	BNE	OPEN00
**

STAT	LDA	ICAX1Z
	AND	#$F0
	ORA	#4
	DTA	BIT3

OPEN	LDA	ICAX1Z
	AND	#$3F

	DTA	BIT3
OPEN014	LDA	#$14

OPEN0	LDX	#$29	;AND
OPEN00	STX	OPNXT

	CMP	#6
	BEQ	OPNDL

	CMP	#32
	BCC	OPNER0
	AND	#255-16

OPNER0	STA	MYA1
	AND	#$0F

	LSR
	BCC	OPNER0B

	CMP	#4
	BNE	OPNER1

OPNER0B	LSR
	BCC	OPN3

*
OPNER1	LDY	#$A8
	DTA	BIT3
NEWI3	LDY	#$A3
	DTA	BIT3
OPNFUL	LDY	#$A9
	DTA	BIT3
DLTOO	LDY	#$A1
	JMP	ERROR
***

NEWILD	LDX	#10

NEWI2	LDA	SRCHNAM,X
	CMP	#'?'
	BEQ	NEWI3

	DEX
	BPL	NEWI2
*GET TD

	JMP	(GETTDA)
***

PTH	LDA	#0
	BPL	OPNNDF

	JSR	UNI
	UCS	SRCHNAM,RENNAM,11
	UCS	DOSNAM,SRCHNAM,11
	UCS	DCBMAIN,FCB1ST,2
	UCALL	OPNDRD
	UCALLE	SRCHIT0

	BNE	OPNNDF

	JSR	UNI
	UCS	RENNAM,SRCHNAM,11
	UCS	DIRBUF+1,FCB1ST,2
	UCALL	OPNDRD
	UCALLE	SRCHIT

	BNE	OPNNDF
	JMP	OPNJ2

***

OPN3	JSR	SRCHDIR

	PHP
	PLA
	ROR
	ROR
	ROR	PTH+1

	LDA	MYA1
	AND	#$10
	BNE	OPNJ2BJ
*
	JSR	OPNDRD

	JSR	SRCHCOK
	BEQ	OPNJE

OPNPT0	LDA	MYA1
	CMP	#4
	BEQ	PTH
	AND	#8
	BNE	OPN4B

OPNNDF	JSR	ERRORS
	DTA	170

*ZALOZ

OPN4BE	JSR	NEWILD
	JSR	ERASDT

OPN4B	BIT	FCBDIRP+1
	BMI	OPNFUL

	JSR	UNI
	UCALL	NEWILD
	UCALL	OPENT
	UCALL	PRIDEL
	UCS	VTOCSEC,DIRBUF+1,2

	UCS	K1C,FCBAUX1,1
	UCALL	SADIR
	UCS	FCBDIRP,SRCHTMP,2

	UCS	NULY,FCBDIRP,2
	UCALL	LDDIR
	UCS	FCBLEN,DIRBUF+3,3
	UCALL	SADIR

	UCS	SRCHTMP,FCBDIRP,2
	UCALLE	LDDIR
*DIR?
	LDA	MYA1
	AND	#$20
	BEQ	OPN4D

*** MEZI, NIKDY NESPLNENO
OPNPT0J	BEQ	OPNPT0
*************************

	JSR	UNI
	UCS	FCB1ST,SRCHTMP,2
	UCS	DIRBUF+1,FCB1ST,2

	UCALL	OPNINT12
	UCALL	OPENT
	UCS	K23,DIRBUF+3,1
	UCS	SRCHTMP,DIRBUF+1,2

	UCALL	SADIRN

	UCS	DIRBUF+1,FCB1ST,2
	UCALL	OPNDRD
	UCALLE	LDDIR

OPN4D	JMP	OPNJ2

OPN4BEJ	BPL	OPN4BE

OPNJ2BJ	BNE	OPNJ2B

**

OPNJE	LDA	MYA1
	EOR	DIRBUF
OPNXT	AND	#$20
	BEQ	OPNJ1

	LDA	MYA1
	AND	#8
	BEQ	OPNPT0J

	LDY	#$97
	DTA	BIT3
OPNJP	LDY	#$A4
	JMP	ERROR
*

OPNJ1	LDA	MYA1
	AND	#8
	BEQ	OPNJ2

	LDA	DIRBUF
	AND	#$21
	LSR
	BNE	OPNJ2
	BCS	OPNJP
*
	LDA	MYA1
	CMP	#8
	BNE	OPNJ2

OPNJ1A	LDA	#0
AFLG	EQU	OPNJ1A+1
	BPL	OPN4BEJ

	INC	MYA1
*

OPNJ2	JSR	UNI
	UCS	FCB1ST,FCBDIR1,2
	UCSE	DIRBUF+1,FCB1ST,5

OPNJ2B	LDA	MYA1
	JSR	OPNINT

	LDA	MYA1
	CMP	#16
	BCC	OPNJ3
*DIR
	JSR	UNI
	UCS	FCB1ST,FCBDIR1,2
	UCSE	NULY,FCBDIRP,2

	LDA	MYA1
	AND	#7
	BNE	OPNJ3

	JSR	UNI
	UCSE	FCBPOS,FCBLEN,3
*

OPNJ3	LDA	MYA1
	LSR
	BCC	OPNJ4

	JSR	UNI
	UCSE	FCBLEN,POSTMP,3
	JMP	SETPOS

***

NOTE	LDY	#0
	DTA	BIT3
GETLEN	LDY	#5

NOT2	LDA	FCBPOS,Y
	STA	ICAX3,X
	INY
	INX

	TXA
	AND	#$0F
	CMP	#3
	BCC	NOT2

OPNJ4	RTS

*****

ZAJADR	STX	ZMENEN+1
	LDA	#$FF
	STA	BUFTIME,X

	TXA
	CLC
	ADC	#>STBUFF
	STA	B+1

	LDA	#<STBUFF
	STA	B

	RTS

*****

