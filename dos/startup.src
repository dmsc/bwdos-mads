*Startup code, this initializes the DOS and boots.
STARTUP	LDX	#<HATABS

T2	LDA	HATABS&$FF00,X
	CMP	#'E'
	BEQ	T3

	INX
	INX
	INX
	CPX	#<(HATABS+33)
	BCC	T2

	JMP	BLKBDV
*

T3	INX
	STX	DIORA1+1
	STX	DIORA2+3
	STX	DIORA3+3
	INX
	STX	DIOREP+1

*RESET
	JSR	UNI
	UCE	KRESET,DOSINI,2

*CLR MEM (X=0)

	TXA
	LDY	#<ENTRY
	LDX	#>ENTRY

T5A	STX	T5+2
T5	STA	$FF00,Y
	INY
	BNE	T5

	INX
	CPX	MEMTOP+1
	BCC	T5A

*TITLE (Y=0)

T6	JSR	CPC192
	DTA	PUTCHR
	DTA	A(TNADP,LBUFEND+1-TNADP)

*Reset INIT
	JSR	RESINI

	LDA	#$FF
	STA	$47

	LDY	#1
	JSR	DIVIO
	BPL	RUNCP
*No STARTUP.BAT, just run cartridge
	CLC
	RTS

*End BOOT, jump to the CP and start
*executing from BAT file
RUNCP
	JSR	NULDW
	STX	COLDST

	LDA	BOOTQ
	ORA	#1
	STA	BOOTQ

	JMP	CP

**

KRESET	DTA	A(RESET)

***

* ENTRY: Copy to run address and start
ENTRY	CLD

	LDX	#0
	LDY	#$20

ENTRY1	LDA	START+LOADAD,X
ENTRY2	STA	START,X
	INX
	BNE	ENTRY1

	INC	ENTRY1+LOADAD+2
	INC	ENTRY2+LOADAD+2

	DEY
	BNE	ENTRY1

	JMP	STARTUP

	RUN	ENTRY+LOADAD
