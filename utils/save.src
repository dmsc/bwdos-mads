	ORG	$500
	JMP	START

*************************************
*				*
*     " S A V E "    PRO BW-DOS	*
*				*
*************************************

HEX	EQU	$6D0

AUX	EQU	$6D2

PUTIX	EQU	$6D3
PUTLEN	EQU	$6D4
PUTIOCB	EQU	$6D5

NAME	EQU	$6D6

** VOLNO: $6F4

***

SYNTAX	DTA 155
	DTA C"Syntax Error!"
OPNER	DTA 155
	DTA C"Can't open file!"
DATERR	DTA 155
	DTA C"I/O Error !"
	DTA 155

HEADER	DTA A($FFFF)
FROM	DTA A(0)
TO	DTA A(0)

DOSVER	DTA 155
	DTA C"Incorrect DOS !"
TXTEND	DTA 155

***

PUTS	STX	PUTIOCB
	STA	PUTLEN
	STY	PUTIX

PUTS2	LDX	PUTIOCB
	LDA	#11
	STA	$342,X
	LDA	#0
	STA	$348,X
	STA	$349,X

	LDY	PUTIX
	LDA	SYNTAX,Y

	JSR	$E456
	BMI	PRTEX

	INC	PUTIX
	DEC	PUTLEN
	BNE	PUTS2

PRTEX	RTS

***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	HEX
	LDX	HEX+1
	RTS
**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEX
	STA	HEX+1

	LDY	#36

GETHX2	CPY	#41
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEX
	ROL	HEX+1
	DEX
	BNE	GETHX4

	PLA
	ORA	HEX
	STA	HEX

	JMP	GETHX2
**

GHXERR	LDY	#0
	LDA	#(OPNER-SYNTAX+1)

ERROR2	LDX	#0
	JSR	PUTS

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERROR3	JSR	CLOSE
	JMP	(10)
***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDY	#(DOSVER-SYNTAX)
	LDA	#(TXTEND-DOSVER+1)
	LDX	#0
	JSR	PUTS
	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#33
STRT3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	STRT3
***
	JSR	GETHEX
	STY	FROM
	STX	FROM+1

	JSR	GETHEX
	STY	TO
	STX	TO+1
***
	JSR	GETNAME

	LDA	#8
	STA	AUX

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	STRT4

	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	STRT4

	INC	AUX
***

STRT4	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#<NAME
	STA	$344,X
	LDA	#>NAME
	STA	$345,X

	LDA	AUX
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BPL	STRT5

	LDY	#(OPNER-SYNTAX)
	LDA	#(DATERR-OPNER+1)
	JMP	ERROR2
***

SAVERR	LDY	#(DATERR-SYNTAX)
	LDA	#(HEADER-DATERR)
	JMP	ERROR2
***

STRT5	LSR	AUX
	BCS	STRT6

	LDX	#$10
	LDY	#(HEADER-SYNTAX)
	LDA	#2
	JSR	PUTS
	BMI	SAVERR

STRT6	LDX	#$10
	LDY	#(FROM-SYNTAX)
	LDA	#4
	JSR	PUTS
	BMI	SAVERR

	LDX	#$10
	LDA	#11
	STA	$342,X

	LDA	FROM
	STA	$344,X
	LDA	FROM+1
	STA	$345,X

	LDA	TO
	SEC
	SBC	FROM
	STA	$348,X
	LDA	TO+1
	SBC	FROM+1
	STA	$349,X

	INC	$348,X
	BNE	STRT7
	INC	$349,X

STRT7	JSR	$E456
	BMI	SAVERR

	JSR	CLOSE
	BMI	SAVERR

	JMP	(10)
***
