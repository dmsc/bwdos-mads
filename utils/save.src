*************************************
*				*
*     " S A V E "    FOR BW-DOS	*
*				*
*************************************

*Use cassette buffer for file name
NAME	EQU	$400

*Use ZP IOCB for temporary variables
HEXL	EQU	$20
HEXH	EQU	$21

*Store in low memory, to allow saving all DOS areas and page 6
	ORG	$480

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDY	#<DOSVER
	LDA	#(TXTEND-DOSVER+1)
*Exit to DOS by RTS

PUTS0	LDX	#0
	DTA	$2C	;Skip
**
PUTS10	LDX	#$10
	STA	$348,X
	LDA	#0
	STA	$349,X
	LDA	#>SYNTAX
PUTAY	STA	$345,X
	TYA
	STA	$344,X

	LDA	#11
CIOCMD	STA	$342,X
	JMP	$E456

**
CLOSE	LDA	#12
CIO10	LDX	#$10
	BNE	CIOCMD

***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#33
STRT3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	STRT3
***
	JSR	GETHEX
	STY	FROM
	STX	FROM+1

	JSR	GETHEX
	STY	TO
	STX	TO+1
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	STRT4

	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	STRT4

	INC	AUX
***

STRT4	JSR	CLOSE

	LDA	#<NAME
	STA	$354
	LDA	#>NAME
	STA	$355

AUX	EQU	*+1
	LDA	#8
	STA	$35A
	LDA	#0
	STA	$35B

	LDA	#3
	JSR	CIO10
	BPL	STRT5

	LDY	#<OPNER
	LDA	#(DATERR-OPNER+1)
	BNE	ERROR2
***

STRT5	LSR	AUX
	BCS	STRT6

	LDY	#<HEADER
	LDA	#2
	JSR	PUTS10
	BMI	SAVERR

STRT6	LDY	#<FROM
	LDA	#4
	JSR	PUTS10
	BMI	SAVERR

	LDA	TO
	SEC
	SBC	FROM
	TAY
	LDA	TO+1
	SBC	FROM+1
	TAX

	INY
	BNE	STRT7
	INX

STRT7	STY	$358
	STX	$359

	LDA	FROM+1
	LDY	FROM
	LDX	#$10

	JSR	PUTAY
	BMI	SAVERR

	JSR	CLOSE
	BPL	EXIT

***

SAVERR	LDY	#<DATERR
	LDA	#(DOSVER-DATERR+1)
ERROR2	JSR	PUTS0

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERROR3	JSR	CLOSE
EXIT	JMP	(10)

***
GHXRTS	CPY	#37
	BEQ	GHXERR
GHXR	LDY	HEXL
	LDX	HEXH
	RTS
**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEXL
	STA	HEXH

	LDY	#36

GETHX2	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	CPY	#41
	BCC	GETHX2

***

GHXERR	LDY	#<SYNTAX
	LDA	#(OPNER-SYNTAX+1)
	BNE	ERROR2

***
GETNAME	JMP	GETNAME

***
*Note: all those must be on the same memory page:
SYNTAX	DTA	155
	DTA	C"Syntax Error!"
OPNER	DTA	155
	DTA	C"Can't open file!"
DATERR	DTA	155
	DTA	C"I/O Error!"
DOSVER	DTA	155
	DTA	C"Incorrect DOS!"
TXTEND	DTA	155

HEADER	DTA	A($FFFF)

FROM	EQU	*
TO	EQU	*+2

***

