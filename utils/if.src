*************************************
*				*
*	" I F "  FOR BW-DOS	*
*				*
*************************************

DPTR	EQU	$80
BUFFER	EQU	$82

***
	ORG	$3000
	ICL	"chkdos.inc"
***
	LDY	#8
	LDA	(10),Y
	STA	XDIVIO+1
	INY
	LDA	(10),Y
	STA	XDIVIO+2

	LDA	10
	STA	DPTR
	LDY	11
	DEY
	STY	DPTR+1

***
	JSR	EOL

	JSR	GETLINE

	JSR	PRINT
	DTA	C"<Y>...Yes  <N>...No  "
	DTA	0
***

START3	JSR	GETKEY

	CMP	#27
	BNE	YESNO

* ESC
	JSR	PRINT
	DTA	155
	DTA	C"(Batch file aborted)"
	DTA	155,0
	BEQ	ERREND

YESNO	AND	#$5F
	CMP	#'Y'
	BNE	NONO

*
YES0	JSR	PRINT
	DTA	C"Yes!"
	DTA	155,0
	BEQ	NO3

***
NONO	CMP	#'N'
	BNE	START3

	JSR	PRINT
	DTA	C"No!"
	DTA	155,155,0

NO2	JSR	GETLINE

	LDY	#0
	JSR	TEST

	LDY	#VZ2-VZOR
	JSR	TEST
	BNE	NO2

***

VZOR	DTA	C"ENDIF"
	DTA	155
VZ2	DTA	C"ELSE"
	DTA	155

***

TEST	LDX	#0

TEST2	LDA	BUFFER,X
	CMP	VZOR,Y
	BNE	TESTX

	INY
	INX

	CMP	#$9B
	BNE	TEST2
	BEQ	NO3

***
*Call K: get handler
GETKEY	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDA	#0
	STA	$2A	;Clears ICAX1Z
	RTS
****

GETLNX	JSR	PRINT
	DTA	155
	DTA	C"Error: End of batch file!"
	DTA	155,0

ERREND	LDY	#1
XDIVIO	JSR	XDIVIO
NO3	JMP	(10)

**

GETLINE	LDY	#256-7	;BATFLG
	LDA	(DPTR),Y
	BMI	GETLNX

	LDY	#5
	LDA	#64
	STA	$348

	LDA	#<BUFFER
	STA	$344
	LDX	#>BUFFER
	STX	$345
*>BUFFER = 0
;	LDX	#0
	JSR	CIOXY0
	BMI	GETLNX

TESTX	RTS

***
	ICL	"print.inc"
***

EOL	LDA	#$9B

	ICL	"print1.inc"
***

