*************************************
*				*
*	" I F "   PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

BUFFER	EQU	$3800

***
	ORG	$3000
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#8
	STA	XDIVIO+1
	LDA	11
	ADC	#0
	STA	XDIVIO+2

	LDA	10
	SEC
	SBC	#7
	STA	BATFLG+1
	LDA	11
	SBC	#0
	STA	BATFLG+2
***
	JSR	EOL

	JSR	GETLINE

	JSR	PRINT
	DTA C"<Y>...Yes  <N>...No  "
	DTA 0
***

START3	JSR	CLOSE

	LDA	#<KNAM
	STA	$344+$10
	LDA	#>KNAM
	STA	$345+$10

	LDA	#4
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDA	#3
	JSR	CIO10
	BMI	START3

	LDA	#0
	STA	$348+$10
	STA	$349+$10

	LDA	#7
	JSR	CIO10
	BMI	START3

	PHA
	JSR	CLOSE
	PLA

	CMP	#27
	BEQ	ESCAPE

	AND	#$5F
	CMP	#'Y'
	BNE	NONO
*

YES0	JSR	PRINT
	DTA C"Yes!"
	DTA 155,0

	JMP	(10)
***

NONO	CMP	#'N'
	BEQ	NO
	BNE	START3

* ESC

ESCAPE	LDY	#1
	JSR	XDIVIO

	JSR	PRINT
	DTA 155
	DTA C"(Batch file aborted)"
	DTA 155,0

NO3	JMP	(10)

***

NO	JSR	PRINT
	DTA C"No!"
	DTA 155,155,0

NO2	JSR	GETLINE

	LDX	#0
	JSR	TEST

	LDX	#VZ2-VZOR
	JSR	TEST
	BNE	NO2

***

VZOR	DTA C"ENDIF"
	DTA 155
VZ2	DTA C"ELSE"
	DTA 155

***

TEST	LDY	#0

TEST2	LDA	BUFFER,Y
	CMP	VZOR,X
	BNE	TESTX

	INY
	INX

	CMP	#$9B
	BNE	TEST2
	BEQ	NO3

***

CLOSE	LDA	#12
CIO10	LDX	#$10
	STA	$342,X
	JMP	$E456
***

KNAM	DTA C"K:"
	DTA 155
****

GETLNX	LDY	#1
	JSR	XDIVIO

	JSR	PRINT
	DTA 155
	DTA C"Error: End of batch "
	DTA C"file!"
	DTA 155,0

	JMP	(10)
**

GETLINE
BATFLG	LDA	$FFFF
	BMI	GETLNX

	LDA	#5
	STA	$342

	LDA	#<BUFFER
	STA	$344
	LDA	#>BUFFER
	STA	$345

	LDA	#64
	STA	$348
	LDX	#0
	STX	$349

	JSR	$E456
	BMI	GETLNX

TESTX	RTS

***
	ICL	"print.inc"
***

EOL	LDA	#$9B

	ICL	"print1.inc"
***

XDIVIO	JMP	($FFFF)

***
