*************************************
*				*
*	" I F "  FOR BW-DOS	*
*				*
*************************************

DPTR	EQU	$80
***
	ORG	$3080

START	LDA	#<PBADVER
	LDX	#PABORT-PBADVER+1

	LDY	$700
	CPY	#'S'
	BNE	ERREND

***
	LDY	#8
	LDA	(10),Y
	STA	XDIVIO+1
	INY
	LDA	(10),Y
	STA	XDIVIO+2

	LDA	10
	STA	DPTR
	LDY	11
	DEY
	STY	DPTR+1

*Print next line of batch file
	JSR	EOL
	JSR	GETLINE

*Ask Y/N question
	LDA	#<PYESNO
	LDX	#PBADVER-PYESNO
	JSR	PRINT
***
START3	JSR	GETKEY

	CMP	#27
	BNE	YESNO
*ESC
	LDA	#<PABORT
	LDX	#PYES-PABORT
	BNE	ERREND

YESNO	LDX	#5
	AND	#$5F
	CMP	#'Y'
	BNE	NONO

*
YES0	LDA	#<PYES
	JSR	PRINT
	BPL	EXIT	;Jump always

***
NONO	CMP	#'N'
	BNE	START3

	LDA	#<PNO
	JSR	PRINT

*Read lines from batch until ELSE/ENDIF
NXTLINE	JSR	GETLINE

	LDY	#0
	JSR	TEST

	LDY	#TELSE-TENDIF
	JSR	TEST

	BNE	NXTLINE

***
TEST	LDX	#0

TEST2	LDA	BUFFER,X
	CMP	TENDIF,Y
	BNE	TESTX

	INY
	INX

	CMP	#$9B
	BNE	TEST2
	BEQ	EXIT

***
*Read a line into buffer
GETLINE	LDY	#256-7	;BATFLG
	LDA	(DPTR),Y
	BMI	GETLNX

	LDY	#5
	LDA	#<BUFFER
	LDX	#64
	JSR	CIOXY0
	BPL	TESTX

*Get line error, end of batch file
GETLNX	LDA	#<PEBAT
	LDX	#PEND-PEBAT+1

*Print error and exit
ERREND	JSR	PRINT
	LDY	#1
XDIVIO	JSR	TESTX
EXIT	JMP	(10)

***
EOL	LDA	#$9B
PRT1	LDX	#0

PRINT	LDY	#11
CIOXY0	STX	$348
	STA	$344
	STY	$342
	LDX	#>PYESNO
	STX	$345
	LDX	#0
	STX	$349
	JMP	$E456

*Call K: get handler
GETKEY	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDA	#0
	STA	$2A	;Clears ICAX1Z
TESTX	RTS
***
*Commands
TENDIF	DTA	C"ENDIF"
	DTA	155
TELSE	DTA	C"ELSE"
	DTA	155
***
*Messages
PYESNO	DTA	C"<Y>...Yes  <N>...No  "
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
PABORT	DTA	155
	DTA	C"(Batch file aborted)"
	DTA	155
PYES	DTA	C"Yes!"
	DTA	155
PNO	DTA	C"No!"
	DTA	155
PEBAT	DTA	155
	DTA	C"Error: End of batch file!"
PEND	DTA	155
BUFFER

