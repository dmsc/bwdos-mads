	ORG	$4000,$A800
	JMP	START

*************************************
*				*
*    " F O R M A T "   PRO BW-DOS	*
*				*
*************************************

PTHLEN	EPZ	128
DOSIX	EPZ	129
DOSLEN	EPZ	130

DENS	EPZ	132
HSFLG	EPZ	133

* VOLNO: 134

BUFF	EQU	$400
NAMBUFF	EQU	$500

******* BOOTER *******

BOOTER	DFB 0,3
BO	EQU	BOOTER-$3000
	DFW $3000,$7E0
	JMP	$3080

BOOMAIN	DFW 0,0,0
	DFB 0
	DFW 4,$20,4
BOONAME	ASC "@@@@@@@@"
	DFB 0
BOOSLEN	DFB 0

	DFB $20,6,1,255,255,0,0
BOORND	DFB 0
BOOBOO	DFB 0,0,0,0,0
DTAIX	DFB 0
MAPIX	DFB 0,0

***

GETLEN	EPZ	$43

*** VOLNE - VYCPAVKA
	DFB 0
***

GADR	JSR	GET1-BO
	STA	GETADR-BO
	JSR	GET1-BO
	STA	GETADR+1-BO

	AND	GETADR-BO
	CMP	#$FF
	RTS
***

READ0F	LDX	#$2F
READ0	LDA	#0
READ	STX	SIOADR+1-BO
	STA	SIOADR-BO

	LDX	#9
READ1	LDA	SIOTAB-BO,X
	STA	$300,X
	DEX
	BPL	READ1

	LDA	$30A
	ORA	$30B
	BEQ	READERR

	JSR	$E459
	BPL	READRTS

READERR	LDA	$D301
	ORA	#1
	STA	$D301

SSAVE	LDX	#$FF
	TXS

	SEC
READRTS	RTS

***

BOORUN	JMP	($2E0)
BOOINI	JMP	($2E2)

**

SIOTAB	DFB $31,1
	ASC "R"
	DFB 64
SIOADR	DFW $2E00
	DFB 7,0
SIOLEN	EQU *

*2 BYTE PAK PREPISE START!

BOOSTRT	TSX
	STX	SSAVE+1-BO

	LDA	BOOSLEN-BO
	AND	#128
	STA	SIOLEN-BO
	STA	MAPIX-BO
	STA	DTAIX-BO
	ASL
	ROL
	EOR	#1
	STA	SIOLEN+1-BO

*XF DENSITY TEST

	INC	$30A
	JSR	READ0F-BO
*START
	JSR	GADR-BO
	BNE	READERR
	BEQ	BOOST2
**

BOOST1	JSR	BOOINI-BO

BOOST2	JSR	GADR-BO
	BEQ	BOOST2

	LDA	GETADR-BO
	ORA	GETADR+1-BO
	BEQ	BOORUN

	LDA	#READRTS-BO:L
	STA	$2E2
	LDA	#READRTS-BO:H
	STA	$2E3

	JSR	GET1-BO
	SEC
	SBC	GETADR-BO
	PHA
	PHP
	JSR	GET1-BO
	PLP
	SBC	GETADR+1-BO
	STA	GETLEN+1
	PLA
	STA	GETLEN

**LD BLOK

BOOST3	JSR	GET-BO
BOOST4	STA	$FFFF
GETADR EQU BOOST4+1

	INC	GETADR-BO
	BNE	BOOST5
	INC	GETADR+1-BO

BOOST5	LDA	GETLEN
	BNE	BOOST6
	LDA	GETLEN+1
	BEQ	BOOST1
**
	DEC	GETLEN+1
BOOST6	DEC	GETLEN

	JMP	BOOST3-BO

***

GET1	LDA	#0
	STA	GETLEN
	STA	GETLEN+1

GET	LDX	DTAIX-BO
	CPX	SIOLEN-BO
	BEQ	GET2

GET1B	LDA	$2E00,X
	INX
	STX	DTAIX-BO
	RTS
**

GETBLOK	LDA	GETADR-BO
	LDX	GETADR+1-BO
	JSR	READ-BO

	LDA	GETADR-BO
	CLC
	ADC	SIOLEN-BO
	STA	GETADR-BO
	LDA	GETADR+1-BO
	ADC	SIOLEN+1-BO
	STA	GETADR+1-BO

	LDA	GETLEN
	SEC
	SBC	SIOLEN-BO
	STA	GETLEN
	LDA	GETLEN+1
	SBC	SIOLEN+1-BO
	STA	GETLEN+1

*NEXT DATA

GET2	LDY	MAPIX-BO
	CPY	SIOLEN-BO
	BNE	GET2B

*NAXT MAP

	LDA	BOOBOO-BO
	STA	$30A
	LDA	BOOBOO+1-BO
	STA	$30B

	JSR	READ0F-BO

	LDA	$2F00
	STA	BOOBOO-BO
	LDA	$2F01
	STA	BOOBOO+1-BO

	LDY	#4
**

GET2B	LDA	$2F00,Y
	STA	$30A
	LDA	$2F01,Y
	STA	$30B

	INY
	INY
	STY	MAPIX-BO
**
	LDA	GETLEN+1
	BNE	GETBLOK

	LDA	SIOLEN-BO
	BPL	GET2C

	LDA	GETLEN
	BMI	GETBLOK

GET2C	LDX	#$2E
	JSR	READ0-BO

	LDX	#0
	JMP	GET1B-BO

************** KONEC BOOTERU *******
***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

ERROR	TYA
	PHA

	JSR	PRINT
	DFB 155
	ASC "Error "
	DFB 0

	LDX	#2
	PLA

ERR2	LDY	#0

ERR3	CMP	ERRTAB,X
	BCC	ERR4

	SBC	ERRTAB,X
	INY
	BNE	ERR3
**

ERR4	PHA
	TXA
	PHA

	TYA
	ORA	#$30
	JSR	PRT1

	PLA
	TAX
	PLA

	DEX
	BPL	ERR2
**
	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DFB 1,10,100

**

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JSR	$E456
	BMI	ERROR

	RTS
***

KNAME	ASC "K:"
	DFB 155

***

GETKEY	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#KNAME:L
	STA	$344,X
	LDA	#KNAME:H
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456

	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456

	PHA
	JSR	CLOSE
	PLA

	CMP	#27
	BNE	GPRTS

	JMP	ABORT

***

PATH	ASC "D0:>DOS>"

**

GETPATH	LDY	#0

GTPT2	LDA	PATH,Y
	STA	BUFF,Y

	INY
	CPY	PTHLEN
	BCC	GTPT2

GPRTS	RTS

***

GETNME	LDX	DOSIX
	DEX

GTN1	INX
	TXA
	AND	#15
	CMP	#8
	BCS	GTN2

	LDA	NAMBUFF,X
	CMP	#$20
	BEQ	GTN2

	STA	BUFF,Y
	INY
	BNE	GTN1
*

GTN2	LDA	#'.'
	STA	BUFF,Y
	INY

	LDX	#7
*

GTN3	INX
	TXA
	AND	#15
	CMP	#11
	BCS	GTN4

	LDA	NAMBUFF,X
	CMP	#$20
	BEQ	GTN4

	STA	BUFF,Y
	INY
	BNE	GTN3
*

GTN4	LDA	#$9B
	STA	BUFF,Y

	RTS

***

DOSFLTR	DFB 155
	ASC "SOD.*"
***

SRCHDOS	JSR	CLOSE

	JSR	GETPATH
	LDX	#5
SRD2	LDA	DOSFLTR,X
	STA	BUFF,Y
	INY
	DEX
	BPL	SRD2

*OPEN DIR

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#6
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOBUFF
	BMI	DIREND
**

SRD3	LDX	#$10
	LDA	#5
	STA	$342,X

	LDA	#BUFF+64:L
	STA	$344,X
	LDA	#BUFF+64:H
	STA	$345,X

	LDA	#64
	STA	$348,X
	LDA	#0
	STA	$349,X

	JSR	$E456
	BMI	DIREND
*
	LDA	BUFF+64
	CMP	#$30
	BCS	DIREND

	LDA	BUFF+74
	BMI	SRD3
*
	LDX	DOSIX
	CPX	#$90
	BCS	SRD3

	LDY	#66

SRD4	LDA	BUFF,Y
	STA	NAMBUFF,X
	INX
	INY

	CPY	#77
	BCC	SRD4

	LDA	PTHLEN
	STA	NAMBUFF,X

	LDA	DOSIX
	CLC
	ADC	#16
	STA	DOSIX

	JMP	SRD3
**

DIREND	JMP	CLOSE

***

ABORT	JSR	CLOSE

	JSR	PRINT
	DFB 155
	ASC "Operation aborted."
	DFB 155,0

	JMP	ERR5X
***

YESNO	JSR	PRINT
	ASC " (Y/N) ? "
	DFB 0

YN1	JSR	GETKEY
	AND	#$5F

	LDX	#0
	CMP	#'N'
	BEQ	YN2

	LDX	#128
	CMP	#'Y'
	BNE	YN1

YN2	STX	YN3+1

	JSR	PRT1
	LDA	#$9B
	JSR	PRT1

YN3	LDA	#0
	ASL

	RTS
***

CIOBUFF	LDA	#BUFF:L
	STA	$344,X
	LDA	#BUFF:H
	STA	$345,X

	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS version"
	DFB 155,0

	JMP	(10)

* HLEDEJ DOSy

START2	JSR	PRINT
	DFB 155
	ASC "BW-DOS Formatter V"
	ASC " 1.00 By BEWESOFT"
	DFB 155,0
*GETPAR
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
*SIO
	LDA	10
	SEC
	SBC	#10
	STA	SIOJMP+1
	STA	DOSLEN
	LDA	11
	SBC	#0
	STA	SIOJMP+2
	STA	DOSLEN+1

	LDY	#8
	LDA	(DOSLEN),Y
	STA	WRTCMD
*
	LDA	#0
	STA	DOSLEN
	STA	DOSLEN+1
*
	JSR	GETNAME
	LDY	#34
	LDA	(10),Y
	STA	PATH+1
*
	LDA	#0
	STA	DOSIX

	LDA	#8
	STA	PTHLEN
	JSR	SRCHDOS

	LSR	PTHLEN
	JSR	SRCHDOS
**

	LDA	DOSIX
	BNE	STRT3

	JSR	PRINT
	DFB 155
	ASC "No DOS-files found..."
	DFB 155,0
	JMP	STRT4

*NABIDKA DOSU

STRT3	JSR	PRINT
	DFB 155
	ASC "Select DOS:"
	DFB 155,155,0

	LDA	#0
	STA	PTHLEN
	LDA	#$31
	STA	STRT3D

STRT3B	LDX	PTHLEN
	CPX	DOSIX
	BCS	STRT3F

	LDY	#0
STRT3C	LDA	NAMBUFF,X
	STA	STRT3E,Y
	INX
	INY
	CPY	#8
	BCC	STRT3C

	JSR	PRINT
	ASC "  "
STRT3D	ASC "0) "
STRT3E	ASC "FILENAME"
	DFB 155,0

	LDA	PTHLEN
	CLC
	ADC	#16
	STA	PTHLEN

	INC	STRT3D

	JMP	STRT3B
***

STRT3NO	JSR	PRINT
	ASC "N"
	DFB 155,0

	JMP	STRT4

***

STRT3F	JSR	PRINT
	ASC "  N) -No DOS-"
	DFB 155,155
	ASC "Choice: "
	DFB 0

STRT3G	JSR	GETKEY
	AND	#127
	TAX

	CMP	#'N'
	BEQ	STRT3NO
	CMP	#'n'
	BEQ	STRT3NO

	SEC
	SBC	#$31
	CMP	#9
	BCS	STRT3G

	ASL
	ASL
	ASL
	ASL
	CMP	DOSIX
	BCS	STRT3G

** DOS ZVOLEN

	PHA

	TXA
	JSR	PRT1
	LDA	#$9B
	JSR	PRT1

	PLA
	STA	DOSIX

	TAX
	LDA	NAMBUFF+11,X
	STA	PTHLEN
	JSR	GETPATH
	JSR	GETNME

	JSR	CLOSE

* OPEN DOS-FILE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOBUFF
	BMI	RDDOSER
*READ IT
	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#END:L
	STA	$344,X
	LDA	#END:H
	STA	$345,X

	LDA	$2E5
	SEC
	SBC	#END:L
	STA	$348,X
	LDA	$2E6
	SBC	#END:H
	STA	$349,X

	JSR	$E456
	BPL	RDDOSTL

	CPY	#136
	BEQ	STRT3H
**

RDDOSER	JSR	PRINT
	DFB 155
	ASC "Can't read DOS-file!"
	DFB 0
	JMP	ABORT
**

RDDOSTL	JSR	PRINT
	DFB 155
	ASC "DOS-file too long!"
	DFB 0
	JMP	ABORT
**

STRT3H	LDX	#$10
	LDA	$348,X
	STA	DOSLEN
	LDA	$349,X
	STA	DOSLEN+1

	JSR	CLOSE

** DOS NACTEN

STRT4	JSR	PRINT
	DFB 155
	ASC "Drive to format: "
	DFB 0

STRT4B	JSR	GETKEY
	SEC
	SBC	#$30
	BEQ	STRT4B
	CMP	#10
	BCS	STRT4B

	STA	SIODR1
	STA	SIODR2
	STA	SIODR3
	STA	SIODR4
	STA	SIODR5

	ORA	#$30
	STA	PATH+1
	STA	STRT4L

	JSR	PRT1
**
	JSR	PRINT
	DFB 155,155
	ASC "Select density:"
	DFB 155,155
	ASC "  1) Single density"
	DFB 155
	ASC "  2) Medium density"
	DFB 155
	ASC "  3) Double density"
	DFB 155
	ASC "  4) Double-sided "
	ASC "Double density"
	DFB 155,155
	ASC "Choice: "
	DFB 0

STRT4C	JSR	GETKEY
	SEC
	SBC	#$31
	CMP	#4
	BCS	STRT4C

	STA	DENS

	CLC
	ADC	#$31
	JSR	PRT1
**
	JSR	PRINT
	DFB 155,155
	ASC "Volume name: "
	DFB 0

	LDA	#0
	STA	HSFLG

STRT4D	JSR	GETKEY
	CMP	#155
	BEQ	STRT4G
	CMP	#126
	BEQ	STRT4F

	CMP	#27
	BCC	STRT4E
	CMP	#32
	BCC	STRT4D
	CMP	#125
	BCC	STRT4E
	CMP	#128
	BCC	STRT4D
	CMP	#155
	BCC	STRT4E
	CMP	#160
	BCC	STRT4D
	CMP	#253
	BCS	STRT4D

STRT4E	LDX	HSFLG
	CPX	#8
	BCS	STRT4D
	STA	BOONAME,X

	INC	HSFLG
	BCC	STRT4F2
**

STRT4F	LDX	HSFLG
	BEQ	STRT4D

	DEC	HSFLG

STRT4F2	JSR	PRT1
	JMP	STRT4D

**

STRT4G	LDX	HSFLG

STRT4H	CPX	#8
	BCS	STRT4I

	LDA	#32
	STA	BOONAME,X
	INX
	BNE	STRT4H
**

STRT4I	JSR	PRINT
	DFB 155,155
	ASC "XF-551 High Speed"
	DFB 0
	JSR	YESNO

	LDA	#0
	ROR
	STA	HSFLG
**
	JSR	PRINT
	DFB 155
	ASC "Insert disk to FORMAT"
	ASC " into drive "
STRT4L	ASC "0,"
	DFB 155
	ASC "and press <RETURN> !"
	DFB 155
	ASC "  (Press <B> to "
	ASC "build directory)"
	DFB 155,0
**

	JSR	GETKEY
	CMP	#155
	BEQ	STRT4M
	AND	#$5F
	CMP	#'B'
	BEQ	STRT4N

	JMP	ABORT

STRT4M	JMP	FORMAT1

STRT4N	JMP	FORM3

***

SION	LDX	#11
	DFB $2C
SIOO	LDX	#23
	DFB $2C
SIOWR	LDX	#35
	DFB $2C
SIOFORM	LDX	#47
	DFB $2C
SIORD	LDX	#59

	LDY	#11
SIO1	LDA	SIOTABS,X
	STA	$300,Y
	DEX
	DEY
	BPL	SIO1

SIOJMP	JMP	($FFFF)

**

SIOTABS	DFB $31
SIODR1	DFB 0
	ASC "N"
	DFB 64
	DFW BUFF
	DFB 7,0
	DFW 12,1

	DFB $31
SIODR2	DFB 0
	ASC "O"
	DFB 128
	DFW BUFF
	DFB 7,0
	DFW 12,1

	DFB $31
SIODR3	DFB 0
WRTCMD	ASC "W"
	DFB 128
	DFW BUFF
	DFB 7,0
WRTLEN	DFW 128
WRTSECT	DFW 1

	DFB $31
SIODR4	DFB 0
FORMCMD	ASC "!"
	DFB 64
	DFW BUFF
	DFB 255,0
FORMLEN	DFW 128,4

	DFB $31
SIODR5	DFB 0
	ASC "R"
	DFB 64
	DFW BUFF
	DFB 7,0
RDLEN	DFW 128
RDSECT	DFW 1

*** VLASTNI FORMATOVANI

FTSTRK	DFB 18,26,18,18
FTSIDE	DFB 0,0,0,1
FTMODUL	DFB 0,4,4,4
FTBSH	DFB 0,0,1,1
FTBSL	DFB 128,128,0,0

FTMAIN	DFB 5,6,5,5
FTTOTAL	DFW 720,1040,720,1440
FTFREE	DFW 713,1032,713,1433
FTMAPS	DFB 1,2,1,1
FTTRKS	DFB 40,40,40,40+128

MAP11ST	DFB 1,0,1,1
MAP1LEN	DFB 90,128,90,180

***

FORMMEDJ	JMP	FORMMED

**

FORMAT1	LDX	DENS
	DEX
	BEQ	FORMMEDJ

	JSR	SION
	BMI	FORMNCF

	LDX	DENS

	LDA	#40
	STA	BUFF
	LDA	#0
	STA	BUFF+2
	LDA	FTSTRK,X
	STA	BUFF+3
	LDA	FTSIDE,X
	STA	BUFF+4
	LDA	FTMODUL,X
	STA	BUFF+5
	LDA	FTBSH,X
	STA	BUFF+6
	LDA	FTBSL,X
	STA	BUFF+7

	JSR	SIOO
	BMI	BADDNS

	LDX	#11
FORM1B	LDA	BUFF,X
	STA	BUFF+64,X
	DEX
	BPL	FORM1B

	JSR	SION
	BMI	BADDNS

	LDX	#11
FORM1BB	LDA	BUFF,X
	CMP	BUFF+64,X
	BNE	BADDNS
	DEX
	BPL	FORM1BB
	BMI	FORM2

** HUSTOTU NALZE NASTAVIT

BADDNS	LDX	DENS
	DEX
	BEQ	FORMNCF

BADDNS2	JSR	PRINT
	DFB 155
	ASC "Drive can't format "
	ASC "selected density!"
	DFB 0
	JMP	ABORT

** DRIVE NEKONFIGUROVATELNY!

FORMNCF	LDX	DENS
	CPX	#1
	BCC	FORM2
	BNE	BADDNS2

FORMMED	LDA	#$22
	DFB $2C
FORM2	LDA	#$21

	ORA	HSFLG
	STA	FORMCMD

	LDX	DENS
	LDA	FTBSL,X
	STA	FORMLEN
	LDA	FTBSH,X
	STA	FORMLEN+1

FORM2B	JSR	PRINT
	DFB 155,155
	ASC "Formatting... "
	DFB 0

	JSR	SIOFORM
	BPL	FORM23J

	JSR	PRINT
	DFB 155,155,253
	ASC "Error while "
	ASC "formatting !"
	DFB 155
	ASC "Try again"
	DFB 0
	JSR	YESNO
	BCS	FORM2B

	JMP	ABORT

FORM23J	JMP	FORM3

***

WRTBOO	STA	WRTB2+1
	STX	WRTB2+2

	LDX	#127
WRTB2	LDA	$FFFF,X
	STA	BUFF,X
	DEX
	BPL	WRTB2

WRTBO2	JSR	SIOWR
	BMI	WRBER

	INC	WRTSECT

	RTS
*

WRBER	JSR	PRINT
	DFB 155,253
	ASC "Can't create "
	ASC "directory !"
	DFB 0
	JMP	ABORT
***

CLBUF	LDA	#0
	TAX

CLBF2	STA	BUFF,X
	DEX
	BNE	CLBF2

	RTS
***

MAINVZ	DFB 23,0,0
	ASC "MAIN       "

*** USPESNE ZFORMATOVANO !

* BOOT SEKTORY

FORM3	LDX	DENS
	LDA	FTBSL,X
	STA	RDLEN
	LDA	FTBSH,X
	STA	RDLEN+1
	LDA	#4
	STA	RDSECT
	LDA	#0
	STA	RDSECT+1
	JSR	SIORD

	LDX	DENS
	LDA	FTMAIN,X
	STA	BOOMAIN
	LDA	#0
	STA	BOOMAIN+1

	LDA	FTMAPS,X
	STA	BOOMAIN+6

	LDA	FTTRKS,X
	STA	BOONAME+8

	LDA	FTBSL,X
	STA	BOOSLEN

	TXA
	ASL
	TAX

	LDA	FTTOTAL,X
	STA	BOOMAIN+2
	LDA	FTTOTAL+1,X
	STA	BOOMAIN+3

	LDA	FTFREE,X
	STA	BOOMAIN+4
	LDA	FTFREE+1,X
	STA	BOOMAIN+5

	LDA	$D20A
	STA	BOORND
*
	LDA	#128
	STA	WRTLEN
	ASL
	STA	WRTLEN+1
	STA	WRTSECT+1
	ROL
	STA	WRTSECT

	LDA	#BOOTER:L
	LDX	#BOOTER:H
	JSR	WRTBOO

	LDA	#(BOOTER+128):L
	LDX	#(BOOTER+128):H
	JSR	WRTBOO

	LDA	#(BOOTER+256):L
	LDX	#(BOOTER+256):H
	JSR	WRTBOO

*** MAPY
	JSR	CLBUF

	LDX	DENS
	LDA	FTBSL,X
	STA	WRTLEN
	LDA	FTBSH,X
	STA	WRTLEN+1

	LDA	#4
	STA	WRTSECT
	LDY	#0
	STY	WRTSECT+1
*
	LDA	MAP11ST,X

	DFB $2C
FORM3B	LDA	#$FF
	STA	BUFF,Y

	INY
	TYA
	CMP	MAP1LEN,X
	BCC	FORM3B

	JSR	WRTBO2

	LDX	DENS
	DEX
	BNE	FORM3C
*ED!
	JSR	CLBUF
	LDX	#255
	STX	BUFF
	STX	BUFF+1

	JSR	WRTBO2

*** MAIN

FORM3C	JSR	CLBUF

	LDX	WRTSECT
	INX
	STX	BUFF+4

	JSR	WRTBO2
*DTA
	JSR	CLBUF

	LDX	#13
FORM3D	LDA	MAINVZ,X
	STA	BUFF+3,X
	DEX
	BPL	FORM3D

	JSR	WRTBO2

** FORMAT HOTOV

	LDA	DOSLEN
	ORA	DOSLEN+1
	BNE	FORM5
	JMP	FORM6

** ZAPIS DOSU

FORM5	JSR	PRINT
	DFB 155,155
	ASC "Writting DOS... "
	DFB 0

	LDA	#7
	STA	PTHLEN
	JSR	GETPATH

	LDA	#$9B
	STA	BUFF,Y

**CREDIR >DOS

	JSR	CLOSE

	LDX	#$10
	LDA	#42
	STA	$342,X

	JSR	CIOBUFF
	BMI	ERRORJ

** OPEN

	INC	PTHLEN
	JSR	GETPATH
	JSR	GETNME

	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#8
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOBUFF
	BPL	FORM5B

ERRORJ	JMP	ERROR

*

FORM5B	LDX	#$10
	LDA	#11
	STA	$342,X

	LDA	#END:L
	STA	$344,X
	LDA	#END:H
	STA	$345,X

	LDA	DOSLEN
	STA	$348,X
	LDA	DOSLEN+1
	STA	$349,X

	JSR	$E456
	BMI	ERRORJ

	JSR	CLOSE

** INSTALL DO BOOTu

	LDA	#128
	STA	WRTLEN
	STA	RDLEN
	ASL
	STA	WRTLEN+1
	STA	RDLEN+1
	STA	WRTSECT+1
	STA	RDSECT+1
	ROL
	STA	WRTSECT
	STA	RDSECT

	JSR	SIORD
	BMI	ERRORJ

	LDA	BOOMAIN+9
	STA	BUFF+40
	LDA	BOOMAIN+10
	STA	BUFF+41

	JSR	SIOWR
	BMI	ERRORJ

*** HOTOVO!

FORM6	JSR	PRINT
	DFB 155,155
	ASC "Diskette initialized."
	DFB 155,155
	ASC "Format another"
	DFB 0

	JSR	YESNO
	BCC	FORM6B

	JMP	STRT4

FORM6B	LDA	#$9B
	JSR	PRT1

	JMP	(10)

*** ZA TO UZ NIC !!!

END	EQU	*

