	ORG	$4000
	JMP	START

*************************************
*				*
*    " F O R M A T "   PRO BW-DOS	*
*				*
*************************************

PTHLEN	EQU	128
DOSIX	EQU	129
DOSLEN	EQU	130

DENS	EQU	132
HSFLG	EQU	133

* VOLNO: 134

BUFF	EQU	$400
NAMBUFF	EQU	$500

******* BOOTER *******

BOOTER	DTA 0,3
BO	EQU	BOOTER-$3000
	DTA A($3000,$7E0)
	JMP	$3080

BOOMAIN	DTA A(0,0,0)
	DTA 0
	DTA A(4,$20,4)
BOONAME	DTA C"@@@@@@@@"
	DTA 0
BOOSLEN	DTA 0

	DTA $20,6,1,255,255,0,0
BOORND	DTA 0
BOOBOO	DTA 0,0,0,0,0
DTAIX	DTA 0
MAPIX	DTA 0,0

***

GETLEN	EQU	$43

*** VOLNE - VYCPAVKA
	DTA 0
***

GADR	JSR	GET1-BO
	STA	GETADR-BO
	JSR	GET1-BO
	STA	GETADR+1-BO

	AND	GETADR-BO
	CMP	#$FF
	RTS
***

READ0F	LDX	#$2F
READ0	LDA	#0
READ	STX	SIOADR+1-BO
	STA	SIOADR-BO

	LDX	#9
READ1	LDA	SIOTAB-BO,X
	STA	$300,X
	DEX
	BPL	READ1

	LDA	$30A
	ORA	$30B
	BEQ	READERR

	JSR	$E459
	BPL	READRTS

READERR	LDA	$D301
	ORA	#1
	STA	$D301

SSAVE	LDX	#$FF
	TXS

	SEC
READRTS	RTS

***

BOORUN	JMP	($2E0)
BOOINI	JMP	($2E2)

**

SIOTAB	DTA $31,1
	DTA C"R"
	DTA 64
SIOADR	DTA A($2E00)
	DTA 7,0
SIOLEN	EQU *

*2 BYTE PAK PREPISE START!

BOOSTRT	TSX
	STX	SSAVE+1-BO

	LDA	BOOSLEN-BO
	AND	#128
	STA	SIOLEN-BO
	STA	MAPIX-BO
	STA	DTAIX-BO
	ASL
	ROL
	EOR	#1
	STA	SIOLEN+1-BO

*XF DENSITY TEST

	INC	$30A
	JSR	READ0F-BO
*START
	JSR	GADR-BO
	BNE	READERR
	BEQ	BOOST2
**

BOOST1	JSR	BOOINI-BO

BOOST2	JSR	GADR-BO
	BEQ	BOOST2

	LDA	GETADR-BO
	ORA	GETADR+1-BO
	BEQ	BOORUN

	LDA	#<(READRTS-BO)
	STA	$2E2
	LDA	#>(READRTS-BO)
	STA	$2E3

	JSR	GET1-BO
	SEC
	SBC	GETADR-BO
	PHA
	PHP
	JSR	GET1-BO
	PLP
	SBC	GETADR+1-BO
	STA	GETLEN+1
	PLA
	STA	GETLEN

**LD BLOK

BOOST3	JSR	GET-BO
BOOST4	STA	$FFFF
GETADR EQU BOOST4+1

	INC	GETADR-BO
	BNE	BOOST5
	INC	GETADR+1-BO

BOOST5	LDA	GETLEN
	BNE	BOOST6
	LDA	GETLEN+1
	BEQ	BOOST1
**
	DEC	GETLEN+1
BOOST6	DEC	GETLEN

	JMP	BOOST3-BO

***

GET1	LDA	#0
	STA	GETLEN
	STA	GETLEN+1

GET	LDX	DTAIX-BO
	CPX	SIOLEN-BO
	BEQ	GET2

GET1B	LDA	$2E00,X
	INX
	STX	DTAIX-BO
	RTS
**

GETBLOK	LDA	GETADR-BO
	LDX	GETADR+1-BO
	JSR	READ-BO

	LDA	GETADR-BO
	CLC
	ADC	SIOLEN-BO
	STA	GETADR-BO
	LDA	GETADR+1-BO
	ADC	SIOLEN+1-BO
	STA	GETADR+1-BO

	LDA	GETLEN
	SEC
	SBC	SIOLEN-BO
	STA	GETLEN
	LDA	GETLEN+1
	SBC	SIOLEN+1-BO
	STA	GETLEN+1

*NEXT DATA

GET2	LDY	MAPIX-BO
	CPY	SIOLEN-BO
	BNE	GET2B

*NAXT MAP

	LDA	BOOBOO-BO
	STA	$30A
	LDA	BOOBOO+1-BO
	STA	$30B

	JSR	READ0F-BO

	LDA	$2F00
	STA	BOOBOO-BO
	LDA	$2F01
	STA	BOOBOO+1-BO

	LDY	#4
**

GET2B	LDA	$2F00,Y
	STA	$30A
	LDA	$2F01,Y
	STA	$30B

	INY
	INY
	STY	MAPIX-BO
**
	LDA	GETLEN+1
	BNE	GETBLOK

	LDA	SIOLEN-BO
	BPL	GET2C

	LDA	GETLEN
	BMI	GETBLOK

GET2C	LDX	#$2E
	JSR	READ0-BO

	LDX	#0
	JMP	GET1B-BO

************** KONEC BOOTERU *******
***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

ERROR	TYA
	PHA

	JSR	PRINT
	DTA 155
	DTA C"Error "
	DTA 0

	LDX	#2
	PLA

ERR2	LDY	#0

ERR3	CMP	ERRTAB,X
	BCC	ERR4

	SBC	ERRTAB,X
	INY
	BNE	ERR3
**

ERR4	PHA
	TXA
	PHA

	TYA
	ORA	#$30
	JSR	PRT1

	PLA
	TAX
	PLA

	DEX
	BPL	ERR2
**
	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DTA 1,10,100

**

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JSR	$E456
	BMI	ERROR

	RTS
***

KNAME	DTA C"K:"
	DTA 155

***

GETKEY	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#<KNAME
	STA	$344,X
	LDA	#>KNAME
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456

	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456

	PHA
	JSR	CLOSE
	PLA

	CMP	#27
	BNE	GPRTS

	JMP	ABORT

***

PATH	DTA C"D0:>DOS>"

**

GETPATH	LDY	#0

GTPT2	LDA	PATH,Y
	STA	BUFF,Y

	INY
	CPY	PTHLEN
	BCC	GTPT2

GPRTS	RTS

***

GETNME	LDX	DOSIX
	DEX

GTN1	INX
	TXA
	AND	#15
	CMP	#8
	BCS	GTN2

	LDA	NAMBUFF,X
	CMP	#$20
	BEQ	GTN2

	STA	BUFF,Y
	INY
	BNE	GTN1
*

GTN2	LDA	#'.'
	STA	BUFF,Y
	INY

	LDX	#7
*

GTN3	INX
	TXA
	AND	#15
	CMP	#11
	BCS	GTN4

	LDA	NAMBUFF,X
	CMP	#$20
	BEQ	GTN4

	STA	BUFF,Y
	INY
	BNE	GTN3
*

GTN4	LDA	#$9B
	STA	BUFF,Y

	RTS

***

DOSFLTR	DTA 155
	DTA C"SOD.*"
***

SRCHDOS	JSR	CLOSE

	JSR	GETPATH
	LDX	#5
SRD2	LDA	DOSFLTR,X
	STA	BUFF,Y
	INY
	DEX
	BPL	SRD2

*OPEN DIR

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#6
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOBUFF
	BMI	DIREND
**

SRD3	LDX	#$10
	LDA	#5
	STA	$342,X

	LDA	#<(BUFF+64)
	STA	$344,X
	LDA	#>(BUFF+64)
	STA	$345,X

	LDA	#64
	STA	$348,X
	LDA	#0
	STA	$349,X

	JSR	$E456
	BMI	DIREND
*
	LDA	BUFF+64
	CMP	#$30
	BCS	DIREND

	LDA	BUFF+74
	BMI	SRD3
*
	LDX	DOSIX
	CPX	#$90
	BCS	SRD3

	LDY	#66

SRD4	LDA	BUFF,Y
	STA	NAMBUFF,X
	INX
	INY

	CPY	#77
	BCC	SRD4

	LDA	PTHLEN
	STA	NAMBUFF,X

	LDA	DOSIX
	CLC
	ADC	#16
	STA	DOSIX

	JMP	SRD3
**

DIREND	JMP	CLOSE

***

ABORT	JSR	CLOSE

	JSR	PRINT
	DTA 155
	DTA C"Operation aborted."
	DTA 155,0

	JMP	ERR5X
***

YESNO	JSR	PRINT
	DTA C" (Y/N) ? "
	DTA 0

YN1	JSR	GETKEY
	AND	#$5F

	LDX	#0
	CMP	#'N'
	BEQ	YN2

	LDX	#128
	CMP	#'Y'
	BNE	YN1

YN2	STX	YN3+1

	JSR	PRT1
	LDA	#$9B
	JSR	PRT1

YN3	LDA	#0
	ASL

	RTS
***

CIOBUFF	LDA	#<BUFF
	STA	$344,X
	LDA	#>BUFF
	STA	$345,X

	JMP	$E456
***
	ICL	"chkdos.inc"
* HLEDEJ DOSy
	JSR	PRINT
	DTA 155
	DTA C"BW-DOS Formatter V"
	DTA C" 1.00 By BEWESOFT"
	DTA 155,0
*GETPAR
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
*SIO
	LDA	10
	SEC
	SBC	#10
	STA	SIOJMP+1
	STA	DOSLEN
	LDA	11
	SBC	#0
	STA	SIOJMP+2
	STA	DOSLEN+1

	LDY	#8
	LDA	(DOSLEN),Y
	STA	WRTCMD
*
	LDA	#0
	STA	DOSLEN
	STA	DOSLEN+1
*
	JSR	GETNAME
	LDY	#34
	LDA	(10),Y
	STA	PATH+1
*
	LDA	#0
	STA	DOSIX

	LDA	#8
	STA	PTHLEN
	JSR	SRCHDOS

	LSR	PTHLEN
	JSR	SRCHDOS
**

	LDA	DOSIX
	BNE	STRT3

	JSR	PRINT
	DTA 155
	DTA C"No DOS-files found..."
	DTA 155,0
	JMP	STRT4

*NABIDKA DOSU

STRT3	JSR	PRINT
	DTA 155
	DTA C"Select DOS:"
	DTA 155,155,0

	LDA	#0
	STA	PTHLEN
	LDA	#$31
	STA	STRT3D

STRT3B	LDX	PTHLEN
	CPX	DOSIX
	BCS	STRT3F

	LDY	#0
STRT3C	LDA	NAMBUFF,X
	STA	STRT3E,Y
	INX
	INY
	CPY	#8
	BCC	STRT3C

	JSR	PRINT
	DTA C"  "
STRT3D	DTA C"0) "
STRT3E	DTA C"FILENAME"
	DTA 155,0

	LDA	PTHLEN
	CLC
	ADC	#16
	STA	PTHLEN

	INC	STRT3D

	JMP	STRT3B
***

STRT3NO	JSR	PRINT
	DTA C"N"
	DTA 155,0

	JMP	STRT4

***

STRT3F	JSR	PRINT
	DTA C"  N) -No DOS-"
	DTA 155,155
	DTA C"Choice: "
	DTA 0

STRT3G	JSR	GETKEY
	AND	#127
	TAX

	CMP	#'N'
	BEQ	STRT3NO
	CMP	#'n'
	BEQ	STRT3NO

	SEC
	SBC	#$31
	CMP	#9
	BCS	STRT3G

	ASL
	ASL
	ASL
	ASL
	CMP	DOSIX
	BCS	STRT3G

** DOS ZVOLEN

	PHA

	TXA
	JSR	PRT1
	LDA	#$9B
	JSR	PRT1

	PLA
	STA	DOSIX

	TAX
	LDA	NAMBUFF+11,X
	STA	PTHLEN
	JSR	GETPATH
	JSR	GETNME

	JSR	CLOSE

* OPEN DOS-FILE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOBUFF
	BMI	RDDOSER
*READ IT
	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#<END
	STA	$344,X
	LDA	#>END
	STA	$345,X

	LDA	$2E5
	SEC
	SBC	#<END
	STA	$348,X
	LDA	$2E6
	SBC	#>END
	STA	$349,X

	JSR	$E456
	BPL	RDDOSTL

	CPY	#136
	BEQ	STRT3H
**

RDDOSER	JSR	PRINT
	DTA 155
	DTA C"Can't read DOS-file!"
	DTA 0
	JMP	ABORT
**

RDDOSTL	JSR	PRINT
	DTA 155
	DTA C"DOS-file too long!"
	DTA 0
	JMP	ABORT
**

STRT3H	LDX	#$10
	LDA	$348,X
	STA	DOSLEN
	LDA	$349,X
	STA	DOSLEN+1

	JSR	CLOSE

** DOS NACTEN

STRT4	JSR	PRINT
	DTA 155
	DTA C"Drive to format: "
	DTA 0

STRT4B	JSR	GETKEY
	SEC
	SBC	#$30
	BEQ	STRT4B
	CMP	#10
	BCS	STRT4B

	STA	SIODR1
	STA	SIODR2
	STA	SIODR3
	STA	SIODR4
	STA	SIODR5

	ORA	#$30
	STA	PATH+1
	STA	STRT4L

	JSR	PRT1
**
	JSR	PRINT
	DTA 155,155
	DTA C"Select density:"
	DTA 155,155
	DTA C"  1) Single density"
	DTA 155
	DTA C"  2) Medium density"
	DTA 155
	DTA C"  3) Double density"
	DTA 155
	DTA C"  4) Double-sided "
	DTA C"Double density"
	DTA 155,155
	DTA C"Choice: "
	DTA 0

STRT4C	JSR	GETKEY
	SEC
	SBC	#$31
	CMP	#4
	BCS	STRT4C

	STA	DENS

	CLC
	ADC	#$31
	JSR	PRT1
**
	JSR	PRINT
	DTA 155,155
	DTA C"Volume name: "
	DTA 0

	LDA	#0
	STA	HSFLG

STRT4D	JSR	GETKEY
	CMP	#155
	BEQ	STRT4G
	CMP	#126
	BEQ	STRT4F

	CMP	#27
	BCC	STRT4E
	CMP	#32
	BCC	STRT4D
	CMP	#125
	BCC	STRT4E
	CMP	#128
	BCC	STRT4D
	CMP	#155
	BCC	STRT4E
	CMP	#160
	BCC	STRT4D
	CMP	#253
	BCS	STRT4D

STRT4E	LDX	HSFLG
	CPX	#8
	BCS	STRT4D
	STA	BOONAME,X

	INC	HSFLG
	BCC	STRT4F2
**

STRT4F	LDX	HSFLG
	BEQ	STRT4D

	DEC	HSFLG

STRT4F2	JSR	PRT1
	JMP	STRT4D

**

STRT4G	LDX	HSFLG

STRT4H	CPX	#8
	BCS	STRT4I

	LDA	#32
	STA	BOONAME,X
	INX
	BNE	STRT4H
**

STRT4I	JSR	PRINT
	DTA 155,155
	DTA C"XF-551 High Speed"
	DTA 0
	JSR	YESNO

	LDA	#0
	ROR
	STA	HSFLG
**
	JSR	PRINT
	DTA 155
	DTA C"Insert disk to FORMAT"
	DTA C" into drive "
STRT4L	DTA C"0,"
	DTA 155
	DTA C"and press <RETURN> !"
	DTA 155
	DTA C"  (Press <B> to "
	DTA C"build directory)"
	DTA 155,0
**

	JSR	GETKEY
	CMP	#155
	BEQ	STRT4M
	AND	#$5F
	CMP	#'B'
	BEQ	STRT4N

	JMP	ABORT

STRT4M	JMP	FORMAT1

STRT4N	JMP	FORM3

***

SION	LDX	#11
	DTA $2C
SIOO	LDX	#23
	DTA $2C
SIOWR	LDX	#35
	DTA $2C
SIOFORM	LDX	#47
	DTA $2C
SIORD	LDX	#59

	LDY	#11
SIO1	LDA	SIOTABS,X
	STA	$300,Y
	DEX
	DEY
	BPL	SIO1

SIOJMP	JMP	($FFFF)

**

SIOTABS	DTA $31
SIODR1	DTA 0
	DTA C"N"
	DTA 64
	DTA A(BUFF)
	DTA 7,0
	DTA A(12,1)

	DTA $31
SIODR2	DTA 0
	DTA C"O"
	DTA 128
	DTA A(BUFF)
	DTA 7,0
	DTA A(12,1)

	DTA $31
SIODR3	DTA 0
WRTCMD	DTA C"W"
	DTA 128
	DTA A(BUFF)
	DTA 7,0
WRTLEN	DTA A(128)
WRTSECT	DTA A(1)

	DTA $31
SIODR4	DTA 0
FORMCMD	DTA C"!"
	DTA 64
	DTA A(BUFF)
	DTA 255,0
FORMLEN	DTA A(128,4)

	DTA $31
SIODR5	DTA 0
	DTA C"R"
	DTA 64
	DTA A(BUFF)
	DTA 7,0
RDLEN	DTA A(128)
RDSECT	DTA A(1)

*** VLASTNI FORMATOVANI

FTSTRK	DTA 18,26,18,18
FTSIDE	DTA 0,0,0,1
FTMODUL	DTA 0,4,4,4
FTBSH	DTA 0,0,1,1
FTBSL	DTA 128,128,0,0

FTMAIN	DTA 5,6,5,5
FTTOTAL	DTA A(720,1040,720,1440)
FTFREE	DTA A(713,1032,713,1433)
FTMAPS	DTA 1,2,1,1
FTTRKS	DTA 40,40,40,40+128

MAP11ST	DTA 1,0,1,1
MAP1LEN	DTA 90,128,90,180

***

FORMMEDJ	JMP	FORMMED

**

FORMAT1	LDX	DENS
	DEX
	BEQ	FORMMEDJ

	JSR	SION
	BMI	FORMNCF

	LDX	DENS

	LDA	#40
	STA	BUFF
	LDA	#0
	STA	BUFF+2
	LDA	FTSTRK,X
	STA	BUFF+3
	LDA	FTSIDE,X
	STA	BUFF+4
	LDA	FTMODUL,X
	STA	BUFF+5
	LDA	FTBSH,X
	STA	BUFF+6
	LDA	FTBSL,X
	STA	BUFF+7

	JSR	SIOO
	BMI	BADDNS

	LDX	#11
FORM1B	LDA	BUFF,X
	STA	BUFF+64,X
	DEX
	BPL	FORM1B

	JSR	SION
	BMI	BADDNS

	LDX	#11
FORM1BB	LDA	BUFF,X
	CMP	BUFF+64,X
	BNE	BADDNS
	DEX
	BPL	FORM1BB
	BMI	FORM2

** HUSTOTU NALZE NASTAVIT

BADDNS	LDX	DENS
	DEX
	BEQ	FORMNCF

BADDNS2	JSR	PRINT
	DTA 155
	DTA C"Drive can't format "
	DTA C"selected density!"
	DTA 0
	JMP	ABORT

** DRIVE NEKONFIGUROVATELNY!

FORMNCF	LDX	DENS
	CPX	#1
	BCC	FORM2
	BNE	BADDNS2

FORMMED	LDA	#$22
	DTA $2C
FORM2	LDA	#$21

	ORA	HSFLG
	STA	FORMCMD

	LDX	DENS
	LDA	FTBSL,X
	STA	FORMLEN
	LDA	FTBSH,X
	STA	FORMLEN+1

FORM2B	JSR	PRINT
	DTA 155,155
	DTA C"Formatting... "
	DTA 0

	JSR	SIOFORM
	BPL	FORM23J

	JSR	PRINT
	DTA 155,155,253
	DTA C"Error while "
	DTA C"formatting !"
	DTA 155
	DTA C"Try again"
	DTA 0
	JSR	YESNO
	BCS	FORM2B

	JMP	ABORT

FORM23J	JMP	FORM3

***

WRTBOO	STA	WRTB2+1
	STX	WRTB2+2

	LDX	#127
WRTB2	LDA	$FFFF,X
	STA	BUFF,X
	DEX
	BPL	WRTB2

WRTBO2	JSR	SIOWR
	BMI	WRBER

	INC	WRTSECT

	RTS
*

WRBER	JSR	PRINT
	DTA 155,253
	DTA C"Can't create "
	DTA C"directory !"
	DTA 0
	JMP	ABORT
***

CLBUF	LDA	#0
	TAX

CLBF2	STA	BUFF,X
	DEX
	BNE	CLBF2

	RTS
***

MAINVZ	DTA 23,0,0
	DTA C"MAIN       "

*** USPESNE ZFORMATOVANO !

* BOOT SEKTORY

FORM3	LDX	DENS
	LDA	FTBSL,X
	STA	RDLEN
	LDA	FTBSH,X
	STA	RDLEN+1
	LDA	#4
	STA	RDSECT
	LDA	#0
	STA	RDSECT+1
	JSR	SIORD

	LDX	DENS
	LDA	FTMAIN,X
	STA	BOOMAIN
	LDA	#0
	STA	BOOMAIN+1

	LDA	FTMAPS,X
	STA	BOOMAIN+6

	LDA	FTTRKS,X
	STA	BOONAME+8

	LDA	FTBSL,X
	STA	BOOSLEN

	TXA
	ASL
	TAX

	LDA	FTTOTAL,X
	STA	BOOMAIN+2
	LDA	FTTOTAL+1,X
	STA	BOOMAIN+3

	LDA	FTFREE,X
	STA	BOOMAIN+4
	LDA	FTFREE+1,X
	STA	BOOMAIN+5

	LDA	$D20A
	STA	BOORND
*
	LDA	#128
	STA	WRTLEN
	ASL
	STA	WRTLEN+1
	STA	WRTSECT+1
	ROL
	STA	WRTSECT

	LDA	#<BOOTER
	LDX	#>BOOTER
	JSR	WRTBOO

	LDA	#<(BOOTER+128)
	LDX	#>(BOOTER+128)
	JSR	WRTBOO

	LDA	#<(BOOTER+256)
	LDX	#>(BOOTER+256)
	JSR	WRTBOO

*** MAPY
	JSR	CLBUF

	LDX	DENS
	LDA	FTBSL,X
	STA	WRTLEN
	LDA	FTBSH,X
	STA	WRTLEN+1

	LDA	#4
	STA	WRTSECT
	LDY	#0
	STY	WRTSECT+1
*
	LDA	MAP11ST,X

	DTA $2C
FORM3B	LDA	#$FF
	STA	BUFF,Y

	INY
	TYA
	CMP	MAP1LEN,X
	BCC	FORM3B

	JSR	WRTBO2

	LDX	DENS
	DEX
	BNE	FORM3C
*ED!
	JSR	CLBUF
	LDX	#255
	STX	BUFF
	STX	BUFF+1

	JSR	WRTBO2

*** MAIN

FORM3C	JSR	CLBUF

	LDX	WRTSECT
	INX
	STX	BUFF+4

	JSR	WRTBO2
*DTA
	JSR	CLBUF

	LDX	#13
FORM3D	LDA	MAINVZ,X
	STA	BUFF+3,X
	DEX
	BPL	FORM3D

	JSR	WRTBO2

** FORMAT HOTOV

	LDA	DOSLEN
	ORA	DOSLEN+1
	BNE	FORM5
	JMP	FORM6

** ZAPIS DOSU

FORM5	JSR	PRINT
	DTA 155,155
	DTA C"Writting DOS... "
	DTA 0

	LDA	#7
	STA	PTHLEN
	JSR	GETPATH

	LDA	#$9B
	STA	BUFF,Y

**CREDIR >DOS

	JSR	CLOSE

	LDX	#$10
	LDA	#42
	STA	$342,X

	JSR	CIOBUFF
	BMI	ERRORJ

** OPEN

	INC	PTHLEN
	JSR	GETPATH
	JSR	GETNME

	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#8
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOBUFF
	BPL	FORM5B

ERRORJ	JMP	ERROR

*

FORM5B	LDX	#$10
	LDA	#11
	STA	$342,X

	LDA	#<END
	STA	$344,X
	LDA	#>END
	STA	$345,X

	LDA	DOSLEN
	STA	$348,X
	LDA	DOSLEN+1
	STA	$349,X

	JSR	$E456
	BMI	ERRORJ

	JSR	CLOSE

** INSTALL DO BOOTu

	LDA	#128
	STA	WRTLEN
	STA	RDLEN
	ASL
	STA	WRTLEN+1
	STA	RDLEN+1
	STA	WRTSECT+1
	STA	RDSECT+1
	ROL
	STA	WRTSECT
	STA	RDSECT

	JSR	SIORD
	BMI	ERRORJ

	LDA	BOOMAIN+9
	STA	BUFF+40
	LDA	BOOMAIN+10
	STA	BUFF+41

	JSR	SIOWR
	BMI	ERRORJ

*** HOTOVO!

FORM6	JSR	PRINT
	DTA 155,155
	DTA C"Diskette initialized."
	DTA 155,155
	DTA C"Format another"
	DTA 0

	JSR	YESNO
	BCC	FORM6B

	JMP	STRT4

FORM6B	LDA	#$9B
	JSR	PRT1

	JMP	(10)

*** ZA TO UZ NIC !!!

END	EQU	*

