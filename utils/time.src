*************************************
*				*
*	'TIME' PRO BW-DOS 1.00	*
*				*
*************************************

INDEX	EQU	130

	ORG	$3000
	ICL	"chkbw.inc"
***

CLOSES	LDA	#0
	STA	752

	LDX	#$20

CLOSE	LDA	#12
	STA	$342,X
	JMP	$E456

***

GETTD	JMP	($706)
SETTD	JMP	($708)

ETAB	DTA	0,1,3,4,6,7,8

***

START2	JSR	GETTD

	JSR	PRINT
	DTA	155
	DTA	C"Current time is "
	DTA	0

	LDA	#':'
	LDY	#16
	JSR	PRTDEC

	LDA	#':'
	LDY	#17
	JSR	PRTDEC

	LDA	#155
	LDY	#18
	JSR	PRTDEC

***
* X=0 after PRT1
	STX	INDEX
***

	JSR	CLOSES

*Hide cursor
	LDX	#15
	STX	752

*Copy IOCB#0 to #2, to call EDITOR
*without redirection.
STL4	LDA	$340,X
	STA	$360,X
	DEX
	BPL	STL4
***

EDLOOP	LDX	INDEX
	LDY	ETAB,X
	LDA	#160
	STA	TEXT,Y

	LDX	#$20
	LDA	#<TXT0
	STA	$344,X
	LDA	#>TXT0
	STA	$345,X
	LDA	#26
	STA	$348,X
	LDA	#0
	STA	$349,X
	LDA	#11
	STA	$342,X
	JSR	$E456

	LDA	#$1C
	JSR	PRT1

**

EDL2	JSR	GETKEY

	CMP	#27
	BEQ	EDESC
	CMP	#155
	BEQ	EDRET
	CMP	#126
	BEQ	EDDEL
*ZNAK
	CMP	#$30
	BCC	EDL2
	CMP	#$3A
	BCS	EDL2

	LDX	INDEX
	CPX	#6
	BCS	EDL2

	LDY	ETAB,X
	STA	TEXT,Y

	INC	INDEX
	JMP	EDLOOP
***

EDDEL	LDX	INDEX
	BEQ	EDL2

	LDY	ETAB,X
	LDA	#32
	STA	TEXT,Y

	DEC	INDEX
	JMP	EDLOOP
***

EDESC	JSR	CLOSES

	JSR	PRINT
	DTA	156
	DTA	C"Aborted."
	DTA	155,0

	JMP	(10)
***

EDRET	JSR	CLOSES

	LDA	INDEX
	BEQ	NOTCHG
	CMP	#6
	BNE	INVALID

	LDA	#32
	STA	TEXT+8

	LDX	#0

	JSR	EDGET
	CMP	#24
	BCS	INVALID
	LDY	#16
	STA	(10),Y

	JSR	EDGET
	CMP	#60
	BCS	INVALID
	LDY	#17
	STA	(10),Y

	JSR	EDGET
	CMP	#60
	BCS	INVALID
	LDY	#18
	STA	(10),Y
*
	JSR	SETTD

	JSR	PRINT

TXT0	DTA	C"Enter new time: "
TEXT	DTA	C"  :  :   "
	DTA	155,0

	JMP	(10)
***

NOTCHG	JSR	PRINT
	DTA	156
	DTA	C"(Time not changed)"
	DTA	155,0
	JMP	(10)
***

INVALID	JSR	PRINT
	DTA	155
	DTA	C"Invalid Time!"
	DTA	155,0

	JMP	(10)
***

EDGET	LDA	TEXT,X
	ASL
	ASL
	ADC	TEXT,X
	ASL
	CLC
	ADC	TEXT+1,X
	SEC
	SBC	#16

	INX
	INX
	INX
	RTS
***
	ICL	"print.inc"
***
	ICL	"print1.inc"
***
PRTDEC	PHA
	LDA	(10),Y

	LDY	#$2F
PDEC1	INY
	SEC
	SBC	#10
	BCS	PDEC1
	PHA
	TYA
	JSR	PRT1
	PLA
	CLC
	ADC	#58
	JSR	PRT1

	PLA
	JMP	PRT1

*Call K: get handler
GETKEY	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDX	#0
	STX	$2A	;Clears ICAX1Z
	RTS
***
