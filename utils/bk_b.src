	OUT	N
*************************************
*				*
* " BACKUP "  PRO BW-DOS  -  CAST 2	*
*				*
*************************************

*PRENOS

START2	EQU	$480D
NEHOT	EQU	$443E
NEHRND	EQU	$4451

** DOMYSLENO

START1	EQU	$4000

PRINT	EQU	START1+3
DSKWAIT	EQU	START1+6
TESTID	EQU	START1+9
RSECT1	EQU	START1+12
ERROR	EQU	START1+15
RSECT	EQU	START1+18
WSECT	EQU	START1+21
SIOSRC	EQU	START1+24
SIODST	EQU	START1+27
ERR5X	EQU	START1+30
PRTDENS	EQU	START1+33
PRTEOL	EQU	START1+36
READID	EQU	START1+39
TRYMAXS	EQU	START1+42
TSTVTOC	EQU	START1+45
ZAJINSC	EQU	START1+48
FORMAT	EQU	START1+51
UVTRK	EQU	START1+54
CLOSE	EQU	START1+57
CONVDC	EQU	START1+60
PRT1	EQU	START1+63

***
	ORG	START2,$A800

** PRENOS

	JMP	RDMEM
	JMP	WRMEM
***

HEX	EPZ	128
DECIN	EPZ	131
DECOUT	EPZ	133

INMODE	EPZ	136
OUTMODE	EPZ	137

MAXSECT	EPZ	138
MAXFILE	EPZ	140
FORMFLG	EPZ	143
DENS	EPZ	144
FNMIX	EPZ	145
FILEC	EPZ	146
LASTFLG	EPZ	148
IN1ST	EPZ	149
OUT1ST	EPZ	150
FILEPOS	EPZ	151

BYTSSRC	EPZ	154
BYTSDST	EPZ	156
SRCDRIV	EPZ	158
DSTDRIV	EPZ	159

ZTSTID	EPZ	160

DTA2	EPZ	162

INSECT	EPZ	164

BUF1MP	EPZ	166
BUF2MP	EPZ	182

MEMADR	EPZ	198
MEMTOP	EPZ	200
PACKAD1	EPZ	202
PACKAD2	EPZ	204
VTOCADR	EPZ	206
PACKIX1	EPZ	208
PACKEOF1	EPZ	209
PKDAT1	EPZ	210
PKDAT2	EPZ	211

RDTRKA	EPZ	210
RDTRKS	EPZ	212

*** JEN CAST 2

MAXFLEN	EPZ	214

* VOLNO: 217

TMPBUF	EQU	$500

******** CTENI

RDMEM	LDA	INMODE
	BPL	RMS1
	JMP	RMF1
*SECTS

RMS1	LDA	SRCDRIV
	CMP	DSTDRIV
	BEQ	RMS1B

	LDA	IN1ST
	BNE	RMS1C

RMS1B	JSR	PRINT
	DFB 155
	ASC "Insert source"
	DFB 0

	LDA	SRCDRIV
	JSR	DSKWAIT
**

RMS1C	LDA	IN1ST
	BEQ	RMS1D
*TEST
	LDA	#0
	LDY	#ID1:L
	LDX	#ID1:H
	JSR	TESTID
	BEQ	RMS1CB

	JSR	THISISN

	JSR	PRINT
	ASC "source disk !!!"
	DFB 253,155,0

	JMP	RMS1B
**

RMS1CB	JMP	RMS2

RMS1EJ	JMP	RMS1E

**INIT IN - SECTS

RMS1D	INC	IN1ST

	LDA	INMODE
	BNE	RMS1EJ
*SPARTA

	JSR	RSECT1

	LDA	TMPBUF+7
	CMP	#$80
	BEQ	RMS1DB

*NOT SPARTA

NOTSP	LDY	#$94
	JMP	ERROR
**

RMS1DB	LDA	TMPBUF+31
	STA	BYTSSRC

	ASL
	BNE	NOTSP
	ROL
	EOR	#1
	STA	BYTSSRC+1

	LDA	TMPBUF+11
	STA	MAXSECT
	LDA	TMPBUF+12
	STA	MAXSECT+1

**READ BITMAPS

RMS1DC	LDA	TMPBUF+16
	STA	$30A
	LDA	TMPBUF+17
	STA	$30B

	LDY	DTA2
	LDX	DTA2+1
	LDA	#0
	JSR	RSECT

	LDA	DTA2
	CLC
	ADC	BYTSSRC
	STA	DTA2
	LDA	DTA2+1
	ADC	BYTSSRC+1
	STA	DTA2+1

	INC	TMPBUF+16
	BNE	RMS1DD
	INC	TMPBUF+17

RMS1DD	DEC	TMPBUF+15
	BNE	RMS1DC

*DAL SPOLECNE

	JMP	RMS1F

**SECTORS

RMS1ETB	DFW 128,128,256,256
RMS1ETS	DFW 720,1040,720,1440
RMS1EBET	DFB 0,$80,$20,$60

**

RMS1E	LDX	DENS
	BEQ	RMS1EB

	DEX
	JMP	RMS1EC

*ZJISTENI HUSTOTY

RMS1EBT	DFB $31,0
	DFB 'R'
	DFB 0
	DFW TMPBUF,6,0,4

RMS1EBS	DFB $31,0
	DFB 'S'
	DFB 64
	DFW TMPBUF,6,4,4

** SET XF551 DENSITY

RMS1EB	JSR	RSECT1

	LDX	#11
RMS1EBB	LDA	RMS1EBT,X
	STA	$300,X
	DEX
	BPL	RMS1EBB

	JSR	SIOSRC

	LDY	#16
RMS1EBC	STA	$D40A
	DEX
	BNE	RMS1EBC
	DEY
	BNE	RMS1EBC

* ZJISTI HUSTOTU

	LDX	#11
RMS1EBD	LDA	RMS1EBS,X
	STA	$300,X
	DEX
	BPL	RMS1EBD

	JSR	SIOSRC
	BMI	RMS1EER

	LDX	#3
	LDA	TMPBUF

*ODSTRAN ZAHADNE BITY RUZNYCH SPEEDY
*APOD.
	AND	#$E0


RMS1EBE	CMP	RMS1EBET,X
	BEQ	RMS1EBF

	DEX
	BPL	RMS1EBE
*DIVNE

RMS1EER	JSR	PRINT
	DFB 155
	ASC "Status-byte Error !"
	DFB 155,0
	JMP	ERR5X
**

RMS1EBF	TXA
	PHA

	JSR	PRINT
	DFB 155
	ASC "Density: "
	DFB 0

	PLA
	PHA
	JSR	PRTDENS

	JSR	PRTEOL

	PLA
	TAX

*HUSTOTA SPECIFIKOVANA

RMS1EC	TXA
	ASL
	TAX

	LDA	RMS1ETB,X
	STA	BYTSSRC
	LDA	RMS1ETB+1,X
	STA	BYTSSRC+1

	LDA	MAXSECT
	ORA	MAXSECT+1
	BNE	RMS1F

	LDA	RMS1ETS,X
	STA	MAXSECT
	LDA	RMS1ETS+1,X
	STA	MAXSECT+1

** BYTES/SECT A MAX SECT (+MAPS)
** NASTAVENO!

RMS1F	LDA	#0
	JSR	READID

	LDX	#0
RMS1FB	LDA	IDBUF,X
	STA	ID1,X
	LDA	IDBUF+128,X
	STA	ID1+128,X
	DEX
	BNE	RMS1FB

*ZKUS CIST MAX SECT

	LDA	#0
	JSR	TRYMAXS

	LDA	$2E7
	STA	MEMADR
	CLC
	ADC	#8
	STA	MEMTOP
	LDA	$2E8
	STA	MEMADR+1
	ADC	#0
	STA	MEMTOP+1

	LDY	#4
RMS1GA	LDA	IDTXT,Y
	STA	(MEMADR),Y
	DEY
	BPL	RMS1GA

	LDY	#5
	LDA	MAXSECT
	STA	(MEMADR),Y
	INY
	LDA	MAXSECT+1
	STA	(MEMADR),Y
	INY
	LDA	BYTSSRC
	STA	(MEMADR),Y

	JMP	RMS2B0
***

IDTXT	ASC /bwdBK/

RMS2BX	RTS

*** VLASTNI SEKTOROVE CTENI!

RMS2	LDA	$2E7
	STA	MEMTOP
	LDA	$2E8
	STA	MEMTOP+1
**

RMS2B0	JSR	READING

RMS2B	LDA	$2E5
	SEC
	SBC	MEMTOP
	TAY
	LDA	$2E6
	SBC	MEMTOP+1

	CMP	#1
	BCC	RMS2BX
	BNE	RMS2BC
	CPY	#12
	BCC	RMS2BX

RMS2BC	LDA	MAXSECT
	CMP	INSECT
	LDA	MAXSECT+1
	SBC	INSECT+1
	BCS	RMS2C

	LDX	#1
	STX	INSECT
	DEX
	STX	INSECT+1
**

RMS2C	LDY	INSECT
	LDX	INSECT+1
	JSR	TSTVTOC
	BNE	RMS2D

	JSR	PACKSEC

	LDA	#TMPBUF:L
	STA	PACKAD1
	LDA	#TMPBUF:H
	STA	PACKAD1+1

RMS2CB	LDY	#0
	LDA	(PACKAD1),Y

	JSR	RMSPUT

	INC	PACKAD1
	BNE	RMS2CC
	INC	PACKAD1+1

RMS2CC	LDA	PACKAD1
	CMP	PACKAD2
	BNE	RMS2CB
	LDA	PACKAD1+1
	CMP	PACKAD2+1
	BNE	RMS2CB
***

RMS2D	LDA	INSECT+1
	BNE	RMS2DB
	LDX	INSECT
	DEX
	BNE	RMS2DB

	LDA	#0
	JSR	RMSPUT
	LDA	#0
	JSR	RMSPUT

	LDA	#$FF
	STA	LASTFLG
	RTS
***

RMS2DB	INC	INSECT
	BNE	RMS2B
	INC	INSECT+1
	JMP	RMS2B

******

READING	JSR	PRINT
	DFB 155
	ASC "Reading..."
	DFB 155,0

	RTS
******

RMSPUT	LDY	#0
	STA	(MEMTOP),Y

	INC	MEMTOP
	BNE	RMSP2
	INC	MEMTOP+1

RMSP2	LDA	MEMTOP
	BNE	RMSP3
	LDA	MEMTOP+1
	CMP	#$40
	BNE	RMSP3

	LDA	DTA2
	STA	MEMTOP
	LDA	DTA2+1
	STA	MEMTOP+1

RMSP3	RTS

** PAKOVANI SEKTORU (INSECT) DO
** TMPBUF, KONEC PAKOVANYCH DAT DO
** PACKAD2

PACKSEC	JSR	ZAJINSC

	LDA	#0
	STA	PACKIX1
*UNIQ
	TAY
PCKSC1	STA	TMPBUF,Y
	DEY
	BNE	PCKSC1

PCKSC2	LDA	(PACKAD1),Y
	TAX

	CLC
	ADC	PACKIX1
	ADC	#0
	STA	PACKIX1

	INC	TMPBUF,X
	BNE	PCKSC2B
	DEC	TMPBUF,X

PCKSC2B	INY
	CPY	BYTSSRC
	BNE	PCKSC2

	LDX	#0
PCKSC3	LDA	TMPBUF,X
	BEQ	PCKSC4
	INX
	BNE	PCKSC3

PCKSC4	STX	TMPBUF+3

	LDA	PACKIX1
	STA	TMPBUF+4

	LDA	INSECT
	STA	TMPBUF
	LDA	INSECT+1
	STA	TMPBUF+1

	LDA	#0
	STA	PACKIX1
	STA	PACKEOF1

	LDA	#(TMPBUF+5):L
	STA	PACKAD2
	LDA	#(TMPBUF+5):H
	STA	PACKAD2+1

**VLASTNI PAKOVANI

PACKSC5	JSR	PK5GET

	LDX	#0
	STX	PACKEOF1

PACKSC6	LDX	#1
	STA	PKDAT1

PACKSC7	JSR	PK5GET
	BMI	PACKSC8

	CPX	#0
	BEQ	PACKSC8

	CMP	PKDAT1
	BNE	PACKSC8

	INX
	JMP	PACKSC7

PACKSC8	STA	PKDAT2

	TXA
	BEQ	PACKSC9
	CMP	#4
	BCS	PACKSC9

	LDA	PKDAT1
	CMP	TMPBUF+3
	BEQ	PACKSC9

PACKSC8B	LDA	PKDAT1
	JSR	PK5PUT

	DEX
	BNE	PACKSC8B
	BEQ	PACKSC10
**

PACKSC9	TXA
	PHA

	LDA	TMPBUF+3
	JSR	PK5PUT

	PLA
	JSR	PK5PUT

	LDA	PKDAT1
	JSR	PK5PUT

PACKSC10	LDA	PKDAT2
	LDX	PACKEOF1
	BEQ	PACKSC6
**

	LDA	PACKAD2
	SEC
	SBC	#(TMPBUF+5):L
	TAY
	LDA	PACKAD2+1
	SBC	#(TMPBUF+5):H

	CMP	#1
	BCC	PACKSC11
	BNE	PCKSCER

	CPY	#3
	BCC	PACKSC12
	BCS	PCKSCER

PACKSC11	CPY	#3
	BCC	PCKSCER

PACKSC12	STY	TMPBUF+2

	RTS
***

PCKSCER	JSR	PRINT
	DFB 155
	ASC "Fatal internal Error !"
	DFB 155,0
	JMP	ERR5X
***

PK5GET	LDY	PACKIX1
	INC	PACKIX1
	LDA	(PACKAD1),Y

	CPY	BYTSSRC
	BNE	PK5GB

	LDY	#$FF
	STY	PACKEOF1
	RTS
**

PK5GB	LDY	#0
	RTS

***

PK5PUT	LDY	#0
	STA	(PACKAD2),Y

	INC	PACKAD2
	BNE	PK5PT2
	INC	PACKAD2+1

	LDY	PACKAD2+1
	CPY	#7
	BCS	PCKSCER

PK5PT2	RTS


********** ZAPIS

WMSGET	JSR	WMSG2
	BCS	WMSGE
	RTS

WMSGE	JMP	PCKSCER

**

WMSG2	LDA	MEMADR
	CMP	MEMTOP
	BNE	WMSG3
	LDA	MEMADR+1
	CMP	MEMTOP+1
	BEQ	WMSGX

WMSG3	LDA	MEMADR
	BNE	WMSG4
	LDA	MEMADR+1
	CMP	#$40
	BNE	WMSG4

	LDA	DTA2
	STA	MEMADR
	LDA	DTA2+1
	STA	MEMADR+1

WMSG4	LDY	#0
	LDA	(MEMADR),Y

	INC	MEMADR
	BNE	WMSG5
	INC	MEMADR+1

WMSG5	CLC
WMSGX	RTS

******

WRMEM	LDA	OUTMODE
	BPL	WMS1
	JMP	WMF1
*SECTS

WMS1	LDA	SRCDRIV
	CMP	DSTDRIV
	BEQ	WMS1B

	LDA	OUT1ST
	BNE	WMS1C

WMS1B	JSR	PRINT
	DFB 155
	ASC "Insert target"
	DFB 0

	LDA	DSTDRIV
	JSR	DSKWAIT
**

WMS1C	LDA	OUT1ST
	BEQ	WMS1D
*TEST
	LDA	#1
	LDY	#ID2:L
	LDX	#ID2:H
	JSR	TESTID
	BEQ	WMS1CB

	JSR	THISISN
	JSR	PRINT
	ASC "target disk !!!"
	DFB 253,155,0

	JMP	WMS1B
**

WMS1CB	JMP	WMS2

** INIT OUT SECTS

WMS1DER	JMP	BADFILE

*

WMS1D	INC	OUT1ST

	LDA	$2E7
	CLC
	ADC	#5
	STA	MEMADR
	LDA	$2E8
	ADC	#0
	STA	MEMADR+1

	JSR	WMSGET
	STA	MAXSECT
	JSR	WMSGET
	STA	MAXSECT+1

	JSR	WMSGET
	STA	BYTSDST
	ASL
	BNE	WMS1DER
	ROL
	EOR	#1
	STA	BYTSDST+1

	LDA	FORMFLG
	BEQ	WMS1DB

	JSR	FORMAT
**

WMS1DB	LDA	#1
	JSR	READID

*ZKUS CIST MAX SECT

	LDA	#1
	JSR	TRYMAXS

*DO BOOTU RUTINU "NEHOTOVE"

	LDA	$D20A
	STA	NEHRND
	LDA	$D20A
	STA	NEHRND+1

	LDX	#1
	STX	$30A
	DEX
	STX	$30B

	LDY	#NEHOT:L
	LDX	#NEHOT:H
	LDA	#1
	JSR	WSECT

*NASTAV ID

	LDA	#1
	JSR	READID

	LDX	#0
WMS1FB	LDA	IDBUF,X
	STA	ID2,X
	LDA	IDBUF+128,X
	STA	ID2+128,X
	DEX
	BNE	WMS1FB
	BEQ	WMS2B0
***

WMS2	LDA	$2E7
	STA	MEMADR
	LDA	$2E8
	STA	MEMADR+1
***

WMS2B0	JSR	WRTNG

WMS2B	JSR	WMSG2
	BCC	WMS2C
*MEM END
	BCS	WMSDSE2

*DSK END

WMSDSE	LDA	#$FF
	STA	LASTFLG

WMSDSE2	JMP	UVTRK

**

WMS2C	STA	RDTRKS
	JSR	WMSGET
	STA	RDTRKS+1

	ORA	RDTRKS
	BEQ	WMSDSE

	JSR	WMSGET
	JSR	WMSGET
	STA	RDTRKA

	JSR	WMSGET
	STA	RDTRKA+1

*PRIDEL BUF

WMS2DA	LDX	#14

WMS2D	LDA	BUF2MP,X
	ORA	BUF2MP+1,X
	BEQ	WMS2E

	DEX
	DEX
	BPL	WMS2D

	JSR	UVTRK
	JMP	WMS2DA
*

WMS2E	LDA	RDTRKS
	STA	BUF2MP,X
	LDA	RDTRKS+1
	STA	BUF2MP+1,X

	TXA
	LSR
	CLC
	ADC	#TRKBUF2:H
	STA	PACKAD2+1
	LDA	#TRKBUF2:L
	STA	PACKAD2

*DEPACK!

	LDA	#0
	STA	PACKIX1
	STA	PACKAD1

	TSX
	STX	PACKEOF1

WMS2F	JSR	WMSGET
	CMP	RDTRKA
	BEQ	WMS2G

	JSR	WMSPUT
	JMP	WMS2F
*
WMS2CNT	DFB 0
*

WMS2G	JSR	WMSGET
	STA	WMS2CNT

	JSR	WMSGET

WMS2H	PHA
	JSR	WMSPUT
	PLA

	DEC	WMS2CNT
	BNE	WMS2H
	BEQ	WMS2F
***

WMSPUT	LDY	PACKIX1
	STA	(PACKAD2),Y

	CLC
	ADC	PACKAD1
	ADC	#0
	STA	PACKAD1

	INY
	STY	PACKIX1

	CPY	BYTSDST
	BEQ	WMSP2

	RTS
***

WMSP2	LDX	PACKEOF1
	TXS

	LDA	PACKAD1
	CMP	RDTRKA+1
	BEQ	WMSP3
**

BADFILE	JSR	PRINT
	DFB 155
	ASC "Bad file !"
	DFB 155,0
	JMP	ERR5X
**

WMSP3	JMP	WMS2B

******

CHKDSK	JSR	CLOSE

	LDX	#$10
	LDA	#47
	STA	$342,X

	LDA	#IDBUF:L
	STA	$348,X
	LDA	#IDBUF:H
	STA	$349,X

	JSR	CIOFNM
	BMI	ERRJP1
	RTS

ERRJP1	JMP	ERROR

***

CIOFNM	LDA	#FNAME:L
	STA	$344,X
	LDA	#FNAME:H
	STA	$345,X

	JMP	$E456
***

WRTNG	JSR	PRINT
	DFB 155
	ASC "Writing..."
	DFB 155,0

	RTS
***

WMFCIS	LDA	FILEC
	STA	DECIN
	LDA	FILEC+1
	STA	DECIN+1
	JSR	CONVDC

	LDX	FNMIX
	LDY	#0

WMF1A	LDA	DECOUT,Y
	STA	FNAME,X

	INX
	INY
	CPY	#3
	BCC	WMF1A

	RTS
***

FILECIS	JSR	WMFCIS

	LDA	DECOUT
	JSR	PRT1
	LDA	DECOUT+1
	JSR	PRT1
	LDA	DECOUT+2
	JSR	PRT1

	JSR	PRINT
	ASC ")"
	DFB 155
	ASC "Insert backup"
	DFB 0

	RTS

******** WRITE - FILE

WMF1	JSR	WMFCIS

	LDA	$2E7
	STA	MEMADR
	LDA	$2E8
	STA	MEMADR+1

*VLOZ DSK

	LDA	SRCDRIV
	CMP	DSTDRIV
	BEQ	WMF1B

	LDA	OUT1ST
	BNE	WMF1C
	INC	OUT1ST

WMF1B	JSR	PRINT
	DFB 155
	ASC "(Writing file "
	DFB 0

	JSR	FILECIS

	LDA	DSTDRIV
	JSR	DSKWAIT
*ID

WMF1C	JSR	WMFCIS
	JSR	CHKDSK

	LDA	FILEPOS
	ORA	FILEPOS+1
	ORA	FILEPOS+2
	BNE	WMF1BB

	LDX	#17
WMF1BA	LDA	IDBUF,X
	STA	ID2,X
	DEX
	BPL	WMF1BA
	BMI	WMF1D
*POKRAC

WMF1BB	LDX	#3

WMF1BC	LDA	IDBUF,X
	CMP	ID2,X
	BNE	WMF1BE

	DEX
	BPL	WMF1BC

	LDX	#7

WMF1BCB	LDA	IDBUF+6,X
	CMP	ID2+6,X
	BNE	WMF1BE

	DEX
	BPL	WMF1BCB

	LDA	IDBUF+15
	CMP	ID2+15
	BEQ	WMF1D
*JINY

WMF1BE	JSR	NOTCURR
	JMP	WMF1B
***

NOTCURR	JSR	PRINT
	DFB 155,253,0
	JSR	THISISN
	JSR	PRINT
	ASC "current"
	ASC " backup-disk!"
	DFB 155,0

	RTS
***

WMF1D	LDA	#0
	STA	MAXFLEN

	LDA	#256-8
	LDX	IDBUF+1
	BMI	WMF1DB

	LDA	#256-4

WMF1DB	CLC
	ADC	IDBUF+4
	STA	MAXFLEN+1

	LDA	IDBUF+5
	ADC	#$FF
	STA	MAXFLEN+2

	BCC	WMF1DFL
*
	LDX	IDBUF+1
	BPL	WMF1DC

	LSR	MAXFLEN+2
	ROR	MAXFLEN+1
	ROR	MAXFLEN
*OMEZ?

WMF1DC	LDA	MAXFILE
	ORA	MAXFILE+1
	ORA	MAXFILE+2
	BEQ	WMF1E

	LDA	MAXFILE
	SEC
	SBC	FILEPOS
	TAY
	LDA	MAXFILE+1
	SBC	FILEPOS+1
	TAX
	LDA	MAXFILE+2
	SBC	FILEPOS+2

	BCC	NXTFIL

	PHA
	TYA
	CMP	MAXFLEN
	TXA
	SBC	MAXFLEN+1
	PLA
	PHA
	SBC	MAXFLEN+2
	PLA

	BCS	WMF1E

	STA	MAXFLEN+2
	STX	MAXFLEN+1
	STY	MAXFLEN
*

WMF1E	LDA	MAXFLEN
	ORA	MAXFLEN+1
	ORA	MAXFLEN+2
	BNE	WMF1F
*

WMF1DFL	JSR	PRINT
	DFB 155
	ASC "This disk is full. "
	ASC "Use another one!"
	DFB 155,0

NXTFIL	JSR	NXTFLP
	JMP	WMF1B
**

NXTFLP	LDA	FILEPOS
	ORA	FILEPOS+1
	ORA	FILEPOS+2
	BEQ	WMF1DFM

	INC	FILEC
	BNE	WMF1DFM
	INC	FILEC+1

WMF1DFM	LDA	#0
	STA	FILEPOS
	STA	FILEPOS+1
	STA	FILEPOS+2

	RTS

*** OPEN

WMF1F	JSR	CLOSE

	LDY	#8

	LDA	FILEPOS
	ORA	FILEPOS+1
	ORA	FILEPOS+2
	BEQ	WMF1FB

	INY

WMF1FB	LDX	#$10
	LDA	#3
	STA	$342,X

	TYA
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOFNM
	BPL	WMF2ZAC
**
	JSR	CLOSE

	JSR	PRINT
	DFB 155,253,0
	JSR	CANTOPN
	JSR	PRINT
	ASC " Try another disk..."
	DFB 155,0

	JMP	NXTFIL

**WRITE FILE!

WMF2ZAC	JSR	WRTNG

*END?

WMF2	LDA	MEMADR
	CMP	MEMTOP
	BNE	WMF2A

	LDA	MEMADR+1
	CMP	MEMTOP+1
	BNE	WMF2A

	JSR	CLOSE
	BMI	ERRJ2
	RTS
*FULL?

WMF2A	LDA	MAXFLEN
	ORA	MAXFLEN+1
	ORA	MAXFLEN+2
	BNE	WMF2AB

	JSR	CLOSE
	BMI	ERRJ2

	JSR	NXTFLP
	JMP	WMF1C
*ADR

WMF2AB	LDA	MEMADR+1
	CMP	#$40
	BNE	WMF2B

	LDA	DTA2
	STA	MEMADR
	LDA	DTA2+1
	STA	MEMADR+1
*ENDADR

WMF2B	LDY	MEMTOP
	LDX	MEMTOP+1

	LDA	MEMADR+1
	CMP	#$40
	BCS	WMF2C

	CPX	#$40
	BCC	WMF2C

	LDY	#0
	LDX	#$40

WMF2C	JSR	WRF2
	JMP	WMF2
**

ERRJ2	JMP	ERROR

***

WRF2	CPX	MEMADR+1
	BCC	WRF2X
	BNE	WRF2B

	CPY	MEMADR
	BCC	WRF2X
	BEQ	WRF2X

WRF2B	TXA
	PHA

	LDX	#$10
	LDA	MEMADR
	STA	$344,X
	LDA	MEMADR+1
	STA	$345,X

	TYA
	SEC
	SBC	MEMADR
	STA	$348,X
	PLA
	SBC	MEMADR+1
	STA	$349,X
*MAX?
	LDA	MAXFLEN+2
	BNE	WRF2BB

	LDA	MAXFLEN
	CMP	$348,X
	LDA	MAXFLEN+1
	SBC	$349,X
	BCS	WRF2BB

	LDA	MAXFLEN
	STA	$348,X
	LDA	MAXFLEN+1
	STA	$349,X
*

WRF2BB	LDA	FILEPOS
	CLC
	ADC	$348,X
	STA	FILEPOS
	LDA	FILEPOS+1
	ADC	$349,X
	STA	FILEPOS+1
	BCC	WRF2C
	INC	FILEPOS+2

WRF2C	LDA	MAXFLEN
	SEC
	SBC	$348,X
	STA	MAXFLEN
	LDA	MAXFLEN+1
	SBC	$349,X
	STA	MAXFLEN+1
	BCS	WRF2D
	DEC	MAXFLEN+2

WRF2D	LDA	MEMADR
	CLC
	ADC	$348,X
	STA	MEMADR
	LDA	MEMADR+1
	ADC	$349,X
	STA	MEMADR+1
*
	LDA	#11
	STA	$342,X

	JSR	$E456
	BMI	ERRJ3

WRF2X	RTS

ERRJ3	JMP	ERROR

*** READ - FILE

RMF1	LDA	#0
	STA	MAXFLEN+2

	LDA	$2E7
	STA	MEMTOP
	LDA	$2E8
	STA	MEMTOP+1

	LDA	IN1ST
	BNE	RMFA2
*
	LDY	#5
	JSR	RMF1RD0

	LDY	#4
	STY	IN1ST

RMFA0	LDA	IDTXT,Y
	CMP	IDBUF,Y
	BEQ	RMFA1

	JMP	BADFILE

RMFA1	DEY
	BPL	RMFA0

	LDY	#3
	JSR	RMF1RD0
**

RMFA2	LDA	$2E5
	SEC
	SBC	MEMTOP
	TAY
	LDA	$2E6
	SBC	MEMTOP+1

	CMP	#1
	BCC	RMFA2A
	BNE	RMFA2B

	CPY	#9
	BCS	RMFA2B

*KONEC CTENI - NOTE

RMFA2A	LDX	#$10
	LDA	#38
	STA	$342,X

	JSR	CIOFNM
	BPL	RMFA2AB
	JMP	ERROR
*

RMFA2AB	LDX	#$10
	LDA	$34C,X
	STA	FILEPOS
	LDA	$34D,X
	STA	FILEPOS+1
	LDA	$34E,X
	STA	FILEPOS+2

	JMP	CLOSE
**

RMFA2B	LDY	#2
	JSR	RMF1RD0

	LDA	IDBUF
	ORA	IDBUF+1
	BEQ	RMFA2A
*
	LDY	#3
	JSR	RMF1RD0

	LDX	#0
	LDY	IDBUF

	CPY	#3
	BCS	RMFA2C
	INX

RMFA2C	JSR	RMF1RD
	JMP	RMFA2

***

RMF1RD0	LDX	#0

RMF1RD	STY	MEMADR
	STX	MEMADR+1

	LDA	#IDBUF:L
	STA	MAXFLEN
	LDA	#IDBUF:H
	STA	MAXFLEN+1

	LDA	MAXFLEN+2
	BEQ	RMF1R0
	JMP	RMF3
**

RMF1R0	JSR	WMFCIS

	JSR	CLOSE

	LDA	MAXFLEN+2
	BNE	RMF1C

	LDA	SRCDRIV
	CMP	DSTDRIV
	BEQ	RMF1B

	LDA	IN1ST
	BNE	RMF1C

	INC	IN1ST

RMF1B	JSR	PRINT
	DFB 155
	ASC "(Reading file "
	DFB 0

	JSR	FILECIS

	LDA	SRCDRIV
	JSR	DSKWAIT

** OPEN

RMF1C	LDA	#$FF
	STA	MAXFLEN+2

	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOFNM
	BPL	RMF2B
**
	TYA
	PHA

	JSR	CLOSE

	PLA
	CMP	#170
	BEQ	RMF2A

	JSR	PRINT
	DFB 155,0
	JSR	CANTOPN
	JSR	PRINT
	DFB 155,0

RMF2A	JMP	RMF1B

*** SET POS

RMF2B	LDA	FILEPOS
	ORA	FILEPOS+1
	ORA	FILEPOS+2
	BEQ	RMF31ST
*

	LDX	#$10
	LDA	#37
	STA	$342,X

	LDA	FILEPOS
	STA	$34C,X
	LDA	FILEPOS+1
	STA	$34D,X
	LDA	FILEPOS+2
	STA	$34E,X

	JSR	CIOFNM
	BPL	RMF31ST
**
	JMP	ERROR

*** READ IT !

RMF31ST	JSR	READING

RMF3	LDA	MEMADR
	ORA	MEMADR+1
	BNE	RMF3A
	JMP	RMF6
*

RMF3A	LDX	#$10

	LDA	MEMADR
	STA	$348,X
	LDA	MEMADR+1
	STA	$349,X

	LDA	MAXFLEN
	STA	$344,X
	LDA	MAXFLEN+1
	STA	$345,X

	LDA	#7
	STA	$342,X

	JSR	$E456
	TYA
	PHA
	LDX	#$10

	LDA	MEMADR
	SEC
	SBC	$348,X
	STA	MEMADR
	LDA	MEMADR+1
	SBC	$349,X
	STA	MEMADR+1

	LDA	MAXFLEN
	CLC
	ADC	$348,X
	STA	MAXFLEN
	LDA	MAXFLEN+1
	ADC	$349,X
	STA	MAXFLEN+1

	PLA
	BPL	RMF3

	CMP	#136
	BNE	RMF4

	INC	FILEC
	BNE	RMF3BB
	INC	FILEC+1

RMF3BB	LDA	#0
	STA	FILEPOS
	STA	FILEPOS+1
	STA	FILEPOS+2

	JMP	RMF1R0
**

RMF4	JMP	ERROR

*** ZARAD

RMF6	LDA	#IDBUF:L
	STA	MEMADR
	LDA	#IDBUF:H
	STA	MEMADR+1

RMF6B	LDA	MEMADR
	CMP	MAXFLEN
	BNE	RMF6C
	LDA	MEMADR+1
	CMP	MAXFLEN+1
	BNE	RMF6C
	RTS
*

RMF6C	LDA	MEMTOP+1
	CMP	#$40
	BNE	RMF6D

	LDA	DTA2
	STA	MEMTOP
	LDA	DTA2+1
	STA	MEMTOP+1
*

RMF6D	LDY	#0
	LDA	(MEMADR),Y
	STA	(MEMTOP),Y

	INC	MEMADR
	BNE	RMF6E
	INC	MEMADR+1

RMF6E	INC	MEMTOP
	BNE	RMF6B
	INC	MEMTOP+1
	JMP	RMF6B
****

THISISN	JSR	PRINT
	ASC "This is not "
	DFB 0
	RTS
***

CANTOPN	JSR	PRINT
	ASC "Can't open file !"
	DFB 0
	RTS
*****

FNAME	EQU	*
STATB	EQU	FNAME+34
ID1	EQU	STATB+4
ID2	EQU	ID1+$180
IDBUF	EQU	ID2+$180
TRKBUF1	EQU	IDBUF+$180
TRKBUF2	EQU	TRKBUF1+$800
MAPS	EQU	TRKBUF2+$800

***


PFNAME	EQU	FNAME
PRMEB	EQU	RMS1ETB
PRMES	EQU	RMS1ETS

****

