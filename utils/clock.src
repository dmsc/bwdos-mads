	ORG	$5FFD,$A800
	OUT	N

O	EQU	$A800-$5FFD
	JMP	UTAJ+O
***
*   PRED SAVE SPUSTIT OD $A800,
*   PAK ULOZIT OD A803 NA 6000
***

*************************************
*				*
*     " C L O C K "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EPZ	128

RELZP	EPZ	130

***

ODT0	LDA	START

	LDX	#0
	LDY	#6

ODTAJ	LDA	PRINT,X
	EOR	#$55
ODT2	STA	PRINT,X

	INX
	BNE	ODTAJ

	INC	ODTAJ+2
	INC	ODT2+2

	DEY
	BNE	ODTAJ
*
	LDA	#76
	STA	ODT0

	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BNE	STRTER

	LDA	$703
	CMP	#'B'
	BNE	STRTER
	LDA	$704
	CMP	#'W'
	BEQ	START2

STRTER	JSR	PRINT
	DFB 155,253
	ASC "Error: Not BW-DOS !"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#'N'
	BEQ	ST2ON
	CMP	#'F'
	BNE	SYNTAX
*OFF?
	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	INY
	LDA	(10),Y
	CMP	#$9B
	BEQ	START3
***

SYNTAX	JSR	PRINT
	DFB 155
	ASC "Syntax: CLOCK ON"
	DFB 155
	ASC "        CLOCK OFF"
	DFB 155,0

	JMP	(10)

*** NENI UZ ?

START3	JSR	SRCHRUT
	BCC	START4
*JE!
	JSR	PRINT
	DFB 155
	ASC "Clock already "
	ASC "installed !"
	DFB 155,0
	JMP	(10)

***INSTALL

START4	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1

*PAL/NTSC

	LDX	#8

	LDA	$D014
	AND	#$0E
	BEQ	PALNT2
*NTSC
	LDX	#17
*
PALNT2	LDY	#8

PALNT3	LDA	TDPMLTO,X
	STA	TDPMLT,Y

	DEX
	DEY
	BPL	PALNT3
	BMI	RLK0

***TABULKY
*PAL

TDPMLTO	DFB 0,0,50
	DFB 0,11,$B8
	DFB 2,$BF,32
*NTSC
	DFB 0,0,60
	DFB 0,14,$10
	DFB 3,$4B,$C0
***

*RELOK

RLK0	LDX	#0

RLK1	LDA	RELOKT,X
	STA	RELZP
	LDA	RELOKT+1,X
	STA	RELZP+1

	ORA	RELZP
	BEQ	INSTAL2

	LDA	RELOKT+2,X
	SEC
	SBC	#RESID:L
	TAY
	LDA	RELOKT+3,X
	SBC	#RESID:H
	PHA

	TYA
	LDY	#1

	CLC
	ADC	SRCHADR
	STA	(RELZP),Y
	INY
	PLA
	ADC	SRCHADR+1
	STA	(RELZP),Y

	INX
	INX
	INX
	INX

	JMP	RLK1
***

GETTDJ	JMP	($706)
SETTDJ	JMP	($708)

***

INSTAL2	JSR	GETTDJ

	LDX	#3
INST2B	LDA	$706,X
	STA	OLDADR,X
	DEX
	BPL	INST2B

**

	LDY	#RESLEN

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDX	#3
INSTL4	LDA	NEWADR,X
	STA	$706,X
	DEX
	BPL	INSTL4
**
	JSR	SETTDJ
**

	JSR	PRINT
	DFB 155
	ASC "Clock "
	ASC "Installed."
	DFB 155,0

	JMP	(10)
***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRINT
	DFB 155
	ASC "Clock"
	ASC " not Installed!"
	DFB 155,0

	JMP	DISERR2
***

DISCANT	JSR	PRINT
	DFB 155
	ASC "Clock is not the last"
	ASC " installed"
	DFB 155
	ASC " handler!"
	DFB 155,0

DISERR2	JSR	PRINT
	ASC "Can't remove."
	DFB 155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	CPY	#RESLEN
	BNE	DISCANT
*ODSTRAN

	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
*

	LDY	#(OLDADR-RESID)
	LDX	#0

DISABL3	LDA	(SRCHADR),Y
	STA	$706,X

	INY
	INX
	CPX	#4
	BCC	DISABL3
**

	JSR	PRINT
	DFB 155
	ASC "Clock"
	ASC " Removed."
	DFB 155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#S1-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#TDPLMI-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	LDY	#S19-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR4

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

**

TDINI	LDA	$D40B
	CMP	#$60
	BCS	TDINI

	SEC
	LDX	#2

TDIN2	LDA	$12,X
	PHA

S1	SBC	TDOLDS,X
S2	STA	TDDIF,X

	PLA
S3	STA	TDOLDS,X

	DEX
	BPL	TDIN2

	RTS
***

OLDADR	DFW 0,0

***

SETTD	LDY	#18

STD5	LDX	TDPOR-13,Y
	LDA	(10),Y	;DATER
S5	STA	TDOLDT,X

TST2	DEY
	CPY	#13
	BCS	STD5
	BCC	TDINI
***

TDPLMI	STA	TDPLMC

TDPLIX	LDY	#0
S7	LDX	TDPMIT,Y

	LDY	#2

TDPLM2	LDA	TDDIF,Y
TDPLMC	SBC	TDPMLT,X
S10	STA	TDDIF,Y

	DEX
	DEY
	BPL	TDPLM2

	RTS
*

TDPMIT	DFB 2,5,8

TDPOR	DFB 3,4,5,2,1
*!
TDPMLT	DFB 0,0,50
	DFB 0,11,$B8
	DFB 2,$BF,32
***

TDMAX	DFB 60,60,24,32,13,100

TDDIF	DFB 0,0,0
TDOLDS	DFB 0,0,0

* DEFAULT TD

TDOLDT	DFB 0,25,16
	DFB 11,4,94

***

GETTD	JSR	TDINI

	LDA	#2
S12	STA	TDPLIX+1

GTD1	LDA	#$FD
	SEC
S13	JSR	TDPLMI
	BCC	GTD4

S14	LDY	TDPLIX+1
GTD2	LDA	TDOLDT,Y
	ADC	#0

	CPY	#6
	BCS	GTD1

S16	CMP	TDMAX,Y
	BCC	GTD3

S17	LDA	TDMIN,Y

GTD3	STA	TDOLDT,Y
TST3	INY

	BCS	GTD2
	BCC	GTD1
*

GTD4	LDA	#$7D
S19	JSR	TDPLMI

S20	DEC	TDPLIX+1
	BPL	GTD1
*
	LDY	#13

GTD5	LDX	TDPOR-13,Y
S22	LDA	TDOLDT,X
	STA	(10),Y	;DATER

	INY
	CPY	#19
	BCC	GTD5

	LDY	#2

GTD6	LDA	TDOLDS,Y
S24	SBC	TDDIF,Y
S25	STA	TDOLDS,Y

	DEY
	BPL	GTD6

RET	RTS

TDMIN	DFB 0,0,0,1,1,0

RESLEN	EPZ	*-RESID

***

NEWADR	DFW 0,0

RELOKT	DFW	S1,TDOLDS
	DFW	S2,TDDIF
	DFW	S3,TDOLDS
	DFW	STD5,TDPOR-13
	DFW	S5,TDOLDT
	DFW	TDPLMI,TDPLMC
	DFW	S7,TDPMIT
	DFW	TDPLM2,TDDIF
	DFW	TDPLMC,TDPMLT
	DFW	S10,TDDIF
	DFW	GETTD,TDINI
	DFW	S12,TDPLIX+1
	DFW	S13,TDPLMI
	DFW	S14,TDPLIX+1
	DFW	GTD2,TDOLDT
	DFW	S16,TDMAX
	DFW	S17,TDMIN
	DFW	GTD3,TDOLDT
	DFW	S19,TDPLMI
	DFW	S20,TDPLIX+1
	DFW	GTD5,TDPOR-13
	DFW	S22,TDOLDT
	DFW	GTD6,TDOLDS
	DFW	S24,TDDIF
	DFW	S25,TDOLDS

	DFW	NEWADR-1,GETTD
	DFW	NEWADR+1,SETTD

	DFW	0,0

**************
*
* KONEC PROGRAMU, NASLEDUJE SLUZEBNI
* RUTINKA
*
**************

UTAJ	LDA	#(PRINT+O):L
	STA	0
	LDA	#(PRINT+O):H
	STA	1

UTAJ2	LDY	#0
	LDA	(0),Y
	EOR	#$55
	STA	(0),Y

	INC	0
	BNE	UTAJ3
	INC	1

UTAJ3	LDA	0
	CMP	#(UTAJ+O):L
	BNE	UTAJ2

	LDA	1
	CMP	#(UTAJ+O):H
	BNE	UTAJ2

	RTS

****

SAVEOD	EQU	$A803
SAVETO	EQU	UTAJ+O
