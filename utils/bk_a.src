	ORG	$4000,$A800
	OUT	N
*************************************
*				*
* " BACKUP "  PRO BW-DOS  -  CAST 1	*
*				*
*************************************

*PRENOS

FNAME	EQU	$51AC
RMS1ETB	EQU	$4874
RMS1ETS	EQU	$487C

**DOMYSLENO

STATB	EQU	FNAME+34
ID1	EQU	STATB+4
ID2	EQU	ID1+$180
IDBUF	EQU	ID2+$180
TRKBUF1	EQU	IDBUF+$180
TRKBUF2	EQU	TRKBUF1+$800
MAPS	EQU	TRKBUF2+$800

** KOMUNIKACE

	JMP	START
	JMP	PRINT
	JMP	DSKWAIT
	JMP	TESTID
	JMP	RSECT1
	JMP	ERROR
	JMP	RSECT
	JMP	WSECT
	JMP	SIOSRC
	JMP	SIODST
	JMP	ERR5X
	JMP	PRTDENS
	JMP	PRTEOL
	JMP	READID
	JMP	TRYMAXS
	JMP	TSTVTOC
	JMP	ZAJINSC
	JMP	FORMAT
	JMP	UVTRK
	JMP	CLOSE
	JMP	CONVDC
	JMP	PRT1

******

HEX	EPZ	128
DECIN	EPZ	131
DECOUT	EPZ	133

INMODE	EPZ	136
OUTMODE	EPZ	137

MAXSECT	EPZ	138
MAXFILE	EPZ	140
FORMFLG	EPZ	143
DENS	EPZ	144
FNMIX	EPZ	145
FILEC	EPZ	146
LASTFLG	EPZ	148
IN1ST	EPZ	149
OUT1ST	EPZ	150
FILEPOS	EPZ	151

BYTSSRC	EPZ	154
BYTSDST	EPZ	156
SRCDRIV	EPZ	158
DSTDRIV	EPZ	159

ZTSTID	EPZ	160

DTA2	EPZ	162

INSECT	EPZ	164

BUF1MP	EPZ	166
BUF2MP	EPZ	182

MEMADR	EPZ	198
MEMTOP	EPZ	200
PACKAD1	EPZ	202
PACKAD2	EPZ	204
VTOCADR	EPZ	206
PACKIX1	EPZ	208
PACKEOF1	EPZ	209
PKDAT1	EPZ	210
PKDAT2	EPZ	211

RDTRKA	EPZ	210
RDTRKS	EPZ	212

* VOLNO: 214

TMPBUF	EQU	$500

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRTEOL	LDA	#$9B

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

ERROR	STY	DECIN
	LDA	#0
	STA	DECIN+1

	JSR	PRINT
	DFB 155
	ASC "Error "
	DFB 0

	JSR	CONVDC

	LDA	DECOUT
	JSR	PRT1
	LDA	DECOUT+1
	JSR	PRT1
	LDA	DECOUT+2
	JSR	PRT1

	JSR	PRTEOL
**

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
***


CONVDC	LDX	#0

ERR2	LDY	#0

ERR3	LDA	DECIN
	CMP	ERRTAB,X
	LDA	DECIN+1
	SBC	#0
	BCC	ERR4

	INY

	LDA	DECIN
	SBC	ERRTAB,X
	STA	DECIN
	BCS	ERR3
	DEC	DECIN+1
	BCC	ERR3
**

ERR4	TYA
	ORA	#$30
	STA	DECOUT,X

	INX
	CPX	#3
	BCC	ERR2

	RTS
**

ERRTAB	DFB 100,10,1

***

GHXRTS	LDA	#1
GHXRTS2	RTS

**

GETHEX	LDA	#0
	STA	HEX
	STA	HEX+1
	STA	HEX+2

	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXRTS2

GETHX2	LDA	(10),Y

	CMP	#$9B
	BEQ	GHXRTS
	CMP	#'/'
	BEQ	GHXRTS

	INY

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	PLA
	ORA	HEX
	STA	HEX

	JMP	GETHX2
***

GHXERR	JSR	PRINT
	DFB 155
	ASC "Bad parameter !"
	DFB 155,0

	JMP	ERR5X
***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

GETNAM	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	EOR	#$9B
	BNE	GETN2
	BEQ	GETNDR
*

GETN2	LDY	#60
	LDX	#27

GETN3	LDA	(10),Y
	STA	FNAME,X

	DEY
	DEX
	BPL	GETN3

	LDA	#255
*

GETNDR	PHA

	LDY	#34
	LDA	(10),Y
	AND	#$0F
	TAX

	PLA
	RTS
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS version"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIO+1
	LDA	11
	SBC	#0
	STA	SIO+2

	LDA	10
	SEC
	SBC	#2
	STA	WRTCMD+1
	LDA	11
	SBC	#0
	STA	WRTCMD+2

*** NADPIS

	JSR	PRINT
	DFB 155
	ASC "BACKUP V1.0 for BW-D"
	ASC "OS (by BEWESOFT)"
	DFB 155,0

*** SOURCE, DEST.

	JSR	GETNAM
	STA	INMODE
	STX	SRCDRIV

	JSR	GETNAM
	STA	OUTMODE
	STX	DSTDRIV

	AND	INMODE
	BPL	START3

STRT3ER	JMP	GHXERR

***SWITCHES

START3	LDA	#0
	STA	MAXSECT
	STA	MAXSECT+1

	STA	MAXFILE
	STA	MAXFILE+1
	STA	MAXFILE+2

	STA	FORMFLG
	STA	DENS

	JSR	GETNAME
	LDY	#36
**

STRT3B	LDA	(10),Y
	CMP	#$9B
	BNE	NESTRT4
	JMP	START4

NESTRT4	CMP	#'/'
	BNE	STRT3ER

	INY
	LDA	(10),Y
	INY
	CMP	#'S'
	BEQ	STRT3S
	CMP	#'L'
	BEQ	STRT3L
	CMP	#'F'
	BNE	STRT3NF
*F
	LDA	OUTMODE
	BMI	STRT3ER

	LDX	#127

	LDA	(10),Y
	CMP	#'X'
	BNE	FFF2

	INY
	INX

FFF2	STX	FORMFLG
	JMP	STRT3B
*

STRT3NF	CMP	#'D'
	BNE	STRT3ER
*D
	LDA	INMODE
	BMI	STRT3ER

	LDA	(10),Y
	INY
	LDX	#4

STRT3D	CMP	STRT3DT-1,X
	BEQ	STRT3DB

	DEX
	BNE	STRT3D
	BEQ	STRT3ER
*

STRT3DB	STX	DENS
	BEQ	STRT3B
*

STRT3DT	ASC "SMDX"

*L

STRT3L	LDA	OUTMODE
	BPL	STRT3ERJ

	JSR	GETHEX
	BEQ	STRT3ERJ

	LDA	HEX+2
	BNE	STRT3LA
	LDA	HEX+1
	CMP	#2
	BCC	STRT3ERJ

STRT3LA	LDX	#2
STRT3LB	LDA	HEX,X
	STA	MAXFILE,X
	DEX
	BPL	STRT3LB
	BMI	STRT3B
*S

STRT3S	LDA	INMODE
	BMI	STRT3ERJ

	LDA	#1
	STA	INMODE

	JSR	GETHEX
	BEQ	STRT3B

	LDA	HEX+2
	BNE	STRT3ERJ
	LDA	HEX+1
	BNE	STRT3SB
	LDA	HEX
	CMP	#4
	BCS	STRT3SB

STRT3ERJ	JMP	STRT3ER
*

STRT3SB	LDA	HEX
	STA	MAXSECT
	LDA	HEX+1
	STA	MAXSECT+1

	JMP	STRT3B

*** PRIPRAV FNAME PRO CISLOVANI

START4	LDX	#3

STRT4A	LDA	FNAME,X

	CMP	#'*'
	BEQ	STRT4B
	CMP	#'?'
	BEQ	STRT4B
	CMP	#'_'
	BEQ	STRT4B
	CMP	#'>'
	BEQ	STRT4B
	CMP	#'<'
	BEQ	STRT4B
	CMP	#'.'
	BEQ	STRT4B

	CMP	#$30
	BCC	STRT4C
	CMP	#$3A
	BCC	STRT4B

	CMP	#$41
	BCC	STRT4C
	CMP	#$5B
	BCS	STRT4C

STRT4B	INX
	CPX	#28
	BCC	STRT4A
**

STRT4C	STX	FNMIX

STRT4D	DEX
	BMI	STRT4E
	LDA	FNAME,X

	CMP	#':'
	BEQ	STRT4E
	CMP	#'>'
	BEQ	STRT4E
	CMP	#'<'
	BEQ	STRT4E

	CMP	#'.'
	BEQ	STRT4F
	BNE	STRT4D
*

STRT4E	LDX	FNMIX

STRT4F	LDA	#'.'
	STA	FNAME,X

	INX
	STX	FNMIX

	LDA	#'0'
	STA	FNAME,X
	STA	FNAME+1,X
	STA	FNAME+2,X

	LDA	#$9B
	STA	FNAME+3,X

*** INIT

	LDX	#0
	STX	IN1ST
	STX	OUT1ST
	STX	LASTFLG

	STX	FILEPOS
	STX	FILEPOS+1
	STX	FILEPOS+2

	STX	FILEC+1
	STX	INSECT+1
	INX
	STX	FILEC
	INX
	STX	INSECT

	LDA	#MAPS:L
	STA	DTA2
	LDA	#MAPS:H
	STA	DTA2+1

	LDX	#15
	LDA	#0
STRT4G	STA	BUF1MP,X
	STA	BUF2MP,X
	DEX
	BPL	STRT4G

*** MAIN LOOP

START5	JSR	RDMEM
	JSR	WRMEM

	LDA	LASTFLG
	BEQ	START5

*** KONEC

	JSR	PRINT
	DFB 155
	ASC "Backup complete."
	DFB 155,0

	JMP	ERR5
********

TSTVTOC	LDA	INMODE
	BEQ	TSTVT1

	LDA	#0
	RTS
**

TSTVT1	TYA
	PHA

	LDA	#3
	STA	VTOCADR

TSTVT2	TXA
	LSR
	TAX
	TYA
	ROR
	TAY

	DEC	VTOCADR
	BNE	TSTVT2

	TYA
	CLC
	ADC	#MAPS:L
	STA	VTOCADR
	TXA
	ADC	#MAPS:H
	STA	VTOCADR+1

	PLA
	AND	#7
	TAX

	LDY	#0
	LDA	(VTOCADR),Y
	AND	VTOCTAB,X

	RTS
**

VTOCTAB	DFB 128,64,32,16
	DFB 8,4,2,1
****

ZAJINSC	LDX	#14

ZIS2	LDA	BUF1MP,X
	CMP	INSECT
	BNE	ZIS2B
	LDA	BUF1MP+1,X
	CMP	INSECT+1
	BEQ	ZIS3

ZIS2B	DEX
	DEX
	BPL	ZIS2
	BMI	ZIS4
*JE

ZIS3	TXA
	LSR
	CLC
	ADC	#TRKBUF1:H
	STA	PACKAD1+1

	LDA	#TRKBUF1:L
	STA	PACKAD1

	RTS

*NENI - READ TRACK

ZIS4	LDA	#0
	TAX

ZIS4A	STA	TRKBUF1,X
	STA	TRKBUF1+$100,X
	STA	TRKBUF1+$200,X
	STA	TRKBUF1+$300,X
	STA	TRKBUF1+$400,X
	STA	TRKBUF1+$500,X
	STA	TRKBUF1+$600,X
	STA	TRKBUF1+$700,X

	DEX
	BNE	ZIS4A
**
	LDA	#TRKBUF1:L
	STA	RDTRKA
	LDA	#TRKBUF1:H
	STA	RDTRKA+1

	LDA	INSECT
	STA	RDTRKS
	LDA	INSECT+1
	STA	RDTRKS+1

	LDX	#15
	LDA	#0
ZIS4B	STA	BUF1MP,X
	DEX
	BPL	ZIS4B

	TAX
**

ZIS5	LDA	MAXSECT
	CMP	RDTRKS
	LDA	MAXSECT+1
	SBC	RDTRKS+1
	BCC	ZAJINSC

	TXA
	PHA

	LDY	RDTRKS
	LDX	RDTRKS+1
	JSR	TSTVTOC
	BEQ	ZIS5B

	PLA
	TAX
	JMP	ZIS5C
*RDIT

ZIS5B	LDA	RDTRKS
	STA	$30A
	LDA	RDTRKS+1
	STA	$30B

	LDY	RDTRKA
	LDX	RDTRKA+1
	LDA	#0
	JSR	RSECT

	PLA
	TAX

	LDA	RDTRKS
	STA	BUF1MP,X
	LDA	RDTRKS+1
	STA	BUF1MP+1,X

	INX
	INX
	CPX	#16
	BCS	ZAJINSCJ

	INC	RDTRKA+1

ZIS5C	INC	RDTRKS
	BNE	ZIS5
	INC	RDTRKS+1
	JMP	ZIS5
*

ZAJINSCJ	JMP	ZAJINSC

****

NEHOT	DFB 0,1
	DFW $2000,$2006

	LDA	#NHDL-NEHOT
	STA	560
	LDA	#$20
	STA	561

NHL	CLC
	BCC	NHL

NEHRND	DFW 0

NHDL	DFB 112,112,112,112
	DFB 2+64
	DFB NHVR-NEHOT
	DFB $20
	DFB 2,65
	DFB NHDL-NEHOT
	DFB $20

NHVR	ASC %This copy isn't corr%
	ASC %ect because it isn't%

	ASC %finished. Press RESE%
	ASC %T! (BW-DOS's BACKUP)%
****

UVTRK	LDX	#14

UVTR2	LDA	BUF2MP,X
	STA	$30A
	LDA	BUF2MP+1,X
	STA	$30B
	ORA	$30A
	BEQ	UVTRK3

	TXA
	PHA

	LSR
	CLC
	ADC	#TRKBUF2:H
	TAX
	LDY	#TRKBUF2:L

	LDA	$30B
	BNE	UVTR2B
	LDA	$30A
	CMP	#4
	BEQ	UVTR4
	CMP	#1
	BNE	UVTR2B
*1
	STY	UVTRS1+1
	STX	UVTRS1+2

	LDY	#127
UVTRS1	LDA	$FFFF,Y
	STA	ID2,Y
	DEY
	BPL	UVTRS1

	LDY	UVTRS1+1
	JMP	UVTR2B
*4

UVTR4	STY	UVTRS4+1
	STX	UVTRS4+2

	LDY	#0
UVTRS4	LDA	$FFFF,Y
	STA	ID2+128,Y
	DEY
	BNE	UVTRS4

	LDY	UVTRS4+1
*

UVTR2B	LDA	#1
	JSR	WSECT

	PLA
	TAX

	LDA	#0
	STA	BUF2MP,X
	STA	BUF2MP+1,X

UVTRK3	DEX
	DEX
	BPL	UVTR2

	RTS

*********

FORMAT	LDX	#6

FORM1	LDA	RMS1ETB,X
	CMP	BYTSDST
	BNE	FORM2
	LDA	RMS1ETB+1,X
	CMP	BYTSDST+1
	BNE	FORM2

	LDA	RMS1ETS,X
	CMP	MAXSECT
	BNE	FORM2
	LDA	RMS1ETS+1,X
	CMP	MAXSECT+1
	BEQ	FORM3

FORM2	DEX
	DEX
	BPL	FORM1
**
	JSR	PRINT
	DFB 155
	ASC "Nonstandard disk - "
	DFB 0

FER2	JSR	PRINT
	ASC "Can't format !"
	DFB 155,0
	JMP	ERR5X
***

FORM3	TXA
	LSR
	PHA

	JSR	PRINT
	DFB 155
	ASC "Formatting "
	DFB 0

	PLA
	PHA
	JSR	PRTDENS

	JSR	PRINT
	ASC " density... "
	DFB 0
***
	PLA
	PHA
	CMP	#1
	BNE	FORM4
*ED
	PLA
	LDA	#$22
	JMP	FORM5
**

FORM4	LDX	#11
FORM4B	LDA	FORM4T,X
	STA	$300,X
	DEX
	BPL	FORM4B

	JSR	SIODST
	BPL	FORM4C

	PLA
	BNE	FER2
	JMP	FORM5A
**

FORM4C	PLA
	TAX

	LDA	#40
	STA	TMPBUF

	LDA	#0
	STA	TMPBUF+2
	LDA	F4CT1,X
	STA	TMPBUF+3

	LDA	F4CT2,X
	STA	TMPBUF+4

	LDA	F4CT3,X
	STA	TMPBUF+5

	LDA	F4CT4,X
	STA	TMPBUF+6
	LDA	F4CT5,X
	STA	TMPBUF+7
**
	LDX	#11
FORM4D	LDA	FOR4O,X
	STA	$300,X
	DEX
	BPL	FORM4D

	JSR	SIODST
	BPL	FORM5A
FERR	JMP	ERROR

***

FOR4O	DFB $31,0
	DFB 'O'
	DFB 128
	DFW TMPBUF,7,12,1

FORM4T	DFB $31,0
	DFB 'N'
	DFB 64
	DFW TMPBUF,7,12,1

F4CT1	DFB 18,26,18,18
F4CT2	DFB 0,0,0,1
F4CT3	DFB 0,4,4,4
F4CT4	DFB 0,0,1,1
F4CT5	DFB 128,128,0,0

FORT	DFB $31,0
FORTCMD	DFB 'R'
	DFB 64
	DFW TMPBUF,255,1,4
***

FORM5A	LDA	#$21

FORM5	ASL
	LDX	FORMFLG
	CPX	#128
	ROR

	STA	FORTCMD

	LDX	#11
FORM5B	LDA	FORT,X
	STA	$300,X
	DEX
	BPL	FORM5B

	LDA	BYTSDST
	STA	$308
	LDA	BYTSDST+1
	STA	$309

	JSR	SIODST
	BMI	FERR

	RTS

********

DSKWAIT	PHA

	JSR	PRINT
	ASC " disk into drive "
	DFB 0

	PLA
	ORA	#$30
	JSR	PRT1

	JSR	PRINT
	ASC ","
	DFB 155
	ASC "and press <RETURN>"
	ASC " ... "
	DFB 0
**

DSKW1	LDA	#$FF

DSKW2	CMP	764
	BEQ	DSKW2

	LDX	764
	STA	764

	CPX	#$1C
	BEQ	DSKWESC

	CPX	#$0C
	BNE	DSKW1

	JMP	PRTEOL
**

DSKWESC	JSR	PRINT
	DFB 155
	ASC "<Aborted>"
	DFB 155,0

	JMP	ERR5X
****

READID	PHA

	LDX	#1
	STX	$30A
	DEX
	STX	$30B

	LDY	#IDBUF:L
	LDX	#IDBUF:H

	JSR	RSECT
*
	PLA

	LDX	#4
	STX	$30A
	LDX	#0
	STX	$30B

	LDY	#(IDBUF+128):L
	LDX	#(IDBUF+128):H

*** CTENI/ZAPIS SEKTORU

RSECT	CLC
	DFB $24
WSECT	SEC

*ADR
	STY	$304
	STX	$305
*CMD
	LDX	#$40
	LDY	#'R'
	BCC	WSCT2

	LDX	#$80
WRTCMD	LDY	$FFFF

WSCT2	STY	$302
	STX	$303

*DRIVE
	TAX
	LDA	SRCDRIV,X
	STA	$301
	LDA	#$31
	STA	$300
*TIMEOUT
	LDA	#7
	STA	$306
*LEN
	TXA
	ASL
	TAX

	LDA	BYTSSRC,X
	STA	$308
	LDA	BYTSSRC+1,X
	STA	$309

	LDA	$30B
	BNE	WSCT3
	LDA	$30A
	CMP	#4
	BCS	WSCT3

	LDA	#128
	STA	$308
	ASL
	STA	$309
*R/W

WSCT3	JSR	SIO
	BPL	WSCT4

	JMP	ERROR

WSCT4	RTS

***

SIOSRC	LDA	SRCDRIV
SIOSR2	STA	$301

SIO	JMP	($FFFF)

SIODST	LDA	DSTDRIV
	JMP	SIOSR2

***

RSECT1	LDX	#1
	STX	$30A
	DEX
	STX	$30B

	LDY	#TMPBUF:L
	LDX	#TMPBUF:H
	LDA	#0
	JMP	RSECT
***

TRYMAXS	LDX	MAXSECT
	STX	$30A
	LDX	MAXSECT+1
	STX	$30B

	LDY	#TMPBUF:L
	LDX	#TMPBUF:H
	JMP	RSECT
***

TESTID	STY	ZTSTID
	STX	ZTSTID+1

	STA	TSTID3+1

	JSR	READID
*SECT1
	LDY	#127

TSTID2	LDA	(ZTSTID),Y
	CMP	IDBUF,Y
	BNE	TSTIDX

	DEY
	BPL	TSTID2
*SECT4

TSTID3	LDA	#0
	ASL
	TAX

	LDY	BYTSSRC,X

	LDA	ZTSTID
	EOR	#$80
	STA	ZTSTID
	BMI	TSTID4

	INC	ZTSTID+1

TSTID4	DEY

	LDA	(ZTSTID),Y
	CMP	IDBUF+128,Y
	BNE	TSTIDX

	TYA
	BNE	TSTID4
**

TSTIDX	RTS

***

PRTDENS	TAX
	BEQ	PRTDS
	DEX
	BEQ	PRTDM
	DEX
	BEQ	PRTDD

	JSR	PRINT
	ASC "DS/DD"
	DFB 0
	RTS

PRTDS	JSR	PRINT
	ASC "Single"
	DFB 0
	RTS

PRTDM	JSR	PRINT
	ASC "Medium"
	DFB 0
	RTS

PRTDD	JSR	PRINT
	ASC "Double"
	DFB 0
	RTS

******** DOMYSLENO

RDMEM	EQU	*
WRMEM	EQU	RDMEM+3

** PRENOS

STARTB	EQU	RDMEM
PNEHOT	EQU	NEHOT
PNEHRN	EQU	NEHRND

***

