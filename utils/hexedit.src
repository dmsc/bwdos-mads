	ORG	$6000
	JMP	START

*************************************
*				*
*  " H E X E D I T "   PRO BW-DOS	*
*				*
*************************************

HEX	EQU	128

POS	EQU	131

EDIX	EQU	134

* VOLNO: 135

NAME	EQU	$6800

EDBUF	EQU	$6900

***
	ICL	"print.inc"
***

PRTEOL	LDA	#$9B

**
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

KNAME	DTA C"K:"
	DTA 155
**

GETKEY	JSR	CLOSE2

	LDX	#$20
	LDA	#3
	STA	$342,X

	LDA	#<KNAME
	STA	$344,X
	LDA	#>KNAME
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BMI	KEYERR
**
	LDX	#$20
	LDY	#7
	JSR	GEPU
	BPL	GKEY2
**

KEYERR	LDA	#'?'

GKEY2	PHA
	JSR	CLOSE2

	PLA
	RTS
***

GEPU10	LDX	#$10

GEPU	PHA

	TYA
	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	PLA
	JMP	$E456

***

PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6
	JMP	PRT1
***

GETHEX	LDY	#0
	STY	HEX
	STY	HEX+1
	STY	HEX+2

GETHX2	CPY	#7
	BCS	GHXERR

	LDA	EDBUF,Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	PLA
	ORA	HEX
	STA	HEX

	JMP	GETHX2
**

GHXRTS	RTS

***

GHXERR	JSR	PRINT
	DTA 155
	DTA C"Bad parameter !"
	DTA 155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSE
	JMP	(10)
***

POINT	LDX	#$10
	LDA	#37
	STA	$342,X

	LDA	POS
	STA	$34C,X
	LDA	POS+1
	STA	$34D,X
	LDA	POS+2
	STA	$34E,X

	JSR	CIONAME
	BPL	GHXRTS
***
	JSR	PRINT
	DTA 155
	DTA C"Can't set "
	DTA C"file-position !"
	DTA 155,0
	JMP	ERR2X

***

CLOSE	LDX	#$10
	JSR	CLOSEX

CLOSE2	LDX	#$20

CLOSEX	LDA	#12
	STA	$342,X
	JSR	$E456
	BMI	CLERJ
	RTS

CLERJ	JMP	EDERR

***

CIONAME	LDA	#<NAME
	STA	$344,X
	LDA	#>NAME
	STA	$345,X

	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA 155,253
	DTA C"Incorrect DOS !"
	DTA 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***

	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***

	JSR	GETNAME

	LDY	#36
ST4	LDA	(10),Y
	STA	EDBUF-36,Y
	INY
	CPY	#61
	BCC	ST4

	JSR	GETHEX

	LDX	#2
ST5B	LDA	HEX,X
	STA	POS,X
	DEX
	BPL	ST5B

*** OPEN

ST8	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#12
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIONAME
	BPL	EDIT1
**
	JSR	PRINT
	DTA 155
	DTA C"Can't open file !"
	DTA 155,0
	JMP	ERR2X

*** EDIT LOOP

EDIT1	JSR	PRTEOL

	LDA	POS+2
	JSR	PRTHEX
	LDA	POS+1
	JSR	PRTHEX
	LDA	POS
	JSR	PRTHEX

	JSR	PRINT
	DTA C": "
	DTA 0

** JSME V SOUBORU? (GETLEN)

	LDX	#$10
	LDA	#39
	STA	$342,X

	JSR	CIONAME
	BPL	EDIT1B
**ERR

EDERR	JSR	PRINT
	DTA 155
	DTA C"I/O Error !"
	DTA 155,0
	JMP	ERR2X
***

EDIT1B	LDX	#$10

	LDA	POS
	CMP	$34C,X
	LDA	POS+1
	SBC	$34D,X
	LDA	POS+2
	SBC	$34E,X
	BCC	EDIT1C
*ZA
	JSR	PRINT
	DTA C"  "
	DTA 0
	JMP	EDIT1D
**V

EDIT1C	JSR	POINT

	LDY	#7
	JSR	GEPU10
	BMI	EDERR

	JSR	PRTHEX
***

EDIT1D	JSR	PRINT
	DTA C" => "
	DTA 0

	LDA	#$20
	STA	EDBUF
	STA	EDBUF+1

	LDA	#0
	STA	EDIX

EDIT2	JSR	GETKEY

	CMP	#155
	BEQ	EDIT3
	CMP	#27
	BEQ	ED2ESC
	CMP	#126
	BEQ	ED2DEL

	AND	#127
	CMP	#$30
	BCC	EDIT2
	CMP	#$3A
	BCC	EDIT2B

	AND	#$5F
	CMP	#$41
	BCC	EDIT2
	CMP	#$47
	BCS	EDIT2
**

EDIT2B	LDX	EDIX
	CPX	#2
	BCS	EDIT2

	STA	EDBUF,X
	INC	EDIX

EDIT2C	JSR	PRT1
	JMP	EDIT2
**

ED2DEL	LDX	EDIX
	BEQ	EDIT2

	DEC	EDIX
	JMP	EDIT2C
**

ED2ESC	JSR	PRTEOL
	JMP	ERR2
***

EDIT3	LDX	EDIX
	BEQ	EDIT3B

	LDA	#$9B
	STA	EDBUF,X

	JSR	GETHEX
***

	JSR	POINT

	LDA	HEX
	LDY	#11
	JSR	GEPU10
	BPL	EDIT3B
	JMP	EDERR
***

EDIT3B	INC	POS
	BNE	EDIT4
	INC	POS+1
	BNE	EDIT4
	INC	POS+2
**

EDIT4	JMP	EDIT1

****

