*************************************
*				*
*  " H E X E D I T "   FOR BW-DOS	*
*				*
*************************************

HEX	EQU	128
POS	EQU	131
EDIX	EQU	134
ERRY	EQU	135

NAME	EQU	$3800+$10	; <NAME = $10
EDBUF	EQU	NAME+$103

	ORG	$3080

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<PBADVER
	LDY	#POPENE-PBADVER+1
	JMP	EXIT

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2

*Loop twice, first copy parameter to NAME,
*second time copy to EDBUF:

GETNAME	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
STNAME	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***
	INC	STNAME+2
	DEC	COUNT1
	BPL	GETNAME

*Convert position
	JSR	GETHEX

	LDX	#2
ST5B	LDA	HEX,X
	STA	POS,X
	DEX
	BPL	ST5B

*Open file
ST8	JSR	CLOSE

	LDA	#12
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDY	#3
	JSR	CIONAME
	BPL	EDIT1
**
	LDA	#<POPENE
	LDY	#PIOE-POPENE+1
	JMP	ERR2X

*RETURN key
EDIT3	STA	EDBUF,X	;Store final EOL
	TXA
	BEQ	EDIT3B	;Skip store on empty buffer

*Parse HEX and store into file
	JSR	GETHEX
	JSR	POINT

	LDA	HEX
	LDY	#11
	JSR	GEPU10

***
EDIT3B	INC	POS
	BEQ	EDIT1
	INC	POS+1
	BEQ	EDIT1
	INC	POS+2

*Start byte edit loop
EDIT1	LDA	#$9B
	JSR	PRT1

	LDA	POS+2
	JSR	PRTHEX
	LDA	POS+1
	JSR	PRTHEX
	LDA	POS
	JSR	PRTHEX

	LDA	#<PSEMI
	LDY	#2
	JSR	PRINT

*Get length to see if we are still inside the file
	LDY	#39
	JSR	CIOERR

	LDX	#$80-2
	SEC

EDIT1B	LDA	POS+2-$80,X
	SBC	$34C+$10+2-$80,X
	INX
	BPL	EDIT1B

*Outside file, print spaces and arrow
	LDA	#<PSPACES
	LDY	#5
	BCS	EDIT1D

*Inside, get/print current byte and arrow
	JSR	POINT
	LDY	#7
	JSR	GEPU10

	JSR	PRTHEX

	LDA	#<PARROW
	LDY	#3

EDIT1D	JSR	PRINT

*Input two hex characters
	LDA	#$20
	LDX	#0

EDIT2	STX	EDIX

	JSR	PRT1
EDIT2A	JSR	GETKEY

	LDX	EDIX

	CMP	#155
	BEQ	EDIT3
	CMP	#27
	BEQ	EXITED
	CMP	#126
	BNE	ED2DEL
*DEL key
	DEX
	BPL	EDIT2

*Normal key
ED2DEL	CPX	#2
	BCS	EDIT2A

	AND	#127
	CMP	#$30
	BCC	EDIT2A
	CMP	#$3A
	BCC	EDIT2B

	AND	#$5F
	CMP	#$41
	BCC	EDIT2A
	CMP	#$47
	BCS	EDIT2A

EDIT2B	STA	EDBUF,X
	INX
	BNE	EDIT2

***
PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6
***
PRT1	LDY	#0
***
PRINT	STA	$344
	STY	$348
PRT0	LDX	#0
	STX	$349
	LDY	#>PSEMI
	STY	$345
	LDY	#11
	STY	$342
	JMP	$E456
***
*Print EOL and exit
EXITED	LDA	#$9B
	LDY	#0
	BEQ	EXIT

***
CIONAME	STY	$342+$10
	LDX	#$10
;	LDX	#<NAME	; <NAME = $10
	STX	$344+$10
	LDY	#>NAME
	STY	$345+$10
	JMP	$E456

***
CLOSE	LDY	#12

GEPU10	LDX	#0
	STX	$348+$10
	STX	$349+$10
CIOERR	JSR	CIONAME
	BPL	GHXRTS

*I/O error
	LDA	#<PIOE
	LDY	#PPOINT-PIOE+1
	BPL	ERR2X

*Call K: get handler
GETKEY	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDX	#0
	STX	$2A	;Clears ICAX1Z
***
GHXRTS	RTS

***
GETHEX	LDY	#0
	STY	HEX
	STY	HEX+1
	STY	HEX+2

GETHX2	LDA	EDBUF,Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	CPY	#7
	BCC	GETHX2
**

GHXERR	LDY	#PBADVER-PBADPAR+1
	LDA	#<PBADPAR

ERR2X	PHA
	STY	ERRY

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	PLA
	LDY	ERRY
EXIT	JSR	PRINT
	JSR	CLOSE
	JMP	(10)
***

POINT	LDX	#2

POINT1	LDA	POS,X
	STA	$34C+$10,X
	DEX
	BPL	POINT1

	LDY	#37
	JSR	CIONAME
	BPL	GHXRTS
***
	LDA	#<PPOINT
	LDY	#PEND-PPOINT+1
	BNE	ERR2X

***

COUNT1	DTA	1


PSEMI	DTA	C":"	;Next SPC included
PSPACES	DTA	C"  "
PARROW	DTA	C" =>"
PBADPAR	DTA	155
	DTA	C"Bad parameter!"
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
POPENE	DTA	155
	DTA	C"Can't open file!"
PIOE	DTA	155
	DTA	C"I/O Error!"
PPOINT	DTA	155
	DTA	C"Can't set file-position!"
PEND	DTA	155

