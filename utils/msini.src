	ORG	$3000
	JMP	START
*************************************
*				*
*  INICIALIZACE SOUBORU MSDOS.DAT	*
*				*
*************************************

ZPG	EQU	128
KX	EQU	130

ORIGS	EQU	131
NEWS	EQU	133
NEWEND	EQU	135

** VOLNO: 137



*************************************
*				*
*	    I/O MAKRA		*
*				*
*************************************
*
*	PRINT  "..."    (LPUT #0)
*
*	OPEN   IOCB,AUX1,AUX2,"FNM"
*
*	GET    IOCB	(DO A)
*
*	PUT    IOCB	(Z A)
*
*	BGET   IOCB,ADRESA,DELKA
*
*	BPUT   IOCB,ADRESA,DELKA
*
*	CLOSE  IOCB
*
*	LGET   IOCB,ADRESA,MAX.DELKA
*
*	LPUT   IOCB,ADRESA,MAX.DELKA
*
*	XIO IOCB,CMD,AUX1,AUX2,"FNM"
*
*	IXIO   IOCB,CMD,ADRESA,DELKA
*
*************************************
*
*  PRI CHYBE SKOCI NA NAVESTI ERROR.
*
*************************************
*
XIO	.MACRO	IOCB,CMD,A1,A2,FNM
	PHA
	LDX	#(:IOCB)*16
	LDA	#:A1
	STA	$34A,X
	LDA	#:A2
	STA	$34B,X
	PLA
	IXIO	:IOCB,:CMD,N@,E@-N@+5
	JMP	E@
N@	DTA	C":FNM"
	DTA	$9B
E@
	.MEND
*
***********
*
IXIO	.MACRO	IOCB,CMD,AD,DE
	PHA
	LDX	#(:IOCB)*16
	LDA	#:CMD
	STA	$342,X
	LDA	#<(:AD)
	STA	$344,X
	LDA	#>(:AD)
	STA	$345,X
	LDA	#<(:DE)
	STA	$348,X
	LDA	#>(:DE)
	STA	$349,X
	PLA
	JSR	$E456
	BPL	IE@
	JMP	ERROR
IE@
	.MEND
*
**************
*
OPEN	.MACRO	IOCB,AX1,AX2,FMN
	XIO	:IOCB,3,:AX1,:AX2,:FMN
	.MEND
*
*************
*
CLOSE	.MACRO	IOCB
	LDX	#(:IOCB)*16
	LDA	#12
	STA	$342,X
	JSR	$E456
	BPL	CLS@
	JMP	ERROR
CLS@
	.MEND
*
************
*
GET	.MACRO	IOCB
	IXIO	:IOCB,7,0,0
	.MEND
*
***********
*
PUT	.MACRO	IOCB
	IXIO	:IOCB,11,0,0
	.MEND
*
**********
*
BGET	.MACRO	IOCB,ADR,DEL
	IXIO	:IOCB,7,:ADR,:DEL
	.MEND
*
**********
*
BPUT	.MACRO	IOCB,ADR,DEL
	IXIO	:IOCB,11,:ADR,:DEL
	.MEND
*
**********
*
LGET	.MACRO	IOCB,ADR,DEL
	IXIO	:IOCB,5,:ADR,:DEL
	.MEND
*
**********
*
LPUT	.MACRO	IOCB,ADR,DEL
	IXIO	:IOCB,9,:ADR,:DEL
	.MEND
*
**********
*
MPRINT	.MACRO	TEXT
	JSR	PRINT
	DTA	C":TEXT"
	DTA	$9B
	DTA	0
	.MEND
*
*************************************
*				*
*	KONEC I/O MAKER...	*
*				*
*************************************
*
*
TOH2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	TOH3
	CLC
	ADC	#7
TOH3	RTS
*
ERROR	TYA
	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	TOH2
	STA	ERT2
	PLA
	JSR	TOH2
	STA	ERT2+1

	LPUT	0,ERT,255

*XDIV
	LDY	#8
	LDA	(10),Y
	STA	ERRXD+1
	INY
	LDA	(10),Y
	STA	ERRXD+2
	LDY	#1
ERRXD	JSR	CLS1

*
	LDA	#155
	JSR	DOSCLO

	JMP	(10)

**
ERT	DTA	125,253
	DTA	C"ERROR - $"
ERT2	DTA	C"00"
	DTA	$9B
****

START	LDA	$700
	CMP	#'S'
	BEQ	START0

	MPRINT "Incorrect DOS !"
	JMP	(10)
***

START0	JSR	CLS1
	JSR	CLS2

	MPRINT "Initializer for Micro-SpartaDOS"
	MPRINT "By BEWESOFT (1994)"
	MPRINT " "
	MPRINT "Insert disk into drive 1,"
	MPRINT "and enter path:"

	LGET	0,$403,128

	LDA	#'D'
	STA	$400
	LDA	#'1'
	STA	$401
	LDA	#':'
	STA	$402

	IXIO	1,44,$400,128

	LDA	#125
	JSR	PUT0

	LDA	#<END
	STA	ORIGS
	STA	NEWS
	LDA	#>END
	STA	ORIGS+1
	STA	NEWS+1

	JSR	OPNHV

S1	JSR	GETPOL
	BMI	S3J

	LDA	$400
	BNE	S1AA

S3J	JMP	S3
*
S1AA	AND	#$38
	CMP	#8
	BNE	S1

	JSR	JELIDAT
	BNE	S1

	JSR	CLS1

	OPEN 1,4,0,"D1:MSDOS.DAT"

	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	ORIGS
	STA	$344,X
	LDA	ORIGS+1
	STA	$345,X

	LDA	741
	SEC
	SBC	ORIGS
	STA	$348,X
	LDA	742
	SBC	ORIGS+1
	STA	$349,X

	JSR	$E456
	BPL	S2B
	CPY	#136
	BEQ	S2B
	JMP	ERROR
*
S2B	LDX	#$10
	LDA	ORIGS
	CLC
	ADC	$348,X
	STA	NEWS
	LDA	ORIGS+1
	ADC	$349,X
	STA	NEWS+1
***
S3	JSR	CLS1

	LDA	NEWS
	STA	NEWEND
	LDA	NEWS+1
	STA	NEWEND+1

	JSR	OPNHV
**
FILE	JSR	GETPOL
	BPL	FL0AA

KONJ	JMP	KON
**
FL0AA	LDA	$400
	BEQ	KONJ
	AND	#$18
	CMP	#8
	BNE	FILE

	JSR	JELIMSD
	BEQ	FILE
**
	LDX	#34
	LDA	#$20
FL2	STA	EDIT,X
	DEX
	BPL	FL2

	JSR	JELIOLD

	LDA	#0
	STA	KX
***
	LDA	#125
	JSR	PUT0

	BPUT 0,TXTENM,TXTEL

	LDA	$400
	AND	#32
	BEQ	FL2AA

	MPRINT "directory:"

	JMP	FL2AB

TXTENM	DTA C"Enter a name for this "

**

FL2AA	MPRINT "file:"

TXTEL	EQU	FL2AA-TXTENM

FL2AB	BPUT	0,$406,11
	LDA	#$9B
	JSR	PUT0
	LDA	#$9B
	JSR	PUT0

	MPRINT "(Press ESC to skip this one)"

	LDA	#1
	STA	752

EDT0	JSR	EDINV

	LDA	#0
	STA	85
	STA	86
	LDA	#5
	STA	84

	LPUT	0,ED0,128

	JSR	EDINV
*
EDT1	JSR	GETK

	CMP	#155
	BEQ	EDTX
	CMP	#27
	BEQ	EDTESC

	CMP	#$7E
	BEQ	EDDEL
	CMP	#30
	BEQ	EDLEFT
	CMP	#31
	BEQ	EDRIG

	STA	ZPG

	LDX	#0
EDT2	LDA	EDT2T,X
	BEQ	EDT3
	CMP	ZPG
	BEQ	EDT1
	INX
	JMP	EDT2
*
EDT2T	DTA 27,28,29,125,127,156
	DTA 157,158,159,253,254,255
	DTA 0
*
EDTESC	JMP	FILE
*
EDT3	LDA	ZPG
	BEQ	EDT1

	LDX	KX
	STA	EDIT,X
	CPX	#34
	BCS	EDT0J

EDRIG	INC	KX
	LDA	KX
	CMP	#35
	BCC	EDT0J
	LDA	#0
	STA	KX
EDT0J	JMP	EDT0
*
EDDEL	LDX	KX
	BEQ	EDT0J

	DEX
	STX	KX
	LDA	#32
	STA	EDIT,X
	BNE	EDT0J
*
EDLEFT	DEC	KX
	BPL	EDT0J
	LDA	#34
	STA	KX
	BNE	EDT0J
**
EDTX	LDY	#0

EDX2	LDA	$406,Y
	STA	(NEWEND),Y
	INY
	CPY	#11
	BCC	EDX2

EDX3	LDA	EDIT-11,Y
	STA	(NEWEND),Y
	INY
	CPY	#46
	BCC	EDX3
*
	LDA	NEWEND
	CLC
	ADC	#46
	STA	NEWEND
	BCC	EDX4
	INC	NEWEND+1
*
EDX4	LDA	NEWEND+1
	CLC
	ADC	#1
	CMP	742
	BCS	KON

FILEJ	JMP	FILE
**

KON	JSR	CLS1

	OPEN 1,8,0,"D1:MSDOS.DAT"

	LDX	#$10
	LDA	#11
	STA	$342,X

	LDA	NEWS
	STA	$344,X
	LDA	NEWS+1
	STA	$345,X

	LDA	NEWEND
	SEC
	SBC	NEWS
	STA	$348,X
	LDA	NEWEND+1
	SBC	NEWS+1
	STA	$349,X

	JSR	$E456
	BPL	KON2
	JMP	ERROR
*
KON2	LDA	#125
	JSR	DOSCLO

	MPRINT "Done..."

	JMP	(10)

**
EDINV	LDX	KX
	LDA	EDIT,X
	EOR	#$80
	STA	EDIT,X
	RTS
***
PUT0	PUT	0
	RTS
***

ED0	DTA	C">"
EDIT	DTA	C"                    "
	DTA	C"               <"
	DTA	$9B

***
GETPOL	LDX	#$10
	LDA	#7
	STA	$342,X
	LDA	#0
	STA	$344,X
	STA	$349,X
	LDA	#4
	STA	$345,X
	LDA	#23
	STA	$348,X
	JSR	$E456
	BPL	GP2
	CPY	#136
	BEQ	GP2
	JMP	ERROR
*
GP2	TYA
	RTS
***

JELIDAT	LDX	#10
	BNE	JLD2

JELIMSD	LDX	#7

JLD2	LDA	JLDT,X
	CMP	$406,X
	BNE	JLDX
	DEX
	BPL	JLD2

	LDY	#0
JLDX	RTS
*
JLDT	DTA	C"MSDOS   DAT"

****
*
JELIOLD	LDA	ORIGS
	STA	ZPG
	LDA	ORIGS+1
	STA	ZPG+1
*
JELIO2	LDA	ZPG+1
	CMP	NEWS+1
	BCC	JELIO3
	BNE	JELIO4
	LDA	ZPG
	CMP	NEWS
	BCS	JELIO4
*
JELIO3	LDY	#10

JELIO3B	LDA	(ZPG),Y
	CMP	$406,Y
	BNE	JELIO3C
	DEY
	BPL	JELIO3B
	BMI	JELIO5
*
JELIO3C	LDA	ZPG
	CLC
	ADC	#46
	STA	ZPG
	BCC	JELIO2
	INC	ZPG+1
	JMP	JELIO2
*
JELIO5	LDY	#11

JELIO5B	LDA	(ZPG),Y
	STA	EDIT-11,Y
	INY
	CPY	#46
	BCC	JELIO5B
*
JELIO4	RTS
***
OPNHV	OPEN 1,20,0,"D:*.*"
	RTS
***

GETK	OPEN	2,4,0,"K:"
	GET	2
	PHA

	JSR	CLS2

	PLA
CLS2X	RTS

***

DOSCLO	LDX	#0
	STX	752

	JSR	PUT0
	JSR	CLS2

CLS1	LDX	#$10
	DTA $2C
CLS2	LDX	#$20

	LDA	#12
	STA	$342,X
	JSR	$E456
	BPL	CLS2X

	JMP	ERROR
***
	ICL	"print.inc"
	ICL	"print1.inc"

END	EQU	*
