	ORG	$4000,$A800
	JMP	START

*************************************
*				*
*       " C U T "   PRO BW-DOS	*
*				*
*************************************

HEX	EPZ	128

POS	EPZ	131
LEN	EPZ	134

LENFLG	EPZ	137

MEMLEN	EPZ	138

STATUS	EPZ	140

* VOLNO: 141

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456

***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#1
GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEX
	STA	HEX+1
	STA	HEX+2

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2

GETHX2	CPY	#43
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	PLA
	ORA	HEX
	STA	HEX

	JMP	GETHX2
***

GHXERR	JSR	PRINT
	DFB 155
	ASC "Bad parameter !"
	DFB 155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSES
	JMP	(10)

***

CLOSES	LDX	#$10
	JSR	CLSS2
	LDX	#$20

CLSS2	LDA	#12
	STA	$342,X
	JSR	$E456
	BMI	IOERR
	RTS
***

IOERR	JSR	PRINT
	DFB 155
	ASC "I/O Error !"
	DFB 155,0
	JMP	ERR2X
***

CIONAM1	LDA	#NAME1:L
	STA	$344,X
	LDA	#NAME1:H
	STA	$345,X

	JMP	$E456
***

GETN2	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3

	RTS
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS !"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

*** NAMES

	JSR	GETN2

	LDX	#29
ST2B	LDA	NAME,X
	STA	NAME1,X
	DEX
	BPL	ST2B

	JSR	GETN2
***

	LDX	#0
	STX	POS
	STX	POS+1
	STX	POS+2

	STX	LENFLG

	DEX
	STX	LEN
	STX	LEN+1
	STX	LEN+2

***POS

	JSR	GETHEX
	BEQ	ST6

	LDX	#2
ST5B	LDA	HEX,X
	STA	POS,X
	DEX
	BPL	ST5B
***LEN

ST6	JSR	GETHEX
	BEQ	ST7

	LDX	#2
ST6C	LDA	HEX,X
	STA	LEN,X
	DEX
	BPL	ST6C

	STX	LENFLG

*** OPEN1

ST7	JSR	CLOSES

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIONAM1
	BPL	ST9
**

ST7ER	JSR	PRINT
	DFB 155
	ASC "Can't open file !"
	DFB 155,0
	JMP	ERR2X

*** POINT

ST9	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDX	#$10
	LDA	#37
	STA	$342,X

	LDA	POS
	STA	$34C,X
	LDA	POS+1
	STA	$34D,X
	LDA	POS+2
	STA	$34E,X

	JSR	CIONAM1
	BPL	ST10
***
	JSR	PRINT
	DFB 155
	ASC "Can't set "
	ASC "file-position !"
	DFB 155,0
	JMP	ERR2X

*** OPEN2

ST10	LDX	#$20

	LDA	#3
	STA	$342,X

	LDA	#8
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	LDA	#NAME:L
	STA	$344,X
	LDA	#NAME:H
	STA	$345,X

	JSR	$E456
	BMI	ST7ER

***COPY IT

ST11	JSR	JELI0

	BNE	ST11B
	JMP	STXX
**

ST11B	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#BUFF:L
	STA	$344,X
	LDA	#BUFF:H
	STA	$345,X

	LDA	741
	SEC
	SBC	#BUFF:L
	STA	$348,X
	LDA	742
	SBC	#BUFF:H
	STA	$349,X

	LDA	LEN+2
	BNE	ST11C

	LDA	LEN
	CMP	$348,X
	LDA	LEN+1
	SBC	$349,X
	BCS	ST11C

	LDA	LEN
	STA	$348,X
	LDA	LEN+1
	STA	$349,X
*

ST11C	JSR	$E456
	STY	STATUS
	BPL	ST11D

	CPY	#136
	BNE	IOERRJ

ST11D	LDX	#$10
	LDA	$348,X
	STA	MEMLEN
	LDA	$349,X
	STA	MEMLEN+1

	ORA	MEMLEN
	BEQ	ST12
**SAVE

	LDX	#$20
	LDA	#11
	STA	$342,X

	LDA	#BUFF:L
	STA	$344,X
	LDA	#BUFF:H
	STA	$345,X

	LDA	MEMLEN
	STA	$348,X
	LDA	MEMLEN+1
	STA	$349,X

	JSR	$E456
	BPL	ST12
IOERRJ	JMP	IOERR

***

ST12	LDA	LEN
	SEC
	SBC	MEMLEN
	STA	LEN

	LDA	LEN+1
	SBC	MEMLEN+1
	STA	LEN+1
	BCS	ST14

	DEC	LEN+2
***

ST14	LDA	STATUS
	BMI	ST14EOF

STXX	JSR	JELI0
	BEQ	ST15

	JMP	ST11
***

ST14EOF	LDA	LENFLG
	BEQ	ST15

	JSR	PRINT
	DFB 155
	ASC "EOF found !"
	DFB 155,0
**

ST15	JMP	ERR2

***

JELI0	LDA	LEN
	ORA	LEN+1
	ORA	LEN+2

	RTS
***

NAME1	EQU	*
NAME	EQU	*+30
BUFF	EQU	*+60

***
