*************************************
*				*
*       " C U T "   FOR BW-DOS	*
*				*
*************************************

HEX	EQU	128

POS	EQU	131
LEN	EQU	134

LENFLG	EQU	137

MEMLEN	EQU	138

STATUS	EQU	140

NAME1	EQU	141
NAME	EQU	141+30
* Last: 201

	ORG	$3000
***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

*Get input file name
	LDA	#<NAME1-33
	JSR	GETN2

*Get output file name
	LDA	#<NAME-33
	JSR	GETN2
***
	LDA	#0
	LDX	#5
CLN	STA	POS,X
	DEC	LEN,X
	DEX
	BPL	CLN

*Get POS
	LDA	#<POS
	JSR	GETHEX
*Get LEN
	LDA	#<LEN
	JSR	GETHEX
*X=$FF if we got a number, 0 if not
	STX	LENFLG

*OPEN 1 (input file)
ST7	JSR	CLOSES

	LDA	#4
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDA	#3
	JSR	CIONAM1
	BPL	ST9
**

ST7ER	JSR	PRINT
	DTA	155
	DTA	C"Can't open file"
	DTA	0
	BEQ	JERR2X

*POINT
ST9	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDA	POS
	STA	$34C+$10
	LDA	POS+1
	STA	$34D+$10
	LDA	POS+2
	STA	$34E+$10

	LDA	#37
	JSR	CIONAM1
	BPL	ST10
***
	JSR	PRINT
	DTA	155
	DTA	C"Can't set file-position"
	DTA	0
JERR2X	JMP	ERR2X

*OPEN 2 (output file)
ST10
	LDA	#8
	STA	$34A+$20
	LDA	#0
	STA	$34B+$20

	LDA	#<NAME
	STA	$344+$20
	LDA	#>NAME
	STA	$345+$20

	LDA	#3
	JSR	CIO20
	BMI	ST7ER

*COPY IT
ST11	LDA	LEN
	ORA	LEN+1
	ORA	LEN+2
	BNE	ST11B

	JMP	ERR2	;Close and exit
**

ST11B	LDA	#<BUFF
	STA	$344+$10
	LDA	#>BUFF
	STA	$345+$10

	LDA	741
	SEC
	SBC	#<BUFF
	STA	$348+$10
	LDA	742
	SBC	#>BUFF
	STA	$349+$10

	LDA	LEN+2
	BNE	ST11C

	LDX	LEN
	CPX	$348+$10
	LDA	LEN+1
	SBC	$349+$10
	BCS	ST11C

	STX	$348+$10
	LDA	LEN+1
	STA	$349+$10
*

ST11C	LDA	#7
	JSR	CIO10
	STY	STATUS
	BPL	ST11D

	CPY	#136
	BNE	IOERRJ

ST11D	LDA	$348+$10
	STA	MEMLEN
	LDA	$349+$10
	STA	MEMLEN+1

	ORA	MEMLEN
	BEQ	ST12
**SAVE
	LDA	#<BUFF
	STA	$344+$20
	LDA	#>BUFF
	STA	$345+$20

	LDA	MEMLEN
	STA	$348+$20
	LDA	MEMLEN+1
	STA	$349+$20

	LDA	#11
	JSR	CIO20
	BPL	ST12
IOERRJ	JMP	IOERR

***

ST12	LDA	LEN
	SEC
	SBC	MEMLEN
	STA	LEN

	LDA	LEN+1
	SBC	MEMLEN+1
	STA	LEN+1
	BCS	ST14

	DEC	LEN+2
***

ST14	LDA	STATUS
	BMI	ST14EOF

	JMP	ST11
***

ST14EOF	LDA	LENFLG
	BEQ	ST15

	JSR	PRINT
	DTA	155
	DTA	C"EOF found!"
	DTA	155,0
**

ST15	JMP	ERR2

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GHXRTS	CPY	#37
	BEQ	GHXR3

	LDX	#2
GHXR1	LDA	HEX,X
GHXR2	STA	POS,X
	DEX
	BPL	GHXR1

GHXR3	RTS

**

GETHEX	STA	GHXR2+1
	JSR	GETNAME

*Make sure we return X=0 when empty parameter
	LDX	#0
	STX	HEX
	STX	HEX+1
	STX	HEX+2

	LDY	#36

GETHX2	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	CPY	#43
	BCC	GETHX2

GHXERR	JSR	PRINT
	DTA	155
	DTA	C"Bad parameter"
	DTA	0

ERR2X	JSR	PRINT
	DTA	C"!"
	DTA	155,0

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSES
	JMP	(10)

***

CLOSES	LDX	#$10
	JSR	CLSS2
	LDX	#$20

CLSS2	LDA	#12
	STA	$342,X
	JSR	$E456
	BMI	IOERR
	RTS
***

IOERR	JSR	PRINT
	DTA	155
	DTA	C"I/O Error"
	DTA	0
	BEQ	ERR2X
***

CIONAM1	LDX	#<NAME1
	STX	$344+$10
	LDX	#>NAME1
	STX	$345+$10
CIO10	LDX	#$10
CIOCMD	STA	$342,X
	JMP	$E456

CIO20	LDX	#$20
	BNE	CIOCMD
***

GETN2	STA	ST4+1
	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
ST4	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3

	RTS

BUFF	EQU	*

***
