	OUT	N
*************************************
*				*
*  " M E N U " PRO BW-DOS - CAST 3  *
*				*
*************************************

** PRENOS

START3	EQU	$5725
BDIR	EQU	$4C23
BTEST	EQU	$4BEA
BREAD	EQU	$4CB5
BWRITE	EQU	$4D59
BRENAME	EQU	$4DC7
BAINIT	EQU	$4701
BCHKDSK	EQU	$4BA9
BXIO	EQU	$4D8F
CLS	EQU	$4DEE
PRINT	EQU	$4E71
REOPNS	EQU	$4EAE
GETKEY	EQU	$4EDD
CONVDC	EQU	$4F22
MENU	EQU	$4FA1
EDIT	EQU	$505E
EDIT0	EQU	$5061
PRTPATH	EQU	$515E
CLTAB	EQU	$51CA
CLLIN	EQU	$52B2
DIRADR	EQU	$548B
INISCR	EQU	$5351
PRTTAB	EQU	$54CD
PRTCMD	EQU	$5395
INIS2S	EQU	$5347
CLDATB	EQU	$5153
RDDIR	EQU	$5630
RESORT	EQU	$568B

LOOPTX	EQU	$485D

** DOMYSLENO

L EQU LOOPTX

ERRTX	EQU	L+$29
SRTM	EQU	L+$34
NDSKT	EQU	L+$6A
NDSKM	EQU	L+$7B
WKTX	EQU	L+$9D
SELTX	EQU	L+$AD
DESTX	EQU	L+$AF
SDTX	EQU	L+$B3
DIT1	EQU	L+$C0
DIT2	EQU	L+$C7
DIT3	EQU	L+$D4
DIT4	EQU	L+$E0
DI1	EQU	L+$EB
DI2	EQU	L+$10C
DI3	EQU	L+$12D
DI4	EQU	L+$14E
NOTSAM	EQU	L+$16F
MDTX	EQU	L+$17D
RNTX	EQU	L+$18D
AINM	EQU	L+$198
AINS	EQU	L+$1CE
AIND	EQU	L+$1EC
FRMTNG	EQU	L+$20D
MTTX	EQU	L+$21C
PRESSK	EQU	L+$9C
COPTX1	EQU	L+$226
COPMEN	EQU	L+$237
COPTX2	EQU	L+$252
SRCTX	EQU	L+$25E
DSTTX	EQU	L+$275

** DEFINICE

TFST1	EQU	$480
TFST2	EQU	$491
TFADRL	EQU	$4A2
TFADRH	EQU	$4B4
TFLENL	EQU	$4C6
TFLENH	EQU	$4D8
TFF	EQU	$4EA

**
	ORG	START3,$A800
**

DRIVNUM	EPZ	131

RWADR	EPZ	138
RWLEN	EPZ	140

TEMP25	EPZ	142

POS	EPZ	148

**

PRTADR	EPZ	153
PRTSAD	EPZ	155
PRTMSK	EPZ	157

EDPOS	EPZ	158
EDMAX	EPZ	159
EDX	EPZ	160
EDY	EPZ	161

**

MEM2	EPZ	162

DIRPOC	EPZ	164
MDIRPOS	EPZ	165
DIRSPOS	EPZ	166

CMDPOS	EPZ	167
CMDSPOS	EPZ	168

DRIVE	EPZ	169

MT1	EPZ	170
PTTY	EPZ	172
PTTP	EPZ	173
PTTI	EPZ	174

SORTA	EPZ	175
MT2	EPZ	177
SORTM	EPZ	179

EXP	EPZ	180
EXF	EPZ	181

INFLG	EPZ	182
OUTFLG	EPZ	183
INPOS	EPZ	184
OUTPOS	EPZ	187
COPIX	EPZ	190
COPTOP	EPZ	191
COPDSTD	EPZ	192
COPSAME	EPZ	193
IN1ST	EPZ	194
OUT1ST	EPZ	195
CRA	EPZ	196
COPENDF	EPZ	198
COPNEWF	EPZ	199

* VOLNO: 200

**

EOL	MACRO
	DFB 155
	MEND

****

COPDNUM	LDA	DRIVE
	STA	DRIVNUM
	RTS
**

PATHCL	LDA	#'>'
	STA	PATH
	LDA	#155
	STA	PATH+1
	RTS
**

NEWDISK	JSR	PATHCL

NEWDIR	JSR	COPDNUM
	JSR	RDDIR
	BPL	NEWD2

	LDA	PATH
	CMP	#'>'
	BNE	NEWDISK
	LDA	PATH+1
	CMP	#155
	BNE	NEWDISK

	LDA	#0
	STA	DIRPOC
	STA	MDIRPOS
	STA	DIRSPOS

	LDA	#MEMBUF:L
	STA	MEM2
	LDA	#MEMBUF:H
	STA	MEM2+1

NEWD2	LDA	DSKSTAT
	BNE	NEWD3

	LDA	#155
	STA	PATH
	JSR	PRTPATH

NEWD3	RTS

**

START	LDX	#0
	STX	CMDPOS
	STX	CMDSPOS
	STX	SORTM
	INX
	STX	DRIVE

	JSR	PATHCL
	JSR	CLTDOV

	JSR	REOPNS
	JSR	INISCR

DIRLP	JSR	PATHCL

NEWLP	JSR	NEWDIR

TABLP	JSR	PRTTAB
	JMP	LP0
*
CMDLP	JSR	PRTCMD

LP0	LDX	#$FF
	TXS

	LDA	#LOOPTX:L
	LDX	#LOOPTX:H
	LDY	#20
	JSR	INIS2S

LP1	JSR	GETKEY

	CMP	#155
	BEQ	LPRETQ

	CMP	#$1E
	BEQ	LPL
	CMP	#$1F
	BEQ	LPR
	CMP	#$1C
	BEQ	LPU
	CMP	#$1D
	BEQ	LPDQ

	AND	#127

	CMP	#'+'
	BEQ	LPL
	CMP	#'*'
	BEQ	LPR
	CMP	#'-'
	BEQ	LPU
	CMP	#'='
	BEQ	LPDQ

	CMP	#32
	BEQ	LPSPCQ
*POVEL?
	LDX	#17
LP2	CMP	LOP2T,X
	BEQ	LP3
	DEX
	BPL	LP2
	BMI	LP1
*
LP3	STX	CMDPOS
	DEX
	BPL	LP3B
	LDX	#17
LP3B	STX	CMDSPOS

CMDLJ	JMP	CMDLP
**
LPL	LDX	CMDPOS
	TXA

	DEX
	BPL	LPL2
	LDX	#17

LPL2	STX	CMDPOS
	CMP	CMDSPOS
	BNE	CMDLJ2

	STX	CMDSPOS

CMDLJ2	JMP	CMDLP
**
LPRETQ	BEQ	LPRET
LPSPCQ	BEQ	LPSPC
LPDQ	BEQ	LPD
**
LPR	LDA	CMDPOS
	JSR	LPRS
	STA	CMDPOS

	SEC
	SBC	CMDSPOS
	BCS	LPR2
	ADC	#18
LPR2	CMP	#4
	BCC	CMDLJ2

	LDA	CMDSPOS
	JSR	LPRS
	STA	CMDSPOS

CMDLJ3	JMP	CMDLP
**UP
LPU	LDA	MDIRPOS
	BNE	LPU2

	LDA	DIRPOC
	STA	MDIRPOS

	SEC
	SBC	#15
	BCS	LPU1
	LDA	#0

LPU1	STA	DIRSPOS

LPU2	DEC	MDIRPOS
*MOVE SCR?

	LDA	DIRSPOS
	CMP	MDIRPOS
	BCC	TABLJ1

	LDA	MDIRPOS
	SBC	#1
	BCS	LPU3
	LDA	#0
LPU3	STA	DIRSPOS

TABLJ1	JMP	TABLP
**SPACE
LPSPC	LDA	MDIRPOS
	JSR	DIRADR

	LDY	#0
	LDA	(MT1),Y
	EOR	#8
	STA	(MT1),Y
**DOLU
LPD	INC	MDIRPOS
	LDA	MDIRPOS
	CMP	DIRPOC
	BCC	LPD2

	LDA	#0
	STA	MDIRPOS
	STA	DIRSPOS

*MOVE SCR?
LPD2	SEC
	SBC	DIRSPOS
	CMP	#15
	BCC	TABLJ1

	LDA	MDIRPOS
	SEC
	SBC	#14
	STA	DIRSPOS
	JMP	TABLP
**RETURN
LPRET	LDA	CMDPOS
	ASL
	TAX

	LDA	LPRTTB,X
	STA	MT1
	LDA	LPRTTB+1,X
	STA	MT1+1

	JMP	(MT1)
**

LPRTTB	DFW NDSK,DSKINFO,SUBDIR
	DFW UPDIR,MKDIR,DLDIR,COPY
	DFW ERASE,RENAME,PROTECT
	DFW UNPROT,VIEWTX,MAKETX
	DFW AINIT,EXIT,SORTING
	DFW SELECT,DESEL
**

LOP2T	ASC "FZTYNDCERPUVIAQS>"
	DFB 126
**

LPRS	CLC
	ADC	#1
	CMP	#18
	BCC	LPRS2
	SBC	#18
LPRS2	RTS

**

EXIT	JSR	CLTDOV
	JSR	REOPNS
	JMP	(10)
**

ERR	TYA
	PHA

	JSR	INISCR

	LDY	#20
	LDX	#ERRTX:H
	LDA	#ERRTX:L
	JSR	INIS2S

	PLA
	TAY
	LDA	#0
	TAX
	JSR	CONVDC

	LDA	#155
	STA	DATBUF+8
	LDA	#(DATBUF+5):L
	STA	PRTADR
	LDA	#(DATBUF+5):H
	STA	PRTADR+1
	LDY	#20
	LDX	#6
	LDA	#0
	JSR	PRINT

	LDX	#11
	JSR	WAITKEY

	JMP	NEWLP
**

NDSK	JSR	CLLIN

	LDA	#NDSKT:L
	LDX	#NDSKT:H
	LDY	#20
	JSR	INIS2S

	JSR	DRIVMEN
	STA	DRIVE

	JMP	DIRLP
**

SORTING	JSR	CLLIN

	LDY	#SRTM:L
	LDX	#SRTM:H
	JSR	MENU

	TAX
	BMI	ABORT1

	STA	SORTM
	JSR	RESORT

ABORT1	JMP	TABLP
**

DRIVMEN	LDY	#NDSKM:L
	LDX	#NDSKM:H
	JSR	MENU

	TAX
	BMI	ABORT1

	INX
	CPX	#5
	BCC	NDSK2
	LDX	#8

NDSK2	TXA
	RTS
**

WAITKEY	LDA	#WKTX:L
	STA	PRTADR
	LDA	#WKTX:H
	STA	PRTADR+1
	LDY	#20
	LDA	#0
	JSR	PRINT

	JMP	GETKEY
**

CONVFNM	LDX	#0
	LDY	#0
	LDA	#8
	JSR	CNVF2

	CMP	#'.'
	BEQ	CNVF1

	CMP	#155
	BNE	CNVFE
	DEX

CNVF1	LDA	#11
	JSR	CNVF2

	CMP	#155
	BNE	CNVFE
	RTS
**
CNVFE	LDY	#165
	JMP	ERR
**
CNVF2	STA	MT1

CNVF3	LDA	DATBUF,X
	INX
	CPY	MT1
	BEQ	CNVF3C

	CMP	#'*'
	BEQ	CNVF3H

	CMP	#'?'
	BEQ	CNVF3B
	CMP	#'_'
	BEQ	CNVF3B
	CMP	#$30
	BCC	CNVF3C
	CMP	#$3A
	BCC	CNVF3B
	CMP	#$41
	BCC	CNVF3C
	CMP	#$5B
	BCS	CNVF3C

CNVF3B	STA	FNAME,Y
	INY
	BNE	CNVF3
*
CNVF3H	LDA	DATBUF,X
	INX

	PHA
	LDA	#'?'
	BNE	CNVF3D
**

CNVF3C	PHA
	LDA	#32

CNVF3D	CPY	MT1
	BCC	CNVF3E

	PLA
ZEROLX	RTS
*
CNVF3E	STA	FNAME,Y
	INY
	BNE	CNVF3D
**

ZEROL	LDA	DIRPOC
	BNE	ZEROLX
	JMP	TABLP
**

SELECT	JSR	CLLIN

	LDA	#SELTX:L
	LDX	#SELTX:H
	LDY	#20
	JSR	INIS2S

	LDX	#1
	LDA	#0
	BEQ	SELDS2
*
DESEL	JSR	CLLIN

	LDA	#DESTX:L
	LDX	#DESTX:H
	LDY	#20
	JSR	INIS2S

	LDX	#3
	LDA	#8
*
SELDS2	STA	PTTI
	STX	PTTP

	JSR	ZEROL

	LDA	#SDTX:L
	STA	PRTADR
	LDA	#SDTX:H
	STA	PRTADR+1
	LDY	#20
	LDA	#0
	JSR	PRINT
*INPUT
	JSR	CLDATB
	LDA	#'*'
	STA	DATBUF
	STA	DATBUF+2
	LDA	#'.'
	STA	DATBUF+1

	LDA	PTTP
	CLC
	ADC	#13
	TAX
	LDY	#20
	LDA	#12
	JSR	EDIT0

	TAX
	BPL	SELD3
ABORT2	JMP	TABLP
*
SELD3	JSR	CONVFNM

	LDA	#0
	STA	PTTP
**

SELD4	LDA	PTTP
	CMP	DIRPOC
	BCC	SELD5
	JMP	TABLP
*
SELD5	LDA	PTTP
	JSR	DIRADR

	LDY	#6

SELD5B	LDA	FNAME-6,Y
	CMP	#'?'
	BEQ	SELD5C

	CMP	(MT1),Y
	BNE	SELD5D

SELD5C	INY
	CPY	#17
	BCC	SELD5B
*NASEL
	LDY	#0
	LDA	(MT1),Y
	AND	#255-8
	ORA	PTTI
	STA	(MT1),Y
*
SELD5D	INC	PTTP
	JMP	SELD4
**

DICLR	LDX	#32
	TXA
DICLR1	STA	DIRBUF,X
	DEX
	BPL	DICLR1

	LDA	#'|'
	STA	DIRBUF
	STA	DIRBUF+31
	LDA	#155
	STA	DIRBUF+32
	RTS
**

DSKINM1	PHA
	LDA	#0

	JSR	CONVDC

	PLA
	TAX
	LDY	#5

DSKINM1B	LDA	DATBUF,Y
	STA	DIRBUF,X
	INX
	INY
	CPY	#8
	BCC	DSKINM1B

	RTS
**

DSKINM2	LDY	#0
	BIT	DSKST2+1
	BPL	DSKINM2A

	LSR
	PHA
	TXA
	ROR
	TAX
	TYA
	ROR
	TAY
	PLA

DSKINM2A	JSR	CONVDC

	LDX	#15
	LDY	#0
	BEQ	DSKINM1B
**

DIPRTDB	LDA	#DIRBUF:L
	LDX	#DIRBUF:H
*
DIPRT	STA	PRTADR
	STX	PRTADR+1

	LDA	#0
	LDX	#4
	JMP	PRINT
**
DSKIE	JMP	ERR
**
DSKINFO	JSR	COPDNUM

	JSR	BCHKDSK
	BMI	DSKIE

	LDY	#6
	LDX	#DI1:H
	LDA	#DI1:L
	JSR	DIPRT

	LDY	#7
	LDX	#DI2:H
	LDA	#DI2:L
	JSR	DIPRT

	LDY	#8
	LDX	#DI3:H
	LDA	#DI3:L
	JSR	DIPRT
*VOLUME
	JSR	DICLR

	LDA	DSKST2
	BEQ	DSKI2
*
	LDX	#6
DSKI1A	LDA	DIT1,X
	STA	DIRBUF+7,X
	DEX
	BPL	DSKI1A
*
	LDX	#7
DSKI1B	LDA	DSKST2+6,X
	STA	DIRBUF+15,X
	DEX
	BPL	DSKI1B
*
	LDA	#24
	LDY	DSKST2+14
	LDX	#0
	JSR	DSKINM1

	LDA	#28
	LDY	DSKST2+15
	LDX	#0
	JSR	DSKINM1
*
DSKI2	LDY	#9
	JSR	DIPRTDB
*B/SECT
	JSR	DICLR

	LDX	#12
DSKI3	LDA	DIT2,X
	STA	DIRBUF+1,X
	DEX
	BPL	DSKI3

	INX
	LDY	DSKST2+1
	BNE	DSKI3B
	INX

DSKI3B	LDA	#15
	JSR	DSKINM1

	LDY	#10
	JSR	DIPRTDB
*TOTAL
	JSR	DICLR

	LDX	#11
DSKI4A	LDA	DIT3,X
	STA	DIRBUF+2,X
	DEX
	BPL	DSKI4A

	LDX	DSKST2+2
	LDA	DSKST2+3
	JSR	DSKINM2

	LDY	#11
	JSR	DIPRTDB
*FREE
	JSR	DICLR

	LDX	#10
DSKI5A	LDA	DIT4,X
	STA	DIRBUF+3,X
	DEX
	BPL	DSKI5A

	LDX	DSKST2+4
	LDA	DSKST2+5
	JSR	DSKINM2

	LDY	#12
	JSR	DIPRTDB
**
	LDY	#13
	LDX	#DI4:H
	LDA	#DI4:L
	JSR	DIPRT
**
	JSR	CLLIN

	LDX	#0
	JSR	WAITKEY
	JMP	TABLP
**

SAMETST	JSR	COPDNUM
	JSR	BTEST
	BMI	SAMEER
	RTS
*
SAMEER2	JMP	ERR
*
SAMEER	CPY	#150
	BNE	SAMEER2
*
	JSR	CLLIN

	LDY	#20
	LDX	#NOTSAM:H
	LDA	#NOTSAM:L
	JSR	INIS2S

	LDX	#14
	JSR	WAITKEY

	JMP	DIRLP
**

SUBDIR	JSR	ZEROL

*JE TO DIR?

	LDA	MDIRPOS
	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	SBD0
	JMP	TABLP
**
SBD0	LDX	#0

SBD1	LDA	PATH,X
	CMP	#155
	BEQ	SBD2

	INX
	CPX	#$D9
	BCC	SBD1

*PATH TOO LONG

	LDY	#165
	JMP	ERR
*PRIDEJ

SBD2	LDY	#6
	LDA	#14
	JSR	SBD2D

	LDY	#14
SBD2B	LDA	(MT1),Y
	CMP	#32
	BNE	SBD2C
	INY
	CPY	#17
	BCC	SBD2B
	BCS	SBD2CB
*
SBD2C	LDA	#'.'
	JSR	SBD3D

	LDY	#14
	LDA	#17
	JSR	SBD2D

SBD2CB	LDA	#'>'
	JSR	SBD3D
	LDA	#155
	JSR	SBD3D
**
	JSR	SAMETST
	JMP	NEWLP
**
SBD3D	STA	PATH,X
	INX
SBD2F	RTS
**
SBD2D	STA	PTTI

SBD2E	CPY	PTTI
	BCS	SBD2F

	LDA	(MT1),Y
	INY
	CMP	#32
	BEQ	SBD2F

	JSR	SBD3D
	JMP	SBD2E
**

UPDBK	DEX
	CPX	#$FF
	BEQ	UPDNO
	RTS
**
UPDNO	JMP	TABLP
**

UPDIR	LDX	#0
UPD1	LDA	PATH,X
	CMP	#155
	BEQ	UPD2

	INX
	BNE	UPD1
**

UPD2	JSR	UPDBK

UPD2B	JSR	UPDBK
	LDA	PATH,X
	CMP	#'>'
	BNE	UPD2B

	LDA	#155
	STA	PATH+1,X
**
	JSR	SAMETST
	JMP	NEWLP
**

MKDIR	JSR	CLLIN

	LDA	#MDTX:L
	LDX	#MDTX:H
	LDY	#20
	JSR	INIS2S

	LDX	#16
	LDY	#20
	LDA	#12
	JSR	EDIT

	TAX
	BPL	MKDIR2
	JMP	TABLP
*
MKDIR2	JSR	CONVFNM

	JSR	SAMETST

	LDA	#42
	JSR	BXIO
	BMI	MKDER

	JMP	NEWLP
**
MKDER	JMP	ERR
**

PLACE	STA	MDIRPOS

	SEC
	SBC	DIRSPOS
	BCC	PLACP
	CMP	#16
	BCC	PLAC2

	LDA	MDIRPOS
	SEC
	SBC	#15
	JMP	PLAC1
*
PLACP	LDA	MDIRPOS
PLAC1	STA	DIRSPOS

PLAC2	JMP	PRTTAB

**

SRCH	INC	EXP

	LDA	EXP
	CMP	DIRPOC
	BCC	SRCH2

	LDA	#255
	RTS
*
SRCH2	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	AND	#8
	BNE	SRCH

	RTS
**

EXE	STY	EXEJP+1
	STX	EXEJP+2
	STA	EXF

	JSR	ZEROL
	JSR	SAMETST
	JSR	CLLIN

	LDA	#$FF
	STA	EXP

	JSR	SRCH
	BMI	EXENE

EXE2	LDA	EXP
	JSR	EXEN2

	LDA	EXP
	JSR	ODOZN

	JSR	SRCH
	BPL	EXE2
	BMI	EXE3
**
EXENE	LDA	MDIRPOS
	JSR	EXEN2
*
EXE3	LDA	EXF
	BMI	EXE4
	JMP	TABLP
*
EXE4	JSR	NEWLP
**

EXEN2	STA	EXP
	JSR	COPPLC2

EXEJP	JSR	$E474
	BMI	EXEN2E
	RTS
**
EXEN2E	JMP	ERR

**

GETNAME	LDY	#6

EXEN2B	LDA	(MT1),Y
	STA	FNAME-6,Y
	INY
	CPY	#17
	BCC	EXEN2B

	RTS
**

DLDIR	LDY	#RDLD:L
	LDX	#RDLD:H
	LDA	#255
	JMP	EXE
**

RDLD	LDA	#43
	DFB $2C
RERA	LDA	#33
	JMP	BXIO

**

ERASE	LDY	#RERA:L
	LDX	#RERA:H
	LDA	#255
	JMP	EXE
**

PROTECT	LDY	#RPRO:L
	LDX	#RPRO:H
	LDA	#0
	JMP	EXE
**

UNPROT	LDY	#RUNPR:L
	LDX	#RUNPR:H
	LDA	#0
	JMP	EXE
**

RPRO	LDX	#35
	LDA	#1
	BNE	RPRUN
*
RUNPR	LDX	#36
	LDA	#0

RPRUN	LDY	#0
	STA	PTTI
	LDA	(MT1),Y
	AND	#254
	ORA	PTTI
	STA	(MT1),Y

	TXA
	JMP	BXIO
**

RENAME	JSR	CLLIN

	LDA	#RNTX:L
	LDX	#RNTX:H
	LDY	#20
	JSR	INIS2S

	LDX	#11
	LDY	#20
	LDA	#12
	JSR	EDIT

	TAX
	BPL	RNM2
	JMP	TABLP
*
RNM2	JSR	CONVFNM

	LDX	#10
RNM3	LDA	FNAME,X
	STA	$4C0,X
	DEX
	BPL	RNM3

	LDY	#RRNM:L
	LDX	#RRNM:H
	LDA	#128
	JMP	EXE
**

RRNM	LDX	#10

RRNM2	LDA	$4C0,X
	CMP	#'?'
	BNE	RRNM3
	LDA	FNAME,X

RRNM3	STA	FNAME2,X

	DEX
	BPL	RRNM2

*NENI UZ?

	LDA	#255
	STA	PTTP

RRNM4C	INC	PTTP
RRNM4	LDA	PTTP
	CMP	DIRPOC
	BCS	RRNM5

	JSR	DIRADR

	LDY	#6
RRNM4B	LDA	(MT1),Y
	CMP	FNAME2-6,Y
	BNE	RRNM4C

	INY
	CPY	#17
	BCC	RRNM4B

	LDA	PTTP
	CMP	EXP
	BEQ	RRNM4C

	LDY	#151
	JMP	ERR
*
RRNM5	LDA	EXP
	JSR	DIRADR

	LDY	#6
RRNM6	LDA	FNAME2-6,Y
	STA	(MT1),Y
	INY
	CPY	#17
	BCC	RRNM6

	JMP	BRENAME
**
ABORTA	JMP	TABLP
**

AINIT	JSR	CLLIN

*DRIVE
	LDY	#AINM:L
	LDX	#AINM:H
	JSR	MENU

	TAX
	BMI	ABORTA

	INX
	TXA
	PHA
*DENS
	LDY	#AIND:L
	LDX	#AIND:H
	JSR	MENU
	TAX
	BMI	ABORTA

	PHA
*
	LDY	#AINS:L
	LDX	#AINS:H
	JSR	MENU

	TAX
	BMI	ABORTA
	BEQ	ABORTA
*
	JSR	CLLIN

	LDA	#FRMTNG:L
	LDX	#FRMTNG:H
	LDY	#20
	JSR	INIS2S

	PLA
	STA	RWLEN
	PLA
	STA	DRIVE

	JSR	PATHCL
	JSR	CLTAB
	JSR	COPDNUM

	JSR	BAINIT
	BMI	AINIER

	JMP	DIRLP
**
AINIER	JMP	ERR
**

VIEWER	JMP	TABLP
**
VIEWTX	JSR	ZEROL

	LDA	MDIRPOS
	JSR	DIRADR

	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	VIEWER

	JSR	GETNAME

	LDX	#0
	LDY	#255
	BNE	MKT2
**

MAKETX	JSR	CLLIN

	LDA	#MTTX:L
	LDX	#MTTX:H
	LDY	#20
	JSR	INIS2S

	LDX	#10
	LDY	#20
	LDA	#12
	JSR	EDIT

	TAX
	BMI	VIEWER

	JSR	CONVFNM

	LDX	#255
	LDY	#0
**
MKT2	STX	INFLG
	STY	OUTFLG
*
	LDX	#$E8
MKT2A	DEX
	LDA	PATH,X
	STA	PATH2,X
	TXA
	BNE	MKT2A
*
	JSR	SAMETST
	JSR	REOPNS

	LDA	OUTFLG
	STA	$2FE

	LDA	#0
	STA	POS
	STA	POS+1
	STA	POS+2
	STA	COPTOP
*IN

MKT3	LDY	#0
	STY	INPOS
	STY	INPOS+1
	STY	OUTPOS
	STY	OUTPOS+1

	LDA	$2E7
	STA	RWADR
	LDA	$2E8
	STA	RWADR+1

*Y JE 0
	LDX	#$40
	JSR	MKTIN

	STY	INPOS
	STX	INPOS+1

	LDA	COPTOP
	BMI	MKT4

	LDA	MEM2
	STA	RWADR
	LDA	MEM2+1
	STA	RWADR+1

	LDY	$2E5
	LDX	$2E6
	JSR	MKTIN

	STY	OUTPOS
	STX	OUTPOS+1
*OUT

MKT4	LDA	$2E7
	STA	RWADR
	LDA	$2E8
	STA	RWADR+1

	LDY	INPOS
	LDX	INPOS+1
	JSR	MKTOUT

	LDA	MEM2
	STA	RWADR
	LDA	MEM2+1
	STA	RWADR+1

	LDY	OUTPOS
	LDX	OUTPOS+1
	JSR	MKTOUT
*
	LDA	COPTOP
	BPL	MKT3
**
	LDA	OUTFLG
	BPL	MKT5
*
	LDA	#PRESSK:L
	STA	RWADR
	LDA	#PRESSK:H
	STA	RWADR+1

	LDY	#15
	LDX	#0
	JSR	MKTOUT

	JSR	GETKEY
**

MKT5	JSR	INISCR
	JMP	NEWLP
**

MKTIN	TYA
	SEC
	SBC	RWADR
	STA	RWLEN
	TXA
	SBC	RWADR+1
	STA	RWLEN+1

	LDA	INFLG
	BMI	MKTINE

	JSR	BREAD
	JMP	MKTIN2
**

MKTINE	LDA	#7
	JSR	MKTEDIO

MKTIN2	TYA

	LDY	RWLEN
	LDX	RWLEN+1

	CMP	#128
	BCC	MKTIN3
	CMP	#136
	BEQ	MKTINEF

	TAY
	JMP	ERR
*
MKTINEF	STA	COPTOP

MKTIN3	RTS
**
MKTEDIO	LDX	#0
	STA	$342,X

	LDA	RWADR
	STA	$344,X
	LDA	RWADR+1
	STA	$345,X

	LDA	RWLEN
	STA	$348,X
	LDA	RWLEN+1
	STA	$349,X

	LDY	#1
	ORA	RWLEN
	BEQ	MKTENUL

	JSR	$E456

MKTENUL	LDX	#0
	LDA	$348,X
	STA	RWLEN
	LDA	$349,X
	STA	RWLEN+1

MKTOX	RTS
**

MKTOUT	STY	RWLEN
	STX	RWLEN+1

	LDA	OUTFLG
	BMI	MKTOE

	JSR	CLTDOV
	JSR	BWRITE
	JMP	MKTO2
**

MKTOE	LDA	#11
	JSR	MKTEDIO

MKTO2	TYA
	BPL	MKTOX

	JMP	ERR
**
COPPLAC	PHA
	JSR	OZNAC
	PLA

COPPLC2	PHA
	JSR	PLACE
	PLA
	JSR	DIRADR
	JMP	GETNAME
**
OZNAC	CLC
	DFB $24
ODOZN	SEC

	PHP
	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	PLP
	BCC	OZN2

	ORA	#8
	BNE	OZN3

OZN2	AND	#255-8
OZN3	STA	(MT1),Y

CD2XWT	RTS
**

CD1T	DFB 16,33
**

CD2WT	LDA	COPSAME
	BNE	CD2XWT

	JSR	CLLIN
	LDA	#SRCTX:L
	LDX	#SRCTX:H

	LDY	CRA
	BEQ	COP2CB
	LDA	#DSTTX:L
	LDX	#DSTTX:H

COP2CB	LDY	#20
	JSR	INIS2S

	JSR	GETKEY
	CMP	#27
	BNE	CD2XWT
	JMP	NEWLP
**

COPDISK	STA	CRA
	LDY	DRIVE
	TAX
	BEQ	COPD1
	LDY	COPDSTD

COPD1	STY	DRIVNUM

	LDA	IN1ST,X
	BNE	COP2B
	INC	IN1ST,X

	TXA
	BEQ	COPD1B
	JSR	CD2WT

COPD1B	JSR	BCHKDSK
	BMI	CD2ER

	LDX	CRA
	LDY	CD1T,X
	LDX	#16

COP2A	LDA	DSKST2,X
	STA	TFST1,Y
	DEY
	DEX
	BPL	COP2A
	BMI	COP2X
**
COP2B	JSR	CD2WT
*
	JSR	BCHKDSK
	BMI	CD2ER

	LDX	CRA
	LDY	CD1T,X
	LDX	#16

COP2D	CPX	#14
	BEQ	COP2E
	CPX	#4
	BEQ	COP2E
	CPX	#5
	BEQ	COP2E

	LDA	DSKST2,X
	CMP	TFST1,Y
	BNE	COP2B

COP2E	DEY
	DEX
	BPL	COP2D

COP2X	JMP	CLLIN
**
CD2ER	JMP	ERR
**
COPY	JSR	ZEROL

	JSR	CLLIN
	LDA	#COPTX1:L
	LDX	#COPTX1:H
	LDY	#20
	JSR	INIS2S

	JSR	DRIVMEN
	STA	COPDSTD

	EOR	DRIVE
	BNE	COPY1

	JSR	CLLIN
	LDY	#COPMEN:L
	LDX	#COPMEN:H
	JSR	MENU

	EOR	#1
	BPL	COPY1

COPAB	JMP	TABLP
*
COPY1	STA	COPSAME

	JSR	CLLIN
	LDA	#COPTX2:L
	LDX	#COPTX2:H
	LDY	#20
	JSR	INIS2S

	LDY	#20
	LDX	#12
	LDA	#28
	JSR	EDIT

	TAX
	BMI	COPAB
*
	LDX	#30
CPY1A	LDA	DATBUF,X
	STA	PATH2,X
	DEX
	BPL	CPY1A
**
	JSR	SAMETST

	LDA	#0
	STA	OUTFLG
	STA	COPNEWF
	STA	OUTPOS
	STA	OUTPOS+1
	STA	OUTPOS+2
	STA	IN1ST
	STA	OUT1ST
	STA	COPENDF

	STA	EXP
	JSR	SRCH
	BPL	COP1C

	LDA	MDIRPOS
	JSR	OZNAC

COP1C	LDA	#$FF
	STA	EXP
**
COPY2	LDA	#0
	STA	COPTOP
	JSR	COPDISK

	LDA	$2E7
	STA	CRA
	LDA	$2E8
	STA	CRA+1

	LDX	#2
COP2T	LDA	INPOS,X
	STA	POS,X
	DEX
	BPL	COP2T
	BMI	COPY3
**
COP3X	SEC
	ROR	COPENDF
COPYWJ	JMP	COPYW
**
COPY3	LDA	COPNEWF
	BNE	COPY4

	STA	POS
	STA	POS+1
	STA	POS+2
	INC	COPNEWF

COP3A	JSR	SRCH
	BMI	COP3X

	LDA	EXP
	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	COP3A
**
COPY4	LDA	CRA
	CMP	$2E5
	LDA	CRA+1
	SBC	$2E6
	BCS	COPYWJ

	LDA	COPTOP
	CMP	#18
	BCS	COPYWJ
**
	LDA	EXP
	JSR	COPPLAC
**
	LDA	CRA+1
	CMP	#$40
	BNE	COP4B

	LDA	MEM2
	STA	CRA
	LDA	MEM2+1
	STA	CRA+1
*
COP4B	LDA	CRA
	STA	RWADR
	LDA	CRA+1
	STA	RWADR+1

	LDY	#0
	LDX	#$40
	CMP	#$40
	BCC	COP4C
	LDY	$2E5
	LDX	$2E6

COP4C	TYA
	SEC
	SBC	RWADR
	STA	RWLEN
	TXA
	SBC	RWADR+1
	STA	RWLEN+1

	JSR	BREAD
	BPL	COP4D

	TYA
	EOR	#136
	BNE	COPERJ

	STA	COPNEWF

COP4D	LDX	COPTOP
	LDA	CRA
	STA	TFADRL,X
	LDA	CRA+1
	STA	TFADRH,X

	LDA	RWLEN
	STA	TFLENL,X
	CLC
	ADC	CRA
	STA	CRA
	LDA	RWLEN+1
	STA	TFLENH,X
	ADC	CRA+1
	STA	CRA+1

	LDA	EXP
	STA	TFF,X

	INC	COPTOP
	JMP	COPY3
**
COPERJ	JMP	ERR
**

COPYW	LDX	#2
COP4E	LDA	POS,X
	STA	INPOS,X
	DEX
	BPL	COP4E
*
	INX
	STX	COPIX
	CPX	COPTOP
	BNE	CW2
**
CWEX	LDA	COPENDF
	BEQ	CWEX2
	JMP	NEWLP
CWEX2	JMP	COPY2
**
CW2	LDA	#1
	JSR	COPDISK

	LDX	#2
CW2B	LDA	OUTPOS,X
	STA	POS,X
	DEX
	BPL	CW2B
**

CW3	LDX	COPIX
	CPX	COPTOP
	BEQ	CWEX
*
	LDA	TFF,X
	CMP	OUTFLG
	BEQ	CW3B

	LDY	#0
	STY	POS
	STY	POS+1
	STY	POS+2

CW3B	STA	OUTFLG
	JSR	COPPLAC
*
	LDA	#0
	STA	INFLG

	LDY	#17

CW3BB	LDA	(MT1),Y
	INY
	INY
	STA	(10),Y
	ORA	INFLG
	STA	INFLG

	DEY
	CPY	#23
	BCC	CW3BB

	LDA	INFLG
	BEQ	CW3BC
	LDA	#$FF
CW3BC	JSR	CLTDOV2
*
	LDX	COPIX
	LDA	TFADRL,X
	STA	RWADR
	LDA	TFADRH,X
	STA	RWADR+1
	LDA	TFLENL,X
	STA	RWLEN
	LDA	TFLENH,X
	STA	RWLEN+1
*
	JSR	BWRITE
	BMI	COPERJ

	JSR	CLTDOV

	LDA	OUTFLG
	JSR	ODOZN

	LDX	#2
CW4B	LDA	POS,X
	STA	OUTPOS,X
	DEX
	BPL	CW4B
*
	INC	COPIX

	JMP	CW3
**

CLTDOV	LDA	#0

CLTDOV2	LDY	#25
	STA	(10),Y
	RTS

*** DEFINICE

DATBUF	EQU	*

MAPBUF	EQU	DATBUF+256
DIRBUF	EQU	MAPBUF+256
FNAME	EQU	DIRBUF+256
FNAME2	EQU	FNAME+11
PATH	EQU	FNAME2+11
PATH2	EQU	PATH+$E8
DSKSTAT	EQU	PATH2+$E8
DSKST2	EQU	DSKSTAT+17

MEMBUF	EQU	DSKST2+17

** PRENOS

PDATBUF	EQU	DATBUF
PSTART	EQU	START

*
