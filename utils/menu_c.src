*				*
*  " M E N U " PRO BW-DOS - CAST 3  *
*				*

* PRENOS

START3	EQU	$5B96
STARTA	EQU	$4000
START2	EQU	$4F5B
LOOPTX	EQU	$48A6

* DOMYSLENO

L EQU LOOPTX

ERRTX	EQU	L+$29
SRTM	EQU	L+$34
NDSKT	EQU	L+$6E
NDSKM	EQU	L+$7F
PRESSK	EQU	L+$A0
SELTX	EQU	L+$B1
DESTX	EQU	L+$B3
SDTX	EQU	L+$B7
DIT1	EQU	L+$C4
DIT2	EQU	L+$CB
DIT3	EQU	L+$D8
DIT4	EQU	L+$E4
DI1	EQU	L+$EF
DI2	EQU	L+$110
DI3	EQU	L+$131
DI4	EQU	L+$152
MDTX	EQU	L+$181
RNTX	EQU	L+$191
MTTX	EQU	L+$220
COPTX1	EQU	L+$22A
COPMEN	EQU	L+$23B
COPTX2	EQU	L+$256
SRCTX	EQU	L+$262
DSTTX	EQU	L+$279
BELLMEN	EQU	L+$295
QMEN	EQU	L+$4CF

Q EQU STARTA
REOPNS	EQU	Q+51
GETKEY	EQU	Q+54
CONVDC	EQU	Q+57
WAITKEY	EQU	Q+60
WAITKB	EQU	Q+3
GETKB	EQU	Q+63
CONVFNM	EQU	Q+66

W EQU START2
BREAD	EQU	W+3
BWRITE	EQU	W+6
BRENAME	EQU	W+9
BCHKDSK	EQU	W+12
BXIO	EQU	W+15
PRINT	EQU	W
MENU	EQU	W+18
EDIT	EQU	W+21
EDIT0	EQU	W+24
CLLIN	EQU	W+27
DIRADR	EQU	W+30
INISCR	EQU	W+33
PRTTAB	EQU	W+36
PRTCMD	EQU	W+39
INIS2S	EQU	W+42
CLDATB	EQU	W+45
RESORT	EQU	W+48
NEWDIR	EQU	W+51
PCL	EQU	W+54
AINIT	EQU	W+57
NAMBCK	EQU	W+60
SAMETST	EQU	W+63
PLACE	EQU	W+66
GETP	EQU	W+69
TEXIST	EQU	W+72
MENBEL	EQU	W+75

* DEFINICE

TFST1	EQU	$480
TFST2	EQU	$491
TFADRL	EQU	$4A2
TFADRH	EQU	$4B4
TFLENL	EQU	$4C6
TFLENH	EQU	$4D8
TFF	EQU	$4EA

*
	ORG	START3

	JMP	START
	JMP	ERR
	JMP	TABLP
	JMP	COPDNUM
	JMP	DIRLP
*

DRIVNUM	EQU	131

RWA	EQU	138
RWL	EQU	140

POS	EQU	148

*

PRTADR	EQU	153

*

MEM2	EQU	162

DIRPOC	EQU	164
MDP	EQU	165
DIRSPOS	EQU	166

CMDPOS	EQU	167
CMDSPOS	EQU	168

DRIVE	EQU	169

MT1	EQU	170
PTTY	EQU	172
PTTP	EQU	173
PTTI	EQU	174

SORTA	EQU	175
MT2	EQU	177
SORTM	EQU	179

EXP	EQU	180
EXF	EQU	181

INFLG	EQU	182
OUTFLG	EQU	183
INPOS	EQU	184
OUTPOS	EQU	187
COPIX	EQU	190
COPTOP	EQU	191
COPDSTD	EQU	192
COPSAME	EQU	193
IN1ST	EQU	194
OUT1ST	EQU	195
CRA	EQU	196
COPENDF	EQU	198
COPNEWF	EQU	199

BELLFLG	EQU	200
COPPDRV	EQU	201

QFLG	EQU	203

CERRF	EQU	205

* VOLNO: 206

*

EOL	.MACRO
	DTA	155
	.MEND

*

COPDNUM	LDA	DRIVE
	STA	DRIVNUM
	RTS
*

START	LDX	#0
	STX	CMDPOS
	STX	CMDSPOS
	STX	SORTM
	STX	BELLFLG
	STX	QFLG
*
	LDA	10
	CLC
	ADC	#3
	STA	ST0PAR+1
	LDA	11
	ADC	#0
	STA	ST0PAR+2
ST0PAR	JSR	$E474

	LDY	#34
	LDA	(10),Y
	AND	#15
	STA	DRIVE

	LDY	#36
ST0P2	LDA	(10),Y
	CMP	#'/'
	BNE	ST0PX

	INY
	LDA	(10),Y
	TAX
	INY
	LDA	(10),Y
	INY
	CPX	#'S'
	BEQ	ST0P2S
	CPX	#'Q'
	BEQ	ST0P2Q
	CPX	#'B'
	BNE	ST0PX

	LDX	#0
	CMP	#'Y'
	BEQ	ST0P2B
	INX
	CMP	#'N'
	BNE	ST0PX

ST0P2B	STX	BELLFLG
	JMP	ST0P2
ST0P2U	STX	SORTM
	JMP	ST0P2

ST0P2Q	LDX	#0
	CMP	#'Y'
	BEQ	ST0P2W
	INX
	CMP	#'N'
	BNE	ST0PX

ST0P2W	STX	QFLG
	JMP	ST0P2

ST0PTB	DTA	C"ENSD0"

ST0P2S	LDX	#4
ST0P2T	CMP	ST0PTB,X
	BEQ	ST0P2U
	DEX
	BPL	ST0P2T
*

ST0PX	JSR	PCL
	JSR	CLTDOV

	JSR	REOPNS
	JSR	INISCR

*

	LDA	DRIVE

DIRLPP	LDX	DRIVE
	STX	COPPDRV
	STA	DRIVE

	LDX	#$E8
DIRL2	LDA	P-1,X
	STA	P2-1,X
	DEX
	BNE	DIRL2

DIRLP	JSR	GETP

	LDA	#0
	STA	CERRF

NEWLP	LDA	CERRF
	BNE	DIRLP

	JSR	NEWDIR

TABLP	JSR	PRTTAB
	JMP	LP0
*
CMDLP	JSR	PRTCMD

LP0	LDX	#$FF
	TXS

	LDA	#<LOOPTX
	LDX	#>LOOPTX
	LDY	#20
	JSR	INIS2S

LP1	JSR	GETKEY

	CMP	#155
	BEQ	LPRETQ

	CMP	#$1E
	BEQ	LPL
	CMP	#$1F
	BEQ	LPR
	CMP	#$1C
	BEQ	LPU
	CMP	#$1D
	BEQ	LPDQ

	AND	#127

	CMP	#'+'
	BEQ	LPL
	CMP	#'*'
	BEQ	LPR
	CMP	#'-'
	BEQ	LPU
	CMP	#'='
	BEQ	LPDQ

	CMP	#32
	BEQ	LPSPCQ
*
	LDX	#17
LP2	CMP	LOP2T,X
	BEQ	LP3
	DEX
	BPL	LP2
	BMI	LP1
*
LP3	STX	CMDPOS
	DEX
	BPL	LP3B
	LDX	#17
LP3B	STX	CMDSPOS

	JMP	CMDLP
**
LPL	LDX	CMDPOS
	TXA

	DEX
	BPL	LPL2
	LDX	#17

LPL2	STX	CMDPOS
	CMP	CMDSPOS
	BNE	CMDLJ2

	STX	CMDSPOS

CMDLJ2	JMP	CMDLP
*
LPRETQ	BEQ	LPRET
LPSPCQ	BEQ	LPSPC
LPDQ	BEQ	LPD
*
LPR	LDA	CMDPOS
	JSR	LPRS
	STA	CMDPOS

	SEC
	SBC	CMDSPOS
	BCS	LPR2
	ADC	#18
LPR2	CMP	#4
	BCC	CMDLJ2

	LDA	CMDSPOS
	JSR	LPRS
	STA	CMDSPOS

	JMP	CMDLP
*
LPU	LDA	MDP
	BNE	LPU2

	LDA	DIRPOC
	STA	MDP

	SEC
	SBC	#15
	BCS	LPU1
	LDA	#0

LPU1	STA	DIRSPOS

LPU2	DEC	MDP
*

	LDA	DIRSPOS
	CMP	MDP
	BCC	TABLJ1

	LDA	MDP
	SBC	#1
	BCS	LPU3
	LDA	#0
LPU3	STA	DIRSPOS

TABLJ1	JMP	TABLP
*
LPSPC	LDA	MDP
	JSR	DIRADR

	LDY	#0
	LDA	(MT1),Y
	EOR	#8
	STA	(MT1),Y
*
LPD	INC	MDP
	LDA	MDP
	CMP	DIRPOC
	BCC	LPD2

	LDA	#0
	STA	MDP
	STA	DIRSPOS

*
LPD2	SEC
	SBC	DIRSPOS
	CMP	#15
	BCC	TABLJ1

	LDA	MDP
	SEC
	SBC	#14
	STA	DIRSPOS
	JMP	TABLP
*
LPRET	LDA	CMDPOS
	ASL
	TAX

	LDA	LPRTTB,X
	STA	MT1
	LDA	LPRTTB+1,X
	STA	MT1+1

	JMP	(MT1)
*

LPRTTB	DTA	A(NDSK,DSKINFO,SUBDIR)
	DTA	A(UPDIR,MKDIR,DLDIR,COPY)
	DTA	A(ERASE,RENAME,PROTECT)
	DTA	A(UNPROT,VIEWTX,MAKETX)
	DTA	A(AINIT,EXIT,SETUP)
	DTA	A(SELECT,DESEL)
*

LOP2T	DTA	C"FZTYNDCERPUVIAQS>"
	DTA	126
*

LPRS	CLC
	ADC	#1
	CMP	#18
	BCC	LPRS2
	SBC	#18
LPRS2	RTS

*

EXIT	JSR	CLTDOV
	JSR	REOPNS
	JMP	(10)
*

ERR	TYA
	PHA

	JSR	INISCR

	LDY	#20
	LDX	#>ERRTX
	LDA	#<ERRTX
	JSR	INIS2S

	PLA
	TAY
	LDA	#0
	TAX
	JSR	CONVDC

	LDA	#155
	STA	DATB+8
	LDA	#<((DATB+5))
	STA	PRTADR
	LDA	#>((DATB+5))
	STA	PRTADR+1
	LDY	#20
	LDX	#6
	LDA	#0
	JSR	PRINT

	LDX	#11
	JSR	WAITKB

	JMP	NEWLP
*

NDSK	JSR	CLLIN

	LDA	#<NDSKT
	LDX	#>NDSKT
	LDY	#20
	JSR	INIS2S

	JSR	DRIVMEN
*A=DRIVE
	JMP	DIRLPP
*

SETUP	JSR	CLLIN

	LDY	#<SRTM
	LDX	#>SRTM
	JSR	MENU

	TAX
	BMI	ABORT1

	STA	SORTM
	JSR	RESORT
*
	JSR	CLLIN

	LDY	#<BELLMEN
	LDX	#>BELLMEN
	JSR	MENU

	TAX
	BMI	ABORT1

	STA	BELLFLG
*
	JSR	CLLIN
	LDY	#<QMEN
	LDX	#>QMEN
	JSR	MENU

	TAX
	BMI	ABORT1

	STA	QFLG

ABORT1	JMP	TABLP
*

DRIVMEN	LDY	#<NDSKM
	LDX	#>NDSKM
	JSR	MENU

	TAX
	BMI	ABORT1

	INX
	CPX	#5
	BCC	NDSK2
	LDX	#8

NDSK2	TXA
ZEROLX	RTS
*

ZEROL	LDA	DIRPOC
	BNE	ZEROLX
	JMP	TABLP
*

SELECT	JSR	CLLIN

	LDA	#<SELTX
	LDX	#>SELTX
	LDY	#20
	JSR	INIS2S

	LDX	#1
	LDA	#0
	BEQ	SELDS2
*
DESEL	JSR	CLLIN

	LDA	#<DESTX
	LDX	#>DESTX
	LDY	#20
	JSR	INIS2S

	LDX	#3
	LDA	#8
*
SELDS2	STA	PTTI
	STX	PTTP

	JSR	ZEROL

	LDA	#<SDTX
	STA	PRTADR
	LDA	#>SDTX
	STA	PRTADR+1
	LDY	#20
	LDA	#0
	JSR	PRINT
*
	JSR	CLDATB
	LDA	#'*'
	STA	DATB
	STA	DATB+2
	LDA	#'.'
	STA	DATB+1

	LDA	PTTP
	CLC
	ADC	#13
	TAX
	LDY	#20
	LDA	#12
	JSR	EDIT0

	TAX
	BPL	SELD3
ABORT2	JMP	TABLP
*
SELD3	JSR	CONVFNM

	LDA	#$FF
	STA	PTTP
*

SELD4	JSR	SELDSR
	BCS	ABORT2

	LDY	#0
	LDA	(MT1),Y
	AND	#255-8
	ORA	PTTI
	STA	(MT1),Y
	JMP	SELD4
*

SELDSR	INC	PTTP
	LDA	PTTP
	CMP	DIRPOC
	BCS	SELD5X

	JSR	DIRADR

	LDY	#6

SELD5B	LDA	F-6,Y
	CMP	#'?'
	BEQ	SELD5C

	CMP	(MT1),Y
	BNE	SELDSR

SELD5C	INY
	CPY	#17
	BCC	SELD5B
*

	CLC
SELD5X	RTS

*

DICLR	LDX	#32
	TXA
DICLR1	STA	DIRB,X
	DEX
	BPL	DICLR1

	LDA	#'|'
	STA	DIRB
	STA	DIRB+31
	LDA	#155
	STA	DIRB+32
	RTS
*

DSKINM1	PHA
	LDA	#0

	JSR	CONVDC

	PLA
	TAX
	LDY	#5

DSKINM1B	LDA	DATB,Y
	STA	DIRB,X
	INX
	INY
	CPY	#8
	BCC	DSKINM1B

	RTS
*

DSKINM2	LDY	#0
	BIT	DSKST2+1
	BPL	DSKINM2A

	LSR
	PHA
	TXA
	ROR
	TAX
	TYA
	ROR
	TAY
	PLA

DSKINM2A	JSR	CONVDC

	LDX	#15
	LDY	#0
	BEQ	DSKINM1B
*

DIPRTDB	LDA	#<DIRB
	LDX	#>DIRB
*
DIPRT	STA	PRTADR
	STX	PRTADR+1

	LDA	#0
	LDX	#4
	JMP	PRINT
*
DSKIE	JMP	ERR
*
DSKINFO	JSR	COPDNUM

	JSR	BCHKDSK
	BMI	DSKIE

	LDY	#6
	LDX	#>DI1
	LDA	#<DI1
	JSR	DIPRT

	LDY	#7
	LDX	#>DI2
	LDA	#<DI2
	JSR	DIPRT

	LDY	#8
	LDX	#>DI3
	LDA	#<DI3
	JSR	DIPRT
*
	JSR	DICLR

	LDA	DSKST2
	BEQ	DSKI2
*
	LDX	#6
DSKI1A	LDA	DIT1,X
	STA	DIRB+7,X
	DEX
	BPL	DSKI1A
*
	LDX	#7
DSKI1B	LDA	DSKST2+6,X
	STA	DIRB+15,X
	DEX
	BPL	DSKI1B
*
	LDA	#24
	LDY	DSKST2+14
	LDX	#0
	JSR	DSKINM1

	LDA	#28
	LDY	DSKST2+15
	LDX	#0
	JSR	DSKINM1
*
DSKI2	LDY	#9
	JSR	DIPRTDB
*
	JSR	DICLR

	LDX	#12
DSKI3	LDA	DIT2,X
	STA	DIRB+1,X
	DEX
	BPL	DSKI3

	INX
	LDY	DSKST2+1
	BNE	DSKI3B
	INX

DSKI3B	LDA	#15
	JSR	DSKINM1

	LDY	#10
	JSR	DIPRTDB
*
	JSR	DICLR

	LDX	#11
DSKI4A	LDA	DIT3,X
	STA	DIRB+2,X
	DEX
	BPL	DSKI4A

	LDX	DSKST2+2
	LDA	DSKST2+3
	JSR	DSKINM2

	LDY	#11
	JSR	DIPRTDB
*
	JSR	DICLR

	LDX	#10
DSKI5A	LDA	DIT4,X
	STA	DIRB+3,X
	DEX
	BPL	DSKI5A

	LDX	DSKST2+4
	LDA	DSKST2+5
	JSR	DSKINM2

	LDY	#12
	JSR	DIPRTDB
*
	LDY	#13
	LDX	#>DI4
	LDA	#<DI4
	JSR	DIPRT
*
	JSR	CLLIN

	LDX	#0
	JSR	WAITKEY
	JMP	TABLP
*

SUBDIR	JSR	ZEROL

*

	LDA	MDP
	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	SBD0
	JMP	TABLP
*
SBD0	LDY	#6
SBD0B	LDA	(MT1),Y
	STA	F-6,Y
	INY
	CPY	#17
	BCC	SBD0B
	JSR	NAMBCK

	LDX	#0

SBD1	LDA	P,X
	CMP	#155
	BEQ	SBD2

	INX
	CPX	#$D8
	BCC	SBD1

*PATHTOOLONG

	LDY	#165
	JMP	ERR
*

SBD2	LDY	#$FF
	BNE	SBD2CB

SBD2D	JSR	SBD3

SBD2CB	INY
	LDA	DATB,Y
	CMP	#$9B
	BNE	SBD2D

	LDA	#'>'
	JSR	SBD3
	LDA	#155
	JSR	SBD3
*
	JSR	SAMETST
	JMP	NEWLP
*

SBD3	STA	P,X
	INX
	RTS

*

UPDBK	DEX
	CPX	#$FF
	BEQ	UPDNO
	RTS
*
UPDNO	JMP	TABLP
*

UPDIR	LDX	#0
UPD1	LDA	P,X
	CMP	#155
	BEQ	UPD2

	INX
	BNE	UPD1
*

UPD2	JSR	UPDBK

UPD2B	JSR	UPDBK
	LDA	P,X
	CMP	#'>'
	BNE	UPD2B

	TXA
	PHA
	LDY	#0
UPD2C	LDA	P+1,X
	CMP	#155
	BEQ	UPD2CB
	STA	DATB,Y
	INX
	INY
	BNE	UPD2C

UPD2CB	STA	DATB-1,Y
	PLA
	TAX

	LDA	#155
	STA	P+1,X

	JSR	CONVFNM
	LDX	#10
UPD2D	LDA	F,X
	STA	UPDNAM,X
	DEX
	BPL	UPD2D

	JSR	SAMETST

	JSR	NEWDIR

	LDX	#10
UPD2E	LDA	UPDNAM,X
	STA	F,X
	DEX
	BPL	UPD2E
*
	STX	PTTP
	JSR	SELDSR
	LDA	#0
	BCS	UPD2F
	LDA	PTTP

UPD2F	JSR	PLACE

	JMP	TABLP
*

MKDIR	JSR	CLLIN

	LDA	#<MDTX
	LDX	#>MDTX
	LDY	#20
	JSR	INIS2S

	LDX	#16
	LDY	#20
	LDA	#12
	JSR	EDIT

	TAX
	BPL	MKDIR2
	JMP	TABLP
*
MKDIR2	JSR	CONVFNM

	JSR	SAMETST

	LDA	#42
	JSR	BXIO
	BMI	MKDER

	JMP	NEWLP
*
MKDER	JMP	ERR
*

SRCH	INC	EXP

SRCHOD	LDA	EXP
	CMP	DIRPOC
	BCC	SRCH2

	LDA	#255
	RTS
*
SRCH2	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	AND	#8
	BNE	SRCH

	RTS
*

EXE	STY	EXEJP+1
	STX	EXEJP+2
	STA	EXF

	JSR	ZEROL
	JSR	SAMETST
	JSR	CLLIN

	LDA	#$FF
	STA	EXP

	JSR	SRCH
	BMI	EXENE

EXE2	LDA	EXP
	JSR	EXEN2
	BCC	EXE2B

	LDA	EXP
	JSR	ODOZN

EXE2B	JSR	SRCH
	BPL	EXE2
	BMI	EXE3
*
EXENE	LDA	MDP
	JSR	EXEN2
*
EXE3	LDA	EXF
	BMI	EXE4
	JMP	TABLP
*
EXE4	JSR	NEWLP
*

EXEN2	STA	EXP
	JSR	COPPLC2

EXEJP	JSR	$E474
	BMI	EXEN2E
	RTS
*
EXEN2E	JMP	ERR

*

GETNAME	LDY	#6

EXEN2B	LDA	(MT1),Y
	STA	F-6,Y
	INY
	CPY	#17
	BCC	EXEN2B

	RTS
*

DLDIR	LDY	#<RDLD
	LDX	#>RDLD
	LDA	#255
	JMP	EXE
*

RDLD	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BEQ	RDENE
	LDA	#43
	BNE	RER2

RERA	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	RDENE

	LDA	#33
RER2	JSR	BXIO
	SEC
	RTS

RDENE	CLC
	RTS

*

ERASE	LDY	#<RERA
	LDX	#>RERA
	LDA	#255
	JMP	EXE
*

PROTECT	LDY	#<RPRO
	LDX	#>RPRO
	LDA	#0
	JMP	EXE
*

UNPROT	LDY	#<RUNPR
	LDX	#>RUNPR
	LDA	#0
	JMP	EXE
*

RPRO	LDX	#35
	LDA	#1
	BNE	RPRUN
*
RUNPR	LDX	#36
	LDA	#0

RPRUN	LDY	#0
	STA	PTTI
	LDA	(MT1),Y
	AND	#254
	ORA	PTTI
	STA	(MT1),Y

	TXA
	JMP	RER2
*

RENAME	JSR	CLLIN

	LDA	#<RNTX
	LDX	#>RNTX
	LDY	#20
	JSR	INIS2S

	LDX	#11
	LDY	#20
	LDA	#12
	JSR	EDIT

	TAX
	BPL	RNM2
	JMP	TABLP
*
RNM2	JSR	CONVFNM

	LDX	#10
RNM3	LDA	F,X
	STA	$4C0,X
	DEX
	BPL	RNM3

	LDY	#<RRNM
	LDX	#>RRNM
	LDA	#128
	JMP	EXE
**

RRNM	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BEQ	RRNM1
	CLC
	RTS

RRNM1	LDX	#10

RRNM2	LDA	$4C0,X
	CMP	#'?'
	BNE	RRNM3
	LDA	F,X

RRNM3	STA	F2,X

	DEX
	BPL	RRNM2

*
	LDA	#255
	STA	PTTP

RRNM4C	INC	PTTP
	LDA	PTTP
	CMP	DIRPOC
	BCS	RRNM5

	JSR	DIRADR

	LDY	#6
RRNM4B	LDA	(MT1),Y
	CMP	F2-6,Y
	BNE	RRNM4C

	INY
	CPY	#17
	BCC	RRNM4B

	LDA	PTTP
	CMP	EXP
	BEQ	RRNM4C

	LDY	#151
	JMP	ERR
*
RRNM5	LDA	EXP
	JSR	DIRADR

	LDY	#6
RRNM6	LDA	F2-6,Y
	STA	(MT1),Y
	INY
	CPY	#17
	BCC	RRNM6

	JSR	BRENAME
	SEC
	RTS
*

VIEWER	JMP	TABLP
*
VIEWTX	JSR	ZEROL

	LDA	MDP
	JSR	DIRADR

	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	VIEWER

	JSR	GETNAME

	LDX	#0
	LDY	#255
	BNE	MKT2
*

MAKETX	JSR	CLLIN

	LDA	#<MTTX
	LDX	#>MTTX
	LDY	#20
	JSR	INIS2S

	LDX	#10
	LDY	#20
	LDA	#12
	JSR	EDIT

	TAX
	BMI	VIEWER

	JSR	CONVFNM

	LDX	#$E8
MKT2A	DEX
	LDA	P,X
	STA	P2,X
	TXA
	BNE	MKT2A
*
	DEX
	STX	COPPDRV
	LDY	#0
*
MKT2	STX	INFLG
	STY	OUTFLG
*
	JSR	SAMETST
	JSR	REOPNS

	LDA	OUTFLG
	STA	$2FE

	LDA	#0
	STA	POS
	STA	POS+1
	STA	POS+2
	STA	COPTOP
*IN

MKT3	LDY	#0
	STY	INPOS
	STY	INPOS+1
	STY	OUTPOS
	STY	OUTPOS+1

	LDA	$2E7
	STA	RWA
	LDA	$2E8
	STA	RWA+1

*
	LDX	#$40
	JSR	MKTIN

	STY	INPOS
	STX	INPOS+1

	LDA	COPTOP
	BMI	MKT4

	LDA	MEM2
	STA	RWA
	LDA	MEM2+1
	STA	RWA+1

	LDY	$2E5
	LDX	$2E6
	JSR	MKTIN

	STY	OUTPOS
	STX	OUTPOS+1
*OUT

MKT4	LDA	$2E7
	STA	RWA
	LDA	$2E8
	STA	RWA+1

	LDY	INPOS
	LDX	INPOS+1
	JSR	MKTOUT

	LDA	MEM2
	STA	RWA
	LDA	MEM2+1
	STA	RWA+1

	LDY	OUTPOS
	LDX	OUTPOS+1
	BNE	MKT4B
	TYA
	BEQ	MKT4C

MKT4B	JSR	MKTOUT
*
MKT4C	LDA	COPTOP
	BPL	MKT3
*
	LDA	OUTFLG
	BPL	MKT5
*
	LDA	#<PRESSK
	STA	RWA
	LDA	#>PRESSK
	STA	RWA+1

	LDY	#15
	LDX	#0
	JSR	MKTOUT

	JSR	GETKEY
**

MKT5	JSR	INISCR
	JMP	NEWLP
*

MKTIN	TYA
	SEC
	SBC	RWA
	STA	RWL
	TXA
	SBC	RWA+1
	STA	RWL+1

	LDA	INFLG
	BMI	MKTINE

	JSR	BREAD
	JMP	MKTIN2
*

MKTINE	LDA	#7
	JSR	MKTEDIO

MKTIN2	TYA

	LDY	RWL
	LDX	RWL+1

	CMP	#128
	BCC	MKTIN3
	CMP	#136
	BEQ	MKTINEF

	TAY
	JMP	ERR
*
MKTINEF	STA	COPTOP

MKTIN3	RTS
*
MKTEDIO	LDX	#0
	STA	$342,X

	LDA	RWA
	STA	$344,X
	LDA	RWA+1
	STA	$345,X

	LDA	RWL
	STA	$348,X
	LDA	RWL+1
	STA	$349,X

	LDY	#1
	ORA	RWL
	BEQ	MKTENUL

	JSR	$E456

MKTENUL	LDX	#0
	LDA	$348,X
	STA	RWL
	LDA	$349,X
	STA	RWL+1

MKTOX	RTS
*

MKTOUT	STY	RWL
	STX	RWL+1

	LDA	OUTFLG
	BMI	MKTOE

	JSR	CLTDOV
	JSR	OVERTST
	BCS	MKTOAB1

	JSR	BWRITE
	JMP	MKTO2

MKTOAB1	JMP	OVRAB

*

MKTOE	LDA	#11
	JSR	MKTEDIO

MKTO2	TYA
	BPL	MKTOX

	JMP	ERR
*
COPPLAC	PHA
	JSR	OZNAC
	PLA

COPPLC2	PHA
	JSR	PLACE
	PLA
	JSR	DIRADR
	JMP	GETNAME
*
OZNAC	CLC
	DTA	$24
ODOZN	SEC

	PHP
	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	PLP
	BCC	OZN2

	ORA	#8
	BNE	OZN3

OZN2	AND	#255-8
OZN3	STA	(MT1),Y

CD2XWT	RTS
*

CD1T	DTA	16,33
*

CD2WT	LDA	COPSAME
	BNE	CD2XWT

	JSR	CLLIN
	LDA	#<SRCTX
	LDX	#>SRCTX

	LDY	CRA
	BEQ	COP2CB
	LDA	#<DSTTX
	LDX	#>DSTTX

COP2CB	LDY	#20
	JSR	INIS2S

	JSR	GETKB
	CMP	#27
	BNE	CD2XWT
	JMP	NEWLP
*

COPDISK	STA	CRA
	LDY	DRIVE
	TAX
	BEQ	COPD1
	LDY	COPDSTD

COPD1	STY	DRIVNUM

	LDA	IN1ST,X
	BNE	COP2B
	INC	IN1ST,X

	TXA
	BEQ	COPD1B
	JSR	CD2WT

COPD1B	JSR	BCHKDSK
	BMI	CD2ER

	LDX	CRA
	LDY	CD1T,X
	LDX	#16

COP2A	LDA	DSKST2,X
	STA	TFST1,Y
	DEY
	DEX
	BPL	COP2A
	BMI	COP2X
*
COP2B	JSR	CD2WT
*
	JSR	BCHKDSK
	BMI	CD2ER

	LDX	CRA
	LDY	CD1T,X
	LDX	#16

COP2D	CPX	#14
	BEQ	COP2E
	CPX	#4
	BEQ	COP2E
	CPX	#5
	BEQ	COP2E

	LDA	DSKST2,X
	CMP	TFST1,Y
	BNE	COP2B

COP2E	DEY
	DEX
	BPL	COP2D

COP2X	JMP	CLLIN
*
CD2ER	JMP	ERR
*
COPY	JSR	ZEROL

	JSR	CLLIN
	LDA	#<COPTX1
	LDX	#>COPTX1
	LDY	#20
	JSR	INIS2S

	JSR	DRIVMEN
	STA	COPDSTD

	EOR	DRIVE
	BNE	COPY1

	JSR	CLLIN
	LDY	#<COPMEN
	LDX	#>COPMEN
	JSR	MENU

	EOR	#1
	BPL	COPY1

COPAB	JMP	TABLP
*
COPY1	STA	COPSAME

*
	JSR	CLLIN
	LDA	#<COPTX2
	LDX	#>COPTX2
	LDY	#20
	JSR	INIS2S

	LDX	#$E8
	LDA	#$20
COP1A1	STA	DATB-1,X
	DEX
	BNE	COP1A1

	LDA	COPDSTD
	CMP	COPPDRV
	BNE	COP1A3

COP1A2	LDA	P2,X
	CMP	#155
	BEQ	COP1A3
	STA	DATB,X
	INX
	CPX	#$E7
	BCC	COP1A2

COP1A3	LDY	#20
	LDX	#12
	LDA	#$E7
	JSR	EDIT0

	TAX
	BMI	COPAB
*
	LDX	#$E8
CPY1A	LDA	DATB-1,X
	STA	P2-1,X
	DEX
	BNE	CPY1A

	LDA	COPDSTD
	STA	COPPDRV
**
	JSR	SAMETST

	LDA	#0
	STA	OUTFLG
	STA	COPNEWF
	STA	OUTPOS
	STA	OUTPOS+1
	STA	OUTPOS+2
	STA	IN1ST
	STA	OUT1ST
	STA	COPENDF

	STA	EXP
	JSR	SRCHOD
	BPL	COP1C

	LDA	MDP
	JSR	OZNAC

COP1C	LDA	#$FF
	STA	EXP
	STA	CERRF
*
COPY2	LDA	#0
	STA	COPTOP
	JSR	COPDISK

	LDA	$2E7
	STA	CRA
	LDA	$2E8
	STA	CRA+1

	LDX	#2
COP2T	LDA	INPOS,X
	STA	POS,X
	DEX
	BPL	COP2T
	BMI	COPY3
*
COP3X	SEC
	ROR	COPENDF
COPYWJ	JMP	COPYW
*
COPY3	LDA	COPNEWF
	BNE	COPY4

	STA	POS
	STA	POS+1
	STA	POS+2
	INC	COPNEWF

COP3A	JSR	SRCH
	BMI	COP3X

	LDA	EXP
	JSR	DIRADR
	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BNE	COP3A
*
COPY4	LDA	CRA
	CMP	$2E5
	LDA	CRA+1
	SBC	$2E6
	BCS	COPYWJ

	LDA	COPTOP
	CMP	#18
	BCS	COPYWJ
*
	LDA	EXP
	JSR	COPPLAC
*
	LDA	CRA+1
	CMP	#$40
	BNE	COP4B

	LDA	MEM2
	STA	CRA
	LDA	MEM2+1
	STA	CRA+1
*
COP4B	LDA	CRA
	STA	RWA
	LDA	CRA+1
	STA	RWA+1

	LDY	#0
	LDX	#$40
	CMP	#$40
	BCC	COP4C
	LDY	$2E5
	LDX	$2E6

COP4C	TYA
	SEC
	SBC	RWA
	STA	RWL
	TXA
	SBC	RWA+1
	STA	RWL+1

	JSR	BREAD
	BPL	COP4D

	TYA
	EOR	#136
	BNE	COPERJ

	STA	COPNEWF

COP4D	LDX	COPTOP
	LDA	CRA
	STA	TFADRL,X
	LDA	CRA+1
	STA	TFADRH,X

	LDA	RWL
	STA	TFLENL,X
	CLC
	ADC	CRA
	STA	CRA
	LDA	RWL+1
	STA	TFLENH,X
	ADC	CRA+1
	STA	CRA+1

	LDA	EXP
	STA	TFF,X

	INC	COPTOP
	JMP	COPY3
*
COPERJ	JMP	ERR
*

COPYW	LDX	#2
COP4E	LDA	POS,X
	STA	INPOS,X
	DEX
	BPL	COP4E
*
	INX
	STX	COPIX
	CPX	COPTOP
	BNE	CW2
*
CWEX	LDA	COPENDF
	BEQ	CWEX2
	JMP	NEWLP
CWEX2	JMP	COPY2
*
CW2	LDA	#1
	JSR	COPDISK

	LDX	#2
CW2B	LDA	OUTPOS,X
	STA	POS,X
	DEX
	BPL	CW2B
*

CW3	LDX	COPIX
	CPX	COPTOP
	BEQ	CWEX
*
	LDA	TFF,X
	CMP	OUTFLG
	BEQ	CW3B

CW3A	LDY	#0
	STY	POS
	STY	POS+1
	STY	POS+2

CW3B	STA	OUTFLG
	JSR	COPPLAC
*
	LDA	#0
	STA	INFLG

	LDY	#17

CW3BB	LDA	(MT1),Y
	INY
	INY
	STA	(10),Y
	ORA	INFLG
	STA	INFLG

	DEY
	CPY	#23
	BCC	CW3BB

	LDA	INFLG
	BEQ	CW3BC
	LDA	#$FF
CW3BC	JSR	CLTDOV2
*
	LDX	COPIX
	LDA	TFADRL,X
	STA	RWA
	LDA	TFADRH,X
	STA	RWA+1
	LDA	TFLENL,X
	STA	RWL
	LDA	TFLENH,X
	STA	RWL+1
*
	JSR	OVERTST
	BCC	CW3BD
*SKIP
	JSR	CLTDOV

CW3BF	INC	COPIX

	LDX	COPIX
	CPX	COPTOP
	BNE	CW3BE

	LDA	#0
	STA	COPNEWF
	BEQ	CWEX

CW3BE	LDA	TFF,X
	CMP	OUTFLG
	BEQ	CW3BF
	BNE	CW3A
*

CW3BD	JSR	BWRITE
	BMI	COPERJ2

	JSR	CLTDOV

	LDA	OUTFLG
	JSR	ODOZN

	LDX	#2
CW4B	LDA	POS,X
	STA	OUTPOS,X
	DEX
	BPL	CW4B
*
	INC	COPIX

	JMP	CW3
*
COPERJ2	JMP	ERR
*

CLTDOV	LDA	#0

CLTDOV2	LDY	#25
	STA	(10),Y
	RTS
*

OVERTST	LDA	POS
	ORA	POS+1
	ORA	POS+2
	ORA	QFLG
	BNE	OVTX

	JSR	TEXIST
	BMI	OVTO

	JSR	CLLIN

	LDY	#<QUEST
	LDX	#>QUEST
	JSR	MENBEL

	PHA
	JSR	CLLIN
	PLA

	TAX
	BMI	OVRAB
	BEQ	OVTO
	DEX
	BNE	OVRAB

	SEC
	DTA	$24
OVTO	CLC

	LDA	#0
	STA	POS
	STA	POS+1
	STA	POS+2

	RTS

OVTX	CLC
	RTS

OVRAB	JSR	INISCR
	JMP	NEWLP

QUEST	DTA	3,0,20
	DTA	16,26,31
	DTA	9,4,5
	DTA	C"OSA"
	DTA	C" File exists !"
	DTA	C"  Overwrite Skip"
	DTA	C" Abort"
	EOL

*** DEFINICE

DATB	EQU	*

MAPB	EQU	DATB+256
DIRB	EQU	MAPB+256
F	EQU	DIRB+256	;FNAME
F2	EQU	F+11	;FNAME2
P	EQU	F2+11	;PATH
P2	EQU	P+$E8	;PATH2
DSKSTAT	EQU	P2+$E8
DSKST2	EQU	DSKSTAT+17
UPDNAM	EQU	DSKST2+17

MEMB	EQU	UPDNAM+11

* PRENOS

PDATB	EQU	DATB
