	ORG	$6000,$A800
	JMP	START

*************************************
*				*
*	C O P Y   PRO BW-DOS	*
*				*
*************************************

SRCIX	EPZ	128
DSTIX	EPZ	129

TMP	EPZ	130

FFLAG	EPZ	131
DSTAUX1	EPZ	132
ENDFLG	EPZ	133

NOFLAG	EPZ	134

* VOLNO: 134

SRC	EQU	$500
DST	EQU	$540

DIRBUF	EQU	$580

DSTMSK	EQU	$5E0

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

GETPAR	PHA

	JSR	GETNAME

	PLA
	TAX
*
	LDY	#33

GTPAR0	STX	TMP

GTPAR1	LDA	(10),Y
	STA	SRC,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

	LDA	TMP

CLOSEX	RTS

***

CLOSES	LDX	#$10
	JSR	CLOS3

CLOSE23	LDX	#$20
	JSR	CLOS3

	LDX	#$30

CLOS3	LDA	#12
	STA	$342,X

	JSR	$E456
	BPL	CLOSEX

	JMP	ERROR
***

HVEZDY	DFB 155
	ASC "*.*"
***

OPEN	STA	$34A,X
	LDA	#0
	STA	$34B,X

OPEN2	TYA
	STA	$344,X
	LDA	#SRC:H
	STA	$345,X

	LDA	#3
	STA	$342,X

	JSR	$E456
	BMI	ERROR

DGETOK	RTS

***

DIRGET	LDX	#$10
	LDA	#5
	STA	$342,X

	LDA	#DIRBUF:L
	STA	$344,X
	LDA	#DIRBUF:H
	STA	$345,X

	LDA	#64
	STA	$348,X
	LDA	#0
	STA	$349,X

	JSR	$E456
	BPL	DGETOK

	CPY	#136
	BEQ	ERR5
**

ERROR	TYA
	PHA

	JSR	PRINT
	DFB 155
	ASC "Error "
	DFB 0

	LDX	#2
	PLA

ERR2	LDY	#0

ERR3	CMP	ERRTAB,X
	BCC	ERR4

	SBC	ERRTAB,X
	INY
	BNE	ERR3
**

ERR4	PHA
	TXA
	PHA

	TYA
	ORA	#$30
	JSR	PRT1

	PLA
	TAX
	PLA

	DEX
	BPL	ERR2
**
	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSES

	JMP	(10)
**

ERRTAB	DFB 1,10,100

***

DC2DGET	LDA	#$20
	STA	DIRBUF+12
	STA	DIRBUF+8

	LDY	#255

DC2DG2	INY
	LDA	DIRBUF,Y
	CMP	#$20
	BEQ	DC2DG3

	JSR	JELIPL
	BCS	DC2DG5

	JSR	DC2DGP
	JMP	DC2DG2
**

DC2DG3	CPY	#9
	BCS	DC2DG4

	LDA	#'.'
	JSR	DC2DGP

	LDY	#8
	BNE	DC2DG2
**

DC2DG4	LDA	#$9B
	JSR	DC2DGP

	CLC
DC2DG5	RTS

**

DC2DGP	STA	SRC,X
	INX
	RTS

***

JELIPL	CMP	#'_'
	BEQ	JLPP

	CMP	#$30
	BCC	JLPM
	CMP	#$3A
	BCC	JLPP
	CMP	#$41
	BCC	JLPM
	CMP	#$5B
	BCS	JLPM

JLPP	CLC
	RTS
*

JLPM	SEC
	RTS
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS version"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	CLOSES

	LDA	#0
	JSR	GETPAR
	STA	SRCIX

	LDA	#(DST-SRC)
	JSR	GETPAR
	STA	DSTIX

	LDA	SRC
	CMP	#'D'
	BEQ	DCOP1

	LDA	#8
	STA	DSTAUX1

	JSR	COPY1

	JSR	CLOSES
	JMP	(10)
**

HVE1	LDA	SRC,X
	CMP	#$9B
	BNE	DCOP1AX

	LDY	#3
DCOP1A	LDA	HVEZDY,Y
	STA	SRC,X
	INX
	DEY
	BPL	DCOP1A

DCOP1AX	RTS

***DISK!

DCOP1	LDA	#0
	STA	FFLAG
* *.*?
	LDX	SRCIX
	JSR	HVE1

	LDA	DSTIX
	TAX
	JSR	HVE1

* DEST. MASKA

	LDX	DSTIX
	LDY	#0

	LDA	#6
	STA	TMP

	DEX

DC1B1	INX

	LDA	SRC,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	CMP	#'?'
	BEQ	DC1BZN

	JSR	JELIPL
	BCS	DC1BNEP
*

DC1BZN	JSR	DC1BPUT
	JMP	DC1B1
**

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP3

DC1BP1	RTS

*

DC1BP2	STA	DSTMSK,Y
	INY
	RTS

DC1BP3	INY
	STA	DSTMSK,Y
	RTS
**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE
	BCS	DC1B1
**

DC1BNEP	SEC
	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC

	ROR	TMP
	BCC	DC1B1
*END
	LDA	#8
	STA	DSTAUX1

	LDA	SRC,X
	CMP	#'/'
	BNE	DCOP1BX

	LDA	SRC+1,X
	CMP	#'A'
	BNE	DCOP1BX

	INC	DSTAUX1

*DIR OPEN

DCOP1BX	LSR	NOFLAG

	LDX	#$10
	LDA	#6
	STA	$34A,X
	LDA	#128
	STA	$34B,X
	LDY	#SRC:L
	JSR	OPEN2

	JSR	DIRGET
	LDA	DIRBUF
	CMP	#$9B
	BNE	DCOP2B

	JSR	DIRGET
	LDA	DIRBUF+1
	CMP	#'o'
	BNE	DCOP2B

	JSR	DIRGET
	JSR	DIRGET

	DEC	FFLAG

** DISK COPY LOOP

DCOP2	JSR	DIRGET

DCOP2B	LDA	FFLAG
	BMI	DCOP2C

*SHORT

	LDA	DIRBUF
	CMP	#$30
	BCS	DCOP2EX

	LDA	DIRBUF+10
	BMI	DCOP2

*FORMAT NAZVU

	LDX	#2
	LDY	#0

DCOP2BC	LDA	DIRBUF,X
	STA	DIRBUF,Y

	INX
	INY
	CPX	#10
	BNE	DCOP2BD
	INY

DCOP2BD	CPX	#13
	BCC	DCOP2BC
	BCS	DCOP2D

**LONG

DCOP2C	LDA	DIRBUF+17
	CMP	#$3A
	BCC	DCOP2D

	CMP	#$40
	BCC	DCOP2

DCOP2EX	LDA	NOFLAG
	BPL	DC2XER
	JMP	ERR5
**
DC2XER	LDY	#170
	JMP	ERROR
** NAMES

DCOP2D	LDX	SRCIX
	JSR	DC2DGET
	BCS	DCOP2

	LDX	#11
DCOP2DB	LDA	DSTMSK,X
	CMP	#'?'
	BEQ	DCOP2DC
	STA	DIRBUF,X
DCOP2DC	DEX
	BPL	DCOP2DB

	LDX	DSTIX
	JSR	DC2DGET
	BCS	DCOP2

* TIME/DATE

	LDA	#0
	BIT	FFLAG
	BPL	DCOP2EB

	LDY	#19
	LDX	#20

DCOP2E	TXA
	PHA

	LDA	DIRBUF+1,X
	AND	#15
	PHA
	LDA	DIRBUF,X
	AND	#15
	TAX
	PLA
	CLC
	ADC	DCOP2ET,X

	STA	(10),Y

	PLA
	TAX
	INX
	INX
	INX

	INY
	CPY	#24
	BNE	DCOP2E

	LDA	#0
	STA	(10),Y

	LDA	#$FF
**

DCOP2EB	LDY	#25
	STA	(10),Y

**TOTEZ?

	LDX	#255

COP2	INX
	LDA	SRC,X
	CMP	DST,X
	BNE	COP3

	CMP	#$9B
	BNE	COP2
**
	JSR	PRINT
	DFB 155,253
	ASC "Can't copy a file "
	ASC "to itself !"
	DFB 155,0

	JMP	ERR5X

***PRT SOURCE

COP3	JSR	PRINT
	ASC "Copying "
	DFB 0

	LDX	#0
	LDA	#SRC:L
	STA	$344,X
	LDA	#SRC:H
	STA	$345,X
	LDA	#64
	STA	$348,X
	LDA	#0
	STA	$349,X
	LDA	#9
	STA	$342,X
	JSR	$E456
**
	SEC
	ROR	NOFLAG

	JSR	COPY1

	JMP	DCOP2
***

DCOP2ET	DFB 0,10,20,30,40,50,60,70
	DFB 80,90,0,0,0,0,0,0

***OPEN SOURCE

COPY1	JSR	CLOSE23

	LDX	#$20
	LDA	#4
	LDY	#SRC:L
	JSR	OPEN

*OPEN DEST

	LDX	#$30
	LDA	DSTAUX1
	LDY	#DST:L
	JSR	OPEN

*COPY IT

COP4	LDX	#3

COP4B	LDA	#0
	STA	COPLEN,X
	LDA	$2E5,X
	STA	COPEND2,X

	DEX
	BPL	COP4B
*
	LDY	#0
	STY	ENDFLG
	JSR	READIT
	BMI	COP4C

	LDY	#2
	JSR	READIT
	BPL	COP4D

COP4C	DEC	ENDFLG

COP4D	LDY	#0
	JSR	WRITEIT

	LDY	#2
	JSR	WRITEIT

	LDA	ENDFLG
	BEQ	COP4

	JMP	CLOSE23
***

COPEND	DFW $6000
COPEND2	DFW $8000
COPAD	DFW $5000,END
COPLEN	DFW 0,0

***

READIT	LDX	#$20

	LDA	COPAD,Y
	STA	$344,X
	LDA	COPAD+1,Y
	STA	$345,X

	LDA	COPEND,Y
	SEC
	SBC	COPAD,Y
	STA	$348,X
	LDA	COPEND+1,Y
	SBC	COPAD+1,Y
	STA	$349,X

	LDA	#7
	STA	$342,X

	TYA
	PHA

	JSR	$E456
	STY	TMP

	PLA
	TAY

	LDA	$348,X
	STA	COPLEN,Y
	LDA	$349,X
	STA	COPLEN+1,Y

	LDA	TMP
	BPL	RDITEX
	CMP	#136
	BNE	ERRORJ

RDITEX	TAY
	RTS

***

WRITEIT	LDX	#$30

	LDA	COPAD,Y
	STA	$344,X
	LDA	COPAD+1,Y
	STA	$345,X

	LDA	COPLEN,Y
	STA	$348,X
	LDA	COPLEN+1,Y
	STA	$349,X

	ORA	COPLEN,Y
	BEQ	RDITEX

	LDA	#11
	STA	$342,X

	JSR	$E456
	BPL	RDITEX
*

ERRORJ	JMP	ERROR

******************************

END	EQU	*


