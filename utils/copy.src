*************************************
*				*
*	C O P Y   PRO BW-DOS	*
*				*
*************************************

SRCIX	EQU	128
DSTIX	EQU	129

TMP	EQU	130

FFLAG	EQU	131
DSTAUX1	EQU	132
ENDFLG	EQU	133

NOFLAG	EQU	134

OPNIF	EQU	135
OPNOF	EQU	136

DIRENDF	EQU	137

* VOLNO: 138

SRC	EQU	$500
DST	EQU	$540

DIRBUF	EQU	$580

DSTMSK	EQU	$5E0

***

BIT2	EQU	$24
BIT3	EQU	$2C

	ORG	$3000

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

* NO PARAMETERS?

	LDY	#10
	LDA	(10),Y	;BUFOFF
	CLC
	ADC	#63
	TAY
	LDA	(10),Y ;LBUF+BUFOFF

	CMP	#$9B
	BNE	START2A

* NAPOVEDA

	JSR	PRINT
	DTA	155
	DTA	C"COPY 1.3 for BW-DOS"
	DTA	155
	DTA	C"Syntax: COPY source"
	DTA	C" destination[/A]"
	DTA	155,0

	JMP	(10)
***

START2A	LDA	#$9B
	JSR	PRT1
	JSR	CLOSES

	LDA	#0
	JSR	GETPAR
	STA	SRCIX

	LDA	#(DST-SRC)
	JSR	GETPAR
	STA	DSTIX

* WILDCARD -> USE DIRS

	LDX	SRCIX
STRT3	LDA	SRC,X
	INX
	CMP	#'?'
	BEQ	DCOP1B
	CMP	#'*'
	BEQ	DCOP1B
	CMP	#$9B
	BNE	STRT3

* PRO "D:" VZDY - KVULI DATE/TIME

	LDA	SRC
	CMP	#'D'
	BEQ	DCOP1

* JEN 1 FILE!

	JSR	SRCHVE
	JSR	DSTHVE

	LDX	SRCIX
	JSR	GETMASK

	LDX	#11
STRT4	LDA	DSTMSK,X
	STA	DIRBUF,X
	DEX
	BPL	STRT4

	LDX	DSTIX
	JSR	GETMASK	;+ SET AUX!

	JSR	RENAMIT

	LDX	DSTIX
	JSR	DC2DGET
	BCS	STRT4E

	LDY	#25	;TDOVER
	LDA	#0
	STA	(10),Y

	JSR	COPY1

	JMP	ERR5
***

STRT4E	LDY	#165
	JMP	ERROR

***

SRCHVE	LDX	SRCIX
	DTA	BIT3
DSTHVE	LDX	DSTIX

	LDA	SRC,X
	CMP	#$9B
	BNE	DCOP1AX

	LDY	#3
DCOP1A	LDA	HVEZDY,Y
	STA	SRC,X
	INX
	DEY
	BPL	DCOP1A

DCOP1AX	RTS

***DISK!

DCOP1	JSR	SRCHVE

***ADRESAR!

DCOP1B	JSR	DSTHVE

* DEST. MASKA & SET AUX

	LDX	DSTIX
	JSR	GETMASK

*DIR OPEN

	LSR	NOFLAG
	LSR	DIRENDF

	JSR	DIROPEN
	JMP	COPY3T

** DISK COPY LOOP

DC2ERR	JSR	GETDIRP
	JMP	COPY3T
**

DCOP2D	LDX	SRCIX
	JSR	DC2DGET
	BCS	DC2ERR

	JSR	RENAMIT

	LDX	DSTIX
	JSR	DC2DGET
	BCS	DC2ERR

* TIME/DATE

	LDA	#0
	BIT	FFLAG
	BPL	DCOP2EB

	LDY	#19
	LDX	#20

DCOP2E	TXA
	PHA

	LDA	DIRBUF+1,X
	AND	#15
	PHA
	LDA	DIRBUF,X
	AND	#15
	TAX
	PLA
	CLC
	ADC	DCOP2ET,X

	STA	(10),Y

	PLA
	TAX
	INX
	INX
	INX

	INY
	CPY	#24
	BNE	DCOP2E

	LDA	#0
	STA	(10),Y

	LDA	#$FF
**

DCOP2EB	LDY	#25	;TDOVER
	STA	(10),Y

	JSR	GETDIRP

** TOTEZ ? (POUZE PRO "D:")

	LDA	SRC
	CMP	#'D'
	BNE	COP3

	LDX	#255

COP2	INX
	LDA	SRC,X
	CMP	DST,X
	BNE	COP3

	CMP	#$9B
	BNE	COP2
**
	JSR	PRINT
	DTA	155
	DTA	C"Can't copy a file "
	DTA	C"to itself !"
	DTA	155,0

	JMP	ERR5X

***PRT SOURCE

COP3	JSR	PRINT
	DTA	C"Copying "
	DTA	0

	LDX	#0
	LDA	#<SRC
	STA	$344,X
	LDA	#>SRC
	STA	$345,X
	LDA	#64
	STA	$348,X
	LDA	#0
	STA	$349,X
	LDA	#9
	STA	$342,X
	JSR	$E456
**
	SEC
	ROR	NOFLAG

	JSR	COPY1

COPY3T	LDA	DIRENDF
	BMI	COPY3X
	JMP	DCOP2D
**

COPY3X	LDA	NOFLAG
	BPL	DC2XER
	JMP	ERR5
**
DC2XER	LDY	#170
	JMP	ERROR

***

DCOP2ET	DTA	0,10,20,30,40,50,60,70
	DTA	80,90,0,0,0,0,0,0

*** COPY 1 FILE

COPY1	JSR	CLOSE23

	LSR	OPNIF
	LSR	OPNOF

*COPY IT

COP4	LDX	#3

COP4B	LDA	#0
	STA	COPLEN,X
	LDA	$2E5,X
	STA	COPEND2,X

	DEX
	BPL	COP4B
*
	LDA	#0
	STA	ENDFLG
	JSR	READIT
	BMI	COP4C

	LDA	#2
	JSR	READIT
	BPL	COP4D

COP4C	DEC	ENDFLG
	JSR	CLOSE2

COP4D	LDA	#0
	JSR	WRITEIT

	LDA	#2
	JSR	WRITEIT

	LDA	ENDFLG
	BEQ	COP4

	JMP	CLOSE23
***

COPEND	DTA	A(START)
COPEND2	DTA	A($8000)
COPAD	DTA	A($5000,END)
COPLEN	DTA	A(0,0)

***

READIT	PHA

	LDA	OPNIF	;OTEVRENO?
	BMI	RDIT2

	SEC
	ROR	OPNIF

	LDX	#$20	;OPEN!
	LDA	#4
	LDY	#<SRC
	JSR	OPEN
**

RDIT2	PLA
	TAY

	LDX	#$20

	LDA	COPAD,Y
	STA	$344,X
	LDA	COPAD+1,Y
	STA	$345,X

	LDA	COPEND,Y
	SEC
	SBC	COPAD,Y
	STA	$348,X
	LDA	COPEND+1,Y
	SBC	COPAD+1,Y
	STA	$349,X

	LDA	#7
	STA	$342,X

	TYA
	PHA

	JSR	$E456
	STY	TMP

	PLA
	TAY

	LDA	$348,X
	STA	COPLEN,Y
	LDA	$349,X
	STA	COPLEN+1,Y

	LDY	TMP
	BPL	RDITEX
	CPY	#136
	BNE	ERRORJ

RDITEX	TYA
	RTS

***

WRITEIT	PHA

	LDA	OPNOF	;OTEVRENO?
	BMI	WRIT2

	SEC
	ROR	OPNOF

	LDX	#$30	;OPEN!
	LDA	DSTAUX1
	LDY	#<DST
	JSR	OPEN
***

WRIT2	PLA
	TAY

	LDX	#$30

	LDA	COPAD,Y
	STA	$344,X
	LDA	COPAD+1,Y
	STA	$345,X

	LDA	COPLEN,Y
	STA	$348,X
	LDA	COPLEN+1,Y
	STA	$349,X

	ORA	COPLEN,Y
	BEQ	RDITEX

	LDA	#11
	STA	$342,X

	JSR	$E456
	BPL	RDITEX
*

ERRORJ	JMP	ERROR

******

GETMASK	LDY	#0

	LDA	#6
	STA	TMP

	BNE	DC1B1B
**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE

DC1BZN	JSR	DC1BPUT

**

DC1B1	INX

DC1B1B	LDA	SRC,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	JSR	JELIPL
	BCC	DC1BZN

* NEPLATNY ZN. (C=1)

	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC
* C=1
	ROR	TMP
	BCC	DC1B1
* END
	LDA	#8
	STA	DSTAUX1

	LDA	SRC,X
	CMP	#'/'
	BNE	DCOP1BX

	LDA	SRC+1,X
	CMP	#'A'
	BNE	DCOP1BX

	INC	DSTAUX1

DCOP1BX	RTS

***

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP3

DC1BP1	RTS

*

DC1BP2	STA	DSTMSK,Y
DC1BP3	INY
	STA	DSTMSK,Y
	RTS

******

DIROPE	LDY	#148	;DIVNY DIR.
	JMP	ERROR

DIROPEN	LSR	FFLAG

	LDX	#$10
	LDA	#6
	STA	$34A,X

	LDA	#128
	LDY	SRC
	CPY	#'D'
	BEQ	DIROP1B
	ASL
DIROP1B	STA	$34B,X

	LDY	#<SRC
	JSR	OPEN2

	JSR	DIRGET
	BMI	DIROPE

	LDA	DIRBUF
	CMP	#$9B
	BNE	GETDP2

	JSR	DIRGET
	BMI	DIROPE

	LDA	DIRBUF+1
	CMP	#'o'
	BNE	GETDP2

	JSR	DIRGET
	BMI	DIROPE
	JSR	DIRGET
	BMI	DIROPE

	SEC
	ROR	FFLAG

* PRIPRAV DIR POLOZKU

GETDIRP	JSR	DIRGET
	BMI	DCOP2EX

GETDP2	LDA	FFLAG
	BMI	DCOP2C

*SHORT
	LDA	DIRBUF
	CMP	#$30
	BCS	DCOP2EX

	LDA	DIRBUF+10
	BMI	GETDIRP

*FORMAT NAZVU

	LDX	#2
	LDY	#0

DCOP2BC	LDA	DIRBUF,X
	STA	DIRBUF,Y

	INX
	INY
	CPX	#10
	BNE	DCOP2BD
	INY

DCOP2BD	CPX	#13
	BCC	DCOP2BC

DIROP2	RTS

**LONG

DCOP2C	LDA	DIRBUF+17
	CMP	#$3A
	BCC	DIROP2

	CMP	#$40
	BCC	GETDIRP
*EOF

DCOP2EX	SEC
	ROR	DIRENDF

CLOSE1	LDX	#$10
	JMP	CLOS3
***

RENAMIT	LDX	#11

DCOP2DB	LDA	DSTMSK,X
	CMP	#'?'
	BEQ	DCOP2DC
	STA	DIRBUF,X
DCOP2DC	DEX
	BPL	DCOP2DB

	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GETPAR	PHA

	JSR	GETNAME

	PLA
	TAX
*
	LDY	#33

GTPAR0	STX	TMP

GTPAR1	LDA	(10),Y
	STA	SRC,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

	LDA	TMP

CLOSEX	RTS

***

CLOSES	JSR	CLOSE1

CLOSE23	LDX	#$30
	JSR	CLOS3

CLOSE2	LDX	#$20

CLOS3	LDA	#12
	STA	$342,X

	JSR	$E456
	BPL	CLOSEX

	JMP	ERROR
***

HVEZDY	DTA	155
	DTA	C"*.*"
***

OPEN	STA	$34A,X
	LDA	#0
	STA	$34B,X

OPEN2	TYA
	STA	$344,X
	LDA	#>SRC
	STA	$345,X

	LDA	#3
	STA	$342,X

	JSR	$E456
	BMI	ERROR

DGETOK	TYA
	RTS

***

DIRGET	LDX	#$10
	LDA	#5
	STA	$342,X

	LDA	#<DIRBUF
	STA	$344,X
	LDA	#>DIRBUF
	STA	$345,X

	LDA	#64
	STA	$348,X
	LDA	#0
	STA	$349,X

	JSR	$E456
	BPL	DGETOK

	CPY	#136
	BEQ	DGETOK
**
	ICL	"error.inc"

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSES

	LDY	#25	;TDOVER
	LDA	#0
	STA	(10),Y

	JMP	(10)
**

ERRTAB	DTA	1,10,100

***

DC2DGET	LDA	#$20
	STA	DIRBUF+12
	STA	DIRBUF+8

	LDY	#255
	BMI	DC2DG2B
**

DC2DG2	JSR	JELIPL
	BCS	DC2DG5

DC2DG2A	STA	SRC,X
	INX

DC2DG2B	INY
	LDA	DIRBUF,Y
	CMP	#$20
	BNE	DC2DG2

	LDA	#'.'
	CPY	#9
	LDY	#8
	BCC	DC2DG2A

	LDA	#$9B
	STA	SRC,X

JLPP	CLC
DC2DG5	RTS

***

JELIPL	CMP	#'?'
	BEQ	JLPP

	CMP	#'_'
	BEQ	JLPP

	CMP	#$30
	BCC	JLPM
	CMP	#$3A
	BCC	JLPP
	CMP	#$41
	BCC	JLPM
	CMP	#$5B
	BCC	JLPP

JLPM	SEC
	RTS
******************************

END	EQU	*


