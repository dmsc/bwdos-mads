*************************************
*				*
*   " A R G S R T C "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128
RELZP	EQU	130
REDFLG	EQU	132
TCNT	EQU	134
TTMP	EQU	135
COPZPG	EQU	137
COPZP2	EQU	139

***

VCOUNT	EQU	$D40B
WSYNC	EQU	$D40A

*** PRO REZIDENT

COUNT	EQU	$31	;SIO CHECK
PORT	EQU	$D505
PCTL	EQU	$D507

***

	ORG	$6000
	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BNE	STRTER

	LDA	$703
	CMP	#'B'
	BNE	STRTER
	LDA	$704
	CMP	#'W'
	BEQ	START2

STRTER	JSR	PRINT
	DTA	155,253
	DTA	C"Error: Not BW-DOS !"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	ST2ON
*OFF?
	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	SYNTAX

	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	LDX	#0

	CMP	#'/'
	BNE	ST2O2

	INX

	INY
	LDA	(10),Y
	CMP	#'R'
	BNE	SYNTAX

	INY
	LDA	(10),Y

ST2O2	CMP	#$9B
	BEQ	START3

***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: ARGSRTC [/R]"
	DTA	155
	DTA	C"        ARGSRTC OFF"
	DTA	155,0

	JMP	(10)

*** NENI UZ ?

START3	STX	REDFLG

	JSR	SRCHRUT
	BCC	START4
*JE!
	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"already installed!"
	DTA	155,0
	JMP	(10)

***INSTALL

START4	JSR	RT8TEST
	BCS	START4A

*RTC8 NEEX.

	JSR	PRINT
	DTA	155
	DTA	C"ARGS RTC cartridge "
	DTA	C"not installed !"
	DTA	155,0
	JMP	(10)
***

START4A	LDX	REDFLG

	LDA	743
	STA	SRCHADR
	CLC
	ADC	RESLENTL,X
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	RESLENTH,X
	STA	744
	STA	R02+1
*RELOK

RLK0	LDX	#0

RLK1	LDA	RELOKT,X
	STA	RELZP
	LDA	RELOKT+1,X
	STA	RELZP+1

	ORA	RELZP
	BEQ	INSTAL2

	LDA	RELOKT+2,X
	SEC
	SBC	#<RESID
	TAY
	LDA	RELOKT+3,X
	SBC	#>RESID
	PHA

	TYA
	LDY	#1

	CLC
	ADC	SRCHADR
	STA	(RELZP),Y
	INY
	PLA
	ADC	SRCHADR+1
	STA	(RELZP),Y

	INX
	INX
	INX
	INX

	JMP	RLK1
***

INSTAL2	LDA	10
	CLC
	ADC	#13
	TAY
	LDA	11
	ADC	#0
	TAX

	STY	XTR2+1
	STX	XTR2+2
	STY	XTR4+1
	STX	XTR4+2
	STY	XTR5+1
	STX	XTR5+2

	INY
	BNE	INST2XA
	INX

INST2XA	STY	XTR3+1
	STX	XTR3+2

	INY
	BNE	INST2XB
	INX

INST2XB	STY	XTR1+1
	STX	XTR1+2

*** FLAG REZIMU

	LDX	REDFLG
	LDA	REDFT,X
	STA	REDRES
**

	LDX	#3

INST2B	LDA	$706,X
	STA	OLDADR,X
	DEX
	BPL	INST2B
**
	LDA	#<RESID
	STA	COPZPG
	LDA	#>RESID
	STA	COPZPG+1
	LDA	SRCHADR
	STA	COPZP2
	LDA	SRCHADR+1
	STA	COPZP2+1

	LDX	REDFLG
	LDY	#0

INST2C	TYA
	CMP	RESLENTL,X
	BNE	INST2D
	LDA	COPZPG+1
	SEC
	SBC	#>RESID
	CMP	RESLENTH,X
	BEQ	INSTL2E

INST2D	LDA	(COPZPG),Y
	STA	(COPZP2),Y

	INY
	BNE	INST2C
	INC	COPZPG+1
	INC	COPZP2+1
	BNE	INST2C
**

INSTL2E	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDX	#1
	LDY	#3

	LDA	REDFLG
	BEQ	INSTL4
	LDY	#5

INSTL4	LDA	NEWADR,X
	STA	$706,X
	LDA	NEWADR,Y
	STA	$708,X
	DEY
	DEX
	BPL	INSTL4
**

	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"Installed."
	DTA	155,0

	LDA	REDFLG
	BEQ	INSTL5

	JSR	PRINT
	DTA	C"(Reduced version - "
	DTA	C"set time/date is"
	DTA	155
	DTA	C"not supported)"
	DTA	155,0

INSTL5	JMP	(10)

***

DISABLE	JSR	SRCHRUT
	STA	REDFLG
	BCS	DISABLE2

	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"not Installed!"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"is not the last"
	DTA	155
	DTA	C"installed handler!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1

	LDX	REDFLG
	CMP	RESLENTH,X
	BNE	DISCANT

	TYA
	CMP	RESLENTL,X
	BNE	DISCANT
*ODSTRAN
	LDY	#(OLDADR-RESID)
	LDX	#0

DISABL3	LDA	(SRCHADR),Y
	STA	$706,X

	INY
	INX
	CPX	#4
	BCC	DISABL3
*
	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**
	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"removed."
	DTA	155,0

	JMP	(10)
****

ARGSDRV	JSR	PRINT
	DTA	155
	DTA	C"ARGS RTC driver "
	DTA	0
	RTS
****

SRCHRUT	LDA	743
	SEC
	SBC	RESLENTL+1
	STA	SRCHADR
	LDA	744
	SBC	RESLENTH+1
	STA	SRCHADR+1
*

SRCHR1	LDY	#GETTD-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#S02-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#RD3-RESID
	BNE	SRCHR3
*
	LDY	#RDRET-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR4
**REZIM
	LDY	#REDRES-RESID
	LDA	(SRCHADR),Y
	LDX	#1

SRCHR5	CMP	REDFT,X
	BEQ	SRCHR6

	DEX
	BPL	SRCHR5
	BMI	SRCHNXT

SRCHR6	TXA
*C=1
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

**** TEST PRITOMNOSTI RTC

RT8TEST	JSR	TRDREG
	STA	TTMP
	LDA	#40
	STA	TCNT

RT8T	STA	$D40A
	JSR	TRDREG
	EOR	TTMP
	LSR
	BCS	RT8TOK

	DEC	TCNT
	BNE	RT8T
*C=0
RT8TOK	RTS

***

TRDREG	JSR	TSWO

	LDY	#0

TRDR2	DEY
	BEQ	TRDRX

	LDA	PORT
	AND	#16
	BEQ	TRDR2

	LDY	#15
	STY	PORT
	LDA	#15+32
	STA	PORT
	STY	PORT

	LDY	#$60
	JSR	TSW

	LDA	#15+64
	LDY	#15
	STA	PORT
	LDA	PORT
	STY	PORT

	AND	#1

TSWO	LDY	#$6F
TSW	PHA
	LDA	#$30
	STA	PCTL
	STY	PORT
	LDA	#$34
	STA	PCTL
	PLA
TRDRX	RTS

********

REDFT	DTA	9,73
*ORA#,EOR#

***********REZIDENT

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

*RTS VYNECHAN - NEVADI
**
GETTD	JSR	SOUT
	LDY	#$0D
	STY	PORT

RD2	JSR	GETPUL
	STA	COUNT
S01	JSR	GETPUL
RD3	CLC
	ADC	#10
	DEC	COUNT
	BPL	RD3
	SBC	#9

	TAY
	TXA
	LSR
	TAX
	TYA

S02	LDY	TAB1,X
	BNE	XTR4
	DEC	PORT

XTR4	STA	$FFFF,Y	;COTAB+13

	TXA
	BNE	RD2

	RTS

**
OLDADR	DTA	A(0,0)
**
TAB1	DTA	5,4,3,0,1,2
**

GETPUL	DEC	PORT

GETP2B	LDA	PORT
	AND	#31
	SEC
	SBC	#16
	BCC	GETP2B

	TAX
REDRES	ORA	#32
	STA	PORT
	STX	PORT

	LDY	#$60
S03	JSR	SWITCH

TST2	EOR	#64+32
	STA	PORT
	LDA	PORT
	STX	PORT

	AND	#15
	CPX	#5
	BNE	SOUT
	AND	#3

SOUT	LDY	#$6F
SWITCH	PHA
	LDA	#$30
WRJP	STA	PCTL
	STY	PORT
	LDA	#$34
	STA	PCTL
	PLA
RDRET	RTS

******* KONEC GETTD, NASLEDUJE SETTD

STD2	TXA
	LSR
	TAY
S04	LDA	TAB1,Y
	TAY
	BNE	XTR5
	INX

XTR5	LDA	$FFFF,Y	;COMTAB+13

	LDY	#$FF
	SEC

WRR2	INY
	SBC	#10
	BCS	WRR2
	ADC	#10

	STY	COUNT
S05	JSR	WRPUL
	LDA	COUNT
S05B	JSR	WRPUL

	DTA	$2C
SETTD	LDX	#0

	CPX	#$0D
	BCC	STD2

* DEN V TYDNU (C=1)

XTR1	LDA	$FFFF	;YEAR
*			;COMTAB+15
	SBC	#92
	BCS	STD3
	ADC	#100

STD3	STA	COUNT

	LSR
	LSR

	CLC
	ADC	COUNT
XTR2	ADC	$FFFF	;DAY
*			;COMTAB+13
XTR3	LDX	$FFFF	;MONTH
*			;COMTAB+14
S06	ADC	STD4T-1,X

	TAY

	LDA	COUNT
	AND	#3
	BNE	STD5
	CPX	#3
	BCC	STD6

STD5	INY

STD6	TYA

STD7	SEC
	SBC	#7
	BCS	STD7
	ADC	#7

	LDX	#6
**
WRPUL	CPX	#5
	BNE	WRP2
	ORA	#8

WRP2	JSR	SOUT
	TAY

WRP3	LDA	PORT
	AND	#16
	BEQ	WRP3

	TXA
	STA	PORT
	ORA	#32
	STA	PORT
	STX	PORT

	STY	PORT

	INX
	PHA
	LDA	#$3C
S08	JMP	WRJP
*****

STD4T	DTA	2,5,5,1,3,6
	DTA	1,4,0,2,5,0

*****

RESLEN1	EQU	STD2-RESID
RESLEN2	EQU	*-RESID

RESLENTL	DTA	<RESLEN2,<RESLEN1
RESLENTH	DTA	>RESLEN2,>RESLEN1

***

NEWADR	DTA	A(0,0,0)

RELOKT	DTA	A(GETTD,SOUT)
	DTA	A(RD2,GETPUL)
	DTA	A(S01,GETPUL)
	DTA	A(S02,TAB1)
	DTA	A(S03,SWITCH)
	DTA	A(S04,TAB1)
	DTA	A(S05,WRPUL)
	DTA	A(S05B,WRPUL)
	DTA	A(S06,STD4T-1)
	DTA	A(WRP2,SOUT)
	DTA	A(S08,WRJP)

	DTA	A(NEWADR-1,GETTD)
	DTA	A(NEWADR+1,SETTD)
	DTA	A(NEWADR+3,RDRET)

	DTA	A(0,0)

