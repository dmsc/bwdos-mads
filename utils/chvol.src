*************************************
*				*
*    " C H V O L "   PRO BW-DOS	*
*				*
*************************************

ZPG1	EQU	128

	ORG	$3000

***
	ICL	"chkdos.inc"
***
	LDA	10
	STA	ZPG1
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	STA	ZPG1+1
	ADC	#0
	STA	GETNAME+2

	DEC	ZPG1+1

	LDY	#256-10
	LDA	(ZPG1),Y
	STA	SIOJP+1
	INY
	LDA	(ZPG1),Y
	STA	SIOJP+2
***

GETNAME	JSR	GETNAME

	LDY	#33
	LDA	(10),Y
	CMP	#'D'
	BNE	STER1

	INY
	LDA	(10),Y
	AND	#15
	STA	SIODRIV
**
	LDA	#'R'
	LDX	#$40
	JSR	RWSECT
**
	LDY	#148
	LDA	BUFF+7
	CMP	#$80
	BNE	ERROR

	LDA	BUFF+32
	CMP	#$20
	BCC	ERROR

	LDA	BUFF+31
	ASL
	BEQ	START3
**
STER1	LDY	#146
	BNE	ERROR
***

START3	LDY	#36
	LDX	#0

ST3B	LDA	(10),Y
	INY
	CMP	#155
	BNE	ST3C
	DEY
	LDA	#32

ST3C	STA	BUFF+22,X

	INX
	CPX	#8
	BCC	ST3B
**

START4	LDY	#256-2
	LDA	(ZPG1),Y
	LDX	#128
	JSR	RWSECT
**
	JMP	(10)

***


RWSECT	STA	SIOCMD
	STX	SIOSTAT

	LDX	#11
RWS2	LDA	RWST,X
	STA	$300,X
	DEX
	BPL	RWS2

SIOJP	JSR	SIOEX
	BMI	ERROR
SIOEX	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

	ICL	"error.inc"

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	JMP	(10)

**

ERRTAB	DTA	100,10,1


RWST	DTA	$31
SIODRIV	DTA	0
SIOCMD	DTA	0
SIOSTAT	DTA	0
	DTA	A(BUFF,7,128,1)

****

BUFF	EQU	*

****

