	ORG	$6000
	JMP	START

*************************************
*				*
*    " C H V O L "   PRO BW-DOS	*
*				*
*************************************

ZPG1	EQU	128

* VOLNO: 130

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

ERROR	TYA
	PHA

	JSR	PRINT
	DTA	155
	DTA	C"Error "
	DTA	0

	LDX	#2
	PLA

ERR2	LDY	#0

ERR3	CMP	ERRTAB,X
	BCC	ERR4

	SBC	ERRTAB,X
	INY
	BNE	ERR3
**

ERR4	PHA
	TXA
	PHA

	TYA
	ORA	#$30
	JSR	PRT1

	PLA
	TAX
	PLA

	DEX
	BPL	ERR2
**
	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JMP	(10)

**

ERRTAB	DTA	1,10,100

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA	155,253
	DTA	C"Incorrect DOS version"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	STA	ZPG1
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	STA	ZPG1+1
	ADC	#0
	STA	GETNAME+2

	DEC	ZPG1+1

	LDY	#256-10
	LDA	(ZPG1),Y
	STA	SIOJP+1
	INY
	LDA	(ZPG1),Y
	STA	SIOJP+2
***

	JSR	GETNAME

	LDY	#33
	LDA	(10),Y
	CMP	#'D'
	BNE	STER1

	INY
	LDA	(10),Y
	AND	#15
	STA	SIODRIV
**
	LDA	#'R'
	LDX	#$40
	JSR	RWSECT
**
	LDA	BUFF+7
	CMP	#$80
	BNE	STER2

	LDA	BUFF+32
	CMP	#$20
	BCC	STER2

	LDA	BUFF+31
	ASL
	BEQ	START3
**
STER2	LDY	#148
	DTA	$2C
STER1	LDY	#146
	JMP	ERROR
***

START3	LDY	#36

ST3B	LDA	(10),Y
	CMP	#155
	BEQ	ST3C

	STA	BUFF+22-36,Y

	INY
	CPY	#44
	BCC	ST3B
**

ST3C	CPY	#44
	BCS	START4

	LDA	#32
	STA	BUFF+22-36,Y

	INY
	BNE	ST3C
**

START4	LDY	#256-2
	LDA	(ZPG1),Y
	LDX	#128
	JSR	RWSECT
**
	JMP	ERR5

***

SIOEX	RTS

**

RWSECT	STA	SIOCMD
	STX	SIOSTAT

	LDX	#11
RWS2	LDA	RWST,X
	STA	$300,X
	DEX
	BPL	RWS2

SIOJP	JSR	SIOEX
	BPL	SIOEX

	JMP	ERROR
***

RWST	DTA	$31
SIODRIV	DTA	0
SIOCMD	DTA	0
SIOSTAT	DTA	0
	DTA	A(BUFF,7,128,1)

****

BUFF	EQU	*

****

