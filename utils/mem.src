*************************************
*				*
*     " M E M "	FOR BW-DOS	*
*				*
*************************************

HVAL	EQU	128

	ORG	$3000

START	LDA	$700
	CMP	#'S'
	BNE	START3

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#'M'
	BNE	START3

	JMP	LONG
***

START3	JSR	PRINT
	DTA	155
	DTA	C"MEMLO = $"
	DTA	0

	LDX	744
	LDA	743
	JSR	PRTHEX
	DTA	155
	DTA	C"MEMHI = $"
	DTA	0

	LDX	742
	LDA	741
	JSR	PRTHEX
	DTA	155,0

	JMP	(10)
***

LONG	JSR	PRINT
	DTA	155
	DTA	C"Current Memory-map:"
	DTA	155,0

	TAX
	STX	HVAL
	STX	HVAL+1
	LDA	#$80
	JSR	PRTLINE
	DTA	C"System area"
	DTA	0
	LDX	#1
	JSR	PRTLINE
	DTA	C"User area"
	DTA	0
	LDA	#$80
	LDX	#$04
	JSR	PRTLINE
	DTA	C"System area"
	DTA	0
	LDX	#$07
	JSR	PRTLINE
	DTA	C"User area"
	DTA	0

	LDY	#31
	LDA	(10),Y
	TAX
	DEY
	LDA	(10),Y
	JSR	PRTLINE
	DTA	C"DOS"
	DTA	0

	LDA	743
	LDX	744
	CMP	HVAL
	BNE	LONG2
	CPX	HVAL+1
	BEQ	LONG3

LONG2	JSR	PRTLINE
	DTA	C"Resident handlers"
	DTA	0

LONG3	LDA	741
	LDX	742
	JSR	PRTLINE
	DTA	C"User area"
	DTA	0

	LDX	106
	JSR	PRTLINE
	DTA	C"Screen memory"
	DTA	0

	LDX	#$C0

	CPX	106
	BCS	LONG4
**
	JSR	PRTLINE
	DTA	C"Cartridge or Reserved"
	DTA	0

LONG4	LDX	#$D0
	JSR	PRTLINE
	DTA	C"Operating System"
	DTA	0
	LDX	#$D8
	JSR	PRTLINE
	DTA	C"Input/Output area"
	DTA	0
	LDX	#$0
	JSR	PRTLINE
	DTA	C"Operating System"
	DTA	155,0

	JMP	(10)
****

PRTLINE	TAY
	BNE	PHXM2
	DEX
PHXM2	DEY

	TYA
	PHA
	TXA
	PHA
	JSR	PRINT
	DTA	155,'$',0

	JSR	PRTVAL
	JSR	PRINT
	DTA	C"-$"
	DTA	0
	PLA
PRTHX1	STA	HVAL+1
	PLA
	STA	HVAL
	JSR	PRTVAL
	INC	HVAL
	BNE	PRTHX4
	INC	HVAL+1
PRTHX4	LDA	#' '
	JSR	PRT1

	ICL	"print.inc"

PRTHEX	PHA
	TXA
	JMP	PRTHX1

PRTVAL	LDA	HVAL+1
	JSR	PRTHX2
	LDA	HVAL

**

PRTHX2	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX3
	PLA
**

PRTHX3	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6

	ICL	"print1.inc"


