	ORG	$6000
	JMP	START

*************************************
*				*
*     " M E M "	PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

***
	ICL	"print.inc"
***

POML	LDA	#'-'

	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BNE	START3

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#'M'
	BNE	START3

	JMP	LONG
***

START3	JSR	PRINT
	DTA 155
	DTA C"MEMLO = "
	DTA 0

	LDY	743
	LDX	744
	JSR	PRTHEX

	JSR	PRINT
	DTA 155
	DTA C"MEMHI = "
	DTA 0

	LDY	741
	LDX	742
	JSR	PRTHEX

	LDA	#155
	JSR	PRT1

	JMP	(10)
***

LONG	JSR	PRINT
	DTA 155
	DTA C"Current Memory-map :"
	DTA 155,155

	DTA C"$0000-$007F "
	DTA C"System area"
	DTA 155
	DTA C"$0080-$00FF "
	DTA C"User area"
	DTA 155
	DTA C"$0100-$047F "
	DTA C"System area"
	DTA 155
	DTA C"$0480-$06FF "
	DTA C"User area"
	DTA 155
	DTA C"$0700-"
	DTA 0

	LDY	#31
	LDA	(10),Y
	PHA
	TAX
	DEY
	LDA	(10),Y
	PHA
	TAY

	JSR	PRTHEXM

	JSR	PRINT
	DTA C" DOS"
	DTA 155,0

	PLA
	TAY
	PLA
	TAX

	CPY	743
	BNE	LONG2
	CPX	744
	BEQ	LONG3

LONG2	JSR	PRTHEX
	JSR	POML

	LDY	743
	LDX	744
	JSR	PRTHEXM

	JSR	PRINT
	DTA C" Resident handlers"
	DTA 155,0

LONG3	LDY	743
	LDX	744
	JSR	PRTHEX
	JSR	POML

	LDY	741
	LDX	742
	JSR	PRTHEXM

	JSR	PRINT
	DTA C" User area"
	DTA 155,0

	LDY	741
	LDX	742
	JSR	PRTHEX
	JSR	POML

	LDY	#0
	LDX	106
	JSR	PRTHEXM

	JSR	PRINT
	DTA C" Screen memory"
	DTA 155,0

	LDY	#0
	LDX	106

	CPX	#$C0
	BCS	LONG4
**
	JSR	PRTHEX

	JSR	PRINT
	DTA C"-$BFFF Cartridge or "
	DTA C"Reserved"
	DTA 155,0

LONG4	JSR	PRINT
	DTA C"$C000-$CFFF Operating"
	DTA C" System"
	DTA 155
	DTA C"$D000-$D7FF Input"
	DTA C"/Output area"
	DTA 155
	DTA C"$D800-$FFFF Operating"
	DTA C" System"
	DTA 155,0

	JMP	(10)
****

PRTHEXM	TYA
	BNE	PHXM2
	DEX
PHXM2	DEY

PRTHEX	TYA
	PHA
	TXA
	PHA

	LDA	#'$'
	JSR	PRT1

	PLA
	JSR	PRTHX2

	PLA
**

PRTHX2	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX3
	PLA
**

PRTHX3	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	PRTHX4

	CLC
	ADC	#7

PRTHX4	JMP	PRT1

****

