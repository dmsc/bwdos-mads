	ORG	$6000,$A800
	JMP	START

*************************************
*				*
*   " D I S A S S "   PRO BW-DOS	*
*				*
*************************************

HEX	EPZ	128

POS	EPZ	131

LPOS	EPZ	139

STATUS	EPZ	141

* VOLNO: 143

NAME	EQU	$6800

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRTEOL	LDA	#$9B

**

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456

***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#1
GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEX
	STA	HEX+1
	STA	HEX+2

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2

GETHX2	CPY	#43
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	PLA
	ORA	HEX
	STA	HEX

	JMP	GETHX2
***

GHXERR	JSR	PRINT
	DFB 155
	ASC "Bad parameter !"
	DFB 155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSE
	JMP	(10)

***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

CIONAME	LDA	#NAME:L
	STA	$344,X
	LDA	#NAME:H
	STA	$345,X

	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS !"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***

	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***

	LDX	#0
	STX	POS
	STX	POS+1
	STX	POS+2
	STX	STATUS

***

	JSR	GETHEX
	BEQ	ST8
**

	LDX	#2
ST5B	LDA	HEX,X
	STA	POS,X
	DEX
	BPL	ST5B

*** OPEN

ST8	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIONAME
	BPL	ST9
**
	JSR	PRINT
	DFB 155
	ASC "Can't open file !"
	DFB 155,0
	JMP	ERR2X

*** POINT

ST9	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDX	#$10
	LDA	#37
	STA	$342,X

	LDA	POS
	STA	$34C,X
	LDA	POS+1
	STA	$34D,X
	LDA	POS+2
	STA	$34E,X

	JSR	CIONAME
	BPL	ST10
***
	JSR	PRINT
	DFB 155
	ASC "Can't set "
	ASC "file-position !"
	DFB 155,0
	JMP	ERR2X
**

D1ERR	JMP	DUMPX

*** LIST IT !

ST10	JSR	PRINT
	DFB 155
	ASC "BW-DOS Mini-Disassem"
	ASC "bler by BEWESOFT"
	DFB 155,155,0
*GET

DUMP1	LDA	#0
	STA	LPOS

	LDA	POS+2
	JSR	DPRTHX
	LDA	POS+1
	JSR	DPRTHX
	LDA	POS
	JSR	DPRTHX
	JSR	DSPAC

*** DATA
	JSR	DATA1
	BMI	D1ERR
	STA	NAME

	TAX
	LDA	ADTAB,X
	TAX
	LDA	LENTAB,X

	TAX
	DEX
	BEQ	DUMP2

	DEX
	TXA
	PHA

	JSR	DATA1
	BMI	D1ERR
	STA	NAME+1

	PLA
	BEQ	DUMP2

	JSR	DATA1
	BMI	D1ERR
	STA	NAME+2

*** MOVE

DUMP2	LDA	LPOS
	CMP	#18
	BCS	DUMP4

	JSR	DSPAC

	JMP	DUMP2

*** INSTR.

DUMP4	LDX	NAME
	LDA	NMTAB,X
	ASL
	ADC	NMTAB,X
	TAX

	LDA	NMTXT+2,X
	PHA
	LDA	NMTXT+1,X
	PHA
	LDA	NMTXT,X

	JSR	DPRT1
	PLA
	JSR	DPRT1
	PLA
	JSR	DPRT1

	JSR	DSPAC

**OPERAND

	LDX	NAME
	LDA	ADTAB,X
	ASL
	ASL
	PHA

	TAX
	LDA	ADTXT,X
	JSR	DPRT1

	PLA
	PHA
	LSR
	LSR
	CMP	#8
	BNE	DUMP4B

*BRANCH
	LDA	NAME+1
	BMI	DUMP4MI

	PHA
	LDA	#'+'
	JSR	DPRT1
	PLA

	CLC
	ADC	#2
	JMP	DUMP4PM
*

DUMP4MI	PHA
	LDA	#'-'
	JSR	DPRT1
	PLA

	EOR	#$FF
	SEC
	SBC	#1

DUMP4PM	JSR	DOLAR
	JMP	DUMP4C

*ADR

DUMP4B	TAX
	LDA	LENTAB,X
	TAX
	DEX
	BEQ	DUMP4D

	TXA
	JSR	DOLAR
	TAX

	DEX
	BEQ	DUMP4BB

	LDA	NAME+2
	JSR	DPRTHX

DUMP4BB	LDA	NAME+1
DUMP4C	JSR	DPRTHX

**

DUMP4D	PLA
	TAX

	LDA	ADTXT+3,X
	PHA
	LDA	ADTXT+2,X
	PHA
	LDA	ADTXT+1,X

	JSR	DPRT1
	PLA
	JSR	DPRT1
	PLA
	JSR	DPRT1

*** EOL

	JSR	PRTEOL

*** NEXT?

DUMPX	LDA	STATUS
	BMI	DUMPX2

	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1
***

DUMPX2	CMP	#136
	BEQ	DUMPX3

	JSR	PRINT
	DFB 155
	ASC "Error while reading !"
	DFB 155,0
	JMP	ERR2X
***

DESC	LDA	#$FF
	STA	764

	JSR	PRINT
	DFB 155
	ASC "<Aborted>"
	DFB 155,0
DUMPX3	JMP	ERR2

***

DATA1	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456
	STY	STATUS

	BMI	DATA1ER
**

	INC	POS
	BNE	DATA1C
	INC	POS+1
	BNE	DATA1C
	INC	POS+2
**

DATA1C	PHA
	JSR	DSPAC
	PLA
	PHA
	JSR	DPRTHX
	PLA

	LDY	#1
	RTS
**

DATA1ER	CPY	#136
	BNE	D1ERX

	JSR	PRINT
	ASC " <EOF>"
	DFB 155,0

D1ERX	LDY	STATUS
	RTS

***

DOLAR	PHA

	LDA	#'$'
	JSR	DPRT1

	PLA

	RTS
*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DFB $2C
DSPAC	LDA	#' '

DPRT1	TAY
	BEQ	DPRT1EX

	INC	LPOS
	JMP	PRT1

DPRT1EX	RTS

***

NMTAB DFB 10,34,56,56,56,34,2,56
 DFB 36,34,2,56,56,34,2,56
 DFB 9,34,56,56,56,34,2,56
 DFB 13,34,56,56,56,34,2,56
 DFB 28,1,56,56,6,1,39,56
 DFB 38,1,39,56,6,1,39,56
 DFB 7,1,56,56,56,1,39,56
 DFB 44,1,56,56,56,1,39,56

 DFB 41,23,56,56,56,23,32,56
 DFB 35,23,32,56,27,23,32,56
 DFB 11,23,56,56,56,23,32,56
 DFB 15,23,56,56,56,23,32,56
 DFB 42,0,56,56,56,0,40,56
 DFB 37,0,40,56,27,0,40,56
 DFB 12,0,56,56,56,0,40,56
 DFB 46,0,56,56,56,0,40,56

 DFB 56,47,56,56,49,47,48,56
 DFB 22,56,53,56,49,47,48,56
 DFB 3,47,56,56,49,47,48,56
 DFB 55,47,54,56,56,47,56,56
 DFB 31,29,30,56,31,29,30,56
 DFB 51,29,50,56,31,29,30,56
 DFB 4,29,56,56,31,29,30,56
 DFB 16,29,52,56,31,29,30,56

 DFB 19,17,56,56,19,17,20,56
 DFB 26,17,21,56,19,17,20,56
 DFB 8,17,56,56,56,17,20,56
 DFB 14,17,56,56,56,17,20,56
 DFB 18,43,56,56,18,43,24,56
 DFB 25,43,33,56,18,43,24,56
 DFB 5,43,56,56,56,43,24,56
 DFB 45,43,56,56,56,43,24,56

***

ADTAB DFB 9,4,9,9,9,6,6,9
 DFB 9,3,9,9,9,0,0,9
 DFB 8,5,9,9,9,7,7,9
 DFB 9,2,9,9,9,1,1,9
 DFB 0,4,9,9,6,6,6,9
 DFB 9,3,9,9,0,0,0,9
 DFB 8,5,9,9,9,7,7,9
 DFB 9,2,9,9,9,1,1,9

 DFB 9,4,9,9,9,6,6,9
 DFB 9,3,9,9,0,0,0,9
 DFB 8,5,9,9,9,7,7,9
 DFB 9,2,9,9,9,1,1,9
 DFB 9,4,9,9,9,6,6,9
 DFB 9,3,9,9,10,0,0,9
 DFB 8,5,9,9,9,7,7,9
 DFB 9,2,9,9,9,1,1,9

 DFB 9,4,9,9,6,6,6,9
 DFB 9,9,9,9,0,0,0,9
 DFB 8,5,9,9,7,7,11,9
 DFB 9,2,9,9,9,1,9,9
 DFB 3,4,3,9,6,6,6,9
 DFB 9,3,9,9,0,0,0,9
 DFB 8,5,9,9,7,7,11,9
 DFB 9,2,9,9,1,1,2,9

 DFB 3,4,9,9,6,6,6,9
 DFB 9,3,9,9,0,0,0,9
 DFB 8,5,9,9,9,7,7,9
 DFB 9,2,9,9,9,1,1,9
 DFB 3,4,9,9,6,6,6,9
 DFB 9,3,9,9,0,0,0,9
 DFB 8,5,9,9,9,7,7,9
 DFB 9,2,9,9,9,1,1,9

***

NMTXT ASC "ADCANDASLBCCBCSBEQBITBMI"
 ASC "BNEBPLBRKBVCBVSCLCCLDCLI"
 ASC "CLVCMPCPXCPYDECDEXDEYEOR"
 ASC "INCINXINYJMPJSRLDALDXLDY"
 ASC "LSRNOPORAPHAPHPPLAPLPROL"
 ASC "RORRTIRTSSBCSECSEDSEISTA"
 ASC "STXSTYTAXTAYTSXTXATXSTYA"
 ASC "???"

ADTXT	DFB 0,0,0,0

	DFB 0
	ASC ",X"
	DFB 0

	DFB 0
	ASC ",Y"
	DFB 0

	ASC "#"
	DFB 0,0,0

	ASC "(,X)

	ASC "(),Y

	DFB 0,0,0,0

	DFB 0
	ASC ",X"
	DFB 0

	ASC "*"
	DFB 0,0,0

	DFB 0,0,0,0

	ASC "()"
	DFB 0,0

	DFB 0
	ASC ",Y"
	DFB 0
***

LENTAB	DFB 3,3,3,2,2,2,2,2
	DFB 2,1,3,2

***

