*************************************
*				*
*   " D I S A S S "   FOR BW-DOS	*
*				*
*************************************

LPOS	EQU	128
SAVEX	EQU	129
POS	EQU	130
TMP	EQU	133
SAVE2	EQU	136
ILEN	EQU	137

	ORG	$3000

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<MBADVER
	LDY	#MBADPAR-MBADVER+1
	BNE	EXIT

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2

***
	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***
	LDX	#0
	STX	POS
	STX	POS+1
	STX	POS+2
***
	JSR	GETNAME

	LDY	#36

GETHX2	LDA	(10),Y
	CMP	#155
	BEQ	DOOPEN

GTX	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	POS
	ROL	POS+1
	ROL	POS+2
	DEX
	BNE	GETHX4

	INY
	CPY	#43
	BCC	GETHX2

***
GHXERR	LDA	#<MBADPAR
	LDY	#MREADER-MBADPAR+1
	BNE	ERR2X

D1ERX	LDA	#<MREADER
	LDY	#MOPNERR-MREADER+1
	BNE	ERR2X

OPNERR	LDA	#<MOPNERR
	LDY	#MPNTERR-MOPNERR+1
	BNE	ERR2X
**
PNTERR	LDA	#<MPNTERR
	LDY	#MABORT-MOPNERR+1
***
ERR2X	PHA
	STY	SAVE2
	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	PLA
	LDY	SAVE2
EXIT	JSR	PRINT
	JSR	CLOSE
	JMP	(10)

DATA1ER	CPY	#136
	BNE	D1ERX

	LDA	#<MEOF
	LDY	#MBADVER-MEOF+1
	BNE	EXIT

***
*** OPEN
DOOPEN	JSR	CLOSE

	LDA	#4
	STA	$35A
	LDA	#0
	STA	$35B

	LDA	#3
	JSR	CIONAME
	BMI	OPNERR

*** POINT
	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDX	#2
ST9A	LDA	POS,X
	STA	$35C,X
	DEX
	BPL	ST9A

	LDA	#37
	JSR	CIONAME
	BMI	PNTERR

* Start listing
ST10	LDA	#<MBANNER
	LDY	#MEND-MBANNER
	JSR	PRINT

* Disassemble one row
DUMP1	LDA	#0
	STA	LPOS

	LDX	#2
PRTPOS	LDA	POS,X
	JSR	DPRTHX
	DEX
	BPL	PRTPOS
	STX	ILEN

	JSR	DSPAC

* Get data bytes
DUMP1A	INC	ILEN
	LDA	#0
	STA	$358
	STA	$359

	LDA	#7
	JSR	CIOCMD
	BMI	DATA1ER

	LDX	ILEN
	STA	TMP,X

	INC	POS
	BNE	DUMP1B
	INC	POS+1
	BNE	DUMP1B
	INC	POS+2

* Write hex code
DUMP1B	JSR	DSPAC

	LDA	TMP,X
	JSR	DPRTHX

	LDX	TMP
	LDA	ADTAB,X
	TAX
	LDA	ILEN
	CMP	LENTAB,X
	BNE	DUMP1A

* Print spaces before opcode
DUMP2	JSR	DSPAC
	LDA	LPOS
	CMP	#18
	BCC	DUMP2

* Print opcode
	LDX	TMP
	LDA	NMTAB,X
	TAX

	LDA	NMTXT1,X
	JSR	DPRT1
	LDA	NMTXT2,X
	JSR	DPRT1
	LDA	NMTXT3,X
	JSR	DPRT1

	JSR	DSPAC

* Print OPERAND
	LDX	TMP
	LDA	ADTAB,X
	TAX
	LDA	ADTXT1,X
	JSR	DPRT1

	CPX	#8
	BNE	DUMP4B

* Branch - relative addressing
	LDX	TMP+1
	BMI	DUMP4MI

	LDA	#'+'
	JSR	DPRT1

	INX
	INX
	TXA
	BNE	DUMP4PM
*

DUMP4MI	LDA	#'-'
	JSR	DPRT1

	INX
	TXA
	EOR	#$FF

DUMP4PM	STA	TMP+1
	LDX	#8

* Address or immediate
DUMP4B	LDA	LENTAB,X
	BEQ	DUMP4D

	JSR	DOLAR

PRTAD	LDY	ILEN
	LDA	TMP,Y
	JSR	DPRTHX
	DEC	ILEN
	BNE	PRTAD

* Last part of the operand
DUMP4D	LDA	ADTXT2,X
	JSR	DPRT1
	LDA	ADTXT3,X
	JSR	DPRT1
	LDA	ADTXT4,X
	JSR	DPRT1

*** EOL
	LDA	#$9B
	JSR	PRT1

*** NEXT?
	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1

***
DESC	LDA	#$FF
	STA	764

	LDA	#<MABORT
	LDY	#MBANNER-MABORT+1
	JMP	EXIT

***

DOLAR	PHA
	LDA	#'$'
	JSR	DPRT1
	PLA
	RTS
*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DTA	$2C
DSPAC	LDA	#' '

DPRT1	TAY
	BEQ	DPRT1EX

	INC	LPOS
***
PRT1	LDY	#0
***
PRINT	STA	$344
	STY	$348
	STX	SAVEX
	LDX	#0
	STX	$349
	LDY	#>MSTART
	STY	$345
	LDY	#11
	STY	$342
	JSR	$E456
	LDX	SAVEX
DPRT1EX	RTS
***

GETNAME	JMP	GETNAME

***
CLOSE	LDA	#12
CIONAME	LDX	#<NAME
	STX	$354
	LDX	#>NAME
	STX	$355
CIOCMD	LDX	#$10
	STA	$352
	JMP	$E456
***
***
* Instruction OPCODE (name) table
NMTAB	DTA	29,6,0,0,0,6,9,0
	DTA	43,6,9,0,0,6,9,0
	DTA	28,6,0,0,0,6,9,0
	DTA	32,6,0,0,0,6,9,0
	DTA	40,2,0,0,21,2,13,0
	DTA	45,2,13,0,21,2,13,0
	DTA	26,2,0,0,0,2,13,0
	DTA	48,2,0,0,0,2,13,0

	DTA	46,4,0,0,0,4,12,0
	DTA	42,4,12,0,22,4,12,0
	DTA	30,4,0,0,0,4,12,0
	DTA	34,4,0,0,0,4,12,0
	DTA	47,1,0,0,0,1,14,0
	DTA	44,1,14,0,22,1,14,0
	DTA	31,1,0,0,0,1,14,0
	DTA	50,1,0,0,0,1,14,0

	DTA	0,8,0,0,20,8,19,0
	DTA	37,0,54,0,20,8,19,0
	DTA	23,8,0,0,20,8,19,0
	DTA	56,8,55,0,0,8,0,0
	DTA	11,5,10,0,11,5,10,0
	DTA	52,5,51,0,11,5,10,0
	DTA	24,5,0,0,11,5,10,0
	DTA	35,5,53,0,11,5,10,0

	DTA	18,3,0,0,18,3,15,0
	DTA	39,3,36,0,18,3,15,0
	DTA	27,3,0,0,0,3,15,0
	DTA	33,3,0,0,0,3,15,0
	DTA	17,7,0,0,17,7,16,0
	DTA	38,7,41,0,17,7,16,0
	DTA	25,7,0,0,0,7,16,0
	DTA	49,7,0,0,0,7,16,0

***
* Addressing mode table of each opcode
ADTAB	DTA	0,9,0,0,0,2,2,0,0,3,0,0,0,1,1,0
	DTA	8,7,0,0,0,5,5,0,0,6,0,0,0,4,4,0
	DTA	1,9,0,0,2,2,2,0,0,3,0,0,1,1,1,0
	DTA	8,7,0,0,0,5,5,0,0,6,0,0,0,4,4,0

	DTA	0,9,0,0,0,2,2,0,0,3,0,0,1,1,1,0
	DTA	8,7,0,0,0,5,5,0,0,6,0,0,0,4,4,0
	DTA	0,9,0,0,0,2,2,0,0,3,0,0,10,1,1,0
	DTA	8,7,0,0,0,5,5,0,0,6,0,0,0,4,4,0

	DTA	0,9,0,0,2,2,2,0,0,0,0,0,1,1,1,0
	DTA	8,7,0,0,5,5,11,0,0,6,0,0,0,4,0,0
	DTA	3,9,3,0,2,2,2,0,0,3,0,0,1,1,1,0
	DTA	8,7,0,0,5,5,11,0,0,6,0,0,4,4,6,0

	DTA	3,9,0,0,2,2,2,0,0,3,0,0,1,1,1,0
	DTA	8,7,0,0,0,5,5,0,0,6,0,0,0,4,4,0
	DTA	3,9,0,0,2,2,2,0,0,3,0,0,1,1,1,0
	DTA	8,7,0,0,0,5,5,0,0,6,0,0,0,4,4,0

***
* Messages
MSTART
MEOF	DTA	C" <EOF>"
MBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
MBADPAR	DTA	155
	DTA	C"Bad parameter!"
MREADER	DTA	155
	DTA	C"Error while reading!"
MOPNERR	DTA	155
	DTA	C"Can't open file!"
MPNTERR	DTA	155
	DTA	C"Can't set file-position!"
MABORT	DTA	155
	DTA	C"<Aborted>"
MBANNER	DTA	155
	DTA	C"BW-DOS Mini-Disassembler by BEWESOFT"
	DTA	155,155
MEND

***
* Instruction names: 57x3 = 171 bytes
NMTXT1	DTA	C"?AACELOSSALLLRRDICCSSBJBBBBBBBBBCCCCDDIIJNPPPPRRSSSTTTTTT"
NMTXT2	DTA	C"?DNMODRBTSDDSOOENPPTTIMCCEMNPRVVLLLLEENNSOHHLLTTEEEAASXXY"
NMTXT3	DTA	C"?CDPRAACALXYRLRCCXYXYTPCSQIELKCSCDIVXYXYRPAPAPISCDIXYXASA"

* Addressing mode text, 4 bytes per mode
*  1 byte:  text before operand
*  3 bytes: text after operand
*		  IMP  ABS   ZP  IMM  A,X  Z,X  A,Y IIDX  REL IDXI  IND  Z,Y
ADTXT1	DTA	   0,   0,   0, '#',   0,   0,   0, '(', '*', '(', '(',   0
ADTXT2	DTA	   0,   0,   0,   0, ',', ',', ',', ')',   0, ',', ')', ','
ADTXT3	DTA	   0,   0,   0,   0, 'X', 'X', 'Y', ',',   0, 'X',   0, 'Y'
ADTXT4	DTA	   0,   0,   0,   0,   0,   0,   0, 'Y',   0, ')',   0,   0

* Length of each addressing mode
LENTAB	DTA	   0,   2,   1,   1,   2,   1,   2,   1,   1,   1,   2,   1

NAME	EQU	*

***

