*************************************
*				*
*   " D I S A S S "   FOR BW-DOS	*
*				*
*************************************

LPOS	EQU	128
SAVEX	EQU	129
POS	EQU	130
TMP	EQU	133
SAVE2	EQU	136
ILEN	EQU	137

	ORG	$3000

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<MBADVER
	LDY	#MBADPAR-MBADVER+1
	BNE	EXIT

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2

***
	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***
	LDX	#0
	STX	POS
	STX	POS+1
	STX	POS+2
***
	JSR	GETNAME

	LDY	#36

GETHX2	LDA	(10),Y
	CMP	#155
	BEQ	DOOPEN

GTX	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	POS
	ROL	POS+1
	ROL	POS+2
	DEX
	BNE	GETHX4

	INY
	CPY	#43
	BCC	GETHX2

***
GHXERR	LDA	#<MBADPAR
	LDY	#MREADER-MBADPAR+1
	BNE	ERR2X

D1ERX	LDA	#<MREADER
	LDY	#MOPNERR-MREADER+1
	BNE	ERR2X

OPNERR	LDA	#<MOPNERR
	LDY	#MPNTERR-MOPNERR+1
	BNE	ERR2X
**
PNTERR	LDA	#<MPNTERR
	LDY	#MABORT-MOPNERR+1
***
ERR2X	PHA
	STY	SAVE2
	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	PLA
	LDY	SAVE2
EXIT	JSR	PRINT
	JSR	CLOSE
	JMP	(10)

DATA1ER	CPY	#136
	BNE	D1ERX

	LDA	#<MEOF
	LDY	#MBADVER-MEOF+1
	BNE	EXIT

***
*** OPEN
DOOPEN	JSR	CLOSE

	LDA	#4
	STA	$35A
	LDA	#0
	STA	$35B

	LDA	#3
	JSR	CIONAME
	BMI	OPNERR

*** POINT
	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDX	#2
ST9A	LDA	POS,X
	STA	$35C,X
	DEX
	BPL	ST9A

	LDA	#37
	JSR	CIONAME
	BMI	PNTERR

* Start listing
ST10	LDA	#<MBANNER
	LDY	#MEND-MBANNER
	JSR	PRINT

* Disassemble one row
DUMP1	LDA	#0
	STA	LPOS

	LDX	#2
PRTPOS	LDA	POS,X
	JSR	DPRTHX
	DEX
	BPL	PRTPOS
	STX	ILEN

	JSR	DSPAC

* Get data bytes
DUMP1A	INC	ILEN
	LDA	#0
	STA	$358
	STA	$359

	LDA	#7
	JSR	CIOCMD
	BMI	DATA1ER

	LDX	ILEN
	STA	TMP,X

	INC	POS
	BNE	DUMP1B
	INC	POS+1
	BNE	DUMP1B
	INC	POS+2

* Write hex code
DUMP1B	JSR	DSPAC

	LDA	TMP,X
	JSR	DPRTHX

	LDX	TMP
	LDA	ADTAB,X
	TAX
	LDA	ILEN
	CMP	LENTAB,X
	BNE	DUMP1A

* Print spaces before opcode
DUMP2	JSR	DSPAC
	LDA	LPOS
	CMP	#18
	BCC	DUMP2

* Print opcode
	LDX	TMP
	LDA	NMTAB,X
	TAX

	LDA	NMTXT1,X
	JSR	DPRT1
	LDA	NMTXT2,X
	JSR	DPRT1
	LDA	NMTXT3,X
	JSR	DPRT1

	JSR	DSPAC

* Print OPERAND
	LDX	TMP
	LDA	ADTAB,X
	TAX
	LDA	ADTXT1,X
	JSR	DPRT1

	CPX	#8
	BNE	DUMP4B

* Branch - relative addressing
	LDX	TMP+1
	BMI	DUMP4MI

	LDA	#'+'
	JSR	DPRT1

	INX
	INX
	TXA
	BNE	DUMP4PM
*

DUMP4MI	LDA	#'-'
	JSR	DPRT1

	INX
	TXA
	EOR	#$FF

DUMP4PM	STA	TMP+1
	LDX	#8

* Address or immediate
DUMP4B	LDA	LENTAB,X
	BEQ	DUMP4D

	JSR	DOLAR

PRTAD	LDY	ILEN
	LDA	TMP,Y
	JSR	DPRTHX
	DEC	ILEN
	BNE	PRTAD

* Last part of the operand
DUMP4D	LDA	ADTXT2,X
	JSR	DPRT1
	LDA	ADTXT3,X
	JSR	DPRT1
	LDA	ADTXT4,X
	JSR	DPRT1

*** EOL
	LDA	#$9B
	JSR	PRT1

*** NEXT?
	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1

***
DESC	LDA	#$FF
	STA	764

	LDA	#<MABORT
	LDY	#MBANNER-MABORT+1
	JMP	EXIT

***

DOLAR	PHA
	LDA	#'$'
	JSR	DPRT1
	PLA
	RTS
*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DTA	$2C
DSPAC	LDA	#' '

DPRT1	TAY
	BEQ	DPRT1EX

	INC	LPOS
***
PRT1	LDY	#0
***
PRINT	STA	$344
	STY	$348
	STX	SAVEX
	LDX	#0
	STX	$349
	LDY	#>MSTART
	STY	$345
	LDY	#11
	STY	$342
	JSR	$E456
	LDX	SAVEX
DPRT1EX	RTS
***

GETNAME	JMP	GETNAME

***
CLOSE	LDA	#12
CIONAME	LDX	#<NAME
	STX	$354
	LDX	#>NAME
	STX	$355
CIOCMD	LDX	#$10
	STA	$352
	JMP	$E456
***
***
* Instruction OPCODE (name) table
NMTAB	DTA	10,34,56,56,56,34,2,56
	DTA	36,34,2,56,56,34,2,56
	DTA	9,34,56,56,56,34,2,56
	DTA	13,34,56,56,56,34,2,56
	DTA	28,1,56,56,6,1,39,56
	DTA	38,1,39,56,6,1,39,56
	DTA	7,1,56,56,56,1,39,56
	DTA	44,1,56,56,56,1,39,56

	DTA	41,23,56,56,56,23,32,56
	DTA	35,23,32,56,27,23,32,56
	DTA	11,23,56,56,56,23,32,56
	DTA	15,23,56,56,56,23,32,56
	DTA	42,0,56,56,56,0,40,56
	DTA	37,0,40,56,27,0,40,56
	DTA	12,0,56,56,56,0,40,56
	DTA	46,0,56,56,56,0,40,56

	DTA	56,47,56,56,49,47,48,56
	DTA	22,56,53,56,49,47,48,56
	DTA	3,47,56,56,49,47,48,56
	DTA	55,47,54,56,56,47,56,56
	DTA	31,29,30,56,31,29,30,56
	DTA	51,29,50,56,31,29,30,56
	DTA	4,29,56,56,31,29,30,56
	DTA	16,29,52,56,31,29,30,56

	DTA	19,17,56,56,19,17,20,56
	DTA	26,17,21,56,19,17,20,56
	DTA	8,17,56,56,56,17,20,56
	DTA	14,17,56,56,56,17,20,56
	DTA	18,43,56,56,18,43,24,56
	DTA	25,43,33,56,18,43,24,56
	DTA	5,43,56,56,56,43,24,56
	DTA	45,43,56,56,56,43,24,56

***
* Addressing mode table of each opcode
ADTAB	DTA	9,4,9,9,9,6,6,9,9,3,9,9,9,0,0,9
	DTA	8,5,9,9,9,7,7,9,9,2,9,9,9,1,1,9
	DTA	0,4,9,9,6,6,6,9,9,3,9,9,0,0,0,9
	DTA	8,5,9,9,9,7,7,9,9,2,9,9,9,1,1,9

	DTA	9,4,9,9,9,6,6,9,9,3,9,9,0,0,0,9
	DTA	8,5,9,9,9,7,7,9,9,2,9,9,9,1,1,9
	DTA	9,4,9,9,9,6,6,9,9,3,9,9,10,0,0,9
	DTA	8,5,9,9,9,7,7,9,9,2,9,9,9,1,1,9

	DTA	9,4,9,9,6,6,6,9,9,9,9,9,0,0,0,9
	DTA	8,5,9,9,7,7,11,9,9,2,9,9,9,1,9,9
	DTA	3,4,3,9,6,6,6,9,9,3,9,9,0,0,0,9
	DTA	8,5,9,9,7,7,11,9,9,2,9,9,1,1,2,9

	DTA	3,4,9,9,6,6,6,9,9,3,9,9,0,0,0,9
	DTA	8,5,9,9,9,7,7,9,9,2,9,9,9,1,1,9
	DTA	3,4,9,9,6,6,6,9,9,3,9,9,0,0,0,9
	DTA	8,5,9,9,9,7,7,9,9,2,9,9,9,1,1,9

***
* Messages
MSTART
MEOF	DTA	C" <EOF>"
MBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
MBADPAR	DTA	155
	DTA	C"Bad parameter!"
MREADER	DTA	155
	DTA	C"Error while reading!"
MOPNERR	DTA	155
	DTA	C"Can't open file!"
MPNTERR	DTA	155
	DTA	C"Can't set file-position!"
MABORT	DTA	155
	DTA	C"<Aborted>"
MBANNER	DTA	155
	DTA	C"BW-DOS Mini-Disassembler by BEWESOFT"
	DTA	155,155
MEND

***
* Instruction names: 56x3 = 168 bytes
NMTXT1	DTA	C"AAABBBBBBBBBBCCCCCCCDDDEIIIJJLLLLNOPPPPRRRRSSSSSSSTTTTTT?"
NMTXT2	DTA	C"DNSCCEIMNPRVVLLLLMPPEEEONNNMSDDDSORHHLLOOTTBEEETTTAASXXY?"
NMTXT3	DTA	C"CDLCSQTIELKCSCDIVPXYCXYRCXYPRAXYRPAAPAPLRISCCDIAXYXYXASA?"

* Addressing mode text, 4 bytes per mode
*  1 byte:  text before operand
*  3 bytes: text after operand
*		ABS  A,X  A,Y  IMM IDXI IIDX   ZP  Z,X  REL  IMP  IND  Z,Y
ADTXT1	DTA	 0,   0,   0, '#', '(', '(',   0,   0, '*',   0, '(',   0
ADTXT2	DTA	 0, ',', ',',   0, ',', ')',   0, ',',   0,   0, ')', ','
ADTXT3	DTA	 0, 'X', 'Y',   0, 'X', ',',   0, 'X',   0,   0,   0, 'Y'
ADTXT4	DTA	 0,   0,   0,   0, ')', 'Y',   0,   0,   0,   0,   0,   0

***
* Length of each addressing mode
LENTAB	DTA	 2,   2,   2,   1,   1,   1,   1,   1,   1,   0,   2,   1

NAME	EQU	*

***

