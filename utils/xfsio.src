*************************************
*				*
* XF-551 HIGH SPEED SIO PRO BW-DOS  *
*				*
*************************************

SRCHADR	EQU	128
REFLAG	EQU	130
SIOADR	EQU	131


***

	ORG	$3000

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	LDA	11
	SBC	#0
	STA	SIOADR+1
***

GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y

	CMP	#$9B
	BEQ	HELP

	CMP	#'O'
	BNE	STRT4

	LDA	#'F'
	INY
	CMP	(10),Y
	BNE	STRT4
	INY
	CMP	(10),Y
	BNE	STRT4

	LDA	#$9B
	INY
	CMP	(10),Y
	BNE	STRT4

	JMP	DISABLE
***

HELP	JSR	PRINT
	DTA	155
	DTA	C"Syntax: XFSIO drive_number(s)"
	DTA	155
	DTA	C"        XFSIO OFF"
	DTA	155,0
	BEQ	JDOS1
***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax Error"
	DTA	155,0
*XDIV
	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

JDOS1	JMP	(10)
***

STRT4	LDY	#36

STRT5	LDA	(10),Y
	CMP	#$9B
	BEQ	STRT6

	SEC
	SBC	#'1'
	CMP	#4
	BCS	SYNTAX

	TAX
	INY

	DEC	DTAB,X
	BNE	STRT5
***

STRT6	LDX	#1

STRT6A	INC	DRVNUM
	STX	REFLAG
	LDA	DTAB-1,X
	BPL	STRT6C

	LDY	#11
STRT6B	LDA	DRITST,Y
	STA	$300,Y
	DEY
	BPL	STRT6B

	STX	$301
*

	JSR	XCHIRQ
	JSR	$E459
	JSR	XCHIRQ

	TYA
	BPL	STRT6C
*
	LDX	REFLAG
	LSR	DTAB-1,X

	JSR	PRINT
	DTA	155
	DTA	C"Drive "
DRVNUM	DTA	C"0 is not XF 551."
	DTA	0

**

STRT6C	LDX	REFLAG
	INX
	CPX	#5
	BCC	STRT6A
	BCS	STRT6D
***

ST6IRQ	LDA	#$10
	STA	$D204
ST6IQJ	JMP	ST6IRQ

*Exchange IRQ handler with new one
XCHIRQ	LDX	#1
XCIRQ1	LDA	$20A,X
	PHA
	LDA	ST6IQJ+1,X
	STA	$20A,X
	PLA
	STA	ST6IQJ+1,X
	DEX
	BPL	XCIRQ1
	RTS
**

DRITST	DTA	$31,0
	DTA	C"N"*
	DTA	64
	DTA	A($500)
	DTA	7,0
	DTA	A(12,1)

***

STRT6D	;SEC	;C=1 from above
	ROR	REFLAG

	JSR	SRCHRUT
	BCS	STRT7

*Install
	LSR	REFLAG

	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*Relocate
	LDA	#IRQ-RESID
	CLC
	ADC	SRCHADR
	STA	R04+1
	LDA	#0
	ADC	SRCHADR+1
	STA	R05+1

	LDA	#XFRET-RESID
	CLC
	ADC	SRCHADR
	STA	R08+1
	LDA	#0
	ADC	SRCHADR+1
	STA	R08+2

**

	LDY	#1

INSTL1	LDA	(SIOADR),Y
	STA	PUVSIO+1,Y

	LDA	$20A,Y
	STA	IRQJP+1,Y

	DEY
	BPL	INSTL1

	STA	R07+1
	LDA	$20B
	STA	R06+1
**

	LDY	#RESLEN

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDA	SRCHADR
	CLC
	ADC	#(XFSIO-RESID)
	STA	(SIOADR),Y
	INY
	LDA	SRCHADR+1
	ADC	#0
	STA	(SIOADR),Y
**

STRT7	LDA	#0
	LDX	#3

STRT8	ASL	DTAB,X
	ROL
	DEX
	BPL	STRT8

	LDY	#RESTAB-RESID
	STA	(SRCHADR),Y
***

	LDA	REFLAG
	BMI	REINTXT
**
	JSR	PRINT
	DTA	C" Installed."
	DTA	155,0
	BEQ	JDOS

***

REINTXT	JSR	PRINT
	DTA	C": Configuration changed."
	DTA	155,0
	BEQ	JDOS
***

DISABL2J	JMP	DISABLE2

***

DISABLE	JSR	SRCHRUT
	BCS	DISABL2J

	JSR	PRINT
	DTA	C" Not Installed!"
	DTA	155,0

	BEQ	DISERR2
***

DISCANT	JSR	PRINT
	DTA	C" is not the last"
	DTA	155
	DTA	C"handler installed!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

JDOS	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	CPY	#RESLEN
	BNE	DISCANT
*Remove
	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
*

	LDY	#(PUVSIO-RESID)+1
	LDA	(SRCHADR),Y
	TAX
	INY
	LDA	(SRCHADR),Y

	LDY	#1
	STA	(SIOADR),Y
	DEY
	TXA
	STA	(SIOADR),Y
**
	JSR	PRINT
	DTA	C" Removed."
	DTA	155,0
	BEQ	JDOS

****

SRCHRUT	JSR	PRINT
	DTA	155
	DTA	C"XF 551 SIO"
	DTA	0

	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDX	#2
SRCHR3	LDY	SRCHTAB1,X

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	TYA
	CMP	SRCHTAB2,X
	BNE	SRCHR2

	DEX
	BPL	SRCHR3

	RTS

SRCHTAB1	DTA	R03-RESID,R04-RESID,R06-RESID
SRCHTAB2	DTA	TST1-RESID,TST3-RESID,TST2-RESID

**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$19
	BCS	SRCHR1

	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

DTAB	DTA	0,0,0,0

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***

XFSIO	LDA	$300
	CMP	#$31
	BNE	PUVSIO

RESTAB	EQU	*+1
R03	LDA	#0
	LDX	$301
XF1	LSR
	DEX
	BNE	XF1
	BCS	XFSIO2

PUVSIO	JMP	$FFFF

**

XFSIO2	LDA	$10
	PHA
	AND	#$80
R08	JSR	XFRET

TST3	LDA	$302
	PHA

	BMI	XFSIO3
	CMP	#$23
	BCC	XFSIO3

	ORA	#$80
	STA	$302

R04	LDA	#<IRQ
	STA	$20A
R05	LDA	#>IRQ
TST2	STA	$20B

XFSIO3	JSR	$E459

	PLA
	STA	$302

R06	LDA	#$FF
	STA	$20B
R07	LDA	#$FF
	STA	$20A

	PLA
XFRET	STA	$10
	STA	$D20E

	TYA
	RTS

***

IRQ	LDA	#$10
	STA	$D204

IRQJP	JMP	$FFFF

***

RESLEN	EQU	*-RESID

