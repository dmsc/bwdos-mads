*				*
*  " M E N U " PRO BW-DOS - CAST 2  *
*				*

* VOLNO: 201

DJDN	LDX	#0

	LDA	#'D'
	JSR	DJ1
	LDA	DRIVNUM
	ORA	#$30
	JSR	DJ1
	LDA	#':'

	DTA	$2C
DJEOL	LDA	#155
	DTA	$2C
DJHVE	LDA	#'*'

DJ1	STA	DAB,X
	INX

	RTS
*

DNEOL	JSR	DJDN
	JMP	DJEOL

*

DJFNM	LDY	#<F
	LDA	#>F
	BNE	DJF2
*
DJFNM2	LDY	#<F2
	LDA	#>F2

DJF2	STY	T
	STA	T+1
	STX	DJF2A+1

	LDY	#0
	LDA	#8
	JSR	DJFSUB

	LDA	#'.'
	JSR	DJ1

	LDY	#8
	LDA	#11
	JSR	DJFSUB

	LDA	DAB-1,X
	CMP	#'.'
	BNE	DJF2B
	DEX
DJF2A	CPX	#0
	BNE	DJF2B
	INX
DJF2B	RTS

*
DJFSUB	STA	DJF3+1
	JMP	DJF3

DJF3B	JSR	DJ1
DJF3	CPY	#8
	BCS	DJF3X

	LDA	(T),Y
	INY
	CMP	#32
	BNE	DJF3B

DJF3X	RTS

*

DJPTH	LDY	#<P
	LDA	#>P
	BNE	DJP2
*
DJPTH2	LDY	#<P2
	LDA	#>P2

DJP2	STY	T
	STA	T+1

	LDY	#0
	LDA	(T),Y
	CMP	#155
	BNE	DJP2B

	RTS
*
DJP2C	JSR	DJ1
DJP2B	LDA	(T),Y
	INY
	CMP	#155
	BNE	DJP2C

	LDA	DAB-1,X
	CMP	#'>'
	BEQ	DJF3X
	CMP	#'<'
	BEQ	DJF3X

	LDA	#'>'
	JMP	DJ1
*

BERR	LDX	BSS
	TXS

	TYA
	PHA
	JSR	CLOSE
	PLA
	TAY

	RTS

*

BCHKDSK	TSX
	STX	BSS
*

RDCHK	JSRS CLOSE,DNEOL,AUXS0

	LDA	#<DSKST2
	STA	$348,X
	LDA	#>DSKST2
	STA	$349,X

	LDA	#47
	JSR	CIODAB
	BPL	RCHX
*
	CPY	#148
	BEQ	RCH2
RCHER	JMP	BERR
*
RCH2	LDA	RWA
	PHA
	LDA	RWA+1
	PHA

	DSTA	DSKST2,RWA
	JSR	CHKDSK

	PLA
	STA	RWA+1
	PLA
	STA	RWA

	TYA
	BMI	RCHER

RCHX	LDY	#1
	RTS
*

BTEST	TSX
	STX	BSS

	JSR	RDCHK

	LDX	#15

BTST2	CPX	#4
	BEQ	BTST3
	CPX	#5
	BEQ	BTST3
	CPX	#14
	BEQ	BTST3

	LDA	DSKSTAT,X
	CMP	DSKST2,X
	BNE	BTSTER

BTST3	DEX
	BPL	BTST2

	LDY	#1
	RTS
*
BTSTER	LDA	#'>'
	STA	P
	LDA	#155
	STA	P+1

	LDX	#16
	LDA	#0
BTSTE2	STA	DSKSTAT,X
	DEX
	BPL	BTSTE2

	LDY	#150
	RTS
*

BDIR	TSX
	STX	BSS

	JSR	RDCHK

	LDX	#16
BDIR1	LDA	DSKST2,X
	STA	DSKSTAT,X
	DEX
	BPL	BDIR1

	LDA	DSKST2
	BNE	BDIR2
	JMP	READDIR
*CWD

BDIR2	JSRS DJDN,DJPTH,DJEOL

	JSR	AUXS0
	LDA	#44
	JSR	CIODAB
	BMI	BERJ1
*OPN
	JSR	DJDN
	JSR	DJHVE
	LDA	#'.'

	JSRS DJ1,DJHVE,DJEOL

	JSR	AUXS0
	LDA	#$14
	STA	$34A,X
	LDA	#3
	JSR	CIODAB
	BMI	BERJ1

	LDA	#0
	STA	RWL
	STA	RWL+1
	BEQ	BDIR3
*
BERJ1	JMP	BERR
*

BDIR4	LDA	DAB
	BEQ	BDIRX

	AND	#8
	BEQ	BDIR3

	LDY	#22
BDIR4B	LDA	DAB,Y
	STA	(RWA),Y
	DEY
	BPL	BDIR4B

	JSR	RDDNXT
*
BDIR3	LDX	RWA+1
	INX
	CPX	$2E6
	BCS	BDIRX

	LDX	#$10
	LDA	#0
	STA	$349,X
	LDA	#23
	STA	$348,X
	LDA	#7
	JSR	CIODAB
	BPL	BDIR4

	CPY	#136
	BNE	BERJ1
*
BDIRX	JSR	CLOSE
	LDY	#1
	RTS
*

BREAD	TSX
	STX	BSS

	LDA	DSKST2
	BNE	BREAD1
	JMP	READ
*

BREAD1	JSRS DJDN,DJPTH,DJFNM
	JSR	DJEOL

	JSR	AUXS0
	LDA	#4
	STA	$34A,X
	LDA	#3
	JSR	CIODAB
	BMI	BERJ2
*
	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	BREAD2
*
	JSR	DNEOL

	LDX	#$10
	LDA	POS
	STA	$34C,X
	LDA	POS+1
	STA	$34D,X
	LDA	POS+2
	STA	$34E,X
	LDA	#37
	JSR	CIODAB
	BPL	BREAD2

BERJ2	JMP	BERR
*

BREAD2	LDA	#7

RWSPOL	LDX	#$10
	STA	$342,X

	LDA	RWA
	STA	$344,X
	LDA	RWA+1
	STA	$345,X

	LDA	RWL
	STA	$348,X
	LDA	RWL+1
	STA	$349,X

	ORA	RWL
	BEQ	BREAD3

	JSR	$E456
	DTA	$2C
BREAD3	LDY	#1

	TYA
	PHA

	LDX	#$10
	LDA	$348,X
	STA	RWL
	LDA	$349,X
	STA	RWL+1
*
	JSR	DNEOL

	LDX	#$10
	LDA	#38
	JSR	CIODAB
	BMI	BERJ2

	LDX	#$10
	LDA	$34C,X
	STA	POS
	LDA	$34D,X
	STA	POS+1
	LDA	$34E,X
	STA	POS+2

	JSR	CLOSE
	BMI	BERJ2

	PLA
	TAY
	RTS
*

BWRITE	TSX
	STX	BSS

	LDA	DSKST2
	BNE	BWRI1
	JMP	WRITE
*

BWRI1	JSRS DJDN,DJPTH2,DJFNM
	JSR	DJEOL

	JSR	AUXS0
	LDA	#8
	STA	$34A,X
*
	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	BWRI2

	INC	$34A,X

BWRI2	LDA	#3
	JSR	CIODAB
	BMI	BERJ3
*
	LDA	#11
	JMP	RWSPOL
*

BXIO	TSX
	STX	BSS

	PHA

	LDA	DSKST2
	BNE	BXIO1
*
	PLA
	CMP	#33
	BEQ	BXER
	CMP	#35
	BEQ	BXPR
	CMP	#36
	BEQ	BXUN

	LDY	#148
	RTS
*
BXER	JMP	ERASE
BXPR	JMP	PROTECT
BXUN	JMP	UNPROT
*

BXIO1	JSRS DJDN,DJFNM,DJEOL

	JSR	AUXS0
	PLA
	JSR	CIODAB
	BMI	BERJ3
	RTS
*
BERJ3	JMP	BERR

*

BRENAME	TSX
	STX	BSS

	LDA	DSKST2
	BNE	RENAM1
	JMP	RENAME
*

RENAM1	JSR	DJDN
	JSR	DJFNM
	LDA	#','

	JSRS DJ1,DJFNM2,DJEOL

	JSR	AUXS0
	LDA	#32
	JSR	CIODAB
	BMI	BERJ3

	RTS
*

CLS	LDA	88
	STA	PADR
	STA	PSAD
	LDX	89
	INX
	STX	PADR+1
	INX
	STX	PSAD+1

	LDA	#0
	TAY
CLS1	STA	(88),Y
	STA	(PADR),Y
	STA	(PSAD),Y
	DEY
	BNE	CLS1

	INC	PSAD+1
	LDY	#$C0

CLS2	DEY
	STA	(PSAD),Y
	CPY	#0
	BNE	CLS2
*
	LDX	#0

CLS3	TXA
	AND	#$9F
	STA	PMSK

	TXA
	LSR
	LSR
	LSR
	LSR
	LSR
	AND	#3
	TAY

	LDA	CLS3TB,Y
	ORA	PMSK
	STA	$500,X

	DEX
	BNE	CLS3

	RTS
*
CLS3TB	DTA	64,0,32,96
*
PTB	DTA	A(0,40,80,120,160,200)
	DTA	A(240,280,320,360,400)
	DTA	A(440,480,520,560,600)
	DTA	A(640,680,720,760,800)
	DTA	A(840,880,920)
*

PEDIT	DSTA	DAB,PADR

PED2	LDY	EDY
	LDX	EDX
	LDA	#0
*

PRINT	CPY	#24
	BCS	P1EX
	CPX	#40
	BCC	P1
P1EX	RTS
*

P1	AND	#128
	STA	PMSK

	TYA
	ASL
	TAY

	LDA	PTB,Y
	CLC
	ADC	88
	STA	PSAD
	LDA	PTB+1,Y
	ADC	89
	STA	PSAD+1

	TXA
	CLC
	ADC	PSAD
	STA	PSAD
	BCC	P2LAB
	INC	PSAD+1
*
P2LAB	LDY	#0

P3	LDA	(PADR),Y
	CMP	#155
	BEQ	P1EX

	TAX
	LDA	$500,X

	EOR	PMSK
	STA	(PSAD),Y
	INY
	BNE	P3

	RTS
*

MEN1S	STA	MEN1S2+1
	STX	MEN1S2+2

	LDX	#0

MEN1S1	LDA	(PADR),Y
	INY
MEN1S2	STA	DIRB,X
	INX

	CPX	EDMAX
	BCC	MEN1S1

	RTS
*

MENBEL	SEC
	DTA	$90
MENU	CLC
	ROR	MBF	;Make negative on BEL

	STY	PADR
	STX	PADR+1

	LDY	#0
	STY	EDPOS
	LDA	(PADR),Y
	STA	EDMAX
*X,Y
	INY
	LDA	(PADR),Y
	STA	EDX
	INY
	LDA	(PADR),Y
	STA	EDY
	INY
*POS
	LDX	#>MAPB
	LDA	#<MAPB
	JSR	MEN1S
*LEN
	LDX	#>((MAPB+128))
	LDA	#<((MAPB+128))
	JSR	MEN1S
*KEYS
	LDX	#>DIRB
	LDA	#<DIRB
	JSR	MEN1S
*TXT
	LDX	#0

MEN3	LDA	(PADR),Y
	INY
	STA	DAB,X
	INX

	CMP	#155
	BNE	MEN3
*ZOBR

MEN4	LDX	#0

MEN4B	LDA	DAB,X
	CMP	#155
	BEQ	MEN4C

	AND	#127
	STA	DAB,X
	INX
	BNE	MEN4B
*
MEN4C	LDX	EDPOS
	LDA	MAPB+128,X
	TAY
	LDA	MAPB,X
	TAX

MEN4D	LDA	DAB,X
	ORA	#128
	STA	DAB,X

	INX
	DEY
	BNE	MEN4D
*
	JSR	PEDIT
*
MEN5	BIT	MBF
	BPL	MEN5A

	LSR	MBF
	JSR	GETKB
	JMP	MEN5AB

MEN5A	JSR	GETKEY
MEN5AB	CMP	#27
	BEQ	MEN5ESC
	CMP	#155
	BEQ	MEN5RET

	CMP	#$1E
	BEQ	MEN5L
	CMP	#$1F
	BEQ	MEN5R

	AND	#127

	CMP	#'+'
	BEQ	MEN5L
	CMP	#'*'
	BEQ	MEN5R

	LDX	EDMAX
	DEX

MEN5B	CMP	DIRB,X
	BEQ	MEN5C

	DEX
	BPL	MEN5B
	BMI	MEN5
*
MEN5C	TXA
	RTS
*
MEN5ESC	LDA	MBF
	BEQ	MENABRT
	ASL
	RTS

MENABRT	PLA
	PLA
	JMP	TABLP
*
MEN5RET	LDA	EDPOS
	RTS
*
MEN5L	LDX	EDPOS
	BNE	MEN5L2

	LDX	EDMAX

MEN5L2	DEX
MEN5L3	STX	EDPOS
	JMP	MEN4
*
MEN5R	LDX	EDPOS
	INX

	CPX	EDMAX
	BCC	MEN5L3

	LDX	#0
	BEQ	MEN5L3
*

EDCLIT	PHA
	TXA
	PHA

	LDA	#32
	LDX	#0
EDIT0A	STA	DAB,X
	DEX
	BNE	EDIT0A

	PLA
	TAX
	PLA

	RTS
*

EDIT	JSR	EDCLIT

EDIT0	STY	EDY
	STX	EDX
	STA	EDMAX

	TAX

EDIT0B	DEX
	CPX	#$FF
	BEQ	EDIT0C
	LDA	DAB,X
	CMP	#32
	BEQ	EDIT0B

EDIT0C	INX
	STX	EDPOS

	LDA	#0
	STA	EDSPOS
*ZOBR

EDIT1	LDX	EDMAX
	LDA	#155
	STA	DAB,X

	LDX	EDPOS
	LDA	DAB,X
	ORA	#128
	STA	DAB,X
*VYSEC
	LDA	#40
	SEC
	SBC	EDX
	STA	PSAD

	LDA	EDPOS
	SEC
	SBC	EDSPOS
	BCC	ED1B
	CMP	#2
	BCS	ED1C

ED1B	LDA	EDPOS
	SEC
	SBC	#2
	BCS	ED1F
	LDA	#0
	BEQ	ED1F

ED1C	STA	PSAD+1
	LDA	PSAD
	SEC
	SBC	PSAD+1
	BCC	ED1D
	CMP	#2
	BCS	ED1G

ED1D	LDA	EDPOS
	CLC
	ADC	#2
	CMP	EDMAX
	BCC	ED1E
	LDA	EDMAX
ED1E	SEC
	SBC	PSAD
	BCS	ED1F
	LDA	#0

ED1F	STA	EDSPOS

ED1G	LDA	EDSPOS
	CLC
	ADC	#<DAB
	STA	PADR
	LDA	#0
	ADC	#>DAB
	STA	PADR+1

	LDA	EDSPOS
	CLC
	ADC	PSAD
	STA	ED1H+1
	TAX

	LDA	DAB,X
	STA	ED1I+1
	LDA	#155
	STA	DAB,X

	JSR	PED2

ED1H	LDX	#0
ED1I	LDA	#0
	STA	DAB,X

*
	LDX	EDPOS
	LDA	DAB,X
	AND	#127
	STA	DAB,X
*
EDIT2	JSR	GETKEY
	CMP	#27
	BEQ	ED2ESC
	CMP	#155
	BEQ	ED2RETQ
	CMP	#$1E
	BEQ	ED2L
	CMP	#$1F
	BEQ	ED2R
	CMP	#$7D
	BEQ	ED2CLR
	CMP	#$7E
	BEQ	ED2DEL
	CMP	#$FE
	BEQ	ED2CTD
	CMP	#$FF
	BEQ	ED2CTI
*
	AND	#127

	CMP	#'*'
	BEQ	ED2B
	CMP	#'.'
	BEQ	ED2B
	CMP	#'>'
	BEQ	ED2B
	CMP	#'<'
	BEQ	ED2B
	CMP	#'?'
	BEQ	ED2B
	CMP	#'_'
	BEQ	ED2B

	CMP	#$30
	BCC	EDIT2
	CMP	#$3A
	BCC	ED2B

	AND	#$5F

	CMP	#$41
	BCC	EDIT2
	CMP	#$5B
	BCS	EDIT2
*

ED2B	LDX	EDPOS
	STA	DAB,X
*
ED2R	INC	EDPOS
	LDA	EDPOS
	CMP	EDMAX
	BCC	EDIT1J
*
ED2L	LDA	EDPOS
	BEQ	EDIT1J

	DEC	EDPOS

EDIT1J	JMP	EDIT1
*
ED2RETQ	BEQ	ED2RET
*
ED2ESC	LDA	#255
	RTS
*
ED2CLR	JSR	EDCLIT
	LDA	#0
	STA	EDPOS
	BEQ	EDIT1J
*
ED2DEL	LDX	EDPOS
	BEQ	EDIT1J

	DEX
	STX	EDPOS
ED2D2	LDA	#32
	STA	DAB,X
	BNE	EDIT1J
*
ED2CTD	LDX	EDPOS
ED2CD2	LDA	DAB+1,X
	STA	DAB,X
	INX
	CPX	EDMAX
	BCC	ED2CD2

	DEX
	JMP	ED2D2
*
ED2CTI	LDX	EDMAX
ED2CI2	DEX
	LDA	DAB-1,X
	STA	DAB,X
	CPX	EDPOS
	BNE	ED2CI2
	BEQ	ED2D2
*
ED2RET	LDX	EDMAX

ED2RT2	DEX
	CPX	#$FF
	BEQ	ED2RT3
	LDA	DAB,X
	CMP	#32
	BEQ	ED2RT2

ED2RT3	INX
	LDA	#155
	STA	DAB,X

	LDA	#0
	RTS

*
CLDATB	LDA	#32
	LDX	#0
CLDTB2	STA	DAB,X
	DEX
	BNE	CLDTB2
	RTS
*

PP	JSR	CLDATB

	LDA	#'D'
	STA	DAB
	LDA	DRIVE
	ORA	#$30
	STA	DAB+1
	LDA	#':'
	STA	DAB+2

	LDX	#0

PPT2	LDA	P,X
	CMP	#155
	BEQ	PPT4

	CPX	#37
	BCS	PPT3

	STA	DAB+3,X
	INX
	BNE	PPT2
*NE

PPT3	INX
	LDA	P,X
	CMP	#155
	BNE	PPT3

	TXA
	SEC
	SBC	#35
	TAX

PPT3B	INX
	LDA	P,X
	CMP	#'>'
	BNE	PPT3B

	LDA	#'.'
	STA	DAB+3
	STA	DAB+4
	STA	DAB+5

	LDY	#0
PPT3C	LDA	P,X
	CMP	#155
	BEQ	PPT3D

	STA	DAB+6,Y
	INX
	INY
	BNE	PPT3C
*
PPT3D	CPY	#34
	BCS	PPT4

	LDA	#32
	STA	DAB+6,Y
	INY
	BNE	PPT3D
*

PPT4	LDY	#0
	LDX	#0
	LDA	#128
	JMP	PDAB
*

CLTAB	JSR	PP

	LDA	#18
	STA	MT1

CLTB2	LDA	MT1
	TAY
	INY

	ASL
	TAX
	LDA	CLTBT,X
	PHA
	LDA	CLTBT+1,X

	TAX
	PLA
	JSR	INIS2S

	DEC	MT1
	BPL	CLTB2

	RTS
*

CLLIN	JSR	CLDATB
	LDY	#20
	LDX	#0
	TXA
*
PDAB	PHA

	LDA	#155
	STA	DAB+40

	DSTA	DAB,PADR

	PLA
	JMP	PRINT
*

INIS2S	STA	PADR
	STX	PADR+1

	LDX	#0
	TXA
	JMP	PRINT
*

INISCR	JSRS CLS,CLTAB,CLLIN
*
	LDY	#21
	LDX	#>RAMPH
	LDA	#<RAMPH
	JSR	INIS2S

	LDY	#22
	LDX	#>RAMPM
	LDA	#<RAMPM
	JSR	INIS2S

	LDY	#23
	LDX	#>RAMPL
	LDA	#<RAMPL
	JSR	INIS2S
*
	LDX	#0
	LDA	#$30
	TAY

INIS00	STA	$600,X
	PHA
	TYA
	STA	$680,X
	PLA

	CLC
	ADC	#1
	CMP	#$3A
	BCC	INIS00B

	LDA	#$30
	INY
	CPY	#$3A
	BCC	INIS00B
	TAY

INIS00B	INX
	BPL	INIS00
*

PRTCMD	LDA	CMDSPOS
	STA	MT1
	LDA	#1
	STA	MT1+1

PCM2	LDA	MT1
	TAY
	ASL
	ASL
	CLC
	ADC	MT1
	ASL
*10
	CLC
	ADC	#<CMDS
	STA	PADR
	LDA	#0
	ADC	#>CMDS
	STA	PADR+1

	LDA	#128
	CPY	CMDPOS
	BEQ	PCM3
	LSR

PCM3	LDX	MT1+1
	LDY	#22
	JSR	PRINT

	INC	MT1
	LDA	MT1
	CMP	#18
	BCC	PCM4
	LDA	#0
	STA	MT1

PCM4	LDA	MT1+1
	CLC
	ADC	#10
	STA	MT1+1

	CMP	#40
	BCC	PCM2

	RTS
*

DIRADR	STA	MT1
	LDY	#0
	STY	MT1+1

	ASL
	ROL	MT1+1
	ASL
	ROL	MT1+1
*4
	CLC
	ADC	MT1
	BCC	DIRAD2
	INC	MT1+1
*5
DIRAD2	ASL
	ROL	MT1+1
*10
	CLC
	ADC	MT1
	BCC	DIRAD3
	INC	MT1+1
*11
DIRAD3	ASL
	ROL	MT1+1
*22
	CLC
	ADC	MT1
	BCC	DIRAD4
	INC	MT1+1
*23
DIRAD4	CLC
	ADC	#<MEMB
	STA	MT1

	LDA	MT1+1
	ADC	#>MEMB
	STA	MT1+1

	RTS
*

PSIP	LDX	#1

PS1	STA	DAB
	LDA	#155
	STA	DAB+1

	LDA	#0
	JMP	PDAB
*

PLACE	STA	MDP

	SEC
	SBC	DSP
	BCC	PLACP
	CMP	#16
	BCC	PRTTAB

	LDA	MDP
	SEC
	SBC	#15
	JMP	PLAC1
*
PLACP	LDA	MDP
PLAC1	STA	DSP

*

PRTTAB	LDA	#3
	STA	PTTY

	LDA	DIRPOC
	BNE	PT0A
	JMP	PT8
*
PT0A	LDX	#$12
	LDA	DSP
	STA	PTTP
	BEQ	PT0
	LDX	#$9C
PT0	TXA
	LDY	#2
	JSR	PSIP

PT1	LDA	PTTP
	JSR	DIRADR

	LDY	#0
	LDA	(MT1),Y
	AND	#8
	ASL
	ASL
	ASL
	ASL
	EOR	#128
	STA	PTTI
*
	LDX	#39
PT1B	LDA	CTP,X
	STA	DIRB,X
	DEX
	BPL	PT1B
*NM
	LDX	#7
	LDY	#6+7
PT2	LDA	(MT1),Y
	EOR	PTTI
	STA	DIRB+1,X
	DEY
	DEX
	BPL	PT2
*EXT
	LDX	#2
	LDY	#6+10
PT3	LDA	(MT1),Y
	EOR	PTTI
	STA	DIRB+10,X
	DEY
	DEX
	BPL	PT3
*P
	LDX	#' '
	LDY	#0
	LDA	(MT1),Y
	LSR
	BCC	PT4
	LDX	#'*'
PT4	STX	DIRB+14
*TD
	LDY	#22
	LDX	#5
PT4A	LDA	(MT1),Y
	STA	MAPB,X
	DEY
	DEX
	BPL	PT4A
*SIZE
	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BEQ	PT4C

	LDX	#7
PT4B	LDA	SUBDT,X
	STA	DIRB+16,X
	DEX
	BPL	PT4B
	BMI	PT5
*
PT4C	LDY	#5
	LDA	(MT1),Y
	PHA
	DEY
	LDA	(MT1),Y
	TAX
	DEY
	LDA	(MT1),Y
	TAY
	PLA

	JSR	CONVDC

	LDX	#7
PT4D	LDA	DAB,X
	STA	DIRB+16,X
	DEX
	BPL	PT4D
*TD

PT5	LDA	MAPB
	ORA	MAPB+1
	ORA	MAPB+2
	ORA	MAPB+3
	ORA	MAPB+4
	ORA	MAPB+5
	BEQ	PT6
*
	LDA	#4
	STA	PTTI

PT5B	LDX	PTTI
	LDA	MAPB,X
	AND	#127
	TAY

	LDA	PT5T,X
	TAX

	LDA	$680,Y
	STA	DIRB,X
	LDA	$600,Y
	STA	DIRB+1,X

	DEC	PTTI
	BPL	PT5B
*
	LDA	#'-'
	STA	DIRB+27
	STA	DIRB+30
	LDA	#':'
	STA	DIRB+36
*
PT6	LDA	#155
	STA	DIRB+40

	LDA	#<DIRB
	LDX	#>DIRB
	LDY	PTTY
	JSR	INIS2S
*NXT
	INC	PTTY
	INC	PTTP

	LDA	PTTP
	CMP	DIRPOC
	BEQ	PT8

	LDA	PTTY
	CMP	#19
	BCS	PT7

	JMP	PT1
*SCR FUL

PT7	LDA	#$9D
	BNE	PT9
*DIR END

PT8	LDY	PTTY
	CPY	#19
	BCS	PT8B

	LDA	#<CTP
	LDX	#>CTP
	JSR	INIS2S

	INC	PTTY
	BNE	PT8
*

PT8B	LDA	#$12

PT9	LDY	#19
	JSR	PSIP

	LDA	DIRPOC
	BNE	PT9A

	LDA	#$12
	LDY	#2
	JMP	PSIP
*CURS

PT9A	LDA	MDP
	SEC
	SBC	DSP

	CMP	#16
	BCS	PTX

	CLC
	ADC	#3
	TAY
	LDX	#0
	LDA	#255
	JSR	PS1

PTX	RTS

*

PT5T	DTA	25,28,31,34,37

SUBDT	DTA	C"SUB-DIR "

*

NDTX	DTA	C"Reading directory..."
	EOL
*

RDDIR	JSR	CLTAB
	JSR	CLLIN

	DSTA	NDTX,PADR
	LDY	#20
	LDX	#10
	LDA	#0
	JSR	PRINT

	DSTA	MEMB,RWA
	JSR	BDIR
	BPL	RDD2
	RTS
*

RDD2	LDA	RWL
	CMP	#$E9
	LDA	RWL+1
	SBC	#$16
	BCC	RDD2B

	DSTA	$16E9,RWL

RDD2B	LDA	#<MEMB
	CLC
	ADC	RWL
	STA	MEM2
	LDA	#>MEMB
	ADC	RWL+1
	STA	MEM2+1
*
	LDA	#0
	STA	DIRPOC

RDD2C	LDA	RWL
	SEC
	SBC	#23
	STA	RWL
	LDA	RWL+1
	SBC	#0
	STA	RWL+1
	BCC	RESORT

	INC	DIRPOC
	BNE	RDD2C
*SORT

RESORT	LDA	#0
	STA	MDP
	STA	DSP

	LDA	DIRPOC
	CMP	#2
	BCC	RDD2X

	LDA	SORTM
	CMP	#4
	BCC	RDDB3

RDD2X	LDY	#1
	RTS
*
RDDB3	ASL
	TAX
	LDA	RDD3T,X
	STA	SORTA
	LDA	RDD3T+1,X
	STA	SORTA+1
*
RDD4	LDA	#128
	STA	PTTI
	ASL
	STA	PTTP
*
RDD5	LDA	PTTP
	JSR	DIRADR

	LDA	MT1
	CLC
	ADC	#23
	STA	MT2
	LDA	MT1+1
	ADC	#0
	STA	MT2+1
*
	LDX	#0

RDD6	TXA
	TAY
	LDA	(SORTA),Y
	BMI	RDD8
	TAY

	LDA	(MT1),Y
	CMP	(MT2),Y
	BCC	RDD8
	BNE	RDD7

	INX
	BNE	RDD6
*
RDD7	LDY	#22

RDD7B	LDA	(MT1),Y
	TAX
	LDA	(MT2),Y
	STA	(MT1),Y
	TXA
	STA	(MT2),Y

	DEY
	BPL	RDD7B

	LSR	PTTI
*
RDD8	INC	PTTP

	LDX	PTTP
	INX
	CPX	DIRPOC
	BCC	RDD5
*
	LDA	PTTI
	BPL	RDD4

	LDY	#1
	RTS

*

PCL	LDA	#'>'
	STA	P
	LDA	#155
	STA	P+1
	RTS
*

NEWDISK	JSR	PCL

NEWDIR	JSR	COPDNUM
	JSR	RDDIR
	BPL	NEWD2

	LDA	P
	CMP	#'>'
	BNE	NEWDISK
	LDA	P+1
	CMP	#155
	BNE	NEWDISK

	LDA	#0
	STA	DIRPOC
	STA	MDP
	STA	DSP

	DSTA	MEMB,MEM2

NEWD2	LDA	DSKSTAT
	BNE	NEWD3

	LDA	#155
	STA	P
	JSR	PP

NEWD3	RTS

*

AINIT	JSR	CLLIN

*DRV
	LDY	#<AINM
	LDX	#>AINM
	JSR	MENU

	TAX
	INX
	TXA
	PHA
*DENS
	LDY	#<AIND
	LDX	#>AIND
	JSR	MENU

	PHA
*
	LDY	#<AINS
	LDX	#>AINS
	JSR	MENU

	BEQ	ABORTA
*
	JSR	CLLIN

	LDA	#<FRMTNG
	LDX	#>FRMTNG
	LDY	#20
	JSR	INIS2S

	PLA
	STA	RWL
	PLA
	STA	DRIVE

	JSRS PCL,CLTAB,COPDNUM

	JSR	FORMAT
	BMI	AINIER

	JMP	DIRLP
*
ABORTA	JMP	TABLP
AINIER	JMP	ERR

*

SAMETST	JSR	COPDNUM
	JSR	BTEST
	BMI	SAMEER
	RTS
*
SAMEER2	JMP	ERR
*
SAMEER	CPY	#150
	BNE	SAMEER2
*
	JSR	CLLIN

	LDY	#20
	LDX	#>NOTSAM
	LDA	#<NOTSAM
	JSR	INIS2S

	LDX	#14
	JSR	WAITKB

	JMP	DIRLP
*

NAMBCK	LDX	#0
	JSR	DJFNM
	LDA	#155
	JMP	DJ1

*

GETP	LDA	DRIVE
	STA	DRIVNUM

	JSR	BCHKDSK
	BMI	GETPNE
	LDA	DSKST2
	BEQ	GETPNE

	JSR	INIT25
	LDA	DSKST2+1
	STA	SECLEN
	ASL
	ROL
	EOR	#1
	STA	SECLEN+1

	LDX	#$E8
	LDA	#$20
GP1	STA	P-1,X
	DEX
	BNE	GP1
	JSR	PCL

	JSR	CLOSE
	BMI	GETPNE
	JSR	DJDN
	LDA	#'*'

	JSRS DJ1,DJEOL,AUXS0

	LDA	#$14
	STA	$34A,X
	LDA	#3
	JSR	CIODAB
	BMI	GETPNE

	LDX	#$10
	LDA	#0
	STA	$34C,X
	STA	$34D,X
	STA	$34E,X
	LDA	#37
	JSR	CIODAB
	BMI	GETPNE

	LDX	#$10
	LDA	#23
	STA	$348,X
	LDA	#0
	STA	$349,X
	LDA	#7
	JSR	CIODAB
	BMI	GETPNE

GP2	LDA	DAB+1
	STA	$30A
	LDA	DAB+2
	STA	$30B
	ORA	$30A
	BNE	GP3
	RTS
*
GETPNE	JMP	PCL
*

GP3	LDX	#10
GP4	LDA	DAB+6,X
	STA	F,X
	DEX
	BPL	GP4
	JSR	NAMBCK
	DEX
	BPL	GP6

GP5	LDA	DAB,X
	JSR	GPINS
	BCS	GETPNE

GP6	DEX
	BPL	GP5

	LDA	#'>'
	JSR	GPINS
	BCS	GETPNE
*
	JSR	GPRS
	BMI	GETPNE
	LDA	DAB+4
	STA	$30A
	LDA	DAB+5
	STA	$30B
	ORA	$30A
	BEQ	GETPNE
	JSR	GPRS
	BMI	GETPNE
	BPL	GP2
*

GPRS	LDY	#<DAB
	LDX	#>DAB
	LDA	#'R'
	JMP	RWSECTX
*

GPINS	LDY	P+$E7
	CPY	#32
	BEQ	GPI1

	SEC
	RTS

GPI1	LDY	#$E7
	PHA

GPI2	LDA	P-1,Y
	STA	P,Y
	DEY
	BNE	GPI2

	PLA
	STA	P

	CLC
	RTS
*

TXSWP	LDX	#$E8
TXS2	LDA	P-1,X
	PHA
	LDA	P2-1,X
	STA	P-1,X
	PLA
	STA	P2-1,X
	DEX
	BNE	TXS2
	RTS

TEXIST	LDA	DSKST2
	BNE	TX1

	LDA	RWA
	PHA
	LDA	RWA+1
	PHA
	LDA	RWL
	PHA
	LDA	RWL+1
	PHA

	DSTA	UPDNAM,RWA
	DSTA	1,RWL

	JSRS TXSWP,READ,TXSWP

	PLA
	STA	RWL+1
	PLA
	STA	RWL
	PLA
	STA	RWA+1
	PLA
	STA	RWA

	JMP	TX3
*
TX1	JSRS DJDN,DJPTH2,DJFNM
	JSR	DJEOL

	JSR	AUXS0
	LDA	#6
	STA	$34A,X

	LDA	#3
	JSR	CIODAB
	BMI	TX2

	LDX	#$10
	LDA	#40
	STA	$348,X
	LDA	#0
	STA	$349,X

	LDA	#5
	JSR	CIODAB
	BMI	TX2

	LDX	#16
TX1B	LDA	DAB,X
	CMP	#$9B
	BEQ	TX1X
	DEX
	BPL	TX1B

	CMP	#'*'
	BEQ	TX2
	CMP	#$20
	BEQ	TX2

TX1X	LDY	#170

TX2	TYA
	PHA
	JSR	CLOSE
	PLA
	TAY

TX3	TYA
	RTS

