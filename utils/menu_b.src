	OUT	N
*************************************
*				*
*  " M E N U " PRO BW-DOS - CAST 2  *
*				*
*************************************

* PRENOS

START2	EQU	$4AEE
READDIR	EQU	$4232
PROTECT	EQU	$42E1
UNPROT	EQU	$42E3
RENAME	EQU	$4312
ERASE	EQU	$444D
CHKDSK	EQU	$4347
READ	EQU	$4526
WRITE	EQU	$45E9
FORMAT	EQU	$4701
RDDNXT	EQU	$42C5

DATBUF	EQU	$61AB

* DOMYSL

MAPBUF	EQU	DATBUF+256
DIRBUF	EQU	MAPBUF+256
FNAME	EQU	DIRBUF+256
FNAME2	EQU	FNAME+11
PATH	EQU	FNAME2+11
PATH2	EQU	PATH+$E8
DSKSTAT	EQU	PATH2+$E8
DSKST2	EQU	DSKSTAT+17

MEMBUF	EQU	DSKST2+17

*
	ORG	START2,$A800
*


DRIVNUM	EPZ	131

RWADR	EPZ	138
RWLEN	EPZ	140

TEMP25	EPZ	142

POS	EPZ	148

**

BSS	EPZ	152

PRTADR	EPZ	153
PRTSAD	EPZ	155
PRTMSK	EPZ	157

EDPOS	EPZ	158
EDMAX	EPZ	159
EDX	EPZ	160
EDY	EPZ	161

**

MEM2	EPZ	162

DIRPOC	EPZ	164
MDIRPOS	EPZ	165
DIRSPOS	EPZ	166

CMDPOS	EPZ	167
CMDSPOS	EPZ	168

DRIVE	EPZ	169

MT1	EPZ	170
PTTY	EPZ	172
PTTP	EPZ	173
PTTI	EPZ	174

SORTA	EPZ	175
MT2	EPZ	177
SORTM	EPZ	179

* VOLNO: 192

EOL	MACRO
	DFB 155
	MEND

**

AUXS0	LDX	#$10
	LDA	#0
	STA	$34A,X
	STA	$34B,X
	RTS
**

CIODATB	STA	$342,X
	LDA	#DATBUF:L
	STA	$344,X
	LDA	#DATBUF:H
	STA	$345,X

	JMP	$E456
**

CLOSE2	LDX	#$20
	DFB $2C
CLOSE0	LDX	#0
	DFB $2C
CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
**

DEJDN	LDX	#0

	LDA	#'D'
	JSR	DEJ1
	LDA	DRIVNUM
	ORA	#$30
	JSR	DEJ1
	LDA	#':'

	DFB $2C
DEJEOL	LDA	#155
	DFB $2C
DEJHVE	LDA	#'*'

DEJ1	STA	DATBUF,X
	INX

	RTS
**

DNEOL	JSR	DEJDN
	JMP	DEJEOL

**

DEJFNM	LDY	#FNAME:L
	LDA	#FNAME:H
	BNE	DEJF2
*
DEJFNM2	LDY	#FNAME2:L
	LDA	#FNAME2:H

DEJF2	STY	TEMP25
	STA	TEMP25+1

	LDY	#0
	LDA	#8
	JSR	DEJFSUB

	LDA	#'.'
	JSR	DEJ1

	LDY	#8
	LDA	#11
*
DEJFSUB	STA	DEJF3+1
	JMP	DEJF3

DEJF3B	JSR	DEJ1
DEJF3	CPY	#8
	BCS	DEJF3X

	LDA	(TEMP25),Y
	INY
	CMP	#32
	BNE	DEJF3B

DEJF3X	RTS

**

DEJPTH	LDY	#PATH:L
	LDA	#PATH:H
	BNE	DEJP2
*
DEJPTH2	LDY	#PATH2:L
	LDA	#PATH2:H

DEJP2	STY	TEMP25
	STA	TEMP25+1

	LDY	#0
	LDA	(TEMP25),Y
	CMP	#155
	BNE	DEJP2B

	RTS
*
DEJP2C	JSR	DEJ1
DEJP2B	LDA	(TEMP25),Y
	INY
	CMP	#155
	BNE	DEJP2C

	LDA	DATBUF-1,X
	CMP	#'>'
	BEQ	DEJF3X
	CMP	#'<'
	BEQ	DEJF3X

	LDA	#'>'
	JMP	DEJ1
**

BERR	LDX	BSS
	TXS

	TYA
	PHA
	JSR	CLOSE
	PLA
	TAY

	RTS

**

BCHKDSK	TSX
	STX	BSS
*

RDCHK	JSR	CLOSE

	JSR	DNEOL

	JSR	AUXS0
	LDA	#DSKST2:L
	STA	$348,X
	LDA	#DSKST2:H
	STA	$349,X

	LDA	#47
	JSR	CIODATB
	BPL	RDCHKX
*
	CPY	#148
	BEQ	RDCHK2
RDCHKER	JMP	BERR
*
RDCHK2	LDA	RWADR
	PHA
	LDA	RWADR+1
	PHA

	LDA	#DSKST2:L
	STA	RWADR
	LDA	#DSKST2:H
	STA	RWADR+1
	JSR	CHKDSK

	PLA
	STA	RWADR+1
	PLA
	STA	RWADR

	TYA
	BMI	RDCHKER

RDCHKX	LDY	#1
	RTS
**

BTEST	TSX
	STX	BSS

	JSR	RDCHK

	LDX	#15

BTST2	CPX	#4
	BEQ	BTST3
	CPX	#5
	BEQ	BTST3
	CPX	#14
	BEQ	BTST3

	LDA	DSKSTAT,X
	CMP	DSKST2,X
	BNE	BTSTER

BTST3	DEX
	BPL	BTST2

	LDY	#1
	RTS
*
BTSTER	LDA	#'>'
	STA	PATH
	LDA	#155
	STA	PATH+1

	LDX	#16
	LDA	#0
BTSTE2	STA	DSKSTAT,X
	DEX
	BPL	BTSTE2

	LDY	#150
	RTS
**

BDIR	TSX
	STX	BSS

	JSR	RDCHK

	LDX	#16
BDIR1	LDA	DSKST2,X
	STA	DSKSTAT,X
	DEX
	BPL	BDIR1

	LDA	DSKST2
	BNE	BDIR2
	JMP	READDIR
*CWD

BDIR2	JSR	DEJDN
	JSR	DEJPTH
	JSR	DEJEOL

	JSR	AUXS0
	LDA	#44
	JSR	CIODATB
	BMI	BERJ1
*OPN
	JSR	DEJDN
	JSR	DEJHVE
	LDA	#'.'
	JSR	DEJ1
	JSR	DEJHVE
	JSR	DEJEOL

	JSR	AUXS0
	LDA	#$14
	STA	$34A,X
	LDA	#3
	JSR	CIODATB
	BMI	BERJ1

	LDA	#0
	STA	RWLEN
	STA	RWLEN+1
	BEQ	BDIR3
**
BERJ1	JMP	BERR
**

BDIR4	LDA	DATBUF
	BEQ	BDIRX

	AND	#8
	BEQ	BDIR3

	LDY	#22
BDIR4B	LDA	DATBUF,Y
	STA	(RWADR),Y
	DEY
	BPL	BDIR4B

	JSR	RDDNXT
*
BDIR3	LDX	RWADR+1
	INX
	CPX	$2E6
	BCS	BDIRX

	LDX	#$10
	LDA	#0
	STA	$349,X
	LDA	#23
	STA	$348,X
	LDA	#7
	JSR	CIODATB
	BPL	BDIR4

	CPY	#136
	BNE	BERJ1
*
BDIRX	JSR	CLOSE
	LDY	#1
	RTS
**

BREAD	TSX
	STX	BSS

	LDA	DSKST2
	BNE	BREAD1
	JMP	READ
*

BREAD1	JSR	DEJDN
	JSR	DEJPTH
	JSR	DEJFNM
	JSR	DEJEOL

	JSR	AUXS0
	LDA	#4
	STA	$34A,X
	LDA	#3
	JSR	CIODATB
	BMI	BERJ2
*
	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	BREAD2
*
	JSR	DNEOL

	LDX	#$10
	LDA	POS
	STA	$34C,X
	LDA	POS+1
	STA	$34D,X
	LDA	POS+2
	STA	$34E,X
	LDA	#37
	JSR	CIODATB
	BPL	BREAD2

BERJ2	JMP	BERR
**

BREAD2	LDA	#7

RWSPOL	LDX	#$10
	STA	$342,X

	LDA	RWADR
	STA	$344,X
	LDA	RWADR+1
	STA	$345,X

	LDA	RWLEN
	STA	$348,X
	LDA	RWLEN+1
	STA	$349,X

	ORA	RWLEN
	BEQ	BREAD3

	JSR	$E456
	DFB $2C
BREAD3	LDY	#1

BREAD4	TYA
	PHA

	LDX	#$10
	LDA	$348,X
	STA	RWLEN
	LDA	$349,X
	STA	RWLEN+1
*
	JSR	DNEOL

	LDX	#$10
	LDA	#38
	JSR	CIODATB
	BMI	BERJ2

	LDX	#$10
	LDA	$34C,X
	STA	POS
	LDA	$34D,X
	STA	POS+1
	LDA	$34E,X
	STA	POS+2

	JSR	CLOSE
	BMI	BERJ2

	PLA
	TAY
	RTS
**

BWRITE	TSX
	STX	BSS

	LDA	DSKST2
	BNE	BWRITE1
	JMP	WRITE
*

BWRITE1	JSR	DEJDN
	JSR	DEJPTH2
	JSR	DEJFNM
	JSR	DEJEOL

	JSR	AUXS0
	LDA	#8
	STA	$34A,X
*
	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	BWRITE2

	INC	$34A,X

BWRITE2	LDA	#3
	JSR	CIODATB
	BMI	BERJ3
*
	LDA	#11
	JMP	RWSPOL
**

BXIO	TSX
	STX	BSS

	PHA

	LDA	DSKST2
	BNE	BXIO1
*
	PLA
	CMP	#33
	BEQ	BXER
	CMP	#35
	BEQ	BXPR
	CMP	#36
	BEQ	BXUN

	LDY	#148
	RTS
**
BXER	JMP	ERASE
BXPR	JMP	PROTECT
BXUN	JMP	UNPROT
**

BXIO1	JSR	DEJDN
	JSR	DEJFNM
	JSR	DEJEOL

	JSR	AUXS0
	PLA
	JSR	CIODATB
	BMI	BERJ3
	RTS
**
BERJ3	JMP	BERR

**

BRENAME	TSX
	STX	BSS

	LDA	DSKST2
	BNE	RENAM1
	JMP	RENAME
**

RENAM1	JSR	DEJDN
	JSR	DEJFNM
	LDA	#','
	JSR	DEJ1
	JSR	DEJFNM2
	JSR	DEJEOL

	JSR	AUXS0
	LDA	#32
	JSR	CIODATB
	BMI	BERJ3

	RTS
**

CLS	LDA	88
	STA	PRTADR
	STA	PRTSAD
	LDX	89
	INX
	STX	PRTADR+1
	INX
	STX	PRTSAD+1

	LDA	#0
	TAY
CLS1	STA	(88),Y
	STA	(PRTADR),Y
	STA	(PRTSAD),Y
	DEY
	BNE	CLS1

	INC	PRTSAD+1
	LDY	#$C0

CLS2	DEY
	STA	(PRTSAD),Y
	CPY	#0
	BNE	CLS2
*
	LDX	#0

CLS3	TXA
	AND	#$9F
	STA	PRTMSK

	TXA
	LSR
	LSR
	LSR
	LSR
	LSR
	AND	#3
	TAY

	LDA	CLS3TB,Y
	ORA	PRTMSK
	STA	$500,X

	DEX
	BNE	CLS3

	RTS
**
CLS3TB	DFB 64,0,32,96
*
PRTTB	DFW 0,40,80,120,160,200
	DFW 240,280,320,360,400
	DFW 440,480,520,560,600
	DFW 640,680,720,760,800
	DFW 840,880,920
**

PRTEDIT	LDA	#DATBUF:L
	STA	PRTADR
	LDA	#DATBUF:H
	STA	PRTADR+1

	LDY	EDY
	LDX	EDX
	LDA	#0
**

PRINT	CPY	#24
	BCS	PRT1EX
	CPX	#40
	BCC	PRT1
PRT1EX	RTS
**

PRT1	AND	#128
	STA	PRTMSK

	TYA
	ASL
	TAY

	LDA	PRTTB,Y
	CLC
	ADC	88
	STA	PRTSAD
	LDA	PRTTB+1,Y
	ADC	89
	STA	PRTSAD+1

	TXA
	CLC
	ADC	PRTSAD
	STA	PRTSAD
	BCC	PRT2
	INC	PRTSAD+1
*
PRT2	LDY	#0

PRT3	LDA	(PRTADR),Y
	CMP	#155
	BEQ	PRT1EX

	TAX
	LDA	$500,X

	EOR	PRTMSK
	STA	(PRTSAD),Y
	INY
	BNE	PRT3

	RTS
**

REOPNS	JSR	CLOSE0

	LDX	#0
	STX	$2FE
	LDA	#3
	STA	$342,X

	LDA	#ENAM:L
	STA	$344,X
	LDA	#ENAM:H
	STA	$345,X

	LDA	#12
	STA	$34A,X
	TXA
	STA	$34B,X

	JSR	$E456
	BMI	REOPER
	RTS
**
REOPER	JMP	$E474
**
ENAM	ASC "E:"
	DFB 155

KNAM	ASC "K:"
	DFB 155
**

GETKEY	JSR	CLOSE2
*
	LDX	#$20
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	LDA	#KNAM:L
	STA	$344,X
	LDA	#KNAM:H
	STA	$345,X

	JSR	$E456

	LDA	#0
	STA	$2B6
	LDA	#$40
	STA	$2BE

GETK2	LDX	#$20
	LDA	#7
	STA	$342,X
	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456
	BMI	GETK2

	PHA
	JSR	CLOSE2
	PLA

	RTS
**

CONVDC	STY	TEMP25
	STX	TEMP25+1
	STA	TEMP25+2

	LDA	#0
	LDX	#8
COND1	STA	DATBUF-1,X
	DEX
	BNE	COND1
*
COND2	LDA	TEMP25
	SEC
	SBC	CDT3,X
	STA	PRTMSK

	LDA	TEMP25+1
	SBC	CDT2,X
	TAY

	LDA	TEMP25+2
	SBC	CDT1,X
	BCC	COND3
*
	STA	TEMP25+2
	STY	TEMP25+1
	LDA	PRTMSK
	STA	TEMP25

	INC	DATBUF,X
	BNE	COND2
**

COND3	INX
	CPX	#8
	BCC	COND2
*
	LDX	#0
	LDY	#$20

COND4	CPX	#7
	BEQ	COND4B
	LDA	DATBUF,X
	BEQ	COND5
COND4B	LDY	#$30

COND5	TYA
	ORA	DATBUF,X
	STA	DATBUF,X

	INX
	CPX	#8
	BCC	COND4

	RTS
**

CDT1 DFB $98,$0F,$01,0,0,0,0,0
CDT2 DFB $96,$42,$86,$27,3,0,0,0
CDT3 DFB $80,$40,$A0,$10,$E8,100,10,1

**

MEN1S	STA	MEN1S2+1
	STX	MEN1S2+2

	LDX	#0

MEN1S1	LDA	(PRTADR),Y
	INY
MEN1S2	STA	DIRBUF,X
	INX

	CPX	EDMAX
	BCC	MEN1S1

	RTS
**

MENU	STY	PRTADR
	STX	PRTADR+1

	LDY	#0
	STY	EDPOS
	LDA	(PRTADR),Y
	STA	EDMAX
*X,Y
	INY
	LDA	(PRTADR),Y
	STA	EDX
	INY
	LDA	(PRTADR),Y
	STA	EDY
	INY
*POS
	LDX	#MAPBUF:H
	LDA	#MAPBUF:L
	JSR	MEN1S
*LEN
	LDX	#(MAPBUF+128):H
	LDA	#(MAPBUF+128):L
	JSR	MEN1S
*KEYS
	LDX	#DIRBUF:H
	LDA	#DIRBUF:L
	JSR	MEN1S
*TXT
	LDX	#0

MEN3	LDA	(PRTADR),Y
	INY
	STA	DATBUF,X
	INX

	CMP	#155
	BNE	MEN3
*ZOBRAZ

MEN4	LDX	#0

MEN4B	LDA	DATBUF,X
	CMP	#155
	BEQ	MEN4C

	AND	#127
	STA	DATBUF,X
	INX
	BNE	MEN4B
*
MEN4C	LDX	EDPOS
	LDA	MAPBUF+128,X
	TAY
	LDA	MAPBUF,X
	TAX

MEN4D	LDA	DATBUF,X
	ORA	#128
	STA	DATBUF,X

	INX
	DEY
	BNE	MEN4D
*
	JSR	PRTEDIT
*
MEN5	JSR	GETKEY
	CMP	#27
	BEQ	MEN5ESC
	CMP	#155
	BEQ	MEN5RET

	CMP	#$1E
	BEQ	MEN5L
	CMP	#$1F
	BEQ	MEN5R

	AND	#127

	CMP	#'+'
	BEQ	MEN5L
	CMP	#'*'
	BEQ	MEN5R

	LDX	EDMAX
	DEX

MEN5B	CMP	DIRBUF,X
	BEQ	MEN5C

	DEX
	BPL	MEN5B
	BMI	MEN5
*
MEN5C	TXA
	RTS
**
MEN5ESC	LDA	#255
	RTS
**
MEN5RET	LDA	EDPOS
	RTS
**
MEN5L	LDX	EDPOS
	BNE	MEN5L2

	LDX	EDMAX

MEN5L2	DEX
MEN5L3	STX	EDPOS
	JMP	MEN4
**
MEN5R	LDX	EDPOS
	INX

	CPX	EDMAX
	BCC	MEN5L3

	LDX	#0
	BEQ	MEN5L3
**

EDCLIT	PHA
	TXA
	PHA

	LDA	#32
	LDX	#0
EDIT0A	STA	DATBUF,X
	DEX
	BNE	EDIT0A

	PLA
	TAX
	PLA

	RTS
**

EDIT	JSR	EDCLIT

EDIT0	STY	EDY
	STX	EDX
	STA	EDMAX

	TAX

EDIT0B	DEX
	BMI	EDIT0C
	LDA	DATBUF,X
	CMP	#32
	BEQ	EDIT0B

EDIT0C	INX
	STX	EDPOS
**ZOBRAZ

EDIT1	LDX	EDMAX
	LDA	#155
	STA	DATBUF,X

	DEX
EDIT1B	LDA	DATBUF,X
	AND	#127
	STA	DATBUF,X
	DEX
	BPL	EDIT1B

	LDX	EDPOS
	LDA	DATBUF,X
	ORA	#128
	STA	DATBUF,X
*
	JSR	PRTEDIT
*
	LDX	EDPOS
	LDA	DATBUF,X
	AND	#127
	STA	DATBUF,X
*
EDIT2	JSR	GETKEY
	CMP	#27
	BEQ	ED2ESC
	CMP	#155
	BEQ	ED2RETQ
	CMP	#$1E
	BEQ	ED2L
	CMP	#$1F
	BEQ	ED2R
	CMP	#$7D
	BEQ	ED2CLR
	CMP	#$7E
	BEQ	ED2DEL
	CMP	#$FE
	BEQ	ED2CTD
	CMP	#$FF
	BEQ	ED2CTI
*
	AND	#127

	CMP	#'*'
	BEQ	ED2B
	CMP	#'.'
	BEQ	ED2B
	CMP	#'>'
	BEQ	ED2B
	CMP	#'<'
	BEQ	ED2B
	CMP	#'?'
	BEQ	ED2B
	CMP	#'_'
	BEQ	ED2B

	CMP	#$30
	BCC	EDIT2
	CMP	#$3A
	BCC	ED2B

	AND	#$5F

	CMP	#$41
	BCC	EDIT2
	CMP	#$5B
	BCS	EDIT2
*

ED2B	LDX	EDPOS
	STA	DATBUF,X
*
ED2R	INC	EDPOS
	LDA	EDPOS
	CMP	EDMAX
	BCC	EDIT1J
*
ED2L	DEC	EDPOS
	BMI	ED2R

EDIT1J	JMP	EDIT1
**
ED2RETQ	BEQ	ED2RET
**
ED2ESC	LDA	#255
	RTS
**
ED2CLR	JSR	EDCLIT
	LDA	#0
	STA	EDPOS
	BEQ	EDIT1J
**
ED2DEL	LDX	EDPOS
	BEQ	EDIT1J

	DEX
	STX	EDPOS
ED2D2	LDA	#32
	STA	DATBUF,X
	BNE	EDIT1J
**
ED2CTD	LDX	EDPOS
ED2CD2	LDA	DATBUF+1,X
	STA	DATBUF,X
	INX
	CPX	EDMAX
	BCC	ED2CD2

	DEX
	JMP	ED2D2
**
ED2CTI	LDX	EDMAX
ED2CI2	DEX
	LDA	DATBUF-1,X
	STA	DATBUF,X
	CPX	EDPOS
	BNE	ED2CI2
	BEQ	ED2D2
**
ED2RET	LDX	EDMAX

ED2RT2	DEX
	BMI	ED2RT3
	LDA	DATBUF,X
	CMP	#32
	BEQ	ED2RT2

ED2RT3	INX
	LDA	#155
	STA	DATBUF,X

	LDA	#0
	RTS

**

CLDATB	LDA	#32
	LDX	#0
CLDTB2	STA	DATBUF,X
	DEX
	BNE	CLDTB2
	RTS
**

PRTPATH	JSR	CLDATB

	LDA	#'D'
	STA	DATBUF
	LDA	DRIVE
	ORA	#$30
	STA	DATBUF+1
	LDA	#':'
	STA	DATBUF+2

	LDX	#0

PRTPT2	LDA	PATH,X
	CMP	#155
	BEQ	PRTPT4

	CPX	#37
	BCS	PRTPT3

	STA	DATBUF+3,X
	INX
	BNE	PRTPT2
*NE

PRTPT3	INX
	LDA	PATH,X
	CMP	#155
	BNE	PRTPT3

	TXA
	SEC
	SBC	#35
	TAX

PRTPT3B	INX
	LDA	PATH,X
	CMP	#'>'
	BNE	PRTPT3B

	LDA	#'.'
	STA	DATBUF+3
	STA	DATBUF+4
	STA	DATBUF+5

	LDY	#0
PRTPT3C	LDA	PATH,X
	CMP	#155
	BEQ	PRTPT3D

	STA	DATBUF+6,Y
	INX
	INY
	BNE	PRTPT3C
*
PRTPT3D	CPY	#34
	BCS	PRTPT4

	LDA	#32
	STA	DATBUF+6,Y
	INY
	BNE	PRTPT3D
**

PRTPT4	LDY	#0
	LDX	#0
	LDA	#128
	JMP	PRTDATB
**

CLTAB	JSR	PRTPATH

	LDA	#18
	STA	MT1

CLTB2	LDA	MT1
	TAY
	INY

	ASL
	TAX
	LDA	CLTBT,X
	PHA
	LDA	CLTBT+1,X

	TAX
	PLA
	JSR	INIS2S

	DEC	MT1
	BPL	CLTB2

	RTS
**

CLTBT	DFW CTN,CTM,CTP,CTP
	DFW CTP,CTP,CTP,CTP
	DFW CTP,CTP,CTP,CTP
	DFW CTP,CTP,CTP,CTP
	DFW CTP,CTP,CTD

CTN	ASC "|  Filename  |P|  Si"
	ASC "ze  |  Date  |Time |"
	EOL

CTM	ASC %!2222222272223232222%
	ASC %2222322222222322222$%
	EOL

CTP	ASC "|        |   | |    "
	ASC "    |        |     |"
	EOL

CTD	ASC %:2222222282228282222%
	ASC %2222822222222822222#%
	EOL

**

CLLIN	JSR	CLDATB
	LDY	#20
	LDX	#0
	TXA
*
PRTDATB	PHA

	LDA	#155
	STA	DATBUF+40

	LDA	#DATBUF:L
	STA	PRTADR
	LDA	#DATBUF:H
	STA	PRTADR+1

	PLA
	JMP	PRINT
**

RAMPH	ASC %22222222227222222222%
	ASC %72222222227222222222%
	EOL

RAMPM	ASC "          |         "
	ASC "|         |         "
	EOL

RAMPL	ASC %22222222228222222222%
	ASC %82222222228222222222%
	EOL
**

INIS2S	STA	PRTADR
	STX	PRTADR+1

	LDX	#0
	TXA
	JMP	PRINT
**

INISCR	JSR	CLS
	JSR	CLTAB
	JSR	CLLIN
*
	LDY	#21
	LDX	#RAMPH:H
	LDA	#RAMPH:L
	JSR	INIS2S

	LDY	#22
	LDX	#RAMPM:H
	LDA	#RAMPM:L
	JSR	INIS2S

	LDY	#23
	LDX	#RAMPL:H
	LDA	#RAMPL:L
	JSR	INIS2S
*
	LDX	#0
	LDA	#$30
	TAY

INIS00	STA	$600,X
	PHA
	TYA
	STA	$680,X
	PLA

	CLC
	ADC	#1
	CMP	#$3A
	BCC	INIS00B

	LDA	#$30
	INY
	CPY	#$3A
	BCC	INIS00B
	TAY

INIS00B	INX
	BPL	INIS00
**

PRTCMD	LDA	CMDSPOS
	STA	MT1
	LDA	#1
	STA	MT1+1

PRTCM2	LDA	MT1
	TAY
	ASL
	ASL
	CLC
	ADC	MT1
	ASL
*10
	CLC
	ADC	#CMDS:L
	STA	PRTADR
	LDA	#0
	ADC	#CMDS:H
	STA	PRTADR+1

	LDA	#128
	CPY	CMDPOS
	BEQ	PRTCM3
	LSR

PRTCM3	LDX	MT1+1
	LDY	#22
	JSR	PRINT

	INC	MT1
	LDA	MT1
	CMP	#18
	BCC	PRTCM4
	LDA	#0
	STA	MT1

PRTCM4	LDA	MT1+1
	CLC
	ADC	#10
	STA	MT1+1

	CMP	#40
	BCC	PRTCM2

	RTS
**

CMDS	ASC "New disk "
	EOL
	ASC "Disk info"
	EOL
	ASC "Sub dir  "
	EOL
	ASC "Up dir   "
	EOL
	ASC "Make dir "
	EOL
	ASC "Del. dir "
	EOL
	ASC "Copy     "
	EOL
	ASC "Erase    "
	EOL
	ASC "Rename   "
	EOL
	ASC "Protect  "
	EOL
	ASC "Unprotect"
	EOL
	ASC "View text"
	EOL
	ASC "Make text"
	EOL
	ASC "Ainit    "
	EOL
	ASC "Exit     "
	EOL
	ASC "Sorting  "
	EOL
	ASC "Select   "
	EOL
	ASC "Deselect "
	EOL
**

DIRADR	STA	MT1
	LDY	#0
	STY	MT1+1

	ASL
	ROL	MT1+1
	ASL
	ROL	MT1+1
*4
	CLC
	ADC	MT1
	BCC	DIRAD2
	INC	MT1+1
*5
DIRAD2	ASL
	ROL	MT1+1
*10
	CLC
	ADC	MT1
	BCC	DIRAD3
	INC	MT1+1
*11
DIRAD3	ASL
	ROL	MT1+1
*22
	CLC
	ADC	MT1
	BCC	DIRAD4
	INC	MT1+1
*23
DIRAD4	CLC
	ADC	#MEMBUF:L
	STA	MT1

	LDA	MT1+1
	ADC	#MEMBUF:H
	STA	MT1+1

	RTS
**

PRTSIP	LDX	#1

PRTS1	STA	DATBUF
	LDA	#155
	STA	DATBUF+1

	LDA	#0
	JMP	PRTDATB
**

PRTTAB	LDA	#3
	STA	PTTY

	LDA	DIRPOC
	BNE	PRTT0A
	JMP	PRTT8
*
PRTT0A	LDX	#$12
	LDA	DIRSPOS
	STA	PTTP
	BEQ	PRTT0
	LDX	#$9C
PRTT0	TXA
	LDY	#2
	JSR	PRTSIP

PRTT1	LDA	PTTP
	JSR	DIRADR

	LDY	#0
	LDA	(MT1),Y
	AND	#8
	ASL
	ASL
	ASL
	ASL
	EOR	#128
	STA	PTTI
*
	LDX	#39
PRTT1B	LDA	CTP,X
	STA	DIRBUF,X
	DEX
	BPL	PRTT1B
*NM
	LDX	#7
	LDY	#6+7
PRTT2	LDA	(MT1),Y
	EOR	PTTI
	STA	DIRBUF+1,X
	DEY
	DEX
	BPL	PRTT2
*EXT
	LDX	#2
	LDY	#6+10
PRTT3	LDA	(MT1),Y
	EOR	PTTI
	STA	DIRBUF+10,X
	DEY
	DEX
	BPL	PRTT3
*P
	LDX	#' '
	LDY	#0
	LDA	(MT1),Y
	LSR
	BCC	PRTT4
	LDX	#'*'
PRTT4	STX	DIRBUF+14
*SAVE TD
	LDY	#22
	LDX	#5
PRTT4A	LDA	(MT1),Y
	STA	MAPBUF,X
	DEY
	DEX
	BPL	PRTT4A
*SIZE
	LDY	#0
	LDA	(MT1),Y
	AND	#32
	BEQ	PRTT4C

	LDX	#7
PRTT4B	LDA	SUBDT,X
	STA	DIRBUF+16,X
	DEX
	BPL	PRTT4B
	BMI	PRTT5
*
PRTT4C	LDY	#5
	LDA	(MT1),Y
	PHA
	DEY
	LDA	(MT1),Y
	TAX
	DEY
	LDA	(MT1),Y
	TAY
	PLA

	JSR	CONVDC

	LDX	#7
PRTT4D	LDA	DATBUF,X
	STA	DIRBUF+16,X
	DEX
	BPL	PRTT4D
*TD

PRTT5	LDA	MAPBUF
	ORA	MAPBUF+1
	ORA	MAPBUF+2
	ORA	MAPBUF+3
	ORA	MAPBUF+4
	ORA	MAPBUF+5
	BEQ	PRTT6
*
	LDA	#4
	STA	PTTI

PRTT5B	LDX	PTTI
	LDA	MAPBUF,X
	AND	#127
	TAY

	LDA	PRTT5T,X
	TAX

	LDA	$680,Y
	STA	DIRBUF,X
	LDA	$600,Y
	STA	DIRBUF+1,X

	DEC	PTTI
	BPL	PRTT5B
*
	LDA	#'-'
	STA	DIRBUF+27
	STA	DIRBUF+30
	LDA	#':'
	STA	DIRBUF+36
*
PRTT6	LDA	#155
	STA	DIRBUF+40

	LDA	#DIRBUF:L
	LDX	#DIRBUF:H
	LDY	PTTY
	JSR	INIS2S
*NXT
	INC	PTTY
	INC	PTTP

	LDA	PTTP
	CMP	DIRPOC
	BEQ	PRTT8

	LDA	PTTY
	CMP	#19
	BCS	PRTT7

	JMP	PRTT1
*SCR FUL

PRTT7	LDA	#$9D
	BNE	PRTT9
*DIR END

PRTT8	LDY	PTTY
	CPY	#19
	BCS	PRTT8B

	LDA	#CTP:L
	LDX	#CTP:H
	JSR	INIS2S

	INC	PTTY
	BNE	PRTT8
**

PRTT8B	LDA	#$12

PRTT9	LDY	#19
	JSR	PRTSIP

	LDA	DIRPOC
	BNE	PRTT9A

	LDA	#$12
	LDY	#2
	JMP	PRTSIP
*CURS

PRTT9A	LDA	MDIRPOS
	SEC
	SBC	DIRSPOS

	CMP	#16
	BCS	PRTTX

	CLC
	ADC	#3
	TAY
	LDX	#0
	LDA	#255
	JSR	PRTS1

PRTTX	RTS

**

PRTT5T	DFB 25,28,31,34,37

SUBDT	ASC "SUB-DIR "

*

NDTX	ASC "Reading directory..."
	EOL
**

RDDIR	JSR	CLTAB
	JSR	CLLIN

	LDA	#NDTX:L
	STA	PRTADR
	LDA	#NDTX:H
	STA	PRTADR+1
	LDY	#20
	LDX	#10
	LDA	#0
	JSR	PRINT

	LDA	#MEMBUF:L
	STA	RWADR
	LDA	#MEMBUF:H
	STA	RWADR+1
	JSR	BDIR
	BPL	RDD2
	RTS
*

RDD2	LDA	RWLEN
	CMP	#$E9
	LDA	RWLEN+1
	SBC	#$16
	BCC	RDD2B

	LDA	#$16
	STA	RWLEN+1
	LDA	#$E9
	STA	RWLEN

RDD2B	LDA	#MEMBUF:L
	CLC
	ADC	RWLEN
	STA	MEM2
	LDA	#MEMBUF:H
	ADC	RWLEN+1
	STA	MEM2+1
*
	LDA	#0
	STA	DIRPOC

RDD2C	LDA	RWLEN
	SEC
	SBC	#23
	STA	RWLEN
	LDA	RWLEN+1
	SBC	#0
	STA	RWLEN+1
	BCC	RESORT

	INC	DIRPOC
	BNE	RDD2C
*SORT

RESORT	LDA	#0
	STA	MDIRPOS
	STA	DIRSPOS

	LDA	DIRPOC
	CMP	#2
	BCC	RDD2X

	LDA	SORTM
	CMP	#4
	BCC	RDD3

RDD2X	LDY	#1
	RTS
*
RDD3	ASL
	TAX
	LDA	RDD3T,X
	STA	SORTA
	LDA	RDD3T+1,X
	STA	SORTA+1
*
RDD4	LDA	#128
	STA	PTTI
	ASL
	STA	PTTP
*
RDD5	LDA	PTTP
	JSR	DIRADR

	LDA	MT1
	CLC
	ADC	#23
	STA	MT2
	LDA	MT1+1
	ADC	#0
	STA	MT2+1
*
	LDX	#0

RDD6	TXA
	TAY
	LDA	(SORTA),Y
	BMI	RDD8
	TAY

	LDA	(MT1),Y
	CMP	(MT2),Y
	BCC	RDD8
	BNE	RDD7

	INX
	BNE	RDD6
*
RDD7	LDY	#22

RDD7B	LDA	(MT1),Y
	TAX
	LDA	(MT2),Y
	STA	(MT1),Y
	TXA
	STA	(MT2),Y

	DEY
	BPL	RDD7B

	LSR	PTTI
*
RDD8	INC	PTTP

	LDX	PTTP
	INX
	CPX	DIRPOC
	BCC	RDD5
*
	LDA	PTTI
	BPL	RDD4

	LDY	#1
	RTS
**

RDD3T	DFW SRTB0,SRTB1,SRTB2,SRTB3

SRTB0	DFB 14,15,16
	DFB 6,7,8,9,10,11,12,13
	DFB 255

SRTB1	DFB 6,7,8,9,10,11,12,13
	DFB 14,15,16
	DFB 255

SRTB2	DFB 5,4,3
	DFB 255

SRTB3	DFB 19,18,17,20,21,22
	DFB 255

** PRENOS

START3	EQU	*
PBDIR	EQU	BDIR
PBTEST	EQU	BTEST
PBREAD	EQU	BREAD
PBWRITE	EQU	BWRITE
PBRENAM	EQU	BRENAME
PBAINIT	EQU	FORMAT
PBCHKD	EQU	BCHKDSK
PBXIO	EQU	BXIO
PCLS	EQU	CLS
PPRINT	EQU	PRINT
PREOPNS	EQU	REOPNS
PGETKEY	EQU	GETKEY
PCONVDC	EQU	CONVDC
PMENU	EQU	MENU
PEDIT	EQU	EDIT
PEDIT0	EQU	EDIT0
PPRTPTH	EQU	PRTPATH
PCLTAB	EQU	CLTAB
PCLLIN	EQU	CLLIN
PDIRADR	EQU	DIRADR
PINISCR	EQU	INISCR
PPRTTAB	EQU	PRTTAB
PPRTCMD	EQU	PRTCMD
PINIS2S	EQU	INIS2S
PCLDATB	EQU	CLDATB
PRDDIR	EQU	RDDIR
PRESORT	EQU	RESORT

*
