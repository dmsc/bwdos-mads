*************************************
*				*
*     " N E W E D "  PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

NAME	EQU	$3800

	ORG	$3000

***

START	LDA	$700
	CMP	#'S'
	BNE	ST0ER

	LDA	$703
	CMP	#'B'
	BNE	ST0ER
	LDA	$704
	CMP	#'W'
	BNE	ST0ER

	LDA	$705
	CMP	#$12
	BCS	START2

ST0ER	JSR	PRINT
	DTA	155
	DTA	C"Error: Not BW-DOS "
	DTA	C"1.20 or later!"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

GETNAME	JSR	XDOS

***

	LDY	#33

ST3	LDA	(10),Y
	STA	NAME-33,Y

	CMP	#$9B
	BEQ	ST4

	INY
	CPY	#61
	BCC	ST3
*

ST4	LDX	#4

ST5	LDA	COMNAM,X
	STA	NAME-33,Y

	INY
	DEX
	BPL	ST5

**TEST
	JSR	CLOSE

	LDA	#3
	JSR	DOIT
	BMI	ERROR
*

ST6	JSR	TESTFF
	JSR	TESTFF

	JSR	CLOSE

*ROMkove E:

	LDY	#0

RED1	LDA	$31A,Y
	CMP	#'E'
	BEQ	RED2

	INY
	INY
	INY
	CPY	#33
	BCC	RED1
*
	JSR	PRINT
	DTA	155
	DTA	C"Error: No 'E:' handler!"
	DTA	155,0

	JMP	ERR5
**

RED2	LDA	#0
	STA	$31B,Y
	LDA	#$E4
	STA	$31C,Y

*LOAD
	LDA	#40
*DO IT!

DOIT	LDX	#$10
	STA	$342,X

	LDA	#<NAME
	STA	$344,X
	LDA	#>NAME
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JMP	$E456

**CLOSE

CLOSE	LDA	#12
	BNE	DOIT
***

TESTFF	LDA	#0
	STA	$348+$10
	STA	$349+$10
	LDA	#7
	JSR	DOIT
	BMI	TFFER

	CMP	#$FF
	BNE	TFFER

	RTS

****

TFFER	LDY	#152

**
	ICL	"error.inc"

ERR5	JSR	CLOSE

*XDIV

	LDY	#8
	LDA	(10),Y
	STA	ERR5J+1
	INY
	LDA	(10),Y
	STA	ERR5J+2

	LDY	#1
ERR5J	JSR	XDOS

**

XDOS	JMP	(10)
**

ERRTAB	DTA	1,10,100

***
	ICL	"print.inc"
	ICL	"print1.inc"
***
COMNAM	DTA 155
	DTA C"MOC."
***

