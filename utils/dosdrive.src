*************************************
*				*
*  " D O S D R I V E "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128

NUMB	EQU	130

***

	ORG	$6000
	JMP	START

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y

	CMP	#'O'
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	START3
*OFF?
	INY
	CMP	(10),Y
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
	JMP	DISABLE
***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: "
	DTA	C"DOSDRIVE n"
	DTA	155
	DTA	C"        DOSDRIVE OFF"
	DTA	155,0

	JMP	(10)

*** PARAMETR

START3	LDY	#37
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX

	DEY
	LDA	(10),Y
	SEC
	SBC	#'0'
	BEQ	SYNTAX
	CMP	#10
	BCS	SYNTAX

	STA	NUMB

* NENI UZ?

	JSR	SRCHRUT
	BCC	START4
*JE!
	LDY	#OPEN4+1-RESID
	LDA	NUMB
	STA	(SRCHADR),Y

	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE: Drive "
	DTA	C"number changed."
	DTA	155,0

	JMP	(10)

***INSTALL

STRT4ER	JSR	PRINT
	DTA	155
	DTA	C"Handler 'D:' not "
	DTA	C"present !"
	DTA	155
	DTA	C"Can't install."
	DTA	155,0

	JMP	(10)

* PUV. "D:"

START4	JSR	SRCHD
	BMI	STRT4ER

	LDA	$31B,X
	STA	STRT4C+1
	STA	TABPUV
	LDA	$31C,X
	STA	STRT4C+2
	STA	TABPUV+1

	LDY	#11
STRT4C	LDA	$FFFF,Y
	STA	TAB,Y
	DEY
	BPL	STRT4C

	LDA	TAB
	CLC
	ADC	#1
	STA	OPNPUV+1
	LDA	TAB+1
	ADC	#0
	STA	OPNPUV+2

	LDA	TAB+10
	CLC
	ADC	#1
	STA	SPECPUV+1
	LDA	TAB+11
	ADC	#0
	STA	SPECPUV+2

* MEMLO
	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*RELOK
	LDA	SRCHADR
	CLC
	ADC	#TAB-RESID
	STA	R03+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R04+1

	LDA	SRCHADR
	CLC
	ADC	#OPEN-RESID-1
	STA	TAB
	LDA	SRCHADR+1
	ADC	#0
	STA	TAB+1

	LDA	SRCHADR
	CLC
	ADC	#SPEC-RESID-1
	STA	TAB+10
	LDA	SRCHADR+1
	ADC	#0
	STA	TAB+11

	LDA	SRCHADR
	CLC
	ADC	#JUMP-RESID
	STA	R05+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R05+2

* DRIVE#
	LDA	NUMB
	STA	OPEN4+1

* COPY
	LDY	#RESLEN

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2

* RESET

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3

* INIT IT

	LDA	SRCHADR
	CLC
	ADC	#3
	STA	INSTL4+1
	LDA	SRCHADR+1
	ADC	#0
	STA	INSTL4+2

INSTL4	JSR	$E474

* HOTOVO

	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE installed."
	DTA	155,0

	JMP	(10)
***

SRCHD	LDX	#33

STRT4B	DEX
	DEX
	DEX
	BMI	SRCHDX

	LDA	$31A,X
	CMP	#'D'
	BNE	STRT4B

SRCHDX	RTS

***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE"
	DTA	C" not Installed!"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE is not the"
	DTA	C" last installed"
	DTA	155
	DTA	C"handler! "
	DTA	0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	CPY	#RESLEN
	BNE	DISCANT
*ODSTRAN

	JSR	SRCHD
	BMI	DISBL2A

	LDY	#TABPUV-RESID
	LDA	(SRCHADR),Y
	STA	$31B,X
	INY
	LDA	(SRCHADR),Y
	STA	$31C,X
**

DISBL2A	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**
	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE"
	DTA	C" Removed."
	DTA	155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R03-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID-1
	BNE	SRCHR2
*
	LDY	#OPEN4-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#OPEN-RESID-1
	BNE	SRCHR3
*
	LDY	#OPNPUV-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID-1
	BNE	SRCHR4
*
	LDY	#SPECPUV-RESID

SRCHR5	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#SPEC-RESID-1
	BNE	SRCHR5
*
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	LDY	#33

RHTSRCH	DEY
	DEY
	DEY
	BMI	RINIX

	LDA	$31A,Y
	CMP	#'D'
	BNE	RHTSRCH

R03	LDA	#<TAB
	STA	$31B,Y
R04	LDA	#>TAB
	STA	$31C,Y

RINIX	RTS

**

TABPUV	DTA	A(0)

TAB	DTA	A($E473)
	DTA	A($E473)
	DTA	A($E473)
	DTA	A($E473)
	DTA	A($E473)
	DTA	A($E473)
**

OPEN	LDA	$2A	;AUX1=4?
	EOR	#4
	BNE	JUMP
*A=0
	TAY

OPEN2	LDA	($24),Y	;NO PATH?
	CMP	#$9B
	BEQ	OPEN3

	AND	#$FD
	CMP	#'<'	;< OR >
	BEQ	JUMP

	INY
	BNE	OPEN2

OPEN3	LDA	$21	;DRIVE#
	PHA

OPEN4	LDA	#9	;DOSDRIVE#
	STA	$21

R05	JSR	JUMP

TST2	PLA
	STA	$21

	TYA
	BPL	RINIX

JUMP	LDA	$22
	CMP	#40
	BEQ	SPECPUV

OPNPUV	JMP	$E474	;ORIG. OPEN

**

SPEC	LDA	$22
	CMP	#40
	BEQ	OPEN

SPECPUV	JMP	$E474	;ORIG. SPEC

***

RESLEN	EQU	*-RESID

***
