*************************************
*				*
*  " D O S D R I V E "  FOR BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128

NUMB	EQU	130

DTAB	EQU	131

***

	ORG	$3000

	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y

	CMP	#'O'
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	START3
*OFF?
	INY
	CMP	(10),Y
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
	JMP	DISABLE
***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: DOSDRIVE n"
	DTA	155
	DTA	C"        DOSDRIVE OFF"
	DTA	155,0

	JMP	(10)

*Check parameter
START3	LDY	#37
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX

	DEY
	LDA	(10),Y
	EOR	#'0'
	BEQ	SYNTAX
	CMP	#10
	BCS	SYNTAX

	STA	NUMB

*Check if already installed
	JSR	SRCHRUT
	BCC	START4
*Yes!
	LDY	#OPEN4+1-RESID
	LDA	NUMB
	STA	(SRCHADR),Y

	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE: Drive number changed."
	DTA	155,0

	JMP	(10)

*Install error
STRT4ER	JSR	PRINT
	DTA	155
	DTA	C"Handler 'D:' not present!"
	DTA	0
	BEQ	STRT4E2

STRT4RM	JSR	PRINT
	DTA	155
	DTA	C"DOS is in ROM!"
	DTA	0
STRT4E2	JSR	PRINT
	DTA	155
	DTA	C"Can't install."
	DTA	155,0

	JMP	(10)

*Get the new install address from MEMLO
START4	LDA	743
	STA	SRCHADR
	LDA	744
	STA	SRCHADR+1

*Search "D:" handler
	JSR	SRCHD
	BMI	STRT4ER

	LDA	$31B,X
	STA	DTAB
	LDA	$31C,X
	STA	DTAB+1

*We only need to patch OPEN and SPEC into the table,
*get the original handlers:
	LDY	#0
	LDA	(DTAB),Y
	CLC
	ADC	#1
	STA	OPNPUV+1
	INY
	LDA	(DTAB),Y
	ADC	#0
	STA	OPNPUV+2

	LDY	#10
	LDA	(DTAB),Y
	CLC
	ADC	#1
	STA	SPECPUV+1
	INY
	LDA	(DTAB),Y
	ADC	#0
	STA	SPECPUV+2

*Patch the handler table, checking if it is in RAM
	LDY	#0
	LDA	SRCHADR
	CLC
	ADC	#OPEN-RESID-1
	STA	(DTAB),Y
	EOR	(DTAB),Y
	BNE	STRT4RM
	INY
	ADC	SRCHADR+1
	STA	(DTAB),Y
	EOR	(DTAB),Y
	BNE	STRT4RM

	LDY	#10
	LDA	SRCHADR
	CLC
	ADC	#SPEC-RESID-1
	STA	(DTAB),Y
	INY
	LDA	#0
	ADC	SRCHADR+1
	STA	(DTAB),Y

*Setup new MEMLO
	LDA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1

*Relocate sources
	LDA	SRCHADR
	CLC
	ADC	#JUMP-RESID
	STA	R05+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R05+2

*Set DRIVE#
	LDA	NUMB
	STA	OPEN4+1

*Copy
	LDY	#RESLEN

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2

*Set RESET handler
	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3

*Done
	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE installed."
	DTA	155,0

	JMP	(10)
***

*Search "D:" handler
SRCHD	LDX	#33

STRT4B	DEX
	DEX
	DEX
	BMI	SRCHDX

	LDA	$31A,X
	CMP	#'D'
	BNE	STRT4B

SRCHDX	RTS

***
DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE not Installed!"
	DTA	0
	BEQ	DISERR2
***

DISCANT	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE is not the last handler"
	DTA	155
	DTA	C"installed! "
	DTA	0

DISERR2	JSR	PRINT
	DTA	155
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	CPY	#RESLEN
	BNE	DISCANT

*Remove handler
	JSR	SRCHD
	BMI	DISBL2A

	LDA	$31B,X
	STA	DTAB
	LDA	$31C,X
	STA	DTAB+1

*Un-patch DOS table
	LDY	#OPNPUV+1-RESID
	LDA	(SRCHADR),Y
	SEC
	SBC	#1
	LDY	#0
	STA	(DTAB),Y
	LDY	#OPNPUV+2-RESID
	LDA	(SRCHADR),Y
	SBC	#0
	LDY	#1
	STA	(DTAB),Y

	LDY	#SPECPUV+1-RESID
	LDA	(SRCHADR),Y
	SEC
	SBC	#1
	LDY	#10
	STA	(DTAB),Y
	LDY	#SPECPUV+2-RESID
	LDA	(SRCHADR),Y
	SBC	#0
	LDY	#11
	STA	(DTAB),Y

*And restore RESET and MEMLO
DISBL2A	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**
	JSR	PRINT
	DTA	155
	DTA	C"DOSDRIVE Removed."
	DTA	155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#OPEN4-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID-1
	BNE	SRCHR2
*
	LDY	#OPNPUV-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID-1
	BNE	SRCHR4
*
	LDY	#SPECPUV-RESID

SRCHR5	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#SPEC-RESID-1
	BNE	SRCHR5
*
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

RINIX	RTS

**
OPEN	LDA	$2A	;AUX1=4?
	EOR	#4
	BNE	JUMP
*A=0
	TAY

OPEN2	LDA	($24),Y	;NO PATH?
	CMP	#$9B
	BEQ	OPEN3

	AND	#$FD
	CMP	#'<'	;< OR >
	BEQ	JUMP

	INY
	BNE	OPEN2

OPEN3	LDA	$21	;DRIVE#
	PHA

OPEN4	LDA	#9	;DOSDRIVE#
	STA	$21

R05	JSR	JUMP

TST2	PLA
	STA	$21

	TYA
	BPL	RINIX

JUMP	LDA	$22
	CMP	#40
	BEQ	SPECPUV

OPNPUV	JMP	$E474	;ORIG. OPEN

**

SPEC	LDA	$22
	CMP	#40
	BEQ	OPEN

SPECPUV	JMP	$E474	;ORIG. SPEC

***

RESLEN	EQU	*-RESID

***
