	ORG	$500
	JMP	START

*************************************
*				*
*    " B L O A D "   PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

NAME	EQU	$6E4

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456

***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#0
	LDX	#0
**
HEXL	EQU	GHXRTS+1
HEXH	EQU	GHXRTS+3
**
	LDA	#1

GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEXL
	STA	HEXH

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2

GETHX2	CPY	#41
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	PLA
	ORA	HEXL
	STA	HEXL

	JMP	GETHX2
***

GHXERR	JSR	PRINT
	DTA	155
	DTA	C"Bad parameter !"
	DTA	155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSE
	JMP	(10)
***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA	155,253
	DTA	C"Bad DOS !"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
**NAME

	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3A

**SYNTAX

	JSR	PRINT
	DTA	155
	DTA	C"BLOAD fname adr. "
	DTA	C"[len.]"
	DTA	155,0
	JMP	ERR2
***

START3A	LDY	#33

START3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	START3
**ADR

	JSR	GETHEX
	BNE	START4
**
	JSR	PRINT
	DTA	155
	DTA	C"No address"
	DTA	C" specified !"
	DTA	155,0
	JMP	ERR2X
**

START4	TXA
	PHA
	TYA
	PHA
**LEN
	LDA	#$FF
	STA	LENFLG

	JSR	GETHEX
	BNE	START5

	LDY	#$FF
	LDX	#$FF
	LSR	LENFLG
**

START5	TXA
	PHA
	TYA
	PHA

***OPEN

	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#<NAME
	STA	$344,X
	LDA	#>NAME
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BPL	START6
***
	JSR	PRINT
	DTA	155
	DTA	C"Can't open !"
	DTA	155,0
	JMP	ERR2X
***

START6	LDX	#$10
	LDA	#7
	STA	$342,X

	PLA
	STA	$348,X
	PLA
	STA	$349,X

	PLA
	STA	$344,X
	PLA
	STA	$345,X

	JSR	$E456
	BPL	START7
**
	CPY	#136
	BEQ	ST6EOF

	JSR	PRINT
	DTA	155
	DTA	C"I/O Error !"
	DTA	155,0
	JMP	ERR2X
**

ST6EOF	LDA	#0
**
LENFLG	EQU	ST6EOF+1
**
	BPL	START7

	JSR	PRINT
	DTA	155
	DTA	C"EOF found !"
	DTA	155,0

START7	JMP	ERR2

***
