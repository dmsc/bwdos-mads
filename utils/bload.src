*************************************
*				*
*    " B L O A D "   FOR BW-DOS	*
*				*
*************************************

*Use CIO ZP for temporaries
HEXL	EQU	$20
HEXH	EQU	$21

	ORG	$480

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<PBADVER
	LDX	#PEND-PBADVER+1
	JMP	PRINT

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2

*Get file name
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BEQ	SYNTAX

START3A	LDY	#33

START3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	START3

*Get load address
	JSR	GETHEX
	BNE	START4

*Show command syntax
SYNTAX	LDA	#<PSYNTAX
	LDX	#PBADVER-PSYNTAX+1
	BNE	ERRJ

START4	PHA
	TXA
	PHA

*Get length
	JSR	GETHEX
	BNE	START5

	LDA	#$FF
	TAX
	STA	LENFLG
**

START5	PHA
	TXA
	PHA

*Open file
	JSR	CLOSE

	LDA	#4
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDX	#<NAME
	LDA	#>NAME

	LDY	#3
	JSR	CIOCMD
	BPL	START6
*Open ERROR
	LDA	#<PEOPEN
	LDX	#PEREAD-PEOPEN+1
ERRJ	BNE	PERROR

*Load data into memory
START6	PLA
	STA	$348+$10
	PLA
	STA	$349+$10

	PLA
	TAX
	PLA

	LDY	#7
	JSR	CIOCMD
	BPL	ERR2

	CPY	#136
	BEQ	ST6EOF

*Load ERROR
	LDA	#<PEREAD
	LDX	#PEOF-PEREAD+1
	BNE	PERROR

**
LENFLG	EQU	*+1
ST6EOF	LDA	#0
	BMI	ERR2

	LDA	#<PEOF
	LDX	#PBADPAR-PEOF+1
	BNE	PERROR
***

GETNAME	JMP	GETNAME

***

GHXRTS	LDX	HEXL
	LDA	HEXH
	CPY	#36
	RTS

**
GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEXL
	STA	HEXH

	LDY	#36

GETHX2	LDA	(10),Y

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	INY
	CPY	#41
	BCC	GETHX2
***

GHXERR	LDA	#<PBADPAR
	LDX	#PSYNTAX-PBADPAR+1
PERROR	JSR	PRINT

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSE
	JMP	(10)
***

CLOSE	LDY	#12
CIOCMD	STX	$344+$10
	STA	$345+$10
	STY	$342+$10
	LDX	#$10
	JMP	$E456

***
PRINT	STA	$344
	STX	$348
	LDX	#0
	STX	$349
	LDY	#>PSYNTAX
	STY	$345
	LDY	#11
	STY	$342
	JMP	$E456
***

PEOPEN	DTA	155
	DTA	C"Can't open!"
PEREAD	DTA	155
	DTA	C"I/O Error!"
PEOF	DTA	155
	DTA	C"EOF found!"
PBADPAR	DTA	155
	DTA	C"Bad parameter!"
PSYNTAX	DTA	155
	DTA	C"BLOAD fname addr [len]"
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
PEND	DTA	155

NAME	EQU	PBADVER

