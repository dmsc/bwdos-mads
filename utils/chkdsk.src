	ORG	$6000
	JMP	START

*************************************
*				*
*    " C H K D S K "  PRO BW-DOS	*
*				*
*************************************

DECOUT	EQU	128
PRTADR	EQU	130
PRTCNT	EQU	132

* VOLNO: 133

BUFFER	EQU	$6400

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

ERROR	TYA
	PHA

	JSR	PRINT
	DTA	155
	DTA	C"Error"
	DTA	0

	PLA
	JSR	PRTBYT
**

ERR4	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DTA	1,10,100

***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X

	JMP	$E456

***
	ICL	"chkbw.inc"
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#19
	STA	DECOUT
	LDA	11
	SBC	#0
	STA	DECOUT+1
**
	JSR	GETNAME

	JSR	CLOSE
**
	LDX	#$10
	LDA	#47
	STA	$342,X

	LDA	10
	CLC
	ADC	#33
	STA	$344,X
	LDA	11
	ADC	#0
	STA	$345,X

	LDA	#<BUFFER
	STA	$348,X
	LDA	#>BUFFER
	STA	$349,X

	JSR	$E456
	BPL	START4

	JMP	ERROR
***

START4	JSR	PRINT
	DTA	155
	DTA	C"      Volume: "
	DTA	0

	LDY	#<(BUFFER+6)
	LDX	#>(BUFFER+6)
	LDA	#8
	JSR	PRTIT

	LDA	BUFFER+14
	JSR	PRTBYT

	LDA	BUFFER+15
	JSR	PRTBYT
*
	JSR	PRINT
	DTA	155
	DTA	C"Bytes/sector:"
	DTA	0

	LDX	#0
	LDA	BUFFER+1
	BNE	START5
	INX
START5	JSR	PRTDVA

*
	JSR	PRINT
	DTA	155
	DTA	C" Total bytes: "
	DTA	0

	LDY	#2
	JSR	PRTBTS

	JSR	PRINT
	DTA	155
	DTA	C"  Bytes free: "
	DTA	0

	LDY	#4
	JSR	PRTBTS

	LDA	#$9B
	JSR	PRT1

	JMP	ERR5
***

PRTBTS	LDA	#8
	PHA
	LDA	#0
	PHA

	LDA	BUFFER,Y
	TAX
	LDA	BUFFER+1,Y
	LDY	#0

	BIT	BUFFER+1
	BPL	PRTBTS2

	LSR
	PHA
	TXA
	ROR
	TAX
	TYA
	ROR
	TAY
	PLA

	JMP	PRTBTS2
***

CONVDC	JMP	($70A)

***

PRTBYT	LDX	#0
PRTDVA	TAY

	LDA	#4
	PHA
	PHA

	LDA	#0
**

PRTBTS2	PHA
	TYA

	LDY	#13
	STA	(DECOUT),Y
	INY
	TXA
	STA	(DECOUT),Y
	INY
	PLA
	STA	(DECOUT),Y
**
	JSR	CONVDC
**
	PLA
	CLC
	ADC	DECOUT
	TAY
	LDA	#0
	ADC	DECOUT+1
	TAX

	PLA
***

PRTIT	STY	PRTADR
	STX	PRTADR+1
	STA	PRTCNT

PRTIT2	LDY	#0
	LDA	(PRTADR),Y
	JSR	PRT1

	INC	PRTADR
	BNE	PRTIT3
	INC	PRTADR+1

PRTIT3	DEC	PRTCNT
	BNE	PRTIT2

	RTS
***
