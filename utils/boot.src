	ORG	$6000
	JMP	START

*************************************
*				*
*     " B O O T "  PRO BW-DOS	*
*				*
*************************************

NAMOFFS	EQU	128
TMP	EQU	129
SIOADR	EQU	130

**VOLNO: 132

SRC	EQU	$500
MSK	EQU	$540
DIRBUF	EQU	$5C0

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GETPAR	JSR	GETNAME

	LDX	#0
*
	LDY	#34
	LDA	(10),Y
	AND	#15
	STA	RWSDRIV

	DEY

GTPAR0	STX	NAMOFFS

GTPAR1	LDA	(10),Y
	STA	SRC,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

	RTS
***

GETFLTR	LDX	NAMOFFS
	LDY	#0

	LDA	#6
	STA	TMP

	DEX

DC1B1	INX

	LDA	SRC,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	CMP	#'?'
	BEQ	DC1BZN

	JSR	JELIPL
	BCS	DC1BNEP
*

DC1BZN	JSR	DC1BPUT
	JMP	DC1B1
**

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP2

DC1BP1	RTS

*

DC1BP2	STA	MSK,Y
	INY
	RTS

**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE
	BCS	DC1B1
**

DC1BNEP	SEC
	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC

	ROR	TMP
	BCC	DC1B1

	RTS

****

ERROR	TYA
	PHA

	JSR	PRINT
	DTA	155
	DTA	C"Error "
	DTA	0

	LDX	#2
	PLA

ERR2	LDY	#0

ERR3	CMP	ERRTAB,X
	BCC	ERR4

	SBC	ERRTAB,X
	INY
	BNE	ERR3
**

ERR4	PHA
	TXA
	PHA

	TYA
	ORA	#$30
	JSR	PRT1

	PLA
	TAX
	PLA

	DEX
	BPL	ERR2
**
	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DTA	1,10,100

***

CLOSE	LDX	#$10

	LDA	#12
	STA	$342,X

	JMP	$E456
***

JELIPL	CMP	#'_'
	BEQ	JLPP

	CMP	#$30
	BCC	JLPM
	CMP	#$3A
	BCC	JLPP
	CMP	#$41
	BCC	JLPM
	CMP	#$5B
	BCS	JLPM

JLPP	CLC
	RTS
*

JLPM	SEC
	RTS
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA	155,253
	DTA	C"Incorrect DOS version"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	STA	SPSIO+1
	LDA	11
	SBC	#0
	STA	SIOADR+1
	STA	SPSIO+2
***

	JSR	CLOSE
	JSR	GETPAR
	JSR	GETFLTR
*OPEN
	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#<SRC
	STA	$344,X
	LDA	#>SRC
	STA	$345,X

	LDA	#$14
	STA	$349,X
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BPL	MAIN2

ERRJP	JMP	ERROR

* SEARCH

MAIN2	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#<DIRBUF
	STA	$344,X
	LDA	#>DIRBUF
	STA	$345,X

	LDA	#23
	STA	$348,X
	LDA	#0
	STA	$349,X

	JSR	$E456
	BPL	MAIN2B
*
	CPY	#136
	BNE	ERRJP

	JSR	PRINT
	DTA	155
	DTA	C"File not found!"
	DTA	155,0

	JMP	ERR5X
***

MAIN2B	LDX	#10

MAIN2C	LDA	MSK,X
	CMP	#'?'
	BEQ	MAIN2D

	CMP	DIRBUF+6,X
	BNE	MAIN2

MAIN2D	DEX
	BPL	MAIN2C
*STAT
	LDA	DIRBUF
	AND	#$28
	CMP	#8
	BNE	MAIN2

* NASEL!

	JSR	CLOSE

	JSR	RDSEC1

	LDA	DIRBUF+1
	STA	SRC+40
	LDA	DIRBUF+2
	STA	SRC+41

	JSR	WRSEC1
**

	JSR	PRINT
	DTA	155
	DTA	C"BOOT-file installed."
	DTA	155,0

	JMP	(10)
***

RDSEC1	LDA	#'R'
	LDY	#64
	BNE	RWS1
*

WRSEC1	LDY	#8	;WRTCMD
	LDA	(SIOADR),Y
	LDY	#128
*

RWS1	STA	RWSIOT+2
	STY	RWSIOT+3

	LDX	#11
RWS2	LDA	RWSIOT,X
	STA	$300,X
	DEX
	BPL	RWS2

	JSR	SPSIO

	TYA
	BMI	ERJP2

	RTS
**

ERJP2	JMP	ERROR

**

RWSIOT	DTA	$31
RWSDRIV	DTA	0,0,0
	DTA	A(SRC)
	DTA	7,0
	DTA	A(128)
	DTA	A(1)

***

SPSIO	JMP	($FFFF)

***

