*************************************
*				*
*     " B O O T "  FOR BW-DOS	*
*				*
*************************************

NAMOFFS	EQU	128
TMP	EQU	129
ZPG	EQU	130	; COMTAB-256

DIRBUF	EQU	$88	; 23 bytes, binary directory entry
MASK	EQU	$A0	; 11 bytes, mask for filename to search
DIRNAM	EQU	$B0	; 64 bytes, name of directory to open

SECBUF	EQU	$2F00	; $80 bytes

* Both DIRBUF and DIRNAM must be in the same page
	ERT	>DIRBUF	<>	>DIRNAM

	ORG	$3000

	ICL	"chkdos.inc"
***
	LDA	10
	STA	ZPG
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	STA	ZPG+1
	ADC	#0
	STA	GETNAME+2

	DEC	ZPG+1

	LDY	#256-10
	LDA	(ZPG),Y
	STA	SPSIO+1
	INY
	LDA	(ZPG),Y
	STA	SPSIO+2
***
	JSR	CLOSE

* Get command line
GETNAME	JSR	GETNAME

* Store drive number
	LDY	#34
	LDA	(10),Y
	AND	#15
	STA	RWSDRIV

* Copy full path into DIRNAM, and store offset of filename in NAMOFFS
	LDX	#0
	DEY
GTPAR0	STX	NAMOFFS

GTPAR1	LDA	(10),Y
	STA	DIRNAM,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

***

	JSR	GETFLTR
* OPEN binary directory
	LDA	#$14
	STA	$35A

	LDA	#3
	LDX	#<DIRNAM
	JSR	CIOCMD1
	BMI	ERROR

* SEARCH file
MAIN2	LDA	#23
	STA	$358

	LDA	#7
	LDX	#<DIRBUF
	JSR	CIOCMD1
	BPL	MAIN2B
*
	CPY	#136
	BNE	ERROR

	JSR	PRINT
	DTA	155
	DTA	C"File not found!"
	DTA	155,0
	BEQ	ERR5X
***

MAIN2B	LDX	#10

MAIN2C	LDA	MASK,X
	CMP	#'?'
	BEQ	MAIN2D

	CMP	DIRBUF+6,X
	BNE	MAIN2

MAIN2D	DEX
	BPL	MAIN2C
*STAT
	LDA	DIRBUF
	AND	#$28
	CMP	#8
	BNE	MAIN2

* Ok, found file, read boot sector
	JSR	RDSEC1
* Patch sector map of new boot file
	LDA	DIRBUF+1
	STA	SECBUF+40
	LDA	DIRBUF+2
	STA	SECBUF+41
* Write boot sector
	JSR	WRSEC1
**
	JSR	PRINT
	DTA	155
	DTA	C"BOOT-file installed."
	DTA	155,0
* Close and exit to DOS
	BEQ	ERR5
****
	ICL	"error.inc"

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474	;XDIVIO

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DTA	100,10,1

***
RDSEC1	LDA	#'R'
	LDY	#64
	BNE	RWS1
*

WRSEC1	LDY	#256-2	;WRTCMD
	LDA	(ZPG),Y
	LDY	#128
*

RWS1	STA	RWSIOT+2
	STY	RWSIOT+3

	LDX	#11
RWS2	LDA	RWSIOT,X
	STA	$300,X
	DEX
	BPL	RWS2

SPSIO	JSR	SPSIO

	TYA
	BMI	ERROR

	RTS
**

GETFLTR	LDX	NAMOFFS
	LDY	#0

	LDA	#6
	STA	TMP

	DEX

DC1B1	INX

	LDA	DIRNAM,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	CMP	#'?'
	BEQ	DC1BZN

	CMP	#'_'
	BEQ	DC1BZN

	CMP	#'0'
	BCC	DC1BNEP
	CMP	#'9'+1
	BCC	DC1BZN
	CMP	#'A'
	BCC	DC1BNEP
	CMP	#'Z'+1
	BCS	DC1BNEP
*

DC1BZN	JSR	DC1BPUT
	JMP	DC1B1
**

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP2

DC1BP1	RTS

*

DC1BP2	STA	MASK,Y
	INY
	RTS

**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE
	BCS	DC1B1
**

DC1BNEP	SEC
	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC

	ROR	TMP
	BCC	DC1B1

	RTS

***
	ICL	"print.inc"

*PRINT 1 CHARACTER IN A
PRT1	TAY
	LDA	#11
	LDX	#0
	STX	$348
	BEQ	CIOCMD
***
CLOSE	LDA	#12
CIOCMD1	STX	$354
	LDX	#>DIRNAM
	STX	$355
	LDX	#$10
CIOCMD	STA	$342,X
	LDA	#0
	STA	$349,X
	STA	$34B,X
	TYA
	JMP	$E456
***

RWSIOT	DTA	$31
RWSDRIV	DTA	0,0,0
	DTA	A(SECBUF)
	DTA	7,0
	DTA	A(128)
	DTA	A(1)

***
