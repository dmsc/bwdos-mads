*************************************
*				*
*     " P A U S E "   PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

	ORG	$3000
***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#8
	STA	XDIVIO+1
	LDA	11
	ADC	#0
	STA	XDIVIO+2
***
	JSR	PRINT
	DTA 155
	DTA C"Press <RETURN> "
	DTA C"to continue..."
	DTA 0
***

START3	JSR	CLOSE

	LDA	#<KNAM
	STA	$344+$10
	LDA	#>KNAM
	STA	$345+$10

	LDA	#4
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDA	#3
	JSR	CIOC
	BMI	START3

	LDA	#0
	STA	$348+$10
	STA	$349+$10

	LDA	#7
	JSR	CIOC
	BMI	START3

	PHA
	JSR	CLOSE
	PLA

	CMP	#155
	BEQ	STRET
	CMP	#27
	BNE	START3
* ESC
	LDY	#1
	JSR	XDIVIO

	JSR	PRINT
	DTA 155
	DTA C"(Batch file aborted)"
	DTA 0
* RETURN

STRET	JSR	PRINT
	DTA 155,0

	JMP	(10)
***

CLOSE	LDA	#12
CIOC	LDX	#$10
	STA	$342,X
	JMP	$E456
***

KNAM	DTA C"K:"
	DTA 155
****
	ICL	"print.inc"
	ICL	"print1.inc"
***

XDIVIO	JMP	($FFFF)

***
