*************************************
*				*
*     " P A U S E "   FOR BW-DOS	*
*				*
*************************************

	ORG	$3000
***
	LDY	#8
	LDA	(10),Y
	STA	XDIVIO+1
	INY
	LDA	(10),Y
	STA	XDIVIO+2
***
	LDY	#<MPRESS
	LDA	#MEND-MPRESS
	JSR	PUTS
***

START3	JSR	GETKEY
	CMP	#155
	BEQ	STRET
	CMP	#27
	BNE	START3

*ESC, on older DOS, same as RETURN.
	LDA	$700
	CMP	#'S'
	BNE	STRET

	LDY	#1
XDIVIO	JSR	XDIVIO

	LDA	#MPRESS-MABORT+1
	DTA	$2C	;Skip

* RETURN
STRET	LDA	#1
	JSR	PABORT

	JMP	(10)

*Call K: get handler
GETKEY	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDX	#0
	STX	$2A	;Clears ICAX1Z
	RTS

****
PABORT	LDY	#<MABORT
PUTS	STY	$344
	LDY	#>MABORT
	STY	$345

	LDX	#0
	STA	$348
	STX	$349

	LDY	#11
	STY	$342
	JMP	$E456
***
*Note: all those must be on the same memory page:
MABORT	DTA	155
	DTA	C"(Batch file aborted)"
MPRESS	DTA	155
	DTA	C"Press <RETURN> to continue..."
MEND
