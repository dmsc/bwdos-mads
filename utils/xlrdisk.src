*************************************
*				*
*   RAM-DISK V OS-RAM  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128
SIOADR	EQU	130

DNUMB	EQU	132
FFLAG	EQU	134

ROMSAVE	EQU	143

*** VOLNO: 144

	ORG	$3000

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	LDA	11
	SBC	#0
	STA	SIOADR+1
**
GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
*HELP
	JSR	PRINT
	DTA	155
	DTA	C"  Syntax:"
	DTA	155
	DTA	C"XLRDISK drive#[N]"
	DTA	155
	DTA	C"XLRDISK OFF"
	DTA	155,0

	JMP	(10)
***

START3	CMP	#'O'
	BNE	START4

	INY
	LDA	#'F'
	CMP	(10),Y
	BNE	ST4ER
	INY
	CMP	(10),Y
	BNE	ST4ER

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	ST4ER

	JMP	REMOVE
***

START4	LDA	#0
	STA	FFLAG

	LDY	#36

STRT4A	STA	DNUMB

STRT4B	LDA	(10),Y
	INY
	CMP	#$9B
	BEQ	START5

	CMP	#'N'
	BNE	STRT4D
	STA	FFLAG
	BEQ	STRT4B

STRT4D	EOR	#'0'
	BEQ	ST4ER
	CMP	#10
	BCC	STRT4A
**

ST4ER	JSR	PRINT
	DTA	155,253
	DTA	C"Syntax Error!"
	DTA	155,0

	JMP	(10)
***

START5	LDA	DNUMB
	BEQ	ST4ER

** Test RAM under ROM
	JSR	DISROM

	LDY	#0
	STY	SRCHADR
	LDX	#$C0

ST5A	STX	SRCHADR+1

ST5B	LDA	(SRCHADR),Y

	EOR	#$FF
	STA	(SRCHADR),Y
	CMP	(SRCHADR),Y
	BNE	ST5BAD

	EOR	#$FF
	STA	(SRCHADR),Y
	CMP	(SRCHADR),Y
	BNE	ST5BAD

	INY
	BNE	ST5B

	INX
	BEQ	START6
	CPX	#$D0
	BNE	ST5A

	LDX	#$D8
	BNE	ST5A
***

ST5BAD	JSR	ENROM

	JSR	PRINT
	DTA	155,253
	DTA	C"Error in extra RAM!"
	DTA	155,0

	JMP	(10)


*Ok, search if already installed
START6	JSR	ENROM

	JSR	SRCHRUT
	BCC	STRT6B
	JMP	START7

*Install resident portion

STRT6B	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*SIO
	LDY	#1
	LDA	(SIOADR),Y
	STA	JSIO+2
	DEY
	LDA	(SIOADR),Y
	STA	JSIO+1

	LDA	#(RDSIO-RESID)
	CLC
	ADC	SRCHADR
	STA	(SIOADR),Y
	INY
	LDA	#0
	ADC	SRCHADR+1
	STA	(SIOADR),Y

*COPY
	LDY	#RESLEN

STRT6C	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	STRT6C
*RESET
	LDY	#2

STRT6D	LDA	11,Y
	STA	(SRCHADR),Y
	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	STRT6D

*Copy high area
	JSR	DISROM

	LDY	#0

STRT7C	LDA	RDSIOH,Y
	STA	$C000,Y

	INY
	BNE	STRT7C
*
	JSR	ENROM

*Configure resident portion
START7	JSR	DISROM
	LDA	DNUMB
	STA	RR1+1+RESO

*Format RAMDISK?
	LDA	FFLAG
	BEQ	FORM1

*Test current format
	LDA	$C10B
	CMP	#<110
	BNE	FORM0
	LDA	$C10C
	CMP	#>110
	BNE	FORM0

	LDA	$C120
	CMP	#$22
	BNE	FORM0

	LDA	$C11F
	CMP	#$80
	BNE	FORM0

	JSR	ENROM
	JMP	FORMEND
***

FORM0	JSR	ENROM

	JSR	PRINT
	DTA	155,253
	DTA	C"XL RAM-Disk format invalid!"
	DTA	155,0
	JSR	DISROM

*#MAPS
FORM1	LDX	#0
	TXA
FORM1A	STA	$C100,X
	STA	$C200,X
	DEX
	BNE	FORM1A
***
	LDX	#32
FORM1B	LDA	VZBOOT,X
	STA	$C100,X
	DEX
	BPL	FORM1B

	LDX	#13
FORM1C	LDA	VZDIR,X
	STA	$C283,X
	DEX
	BPL	FORM1C
*BITMAP
	LDX	#13
	LDA	#$FF
FORM1D	STA	$C180,X
	DEX
	BNE	FORM1D

	LDA	#7
	STA	$C180
	LDA	#252
	STA	$C18E

*MAP MAIN DIR

	LDA	#4
	STA	$C204

***
	JSR	ENROM

	JSR	PRINT
	DTA	155
	DTA	C"XL RAM-Disk formatted."
	DTA	0
***

FORMEND	JSR	PRINT
	DTA	155
	DTA	C"XL RAM-Disk is D"
	DTA	0

	LDA	DNUMB
	CLC
	ADC	#$30
	JSR	PRT1

	JSR	PRINT
	DTA	C": (Size: 14 kB)"
	DTA	155,0

	JMP	(10)
***

VZBOOT	DTA	0,3,0,48,$E0,7
	JMP	$3080
	DTA	A(3,110,106)
	DTA	1
	DTA	A(2,16,4)
	DTA	C"XL-RDISK"
	DTA	0,128,$22

VZDIR	DTA	23,0,0
	DTA	C"MAIN       "

****

REMOVE	JSR	SRCHRUT
	BCC	REMNOT
	JMP	REMOV2
**

REMNOT	JSR	PRINT
	DTA	155
	DTA	C"XL RAM-Disk not installed!"
	DTA	155,0

REMCANT	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,253,0

	JMP	(10)
**

REM2LST	JSR	PRINT
	DTA	155
	DTA	C"XL RAM-Disk is not the last"
	DTA	155
	DTA	C"handler installed!"
	DTA	155,0

	JMP	REMCANT
**

REMOV2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	REM2LST

	CPY	#RESLEN
	BNE	REM2LST

*REMOVE IT
	LDY	#2

REM3	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	REM3
*
	JSR	DISROM
	LDX	JSIO+1+RESO
	LDY	JSIO+2+RESO
	JSR	ENROM
	TYA

	LDY	#1
	STA	(SIOADR),Y
	DEY
	TXA
	STA	(SIOADR),Y
***

	JSR	PRINT
	DTA	155
	DTA	C"XL RAM-Disk removed."
	DTA	155,0

	JMP	(10)
****

DISROM	LDA	#0
	SEI
	STA	54286

	LDA	$D301
	STA	ROMSAVE

	LDA	#$FE
	STA	$D301

	RTS
**

ENROM	LDA	ROMSAVE
	STA	$D301

	LDA	#$40
	STA	54286
	CLI

	RTS
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#RESLEN-1

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$18
	BCS	SRCHR1

	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"

***********
*Resident handler in low RAM
RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***
RDSIO	LDA	$D301	;Save original PORTB
	LDY	#0
	STY	54286	;Disable NMI
	SEI		;Disable IRQ
	LDY	#$FE
	STY	$D301	;Disable ROM
	JMP	$C000

RESLEN	EQU	*-RESID


*High memory part, process SIO call
RDSIOH	PHA
	LDA	$300
	CMP	#$31
	BNE	STDSIO

	LDA	$301
RR1	CMP	#0
	BEQ	RDS2

*Exit trough stub to standard SIO
STDSIO	LDA	#SIOST-RDSIOH
	JMP	CSTUB+RESO

*Setup read/write address
RDS2	LDA	$304
	STA	BADR1+1+RESO
	STA	BADR2+1+RESO
	LDA	$305
	STA	BADR1+2+RESO
	STA	BADR2+2+RESO
*Setup length
	LDY	$309
	BNE	R2NAK
	LDY	$308
	DEY

	LDA	$302
	CMP	#'S'
	BNE	R2RW

*STATUS - read from pre-defined area
	CPY	#3
	BNE	R2NAK

	LDA	#<(RDSTAT+RESO)
	LDX	#>(RDSTAT+RESO)
	BNE	SREAD2

*Read/Write
R2RW	CPY	#127
	BNE	R2NAK

*Verify sector number
	LDA	$30B
	BNE	R2NAK
	STA	$30

	LDA	$30A
	BEQ	R2NAK
	CMP	#111	;Max Sector
	BCS	R2NAK

	ADC	#1

	LSR
	ROR	$30

	ADC	#$C0
	CMP	#$D0
	BCC	R2RW2
	ADC	#7
R2RW2	TAX

	LDA	$302
	CMP	#'R'
	BEQ	SREAD
	CMP	#'W'
	BEQ	SWRITE
	CMP	#'P'
	BEQ	SWRITE

*SIO error
R2NAK	LDY	#139
	BNE	R2OK
R2OK	LDY	#1
	STY	$303
	LDA	#EXTST-RDSIOH
	JMP	CSTUB+RESO

SWRITE	LDA	$30
	STX	WADR+2+RESO
	STA	WADR+1+RESO
	LDX	$303
	CPX	#$80
	BNE	R2NAK
	LDA	#WRST-RDSIOH
	JSR	CSTUB+RESO
	BNE	R2OK

SREAD	LDA	$30
SREAD2	STX	RDST+2+RESO
	STA	RDST+1+RESO
	LDX	$303
	CPX	#$40
	BNE	R2NAK
	LDA	#RDST-RDSIOH
	JSR	CSTUB+RESO
	BNE	R2OK

*Copy STUB to low memory and jump
CSTUB	STA	CST1+1+RESO
	LDX	#0
CST1	LDA	$C000,X
	STA	$30,X
	INX
	CPX	#16
	BNE	CST1
	JMP	$0030

*Exit jumping to standard SIO
SIOST	PLA
	STA	$D301
	LDX	#$40
	CLI
	STX	54286
JSIO	JMP	$FFFF

*Read from RAMDISK
RDST	LDA	$FF00,Y
	INC	$D301	; ENROM
BADR1	STA	$FF00,Y
	DEC	$D301	; DISROM
	DEY
	BPL	RDST
	RTS

*Write to RAMDISK
WRST	INC	$D301	; ENROM
BADR2	LDA	$FF00,Y
	DEC	$D301	; DISROM
WADR	STA	$FF00,Y
	DEY
	BPL	WRST
	RTS

*Exit
EXTST	PLA
	STA	$D301
	LDX	#$40
	CLI
	STX	54286
	LDY	$303
	RTS

RDSTAT	DTA	$10,$FF,$FF,$FF

********************************************

RESO	EQU	$C000-RDSIOH

RES2LEN	EQU	*-RDSIOH

