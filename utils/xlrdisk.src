	ORG	$3FFD,$A800
	OUT	N

O	EQU	$A800-$3FFD
	JMP	UTAJ+O
***
*   PRED SAVE SPUSTIT OD $A800,
*   PAK ULOZIT OD A803 NA 4000
***

*************************************
*				*
*   RAM-DISK V OS-RAM  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EPZ	128
SIOADR	EPZ	130

DNUMB	EPZ	132
FFLAG	EPZ	134

ROMSAVE	EPZ	143

*** VOLNO: 144

ODT0	LDA	START

	LDX	#0
	LDY	#9

ODTAJ	LDA	PRINT,X
	EOR	#$55
ODT2	STA	PRINT,X

	INX
	BNE	ODTAJ

	INC	ODTAJ+2
	INC	ODT2+2

	DEY
	BNE	ODTAJ
*
	LDA	#76
	STA	ODT0

	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS version"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	LDA	11
	SBC	#0
	STA	SIOADR+1
**
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
*HELP
	JSR	PRINT
	DFB 155
	ASC "  Syntax:"
	DFB 155
	ASC "XLRDISK "
	ASC "drive#[N]"
	DFB 155
	ASC "XLRDISK OFF"
	DFB 155,0

	JMP	(10)
***

START3	CMP	#'O'
	BNE	START4

	INY
	LDA	#'F'
	CMP	(10),Y
	BNE	START4
	INY
	CMP	(10),Y
	BNE	START4

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START4

	JMP	REMOVE
***

START4	LDA	#0
	STA	FFLAG

	LDY	#36

STRT4A	STA	DNUMB

STRT4B	LDA	(10),Y
	INY
	CMP	#$9B
	BEQ	START5

	CMP	#'N'
	BNE	STRT4D
	STA	FFLAG
	BEQ	STRT4B

STRT4D	SEC
	SBC	#'0'
	BEQ	ST4ER
	CMP	#10
	BCC	STRT4A
**

ST4ER	JSR	PRINT
	DFB 155,253
	ASC "Syntax Error!"
	DFB 155,0

	JMP	(10)
***

START5	LDA	DNUMB
	BEQ	ST4ER

** TESTUJ PAMET

	JSR	DISROM

	LDY	#0
	STY	SRCHADR
	LDA	#$C0

ST5A	STA	SRCHADR+1

ST5B	LDA	(SRCHADR),Y

	EOR	#$FF
	STA	(SRCHADR),Y
	CMP	(SRCHADR),Y
	BNE	ST5BAD

	EOR	#$FF
	STA	(SRCHADR),Y
	CMP	(SRCHADR),Y
	BNE	ST5BAD

	INY
	BNE	ST5B

	INC	SRCHADR+1

	LDA	SRCHADR+1
	BEQ	START6
	CMP	#$D0
	BNE	ST5B

	LDA	#$D8
	BNE	ST5A
***

ST5BAD	JSR	ENROM

	JSR	PRINT
	DFB 155,253
	ASC "Error in extra RAM !"
	DFB 155,0

	JMP	(10)

*PAMET TESTOVANA

START6	JSR	ENROM

	JSR	SRCHRUT
	BCC	STRT6B
	JMP	START7

*INSTALUJ REZIDENT

STRT6B	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*SIO
	LDY	#1
	LDA	(SIOADR),Y
	STA	RDSPUV+2
	DEY
	LDA	(SIOADR),Y
	STA	RDSPUV+1

	LDA	#(RDSIO-RESID)
	CLC
	ADC	SRCHADR
	STA	(SIOADR),Y
	INY
	LDA	#0
	ADC	SRCHADR+1
	STA	(SIOADR),Y

*COPY
	LDY	#RESLEN

STRT6C	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	STRT6C
*RESET
	LDY	#2

STRT6D	LDA	11,Y
	STA	(SRCHADR),Y
	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	STRT6D

*REZIDENT EXISTUJE

START7	LDA	DNUMB
	STA	RR1+1
*RELOK
	LDA	#(R05-RESID)+1
	CLC
	ADC	SRCHADR
	STA	RR8+1
	STA	RR10+1
	LDA	#0
	ADC	SRCHADR+1
	STA	RR8+2
	STA	RR10+2

	LDA	#(R04-RESID)+1
	CLC
	ADC	SRCHADR
	STA	RR7+1
	STA	RR9+1
	LDA	#0
	ADC	SRCHADR+1
	STA	RR7+2
	STA	RR9+2
*BANK0
	JSR	DISROM

	LDY	#0

STRT7C	LDA	RES2,Y
	STA	$C000,Y

	INY
	BNE	STRT7C
*
	JSR	ENROM

* FORMATOVANI

	LDA	FFLAG
	BEQ	FORM1

	JSR	DISROM

	LDA	$C10B
	CMP	#110:L
	BNE	FORM0
	LDA	$C10C
	CMP	#110:H
	BNE	FORM0

	LDA	$C120
	CMP	#$22
	BNE	FORM0

	LDA	$C11F
	CMP	#$80
	BNE	FORM0

	JSR	ENROM
	JMP	FORMEND
***

FORM0	JSR	ENROM

	JSR	PRINT
	DFB 155,253
	ASC "XL RAM-Disk format "
	ASC "was different!"
	DFB 155,0
*#MAPS

FORM1	JSR	DISROM

	LDX	#0
	TXA
FORM1A	STA	$C100,X
	STA	$C200,X
	DEX
	BNE	FORM1A
***
	LDX	#32
FORM1B	LDA	VZBOOT,X
	STA	$C100,X
	DEX
	BPL	FORM1B

	LDX	#13
FORM1C	LDA	VZDIR,X
	STA	$C283,X
	DEX
	BPL	FORM1C
*BITMAP
	LDX	#13
	LDA	#$FF
FORM1D	STA	$C180,X
	DEX
	BNE	FORM1D

	LDA	#7
	STA	$C180
	LDA	#252
	STA	$C18E

*MAP MAIN DIR

	LDA	#4
	STA	$C204

***
	JSR	ENROM

	JSR	PRINT
	DFB 155
	ASC "XL RAM-Disk "
	ASC "formatted."
	DFB 0
***

FORMEND	JSR	PRINT
	DFB 155
	ASC "XL RAM-Disk "
	ASC "is D"
	DFB 0

	LDA	DNUMB
	CLC
	ADC	#$30
	JSR	PRT1

	JSR	PRINT
	ASC ": (Size: 14 kB)
	DFB 155,0

	JMP	(10)
***

VZBOOT	DFB 0,3,0,48,$E0,7
	JMP $3080
	DFW 3,110,106
	DFB 1
	DFW 2,16,4
	ASC "XL-RDISK"
	DFB 0,128,$22

VZDIR	DFB 23,0,0
	ASC "MAIN       "

****

REMOVE	JSR	SRCHRUT
	BCC	REMNOT
	JMP	REMOV2
**

REMNOT	JSR	PRINT
	DFB 155
	ASC "XL RAM-Disk not "
	ASC "installed!"
	DFB 155,0

REMCANT	JSR	PRINT
	ASC "Can't remove."
	DFB 155,253,0

	JMP	(10)
**

REM2LST	JSR	PRINT
	DFB 155
	ASC "XL RAM-Disk is"
	ASC " not the last"
	DFB 155
	ASC "installed handler!"
	DFB 155,0

	JMP	REMCANT
**

REMOV2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	REM2LST

	CPY	#RESLEN
	BNE	REM2LST

*REMOVE IT

	LDY	#2

REM3	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	REM3
*
	LDY	#(RDSPUV-RESID)+1
	LDA	(SRCHADR),Y
	TAX
	INY
	LDA	(SRCHADR),Y

	LDY	#1
	STA	(SIOADR),Y
	DEY
	TXA
	STA	(SIOADR),Y
***

	JSR	PRINT
	DFB 155
	ASC "XL RAM-Disk "
	ASC "removed."
	DFB 155,0

	JMP	(10)
****

DISROM	LDA	#0
	SEI
	STA	54286

	LDA	$D301
	STA	ROMSAVE

	LDA	#$FE
	STA	$D301

	RTS
**

ENROM	LDA	ROMSAVE
	STA	$D301

	LDA	#$40
	STA	54286
	CLI

	RTS
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R04-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#RDSIO-RESID
	BNE	SRCHR2
*
	LDY	#TST4-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR3

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***

RDSPUV	JMP	$FFFF

***

RDSIO	LDA	$D301
	PHA

	LDY	#0
	STY	54286
	SEI

	LDY	#$FE
	STY	$D301

	JSR	$C000

	DEY
	BMI	RDSIO3

R04	LDA	#$FF
	STA	$D301

	LDA	($32),Y

R05	LDX	#$FF
TST3	STX	$D301

	STA	($34),Y

	DEY
	BPL	R04

	INY

RDSIO3	PLA
	STA	$D301

	LDX	#$40
	CLI
	STX	54286

	INY
	BEQ	RDSPUV

	STY	$303
TST4	RTS

**

RESLEN	EPZ	*-RESID

*******

RES2	LDA	$300
	CMP	#$31
	BNE	RES2P

	LDA	$301
RR1	CMP	#0
	BEQ	RES2B

RES2P	LDY	#0
	RTS
**

RES2B	LDA	$304
	STA	$34
	LDA	$305
	STA	$35

	LDA	$309
	BNE	R2NAK
	LDY	$308

	LDA	#$FE
RR7	STA	R04+1
RR8	STA	R05+1

	LDA	$302
	CMP	#'S'
	BNE	R2RW
*STATUS

	LDX	$303
	CPX	#$40
	BNE	R2NAK

	CPY	#4
	BNE	R2NAK

	LDA	#(RDSTAT+RESO):L
	STA	$32
	LDA	#(RDSTAT+RESO):H
	STA	$33

	BNE	RR10
***

R2NAK	LDY	#139
	RTS

***

R2RW	CPY	#128
	BNE	R2NAK

	LDA	$30B	;MAXSECT
	BNE	R2NAK
	STA	$32

	LDA	$30A
	BEQ	R2NAK
	CMP	#111
	BCS	R2NAK
*
	CLC
	ADC	#1

	LSR
	ROR	$32

	ORA	#$C0
	CMP	#$D0
	BCC	R2RWB

	CLC
	ADC	#8

R2RWB	STA	$33

**Y JE 128

	LDX	$303

	LDA	$302
	CMP	#'R'
	BEQ	RR8READ
	CMP	#'W'
	BEQ	RR8WR
	CMP	#'P'
	BNE	R2NAK

RR8WR	CPX	#128
	BNE	R2NAK

	LDA	$32
	LDX	$34
	STA	$34
	STX	$32

	LDA	$33
	LDX	$35
	STA	$35
	STX	$33

RR9	INC	R04+1

	RTS
**

RR8READ	CPX	#$40
	BNE	R2NAK

RR10	INC	R05+1

	RTS
***

RDSTAT	DFB $10,$FF,$FF,$FF

***

RESO	EQU	$C000-RES2

RES2LEN	EQU	*-RES2

**************
*
* KONEC PROGRAMU, NASLEDUJE SLUZEBNI
* RUTINKA
*
**************

UTAJ	LDA	#(PRINT+O):L
	STA	0
	LDA	#(PRINT+O):H
	STA	1

UTAJ2	LDY	#0
	LDA	(0),Y
	EOR	#$55
	STA	(0),Y

	INC	0
	BNE	UTAJ3
	INC	1

UTAJ3	LDA	0
	CMP	#(UTAJ+O):L
	BNE	UTAJ2

	LDA	1
	CMP	#(UTAJ+O):H
	BNE	UTAJ2

	RTS

****

SAVEOD	EQU	$A803
SAVETO	EQU	UTAJ+O
