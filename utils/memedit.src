*************************************
*				*
*  " M E M E D I T "   FOR BW-DOS	*
*				*
*************************************

HEXL	EQU	$20
HEXH	EQU	$21


*Load address to end before $600
	ORG	$490

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<PBADVER
	LDY	#PEND-PBADVER+1
	JMP	PRTEND

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2
***

GETNAME	JSR	GETNAME

	LDY	#36
ST4	LDA	(10),Y
	STA	EDBUF-36,Y
	INY
	CPY	#41
	BCC	ST4

*Read address from command line
	JSR	GETHEX
	STY	POS
	STX	POS+1

*Edit loop
EDIT1	LDA	#$9B
	JSR	PRT1

*Show address and copy to store address
	LDA	POS+1
	STA	EDIT3A+2
	JSR	PRTHEX

	LDA	POS
	STA	EDIT3A+1
	JSR	PRTHEX

*": "
	LDA	#<PSEMI
	LDY	#2
	JSR	PRINT

**
POS	EQU	*+1
	LDA	$FFFF
	JSR	PRTHEX

*" =>"
EDIT1D	LDA	#<PARROW
	LDY	#3
	JSR	PRINT

	LDA	#$20
	LDX	#0

EDIT2	STX	EDIX

	JSR	PRT1
EDIT2A	JSR	GETKEY

**
EDIX	EQU	*+1
	LDX	#0

	CMP	#155	;RET
	BEQ	EDIT3
	CMP	#126	;BKSPC
	BEQ	ED2DEL
	CMP	#27	;ESC
	BEQ	EXIT

	CPX	#2
	BCS	EDIT2A

	AND	#127
	CMP	#$30
	BCC	EDIT2A
	CMP	#$3A
	BCC	EDIT2B

	AND	#$5F
	CMP	#$41
	BCC	EDIT2A
	CMP	#$47
	BCS	EDIT2A

EDIT2B	STA	EDBUF,X
	INX
	BNE	EDIT2

ED2DEL	DEX
	BMI	EDIT2A
	BPL	EDIT2
***

EDIT3	STA	EDBUF,X	;Store final EOL
	TXA
	BEQ	EDIT3B	;Skip store on empty buffer

	JSR	GETHEX
EDIT3A	STY	$FFFF

EDIT3B	INC	POS
	BNE	EDIT1
	INC	POS+1
EDIT4	JMP	EDIT1

***

GETHEX	LDY	#0
	STY	HEXL
	STY	HEXH

GETHX2	LDA	EDBUF,Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	CPY	#5
	BCC	GETHX2
**

GHXERR	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	LDY	#PBADVER-PBADPAR+1
	DTA	$2C	;Skip

EXIT	LDY	#1
	LDA	#<PBADPAR
PRTEND	JSR	PRINT
	JMP	(10)

***

GHXRTS	LDY	HEXL
	LDX	HEXH
	RTS

*Call K: get handler
GETKEY	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDX	#0
	STX	$2A	;Clears ICAX1Z
	RTS

***
PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6
***
PRT1	LDY	#0
***
PRINT	STA	$344
	STY	$348
PRT0	LDX	#0
	STX	$349
	LDY	#>PSEMI
	STY	$345
	LDY	#11
	STY	$342
	JMP	$E456
***

PSEMI	DTA	C":"	;Next SPC included
PARROW	DTA	C" =>"
PBADPAR	DTA	155
	DTA	C"Bad parameter!"
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
PEND	DTA	155
*Reuse error message as edit buffer
EDBUF	EQU	*-5

