	ORG	$500,$A800
	JMP	START

*************************************
*				*
*  " M E M E D I T "   PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

EDBUF	EQU	$6FB

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRTEOL	LDA	#$9B

**

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456

***

GETNAME	JMP	PRTEX

***

KNAME	ASC "K:"
	DFB 155
**

GETKEY	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#KNAME:L
	STA	$344,X
	LDA	#KNAME:H
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456

	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456
	BPL	GKEY2

	LDA	#'?'

GKEY2	PHA

	JSR	CLOSE

	PLA
	RTS

***

PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6
	JMP	PRT1
***

GETHEX	LDY	#0
	STY	HEXL
	STY	HEXH

GETHX2	CPY	#5
	BCS	GHXERR

	LDA	EDBUF,Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	PLA
	ORA	HEXL
	STA	HEXL

	JMP	GETHX2
**

GHXRTS	LDY	#0
	LDX	#0
**
HEXL	EQU	GHXRTS+1
HEXH	EQU	GHXRTS+3
**

	RTS

***

GHXERR	JSR	PRINT
	DFB 155
	ASC "Bad parameter !"
	DFB 155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSE
	JMP	(10)
***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS!"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***

	JSR	GETNAME

	LDY	#36
ST4	LDA	(10),Y
	STA	EDBUF-36,Y
	INY
	CPY	#41
	BCC	ST4

	JSR	GETHEX

	STY	POS
	STX	POS+1

*** EDIT LOOP

EDIT1	JSR	PRTEOL

	LDA	POS+1
	JSR	PRTHEX
	LDA	POS
	JSR	PRTHEX

	JSR	PRINT
	ASC ": "
	DFB 0
*

ED1PS	LDA	$FFFF
**
POS	EQU	ED1PS+1
**

	JSR	PRTHEX
***

EDIT1D	JSR	PRINT
	ASC " => "
	DFB 0

	LDA	#$20
	STA	EDBUF
	STA	EDBUF+1

	LDA	#0
	STA	EDIX

EDIT2	JSR	GETKEY

	CMP	#155
	BEQ	EDIT3
	CMP	#27
	BEQ	ED2ESC
	CMP	#126
	BEQ	ED2DEL

	AND	#127
	CMP	#$30
	BCC	EDIT2
	CMP	#$3A
	BCC	EDIT2B

	AND	#$5F
	CMP	#$41
	BCC	EDIT2
	CMP	#$47
	BCS	EDIT2
**

EDIT2B	LDX	#0
	CPX	#2
	BCS	EDIT2
**
EDIX	EQU	EDIT2B+1
**

	STA	EDBUF,X
	INC	EDIX

EDIT2C	JSR	PRT1
	JMP	EDIT2
**

ED2DEL	LDX	EDIX
	BEQ	EDIT2

	DEC	EDIX
	JMP	EDIT2C
**

ED2ESC	JSR	PRTEOL
	JMP	ERR2
***

EDIT3	LDX	EDIX
	BEQ	EDIT3B

	LDA	#$9B
	STA	EDBUF,X
**
	LDA	POS
	STA	EDIT3A+1
	LDA	POS+1
	STA	EDIT3A+2

	JSR	GETHEX

EDIT3A	STY	$FFFF

***

EDIT3B	INC	POS
	BNE	EDIT4
	INC	POS+1
**

EDIT4	JMP	EDIT1

****

