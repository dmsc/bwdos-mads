*************************************
*				*
*     " E L S E "  FOR BW-DOS	*
*				*
*************************************

DPTR	EQU	$80
***
	ORG	$3000

*Bad DOS version
START	LDA	#<PBADVER
	LDX	#PEBAT-PBADVER+1

	LDY	$700
	CPY	#'S'
	BNE	ERROR

***
	LDY	#8
	LDA	(10),Y
	STA	XDIVIO+1
	INY
	LDA	(10),Y
	STA	XDIVIO+2

	LDA	10
	STA	DPTR
	LDY	11
	DEY
	STY	DPTR+1
***

GETLINE	LDY	#256-7	;BATFLG
	LDA	(DPTR),Y
	BMI	GETLNX

	LDY	#5
	LDA	#<BUFFER
	LDX	#64
	JSR	CIOXY0
	BMI	GETLNX

	LDX	#PBADVER-PATRN

NO3	LDA	BUFFER,X
	CMP	PATRN,X
	BNE	GETLINE

	DEX
	BPL	NO3
	BMI	EXIT
***

*Get line error, end of batch file
GETLNX	LDA	#<PEBAT
	LDX	#PEND-PEBAT+1

*Print error and exit
ERROR	LDY	#11
	JSR	CIOXY0
	LDY	#1
XDIVIO	JSR	XRTS
EXIT	JMP	(10)

CIOXY0	STX	$348
	STA	$344
	STY	$342
	LDX	#>BUFFER
	STX	$345
	LDX	#0
	STX	$349
	JMP	$E456

***
XRTS	RTS
*Messages
PATRN	DTA	C"ENDIF"
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
PEBAT	DTA	155
	DTA	C"Error: End of batch file!"
PEND	DTA	155
BUFFER
