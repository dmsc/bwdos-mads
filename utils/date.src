	ORG	$6000
	JMP	START

*************************************
*				*
*	'DATE' PRO BW-DOS 1.00	*
*				*
*************************************

DECOUT	EQU	128
INDEX	EQU	130
TMP	EQU	131

***
	ICL	"print.inc"
***

PRTNM	LDA	(DECOUT),Y
	ORA	#$30
***
	ICL	"print1.inc"
***

START	LDA	$700
	CMP	#'S'
	BNE	STERR

	LDA	$703
	CMP	#'B'
	BNE	STERR

	LDA	$704
	CMP	#'W'
	BEQ	START2

STERR	JSR	PRINT
	DTA	155
	DTA	C"Error: Not BW-DOS"
	DTA	155,0

	JMP	(10)
***

CLOSES	LDA	#0
	STA	752

	LDX	#$10
	JSR	CLOSE
	LDX	#$20

CLOSE	LDA	#12
	STA	$342,X
	JMP	$E456

***

GETTD	JMP	($706)
SETTD	JMP	($708)
CONVDC	JMP	($70A)

VZOR	DTA	C"  -  -   "

MYTD	DTA	0,0,0

KNAME	DTA	C"K:"
	DTA	155

ETAB	DTA	0,1,3,4,6,7,8

***

START2	LDA	10
	SEC
	SBC	#19
	STA	DECOUT
	LDA	11
	SBC	#0
	STA	DECOUT+1

	JSR	GETTD

	JSR	PRINT
	DTA	155
	DTA	C"Current date is "
	DTA	0

	LDY	#13

STL1	TYA
	PHA

	LDA	(10),Y
	LDY	#13
	STA	(DECOUT),Y

	LDA	#0
	INY
	STA	(DECOUT),Y
	INY
	STA	(DECOUT),Y

	JSR	CONVDC

	LDY	#6
	JSR	PRTNM
	LDY	#7
	JSR	PRTNM

	PLA
	PHA
	CMP	#15
	BEQ	STL2

	LDA	#'-'
	JSR	PRT1

STL2	PLA
	TAY

	INY
	CPY	#16
	BCC	STL1
***

	JSR	PRINT
	DTA	155,0

	LDX	#8
	LDA	#32

STL3	LDA	VZOR,X
	STA	TEXT,X

	DEX
	BPL	STL3

	LDA	#0
	STA	INDEX
***

	JSR	CLOSES

	LDX	#$10
	LDA	#<KNAME
	STA	$344,X
	LDA	#>KNAME
	STA	$345,X
	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X
	LDA	#3
	STA	$342,X

	STA	752

	JSR	$E456

	LDX	#15
STL4	LDA	$340,X
	STA	$360,X
	DEX
	BPL	STL4
***

EDLOOP	LDX	INDEX
	LDY	ETAB,X
	LDA	#160
	STA	TEXT,Y

	LDX	#$20
	LDA	#<TXT0
	STA	$344,X
	LDA	#>TXT0
	STA	$345,X
	LDA	#26
	STA	$348,X
	LDA	#0
	STA	$349,X
	LDA	#11
	STA	$342,X
	JSR	$E456

	LDA	#$1C
	JSR	PRT1

**

EDL2	LDX	#$10
	LDA	#0
	STA	$348,X
	STA	$349,X
	LDA	#7
	STA	$342,X

	JSR	$E456
	BMI	EDL2

	CMP	#27
	BEQ	EDESC
	CMP	#155
	BEQ	EDRET
	CMP	#126
	BEQ	EDDEL
*ZNAK
	CMP	#$30
	BCC	EDL2
	CMP	#$3A
	BCS	EDL2

	LDX	INDEX
	CPX	#6
	BCS	EDL2

	LDY	ETAB,X
	STA	TEXT,Y

	INC	INDEX
	JMP	EDLOOP
***

EDDEL	LDX	INDEX
	BEQ	EDL2

	LDY	ETAB,X
	LDA	#32
	STA	TEXT,Y

	DEC	INDEX
	JMP	EDLOOP
***

EDESC	JSR	CLOSES

	JSR	PRINT
	DTA	155
	DTA	C"Aborted."
	DTA	155,0

	JMP	(10)
***

EDRET	JSR	CLOSES

	LDA	INDEX
	BEQ	NOTCHG
	CMP	#6
	BNE	INVALID

	LDA	#32
	STA	TEXT+8

	LDA	#0
	STA	INDEX

	JSR	EDGET
	CMP	#32
	BCS	INVALID
	TAX
	BEQ	INVALID
	STA	MYTD

	JSR	EDGET
	CMP	#13
	BCS	INVALID
	TAX
	BEQ	INVALID
	STA	MYTD+1

	JSR	EDGET
	STA	MYTD+2
*
	JSR	GETTD

	LDY	#13

EDRET2	LDA	MYTD-13,Y
	STA	(10),Y

	INY
	CPY	#16
	BCC	EDRET2

	JSR	SETTD

	JSR	PRINT

TXT0	DTA	C"Enter new date: "
TEXT	DTA	C"  -  -   "
	DTA	155,0

	JMP	(10)
***

NOTCHG	JSR	PRINT
	DTA	C"(Date not changed)"
	DTA	C"         "
	DTA	155,0
	JMP	(10)
***

INVALID	JSR	PRINT
	DTA	155
	DTA	C"Invalid Date!"
	DTA	155,0

	JMP	(10)
***

EDGTB	DTA	0,10,20,30,40
	DTA	50,60,70,80,90
***

EDGET	JSR	EDGT2
	TAX
	LDA	EDGTB,X
	STA	TMP

	JSR	EDGT2
	CLC
	ADC	TMP
	PHA

	JSR	EDGT2

	PLA
	RTS
***

EDGT2	LDX	INDEX
	INC	INDEX

	LDA	TEXT,X
	AND	#15

	RTS
***

