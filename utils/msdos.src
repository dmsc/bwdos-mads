	ORG	$2500,$A800
	JMP	PSTART
	OUT	N
*************************************
*				*
*     Micro-SpartaDOS Ver. 2.3	*
*				*
*************************************
*				*
*	! ! !  POZOR  ! ! !	*
*				*
*  'FILTAB' nesmi prekrocit $2E00,	*
*  jinak to Boot-loader NEZAVEDE !	*
*				*
*************************************

*DEFINICE

ZPP	EPZ	200
DIRBGN	EPZ	202
FILBGN	EPZ	204
FILEND	EPZ	206

LOADAD	EPZ	208
LOADSEC	EPZ	210
LOADLN	EPZ	212

MIX	EPZ	214

ALLF	EPZ	215
FILEF	EPZ	216
FIX	EPZ	217

BLRA	EPZ	218
BLRB	EPZ	219

PRZ2	EPZ	222
PRZ3	EPZ	224

PAGED	EPZ	226
SIORP	EPZ	227

** VOLNO: 227

****** LOADER

LEN	DFB	0,0

**
O	EQU	LEN-$700
**

STRT	TXA
	PHA
	LDA	LENA
	PHA
	JSR	INIT-O
	PLA
	STA	LENA
	PLA
	TAX

LOOP	LDA	LENSEC-O
	STA	LENSECZ

LP1	JSR	GET-O
	STA	L43
	JSR	GET-O
	STA	L44

	AND	L43
	CMP	#$FF
	BEQ	LP1

	JSR	GET-O
	SBC	L43
	EOR	#$FF
	STA	L45
	PHP
	JSR	GET-O
	PLP
	SBC	L44
	EOR	#$FF
	STA	L46

* TADY Z TOHO BCS UDELA INICIALIZACE
* LDA(...),Y - ZPET TO VRATI TEN DEC

	SEC
LCHG	BCS	L25
	DEC	LCHG-O
*FIRST
	LDA	L43
	STA	$2E0
	LDA	L44
	STA	$2E1

L25	LDA	#RET-O:L
	STA	$2E2
	LDA	#RET-O:H
	STA	$2E3

	LDY	#0

L3	INC	LENA
	BEQ	L3GET0
*
	CPX	LENSECZ
	BEQ	L3GLD

	LDA	DBUFQ,X
	INX

L3B	STA	(L43),Y

	INY
	BNE	L4
	INC	L44

L4	INC	L45
	BNE	L3

	INC	L46
	BNE	L3
	BEQ	STRT
*
L3GET0	JSR	GET0-O
	BCS	L3B

L3GLD	JSR	GLD-O
	BCS	L3B
**
INIT	JMP	($2E2)
*
ERR	JMP	$E471
*
MAPALD	LDX	#MBUFQ:H

*PRESKOC TU DALSI INSTRUKCI (BUDE
*Z TOHO BIT...

	DFB	$2C

DATALD	LDX	#DBUFQ:H

LD2	STX	SAD+1-O
	STY	$30A
	STA	$30B

	ORA	$30A
	BEQ	ERR

LD3	LDX	#9
LD4	LDA	STB-O,X
	STA	$300,X
	DEX
	BPL	LD4

LDSIO	JSR	$E459
	BMI	LD3

	RTS
*
STB	DFB	$31
	DFB	1
	DFB	'R'
	DFB	64
SAD	DFW	DBUFQ
	DFB	10
	DFB	0
LENSEC	DFW	128
*
GET0	INC	LEN-O
	BNE	GET2
	INC	LEN+1-O
	BNE	GET2

RUN	LDA	#$E4
	PHA
	LDA	#$70
	PHA

	JMP	($2E0)
**
*
GET	INC	LENA
	BEQ	GET0
*
GET2	CPX	LENSECZ
	BEQ	GLD
GET44	LDA	DBUFQ,X
	INX

	SEC
RET	RTS

GLD	TYA
	PHA

GL2	LDX	MAPIND
	CPX	LENSECZ
	BNE	GL2B

* LOAD MAP

	LDY	MBUFQ
	LDA	MBUFQ+1

	JSR	MAPALD-O

	LDX	#4

GL2B	LDA	MBUFQ,X
	TAY
	LDA	MBUFQ+1,X

	INX
	INX
	STX	MAPIND

NOCLR	JSR	DATALD-O

	PLA
	TAY
	LDX	#0
	BEQ	GET44

***

L43	EPZ	$24
L44	EPZ	$25
L45	EPZ	$26
L46	EPZ	$27
LENSECZ	EPZ	$28
LENA	EPZ	$29

*
DBUFQ	EQU	$800
MBUFQ	EQU	$900

MAPIND	EQU	MBUFQ+2

*** INICIALIZACE LOADERU - BUDE PAK
*** PREMAZANO DATOVYM SEKTOREM

MAPSE	DFW	0

START	LDY	743
	LDA	744
	STA	SL+2-O

SLL	LDA	#0
SL	STA	$900,Y
	INY
	BNE	SL
	INC	SL+2-O
	LDA	SL+2-O
	CMP	742
	BCC	SLL

	LDA	742
	STA	SLSL+2-O
	LDY	741
	LDA	#0
SLSL	STA	$8000,Y
	DEY
	CPY	#255
	BNE	SLSL
*
	LDA	MAPSE+1-O
	LDY	MAPSE-O
	JSR	MAPALD-O
	LDA	#4
	STA	MAPIND

	LDA	LENAB-O
	STA	LENA

	LDA	#$FF
	STA	764

	INC	LCHG-O

	LDX	LENSEC-O
	JMP	LOOP-O

LENAB	DFB	0

XFFLG	DFB	0
HSFLG	DFB	0

*
*************************************
*
XFTT	DFB	$31
	DFB	1
	DFB	'N'
	DFB	64
	DFW	BOOBUF
	DFB	7,0
	DFW	12
	DFW	1
*
***
*
TOHEX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	TOHX3

	CLC
	ADC	#7

TOHX3	RTS
*
****
*
PNM	ASC	"E:"
	DFB	$9B
*	
PSTART	LDX	#0
	JSR	CLOSE
	BMI	PERR

	LDX	#0
	LDA	#3
	STA	$342,X
	LDA	#12
	STA	$34A,X
	TXA
	STA	$34B,X
	LDA	#PNM:L
	STA	$344,X
	LDA	#PNM:H
	STA	$345,X
	JSR	$E456
	BMI	PERR

	LDA	#$C4
	STA	710
	STA	712

** XF?
	LDA	#1
	STA	XFFLG
	STA	HSFLG

	LDA	$D20F
	AND	#8
	BNE	XFT0
*A JE 0
	STA	HSFLG
	STA	XFFLG
	BEQ	MAINDIR
*
XFT0	LDY	#XFTT:L
	LDX	#XFTT:H
	JSR	SETSIO

	JSR	SIO
	BPL	MAINDIR

	LDA	#0
	STA	XFFLG
	BEQ	MAINDIR

*****

PERR148	LDY	#148

PERR	TYA
	PHA

	JSR	CLOSE1

	PLA
	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	TOHEX2
	STA	ERRT2
	PLA
	JSR	TOHEX2
	STA	ERRT2+1

	JSR	PRINT
	DFB	0,0,125
	ASC	"Error $"
ERRT2	ASC	"00"
	DFB	0

	LDA	#255
	STA	764

ERRT3	CMP	764
	BEQ	ERRT3

	STA	764

	JMP	PSTART
****
*

MAINDIR	LDX	#BOOBUF:H
	LDY	#BOOBUF:L
	JSR	GETBOOT

	LDA	BOOBUF+7
	CMP	#$80
	BNE	PERR148
*
	LDA	BOOBUF+31
	STA	LENSEC
	ASL
	BNE	PERR148
	ROL
	EOR	#1
	STA	LENSEC+1

	LDY	BOOBUF+9
	LDX	BOOBUF+10
***

NEWDIR	STY	LOADSEC
	STX	LOADSEC+1

	LDA	#MEMSTRT:H
	STA	LOADAD+1
	STA	DIRBGN+1
	LDA	#MEMSTRT:L
	STA	LOADAD
	STA	DIRBGN

	LDA	#0
	STA	LOADLN
	STA	LOADLN+1

	LDA	#23
	JSR	BLOAD
**
	LDA	LOADAD
	STA	FILBGN
	LDA	LOADAD+1
	STA	FILBGN+1

	LDA	#0
	STA	ALLF
	STA	FILEF
**
* HLEDEJ MSDOS.DAT
**
	LDA	DIRBGN
	STA	LOADAD
	LDA	DIRBGN+1
	STA	LOADAD+1
*
ND2	LDA	LOADAD
	CMP	FILBGN
	LDA	LOADAD+1
	SBC	FILBGN+1
	BCS	ND5

ND3	LDY	#0
	LDA	(LOADAD),Y
	AND	#$38
	CMP	#8
	BNE	ND3C

	LDY	#16
	LDX	#10

ND3B	LDA	(LOADAD),Y
	CMP	DATNAM,X
	BNE	ND3C
	DEY
	DEX
	BPL	ND3B
	BMI	ND3D
*
DATNAM	ASC	"MSDOS   DAT"
*
ND3C	LDA	LOADAD
	CLC
	ADC	#23
	STA	LOADAD
	BCC	ND3CB
	INC	LOADAD+1
ND3CB	JMP	ND2

**
* LOAD MSDOS.DAT
**
ND3D	LDY	#1
	LDA	(LOADAD),Y
	STA	LOADSEC
	INY
	LDA	(LOADAD),Y
	STA	LOADSEC+1
*
	INY
	LDA	(LOADAD),Y
	STA	LOADLN
	INY
	LDA	(LOADAD),Y
	STA	LOADLN+1
	INY
	LDA	(LOADAD),Y
	BEQ	ND3DB
	LDA	#$FF
	STA	LOADLN
	STA	LOADLN+1
*
ND3DB	LDA	FILBGN
	STA	LOADAD
	LDA	FILBGN+1
	STA	LOADAD+1

	LDA	#46
	JSR	BLOAD

	LDA	LOADAD
	STA	FILEND
	LDA	LOADAD+1
	STA	FILEND+1

	INC	FILEF
***

ND5	LDA	#0
	STA	MIX
	STA	PAGED

	LDA	DIRBGN
	CLC
	ADC	#23
	STA	LOADAD
	LDA	DIRBGN+1
	ADC	#0
	STA	LOADAD+1

ND4B	JSR	PRINT
	DFB	0,0,125
*
	ASC / Micro-Spa/
	ASC /rtaDOS 2.3/
	ASC /  by  BEWE/
	ASC /SOFT 1994 /
	DFB	0
*
	LDA	XFFLG
	ORA	HSFLG
	BEQ	ND4C

	JSR	PRINT
	DFB	1,22
	ASC "(Add "
	ASC /SHIFT/
	ASC " to disable High-"
	ASC "speed SIO!)"
	DFB	0
*
ND4C	JSR	PRINT
	DFB	2,23
	ASC /ESC/
	ASC ":All files  "
	ASC />/
	ASC ":Main Dir.  "
	ASC /</
	ASC ":UP-DIR."
	DFB	0
******
* ZOBRAZENI SOUBORU
******
*

	LDA	#0
	STA	FIX

ND6	LDA	LOADAD
	CMP	FILBGN
	LDA	LOADAD+1
	SBC	FILBGN+1
	BCS	NDENDJ
*
	LDY	#0
	LDA	(LOADAD),Y
	BEQ	NDENDJ
*
	LDA	FIX
	CMP	#18
	BCS	NDCKJ
*
	LDX	#34
	LDA	#$20
ND6BB	STA	NDF+3,X
	DEX
	BPL	ND6BB
*
	LDY	#16
	LDX	#10
ND6C	LDA	(LOADAD),Y
	STA	NDF+3,X
	DEY
	DEX
	BPL	ND6C
*
	LDA	FIX
	CLC
	ADC	#'A'
	STA	NDF
*
	LDA	FILEF
	BNE	ND6FIL
*
	LDY	#0
	LDA	(LOADAD),Y
	AND	#$19

	CMP	#9
	BEQ	ND6D

	LDX	ALLF
	BEQ	NDNXTJ

	CMP	#8
	BNE	NDNXTJ
*
ND6D	LDA	(LOADAD),Y
	AND	#32
	BEQ	ND6E
*
	LDX	#8
ND6DB	LDA	NDSDT,X
	STA	NDF+15,X
	DEX
	BPL	ND6DB
*
ND6E	JMP	ND7

NDENDJ	JMP	NDEND
NDNXTJ	JMP	NDNXT
NDCKJ	JMP	NDCK
*
* HLEDEJ V SOUBORU
*
ND6FIL	LDY	#0
	LDA	(LOADAD),Y
	AND	#$18
	CMP	#8
	BNE	NDNXTJ

	LDA	FILBGN
	STA	LOADLN
	LDA	FILBGN+1
	STA	LOADLN+1
*
ND6F	LDA	LOADLN+1
	CMP	FILEND+1
	BCC	ND6G
	BNE	NDNXTJ
	LDA	LOADLN
	CMP	FILEND
	BCS	NDNXTJ
*
ND6G	LDY	#10
ND6H	LDA	(LOADLN),Y
	CMP	NDF+3,Y
	BNE	ND6I
	DEY
	BPL	ND6H
*
	LDY	#11
ND6HB	LDA	(LOADLN),Y
	STA	NDF-8,Y
	INY
	CPY	#46
	BCC	ND6HB
	BCS	ND7
*
ND6I	LDA	LOADLN
	CLC
	ADC	#46
	STA	LOADLN
	BCC	ND6IB
	INC	LOADLN+1

ND6IB	JMP	ND6F
*
*****
*
ND7	LDA	FIX
	CLC
	ADC	#2
	STA	ND7Y

	JSR	PRINT
	DFB	1
ND7Y	DFB	2
NDF	ASC "A)           "
	ASC "             "
	ASC "            "
	DFB	0

	LDA	FIX
	ASL
	TAX
	LDA	LOADAD
	STA	FILTAB,X
	LDA	LOADAD+1
	STA	FILTAB+1,X

	LDA	LOADAD
	CLC
	ADC	#23
	STA	LOADAD
	BCC	NDFB
	INC	LOADAD+1

NDFB	INC	FIX
	JMP	ND6
*
NDNXT	LDA	LOADAD
	CLC
	ADC	#23
	STA	LOADAD
	BCC	NDNB
	INC	LOADAD+1

NDNB	JMP	ND6

***

NDEND	LDA	PAGED
	BEQ	NDEND2

	LDA	FIX
	CLC
	ADC	#2
	STA	NDEND1X

	JSR	PRINT
	DFB 2
NDEND1X	DFB 10
	ASC "(SPACE to return at"
	ASC " the begin)
	DFB 0

NDEND2	INC	MIX
NDEND3	JMP	CEKEJ

***

NDCK	JSR	PRINT
	DFB 2,20
	ASC "(SPACE for more files)"
	DFB 0

	LDA	#1
	STA	PAGED
	BNE	NDEND3
***

UPDIR	LDY	#2
	LDA	(DIRBGN),Y
	TAX
	DEY
	ORA	(DIRBGN),Y
	BEQ	CEKEJ
	LDA	(DIRBGN),Y
	TAY
	JMP	NEWDIR
***

ESCCE	LDX	#0
	STX	FILEF
	INX
	STX	ALLF
CEKZNV	JMP	ND5
*
CEKSPC	LDA	MIX
	BNE	CEKZNV

	JMP	ND4B
***

MAINJ	JMP	MAINDIR

***

CEKEJ	JSR	GETK
	PHA
**
	LDA	$D20F
	AND	#8
	BNE	CEK2
*A=0
	STA	XFFLG
	STA	HSFLG
**
CEK2	PLA

	CMP	#'>'
	BEQ	MAINJ
	CMP	#'<'
	BEQ	UPDIR

	CMP	#27
	BEQ	ESCCE

	CMP	#32
	BEQ	CEKSPC

	AND	#$5F
	SEC
	SBC	#'A'
	CMP	FIX
	BCS	CEKEJ

* VYBRANO

	ASL
	TAX
	LDA	FILTAB,X
	STA	LOADLN
	LDA	FILTAB+1,X
	STA	LOADLN+1

	LDY	#0
	LDA	(LOADLN),Y
	AND	#32
	BEQ	STARTUJ

	LDY	#2
	LDA	(LOADLN),Y
	TAX
	DEY
	LDA	(LOADLN),Y
	TAY
	JMP	NEWDIR
**
*
NDSDT	ASC "<SUB-DIR>"
*
*
STARTUJ	JSR	TSTBOOT
	BEQ	STR2
	JMP	MAINDIR

*

STR2	LDY	#1
	LDA	(LOADLN),Y
	STA	MAPSE
	INY
	LDA	(LOADLN),Y
	STA	MAPSE+1

	INY
	LDA	(LOADLN),Y
	EOR	#$FF
	STA	LENAB

	INY
	LDA	(LOADLN),Y
	EOR	#$FF
	STA	LEN

	INY
	LDA	(LOADLN),Y
	EOR	#$FF
	STA	LEN+1
*
	LDA	#0
	STA	580

	LDA	#2
	STA	9
	STA	$3F8

	LDA	#REC:L
	STA	2
	LDA	#REC:H
	STA	3
	LDA	#$E4
	STA	11
	STA	13
	LDA	#$77
	STA	10
	STA	12
	JMP	$E474
*
REC	LDA	#0
	STA	743
	STA	9
	STA	14

	LDA	#$0A
	STA	744
	STA	15

	LDA	#$77
	STA	2
	LDA	#$E4
	STA	3

	INC	$33D
	INC	$33E
	DEC	$33F

	LDX	#0
REC2	LDA	LEN,X
	STA	$700,X
	LDA	LEN+256,X
	STA	$800,X
	INX
	BNE	REC2

	LDX	#0
	TXA
REC3	STA	$100,X
	STA	$400,X
	STA	$500,X
	STA	$600,X
	CPX	#128
	BCC	REC4
	STA	0,X
REC4	INX
	BNE	REC3

	LDX	#$FF
	TXS

	JSR	GETHS

	JMP	START-O

****

GETHS	LDA	XFFLG
	BEQ	GHS2

	LDA	$20A
	STA	XFSJ+1
	LDA	$20B
	STA	XFSJ+2

	LDX	#XFSL-1
GHS1	LDA	XFS,X
	STA	$A00,X
	DEX
	BPL	GHS1

	LDY	#XFSL
	LDX	#0
	BEQ	GHS3
*
GHS2	LDA	HSFLG
	BEQ	GHSX
*
	LDY	#HST1:L
	LDX	#HST1:H
	JSR	SETSIO

	JSR	$E459
	BMI	GHSX

	LDY	#HST2:L
	LDX	#HST2:H
	JSR	SETSIO

	JSR	$E459
	BMI	GHSX

	LDY	HSLEN
	LDX	HSLEN+1
*
GHS3	TYA
	CLC
	ADC	743
	STA	743

	TXA
	ADC	744
	STA	744
*
	LDA	#0
	STA	LDSIO-O+1
	LDA	#10
	STA	LDSIO-O+2
*
GHSX	RTS

***

XFS	LDA	$20A
	PHA
	LDA	$20B
	PHA

	ASL	$302
	SEC
	ROR	$302

	LDA	#(XFSI-XFO):L
	STA	$20A
	LDA	#(XFSI-XFO):H
	STA	$20B

	JSR	$E459

	PLA
	STA	$20B
	PLA
	STA	$20A

	TYA
	RTS
*

XFSI	LDA	#$10
	STA	$D204
XFSJ	JMP	$FFFF

*

XFSE	EQU	*
XFSL	EQU	XFSE-XFS

XFO	EQU	XFS-$A00

***

HST1	DFB	$31
	DFB	1
	DFB	$68
	DFB	64
	DFW	HSLEN
	DFB	7,0
	DFW	2
	DFW	$A00
*
HST2	DFB	$31
	DFB	1
	DFB	$69
	DFB	64
	DFW	$A00
	DFB	7,0
HSLEN	DFW	1
	DFW	$A00
***

BLDENDJ	JMP	BLDEND
***
BLOAD	STA	BLRA
	LDA	LOADAD
	STA	BLRB
	LDA	LOADAD+1
	STA	BLRB+1

	JSR	TSTBOOT
	BEQ	BLDGM

	PLA
	PLA
	JMP	MAINDIR
*
BLDGM	LDA	LOADSEC
	STA	SIOSEC
	LDA	LOADSEC+1
	STA	SIOSEC+1
	ORA	SIOSEC
	BEQ	BLDENDJ

	LDX	#BUFFM:H
	LDY	#BUFFM:L
	JSR	GETSEC

	LDA	BUFFM
	STA	LOADSEC
	LDA	BUFFM+1
	STA	LOADSEC+1

	LDA	#4
	STA	MIX
*
BLDGD	LDX	MIX
	CPX	LENSEC
	BEQ	BLDGM

	LDA	BUFFM,X
	STA	SIOSEC
	LDA	BUFFM+1,X
	STA	SIOSEC+1
	ORA	SIOSEC
	BEQ	BLDEND

	INX
	INX
	STX	MIX
*
	LDA	741
	SEC
	SBC	LOADAD
	LDA	742
	SBC	LOADAD+1
	BEQ	BLDEND

	LDY	LOADAD
	LDX	LOADAD+1
	JSR	GETSEC
*
	LDA	LOADLN
	ORA	LOADLN+1
	BNE	BLDGD2
*DIR
	LDY	#3
	LDA	(LOADAD),Y
	STA	LOADLN
	INY
	LDA	(LOADAD),Y
	STA	LOADLN+1
	INY
	LDA	(LOADAD),Y
	BEQ	BLDGD2
	LDA	#$FF
	STA	LOADLN
	STA	LOADLN+1
*
BLDGD2	LDA	LOADAD
	CLC
	ADC	LENSEC
	STA	LOADAD
	LDA	LOADAD+1
	ADC	LENSEC+1
	STA	LOADAD+1
*
	LDA	LOADLN
	SEC
	SBC	LENSEC
	STA	LOADLN
	LDA	LOADLN+1
	SBC	LENSEC+1
	STA	LOADLN+1
*
	BCS	BLDGD

	LDA	LOADAD
	CLC
	ADC	LOADLN
	STA	LOADAD
	LDA	LOADAD+1
	ADC	LOADLN+1
	STA	LOADAD+1
*
BLDEND	LDA	BLRB+1
	CMP	LOADAD+1
	BCC	BLDE2
	BNE	BLDE3

	LDA	BLRB
	CMP	LOADAD
	BCC	BLDE2
	BNE	BLDE3
	RTS
*
BLDE2	LDA	BLRB
	CLC
	ADC	BLRA
	STA	BLRB
	BCC	BLDEND
	INC	BLRB+1
	JMP	BLDEND
*
BLDE3	LDA	BLRB
	SEC
	SBC	BLRA
	STA	LOADAD

	LDA	BLRB+1
	SBC	#0
	STA	LOADAD+1

	RTS
*
****
GETBOOT	LDA	#1
	STA	SIOSEC
	LSR
*A=0
	STA	SIOSEC+1
	ROR
*A=128
	BNE	GETS2

**
GETSEC	LDA	LENSEC

GETS2	AND	#128
	STA	SIOLEN
	ASL
	ROL
	EOR	#1
	STA	SIOLEN+1

	STX	SIOAD+1
	STY	SIOAD

	LDA	#4
	STA	SIORP
*
GETS4	LDY	#SIOT:L
	LDX	#SIOT:H
	JSR	SETSIO

	JSR	SIO
	BMI	GETSE
	RTS
*
GETSE	DEC	SIORP
	BNE	GETS4

	PLA
	PLA
	JMP	PERR
*
SIOT	DFB	$31
	DFB	1
	DFB	'R'
	DFB	64
SIOAD	DFW	BUFFM
	DFB	10,0
SIOLEN	DFW	128
SIOSEC	DFW	1

*
***
*
PRINT	PLA
	STA	ZPP
	PLA
	STA	ZPP+1

	LDA	#0
	STA	PRZ2+1
*
	JSR	PRGET
	PHA

	JSR	PRGET
	STA	PRZ2
*40
	ASL
	ASL
	CLC
	ADC	PRZ2
	ASL
	ASL
	ROL	PRZ2+1
	ASL
	ROL	PRZ2+1

	CLC
	ADC	88
	STA	PRZ2

	LDA	PRZ2+1
	ADC	89
	STA	PRZ2+1

	PLA
	TAY

PRL	JSR	PRGET
	CMP	#0
	BEQ	PRRT

	CMP	#125
	BEQ	PRLCLS

	ASL
	PHP

	CMP	#64
	BCS	PRL2
	ORA	#128
	BNE	PRL3

PRL2	CMP	#192
	BCS	PRL3

	SEC
	SBC	#64

PRL3	PLP
	ROR

	STA	(PRZ2),Y
	INY
	JMP	PRL
**

PRLCLS	TYA
	PHA

	LDA	88
	STA	PRZ3

	LDA	#3
	TAX

	CLC
	ADC	89
	STA	PRZ3+1

	LDY	#$C0

PCLS2	DEY
	LDA	#0
	STA	(PRZ3),Y
	TYA
	BNE	PCLS2

	DEC	PRZ3+1
	DEX
	BPL	PCLS2

	PLA
	TAY
	JMP	PRL
**

PRRT	LDA	ZPP+1
	PHA
	LDA	ZPP
	PHA
	RTS
*

PRGET	INC	ZPP
	BNE	PRGT2
	INC	ZPP+1
PRGT2	LDX	#0
	LDA	(ZPP,X)
PRRTS	RTS

KERR	JMP	PERR
***

SIO	LDA	XFFLG
	BNE	SIO2
	JMP	$E459
*
SIO2	LDA	$20A
	STA	XFSJ+1
	PHA
	LDA	$20B
	STA	XFSJ+2
	PHA

	LDA	16
	PHA
	AND	#128
	STA	16
	STA	$D20E

	ASL	$302
	SEC
	ROR	$302

	LDA	#XFSI:L
	STA	$20A
	LDA	#XFSI:H
	STA	$20B

	JSR	$E459

	PLA
	STA	16
	STA	$D20E

	PLA
	STA	$20B
	PLA
	STA	$20A

	TYA
	RTS

****

SETSIO	STY	SSL+1
	STX	SSL+2

	LDX	#11
SSL	LDA	$FFFF,X
	STA	$300,X
	DEX
	BPL	SSL

	RTS

***
CLOSE1	LDX	#$10

CLOSE	LDA	#12
	STA	$342,X
	JMP	$E456
*
GETK	LDX	#$10
	LDA	#3
	STA	$342,X
	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X
	LDA	#KNM:L
	STA	$344,X
	LDA	#KNM:H
	STA	$345,X
	JSR	$E456
	BMI	KERRJ
*
	LDX	#$10
	LDA	#0
	STA	$348,X
	STA	$349,X
	LDA	#7
	STA	$342,X
	JSR	$E456
	BMI	KERRJ

	PHA
*
	JSR	CLOSE1
	BMI	KERRJ

	PLA
	RTS
*
KERRJ	JMP	KERR
*

KNM	ASC	"K:"
	DFB	$9B
***

TSTBOOT	LDY	#BUFFM:L
	LDX	#BUFFM:H
	JSR	GETBOOT

	LDX	#128

TBT2	DEX
	LDA	BOOBUF,X
	CMP	BUFFM,X
	BNE	TBTE

	TXA
	BNE	TBT2

TBTE	RTS

*****

FILTAB	EQU	TBTE+1

BOOBUF	EQU	FILTAB+38
BUFFM	EQU	BOOBUF+128
MEMSTRT	EQU	BUFFM+256

*****
