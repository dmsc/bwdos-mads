*************************************
*				*
*   " R T I M E 8 "  FOR BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128
RELZP	EQU	130
REDFLG	EQU	132
TCNT	EQU	134
TTMP	EQU	135
RT8TP	EQU	138

***

VCOUNT	EQU	$D40B
WSYNC	EQU	$D40A

*** PRO RESIDENT

PORT	EQU	$D5B8
CNT	EQU	$31	;SIO CHECK
TMP	EQU	$30	;SIO STATUS
TMP2	EQU	$32	;SIO ADR.
CNT2	EQU	$33	;---"---

***

	ORG	$3000

***
	ICL	"chkbw.inc"
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	ST2ON
*OFF?
	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	SYNTAX

	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	LDX	#0

	CMP	#'/'
	BNE	ST2O2

	INX

	INY
	LDA	(10),Y
	CMP	#'R'
	BNE	SYNTAX

	INY
	LDA	(10),Y

ST2O2	CMP	#$9B
	BEQ	START3

***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: RTIME8 [/R]"
	DTA	155
	DTA	C"        RTIME8 OFF"
	DTA	155,0

	BEQ	JDOS

*Install
START3	STX	REDFLG

	JSR	RT8TEST
	BCS	START4

	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 cartridge not installed!"
	DTA	155,0
	BEQ	JDOS

*Already installed?
START4	JSR	SRCHRUT
	BCC	START4A
*Yes!
	JSR	PRINT
	DTA	C"already installed!"
	DTA	155,0
JDOS	JMP	(10)


START4A	LDX	REDFLG

	LDA	743
	STA	SRCHADR
	CLC
	ADC	RESLENT,X
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1

*Relocate
RLK0	LDX	#0

RLK1	LDY	RELOKT,X
	BEQ	INSTAL2

	LDA	RELOKT+1,X
	CLC
	ADC	SRCHADR
	STA	RESID+1,Y

	LDA	#0
	ADC	SRCHADR+1
	STA	RESID+2,Y

	INX
	INX
	BNE	RLK1
***

INSTAL2	LDA	10
	CLC
	ADC	#13
	TAY
	LDA	11
	ADC	#0
	TAX

	STY	XTRA2+1
	STX	XTRA2+2

	INY
	BNE	INST2XA
	INX

INST2XA	STY	XTRA3+1
	STX	XTRA3+2

	INY
	BNE	INST2XB
	INX

INST2XB	STY	XTRA1+1
	STX	XTRA1+2

*** FLAG REZIMU

	LDX	REDFLG
	DEX
	STX	WAIT+1
**

	LDX	#3

INST2B	LDA	$706,X
	STA	OLDADR,X
	DEX
	BPL	INST2B
**
	LDX	REDFLG
	LDY	RESLENT,X

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDX	#1
	LDY	#3

	LDA	REDFLG
	BEQ	INSTL4
	LDY	#5

INSTL4	LDA	NEWADR,X
	STA	$706,X
	LDA	NEWADR,Y
	STA	$708,X
	DEY
	DEX
	BPL	INSTL4
**

	JSR	PRINT
	DTA	C"Installed."
	DTA	155,0

	LDA	REDFLG
	BEQ	JDOS2

	JSR	PRINT
	DTA	C"(Reduced version - set time/date is"
	DTA	155
	DTA	C"not supported)"
	DTA	155,0
JDOS2	BEQ	JDOS1

***

DISABLE	JSR	SRCHRUT
	STA	REDFLG
	BCS	DISABLE2

	JSR	PRINT
	DTA	C"not Installed!"
	DTA	155,0
	BEQ	DISERR2
***

DISCANT	JSR	PRINT
	DTA	C"is not the last"
	DTA	155
	DTA	C"handler installed!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

JDOS1	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	TYA
	LDX	REDFLG
	CMP	RESLENT,X
	BNE	DISCANT
*ODSTRAN

	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
*

	LDY	#(OLDADR-RESID)
	LDX	#0

DISABL3	LDA	(SRCHADR),Y
	STA	$706,X

	INY
	INX
	CPX	#4
	BCC	DISABL3
**

	JSR	PRINT
	DTA	C"Removed."
	DTA	155,0
	BEQ	JDOS1

****

SRCHRUT	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 driver "
	DTA	0

	LDA	743
	SEC
	SBC	RESLENT+1
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDX	#2
SRCHR3	LDY	SRCHTAB1,X

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	TYA
	CMP	SRCHTAB2,X
	BNE	SRCHR2

	DEX
	BPL	SRCHR3

*Check if REDUCED or not
	LDY	#WAIT+1-RESID
	LDA	(SRCHADR),Y
	CLC
	ADC	#1

	SEC
	RTS

SRCHTAB1	DTA	S1-RESID,GTD2-RESID,WT3-RESID
SRCHTAB2	DTA	TST1-RESID,RDR7-RESID,TST2-RESID

**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$19
	BCS	SRCHR1

	RTS

*Check if RTIME8 is present
RT8TL	DTA	0,0,0,1,1,0,1
RT8TH	DTA	60,60,24,32,13,100,8
*
RT8TEST	LDA	#6
	STA	RT8TP

RT8T2	LDX	RT8TP
	JSR	TRDREG

	LDX	RT8TP
	CMP	RT8TL,X
	BCC	RT8TER
	CMP	RT8TH,X
	BCS	RT8TER

	DEC	RT8TP
	BPL	RT8T2

	SEC
	RTS
**

RT8TER	CLC
	RTS
***

*Read one register, twice to detect changes.
TRDREG	SEC
	ROR	TCNT

TRDR2	DEC	TCNT
	BEQ	TRDRET

	JSR	TRDR4
	STA	TTMP
	JSR	TRDR4
	CMP	TTMP
	BNE	TRDR2

TRDRET	RTS

*Read register X from M-3002
TRDR4	JSR	TWAIT

	STX	PORT

*Read TENS
	JSR	TGET15
	TAY
*Read ONES
	JSR	TGET15
*Adds TENS by 10
TRDR7	DEY
	BMI	TRDRET

	CLC
	ADC	#10
	BCC	TRDR7
**

*Synchronize read:
*Read up to 256 times for a '0' in the bus
TWAIT	LDY	#0

TWT2	DEY
	BEQ	TWT3

	JSR	TGET15
	BNE	TWT2
*Omit the RTS, we can read one more time

*Read from the M-3002 bus
TGET15	LDA	PORT
	AND	#15
TWT3	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

************************************
*Resident part
RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***

GTDTB	DTA	18,17,16,13,14,15

***

*Read M-3002 register twice and compare
RDREG	SEC
	ROR	CNT

RDR2	DEC	CNT
	BEQ	RDRET

S1	JSR	RDR4
	STA	TMP
S2	JSR	RDR4
	CMP	TMP
	BNE	RDR2

RDRET	RTS

***
OLDADR	DTA	A(0,0)
***

*Get TENS
RDR4	JSR	WAIT
	TAY
*Get ONES
S4	JSR	GET15
*Add ONES plus TENS by 10.
RDR7	DEY
	BMI	RDRET

	CLC
	ADC	#10
	BCC	RDR7
***

GETTD	LDX	#5

GTD2	JSR	RDREG
S5	LDY	GTDTB,X
	STA	(10),Y

	DEX
	BPL	GTD2
*Omit the RTS, ignore rest of the code

WAIT	LDY	#0

WT2	DEY
	BEQ	WT3

S6	JSR	GET15
TST2	BNE	WT2

	STX	PORT

GET15	LDA	PORT
	AND	#15

WT3	RTS
*End of reduced handler (GETTD only)

STD4T	DTA	2,5,4,7,2,5
	DTA	7,3,6,1,4,6
***

SETTD	LDX	#5

STD2	LDY	GTDTB,X
	LDA	(10),Y
S7	JSR	WRREG

	DEX
	BPL	STD2

*Day of the week
XTRA1	LDA	$FFFF	;COMTAB+15 = YEAR
XTRA3	LDX	$FFFF	;COMTAB+14 = MONTH
	CPX	#3	;Add 1 for mar-dec
	SBC	#82
	BCS	STD3
	ADC	#100

*Here we have A = year - 82 = 0 ... 99
STD3	STA	CNT

	LSR
	LSR

	ADC	CNT
*CNT = 0 ... 124

S8	ADC	STD4T-1,X
*CNT = 0 ... 130

XTRA2	ADC	$FFFF	;COMTAB+13
*CNT = 1 ... 161

*Calculate MODULO 7:
STD7	ADC	#7
	BCC	STD7

*Week number starts at 1
	ADC	#0

	LDX	#6
**

WRREG	STA	TMP2

	SEC
	ROR	CNT2	;MIN.128x

WRRG1	LDA	TMP2
	LDY	#255

WRRG2	INY
	SEC
	SBC	#10
	BCS	WRRG2
	ADC	#10

	PHA
	TYA
	PHA

S9	JSR	WAIT

	LDA	PORT
	STX	PORT

	PLA
	STA	PORT
	PLA
	STA	PORT

S10	JSR	RDREG
	CMP	TMP2
	BEQ	WRRG4

	DEC	CNT2
	BNE	WRRG1

WRRG4	RTS

****

RESLEN1	EQU	STD4T-RESID
RESLEN2	EQU	*-RESID

RESLENT	DTA	RESLEN2,RESLEN1

***

NEWADR	DTA	A(0,0,0)

RELOKT	DTA	S1-RESID,RDR4-RESID
	DTA	S2-RESID,RDR4-RESID
	DTA	RDR4-RESID,WAIT-RESID
	DTA	S4-RESID,GET15-RESID
	DTA	GTD2-RESID,RDREG-RESID
	DTA	S5-RESID,GTDTB-RESID
	DTA	S6-RESID,GET15-RESID
	DTA	STD2-RESID,GTDTB-RESID
	DTA	S7-RESID,WRREG-RESID
	DTA	S8-RESID,STD4T-1-RESID
	DTA	S9-RESID,WAIT-RESID
	DTA	S10-RESID,RDREG-RESID

	DTA	NEWADR-1-RESID,GETTD-RESID
	DTA	NEWADR+1-RESID,SETTD-RESID
	DTA	NEWADR+3-RESID,RDRET-RESID

	DTA	A(0,0)

