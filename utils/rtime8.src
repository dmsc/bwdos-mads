*************************************
*				*
*   " R T I M E 8 "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128
RELZP	EQU	130
REDFLG	EQU	132
TCNT	EQU	134
TTMP	EQU	135
RT8TP	EQU	138

***

VCOUNT	EQU	$D40B
WSYNC	EQU	$D40A

*** PRO RESIDENT

PORT	EQU	$D5B8
CNT	EQU	$31	;SIO CHECK
TMP	EQU	$30	;SIO STATUS
TMP2	EQU	$32	;SIO ADR.
CNT2	EQU	$33	;---"---

***

	ORG	$6000
	JMP	START

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BNE	STRTER

	LDA	$703
	CMP	#'B'
	BNE	STRTER
	LDA	$704
	CMP	#'W'
	BEQ	START2

STRTER	JSR	PRINT
	DTA	155,253
	DTA	C"Error: Not BW-DOS !"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	ST2ON
*OFF?
	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	SYNTAX

	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	LDX	#0

	CMP	#'/'
	BNE	ST2O2

	INX

	INY
	LDA	(10),Y
	CMP	#'R'
	BNE	SYNTAX

	INY
	LDA	(10),Y

ST2O2	CMP	#$9B
	BEQ	START3

***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: RTIME8 [/R]"
	DTA	155
	DTA	C"        RTIME8 OFF"
	DTA	155,0

	JMP	(10)

*** NENI UZ ?

START3	STX	REDFLG

	JSR	SRCHRUT
	BCC	START4
*JE!
	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 driver already "
	DTA	C"installed !"
	DTA	155,0
	JMP	(10)

***INSTALL

START4	JSR	RT8TEST
	BCS	START4A

*RTC8 NEEX.

	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 cartridge not"
	DTA	C" installed !"
	DTA	155,0
	JMP	(10)
***

START4A	LDX	REDFLG

	LDA	743
	STA	SRCHADR
	CLC
	ADC	RESLENT,X
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*RELOK

RLK0	LDX	#0

RLK1	LDA	RELOKT,X
	STA	RELZP
	LDA	RELOKT+1,X
	STA	RELZP+1

	ORA	RELZP
	BEQ	INSTAL2

	LDA	RELOKT+2,X
	SEC
	SBC	#<RESID
	TAY
	LDA	RELOKT+3,X
	SBC	#>RESID
	PHA

	TYA
	LDY	#1

	CLC
	ADC	SRCHADR
	STA	(RELZP),Y
	INY
	PLA
	ADC	SRCHADR+1
	STA	(RELZP),Y

	INX
	INX
	INX
	INX

	JMP	RLK1
***

INSTAL2	LDA	10
	CLC
	ADC	#13
	TAY
	LDA	11
	ADC	#0
	TAX

	STY	XTRA2+1
	STX	XTRA2+2

	INY
	BNE	INST2XA
	INX

INST2XA	STY	XTRA3+1
	STX	XTRA3+2

	INY
	BNE	INST2XB
	INX

INST2XB	STY	XTRA1+1
	STX	XTRA1+2

*** FLAG REZIMU

	LDX	REDFLG
	DEX
	STX	WAIT+1
**

	LDX	#3

INST2B	LDA	$706,X
	STA	OLDADR,X
	DEX
	BPL	INST2B
**
	LDX	REDFLG
	LDY	RESLENT,X

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDX	#1
	LDY	#3

	LDA	REDFLG
	BEQ	INSTL4
	LDY	#5

INSTL4	LDA	NEWADR,X
	STA	$706,X
	LDA	NEWADR,Y
	STA	$708,X
	DEY
	DEX
	BPL	INSTL4
**

	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 driver "
	DTA	C"Installed."
	DTA	155,0

	LDA	REDFLG
	BEQ	INSTL5

	JSR	PRINT
	DTA	C"(Reduced version - "
	DTA	C"set time/date is"
	DTA	155
	DTA	C"not supported)"
	DTA	155,0

INSTL5	JMP	(10)

***

DISABLE	JSR	SRCHRUT
	STA	REDFLG
	BCS	DISABLE2

	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 driver"
	DTA	C" not Installed!"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 driver is "
	DTA	C"not the last"
	DTA	155
	DTA	C"installed handler!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	TYA
	LDX	REDFLG
	CMP	RESLENT,X
	BNE	DISCANT
*ODSTRAN

	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
*

	LDY	#(OLDADR-RESID)
	LDX	#0

DISABL3	LDA	(SRCHADR),Y
	STA	$706,X

	INY
	INX
	CPX	#4
	BCC	DISABL3
**

	JSR	PRINT
	DTA	155
	DTA	C"RTIME8 driver"
	DTA	C" Removed."
	DTA	155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	RESLENT+1
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#S1-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#GTD2-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#RDR7-RESID
	BNE	SRCHR3
*
	LDY	#WT3-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR4
**REZIM
	LDY	#WAIT+1-RESID
	LDA	(SRCHADR),Y
	CLC
	ADC	#1

	SEC
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

**** TEST PRITOMNOSTI RTIME8

RT8TL	DTA	0,0,0,1,1,0,1
RT8TH	DTA	60,60,24,32,13,100,8

*

RT8TEST	LDA	#6
	STA	RT8TP

RT8T2	LDX	RT8TP
	JSR	TRDREG

	LDX	RT8TP
	CMP	RT8TL,X
	BCC	RT8TER
	CMP	RT8TH,X
	BCS	RT8TER

	DEC	RT8TP
	BPL	RT8T2

	SEC
	RTS
**

RT8TER	CLC
	RTS
***

TRDREG	SEC
	ROR	TCNT

TRDR2	DEC	TCNT
	BEQ	TRDRET

	JSR	TRDR4
	STA	TTMP
	JSR	TRDR4
	CMP	TTMP
	BNE	TRDR2

TRDRET	RTS

**

TRDR4	JSR	TWAIT

	STX	PORT

	JSR	TGET15
	TAY

	JSR	TGET15

TRDR7	DEY
	BMI	TRDRET

	CLC
	ADC	#10
	BCC	TRDR7
**

TWAIT	LDY	#0

TWT2	DEY
	BEQ	TWT3

	JSR	TGET15
	BNE	TWT2

*** (RTS VYNECHAN - NEVADI)

TGET15	LDA	PORT
	AND	#15

TWT3	RTS

***********REZIDENT

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***

GTDTB	DTA	18,17,16,13,14,15

***

RDREG	SEC
	ROR	CNT

RDR2	DEC	CNT
	BEQ	RDRET

S1	JSR	RDR4
	STA	TMP
S2	JSR	RDR4
	CMP	TMP
	BNE	RDR2

RDRET	RTS

***

OLDADR	DTA	A(0,0)

***

RDR4	JSR	WAIT

	STX	PORT

S3	JSR	GET15
	TAY

S4	JSR	GET15

RDR7	DEY
	BMI	RDRET

	CLC
	ADC	#10
	BCC	RDR7
***

GETTD	LDX	#5

GTD2	JSR	RDREG
S5	LDY	GTDTB,X
	STA	(10),Y

	DEX
	BPL	GTD2

*** (RTS VYNECHAN - NEVADI)

WAIT	LDY	#0

WT2	DEY
	BEQ	WT3

S6	JSR	GET15
TST2	BNE	WT2

*** (RTS VYNECHAN - NEVADI)

GET15	LDA	PORT
	AND	#15

WT3	RTS

**** END GETTD

STD4T	DTA	0,3,3,6,1,4
	DTA	6,2,5,0,3,5
***

SETTD	LDX	#5

STD2	LDY	GTDTB,X
	LDA	(10),Y
S7	JSR	WRREG

	DEX
	BPL	STD2

* DEN V TYDNU

XTRA1	LDA	$FFFF	;COMTAB+15

	SEC
	SBC	#84
	BCS	STD3
	ADC	#100

STD3	STA	CNT

	LSR
	LSR

	CLC
	ADC	CNT
XTRA2	ADC	$FFFF	;COMTAB+13

XTRA3	LDX	$FFFF	;COMTAB+14
S8	ADC	STD4T-1,X

	TAY

	LDA	CNT
	AND	#3
	BNE	STD5
	CPX	#3
	BCC	STD6

STD5	INY

STD6	TYA

STD7	SEC
	SBC	#7
	BCS	STD7
	ADC	#8

	LDX	#6
**

WRREG	STA	TMP2

	SEC
	ROR	CNT2	;MIN.128x

WRRG1	LDA	TMP2
	LDY	#255

WRRG2	INY
	SEC
	SBC	#10
	BCS	WRRG2
	ADC	#10

	PHA
	TYA
	PHA

S9	JSR	WAIT

	PLA
	STX	PORT
	STA	PORT
	PLA
	STA	PORT

S10	JSR	RDREG
	CMP	TMP2
	BEQ	WRRG4

	DEC	CNT2
	BNE	WRRG1

WRRG4	RTS

****

RESLEN1	EQU	STD4T-RESID
RESLEN2	EQU	*-RESID

RESLENT	DTA	RESLEN2,RESLEN1

***

NEWADR	DTA	A(0,0,0)

RELOKT	DTA	A(S1,RDR4)
	DTA	A(S2,RDR4)
	DTA	A(RDR4,WAIT)
	DTA	A(S3,GET15)
	DTA	A(S4,GET15)
	DTA	A(GTD2,RDREG)
	DTA	A(S5,GTDTB)
	DTA	A(S6,GET15)
	DTA	A(STD2,GTDTB)
	DTA	A(S7,WRREG)
	DTA	A(S8,STD4T-1)
	DTA	A(S9,WAIT)
	DTA	A(S10,RDREG)

	DTA	A(NEWADR-1,GETTD)
	DTA	A(NEWADR+1,SETTD)
	DTA	A(NEWADR+3,RDRET)

	DTA	A(0,0)

