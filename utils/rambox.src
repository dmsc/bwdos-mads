*************************************
*				*
* " R A M B O X "  PRO BW-DOS V1.2	*
*	    (MAX. 4MB)		*
*************************************

SRCHADR	EQU	128
SIOADR	EQU	130

DNUMB	EQU	132
FFLAG	EQU	133

NBANKS	EQU	134

MAXSECT	EQU	135

MAPS	EQU	137

MSAVE	EQU	138
MIX	EQU	139
MKONEC	EQU	140

TEMP1	EQU	142
TEMP2	EQU	143

MAPTMP	EQU	145

*** VOLNO: 146

BUFF	EQU	$500

***

	ORG	$6000
ODT0	LDA	$E474
	LDA	#$4C
	STA	ODT0
	JMP	START

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***
	ICL	"chkbw.inc"
***

START2	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX driver ver. 1"
	DTA	C".2 (for BW-DOS)"
	DTA	155,0

	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	LDA	11
	SBC	#0
	STA	SIOADR+1
**
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
*HELP

STRTHLP	JSR	PRINT
	DTA	155
	DTA	C"Syntax: RAMBOX "
	DTA	C"drive#[F]"
	DTA	155
	DTA	C"        RAMBOX OFF"
	DTA	155,0

	JMP	(10)
***

START3	CMP	#'O'
	BNE	START4

	INY
	LDA	#'F'
	CMP	(10),Y
	BNE	START4
	INY
	CMP	(10),Y
	BNE	START4

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START4

	JMP	REMOVE
***

START4	LDA	#0
	STA	FFLAG

	LDY	#36

STRT4A	STA	DNUMB
	STA	STDRIV

STRT4B	LDA	(10),Y
	INY
	CMP	#$9B
	BEQ	START5

	CMP	#'F'
	BNE	STRT4C
	STA	FFLAG
	BEQ	STRT4B

STRT4C	SEC
	SBC	#'0'
	BEQ	ST4ER
	CMP	#10
	BCC	STRT4A
**

ST4ER	JMP	STRTHLP

***

START5	LDA	DNUMB
	BEQ	ST4ER

	JSR	RAMTEST

	LDA	NBANKS
	BNE	START6
**
	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX cartridge not"
	DTA	C" installed !"
	DTA	155,0

	JMP	(10)

*PAMET TESTOVANA, NBANKS A MAXSECT
*AKTUALNI

START6	JSR	SRCHRUT
	BCC	STRT6B

*CHANGE CONFIG

	LDA	#$FF
	STA	$D57F	;CODE #1

	LDA	RDSPUV+1+RESO
	STA	RDSPUV+1
	LDA	RDSPUV+2+RESO
	STA	RDSPUV+2

	JMP	START7

*INSTALUJ REZIDENT

STRT6B	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*SIO
	LDY	#1
	LDA	(SIOADR),Y
	STA	RDSPUV+2
	DEY
	LDA	(SIOADR),Y
	STA	RDSPUV+1

*COPY
	LDY	#RESLEN

STRT6C	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	STRT6C
*RESET
	LDY	#2

STRT6D	LDA	11,Y
	STA	(SRCHADR),Y
	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	STRT6D
*SIO
*Y=0
	LDA	#(RDSIO-RESID)
	CLC
	ADC	SRCHADR
	STA	(SIOADR),Y
	INY
	LDA	#0
	ADC	SRCHADR+1
	STA	(SIOADR),Y

*REZIDENT EXISTUJE

START7	LDA	DNUMB
	STA	RR1+1

*MAXSECT
	LDA	NBANKS
	STA	RR2+1
*BANK0
	LDA	#$FF
	STA	$D57F	;CODE #1

	LDY	#127
STRT7C	LDA	RES2,Y
	STA	$D580,Y
	DEY
	BPL	STRT7C

	LDA	#$FF
	STA	$D57E	;CODE #2

	LDY	#127
STRT7D	LDA	RES3,Y
	STA	$D580,Y
	DEY
	BPL	STRT7D

* FORMATOVANI

	LDA	FFLAG
	BNE	FORM1

	LDA	#1
	LDX	#'R'
	LDY	#64
	JSR	RWRD

	LDA	BUFF+11
	CMP	MAXSECT
	BNE	FORM1
	LDA	BUFF+12
	CMP	MAXSECT+1
	BNE	FORM1

	LDA	#$80
	CMP	BUFF+7
	BNE	FORM1
	CMP	BUFF+31
	BNE	FORM1

	LDA	BUFF+32
	CMP	#$22
	BNE	FORM1
	JMP	FORMEND

*#MAPS

FORM1	LDA	MAXSECT+1
	LSR
	LSR
	TAX
	INX
	STX	MAPS
*DIR
	JSR	CLBUF

	LDX	MAPS
	INX
	INX
	TXA
	INX
	STX	BUFF+4

	JSR	WRRD

	JSR	CLBUF

	LDX	#13
FORM1B	LDA	VZDIR,X
	STA	BUFF+3,X
	DEX
	BPL	FORM1B

	LDA	MAPS
	CLC
	ADC	#3
	JSR	WRRD

*BITMAPS
	JSR	CLBUF

	LDA	MAXSECT
	STA	MKONEC
	LDA	MAXSECT+1
	LSR
	ROR	MKONEC
	LSR
	ROR	MKONEC
	LSR	MKONEC

	LDX	#2
	STX	MSAVE
	DEX
	STX	MIX
*

	LDA	MAPS	;1ST FREE
	CLC
	ADC	#4
	PHA

	LSR
	LSR
	LSR
	STA	MAPTMP

	PLA
	AND	#7	;EDGE BYTE
	TAY

	LDA	#$FF
	SEC

FRM1CB	ROR
	CLC
	DEY
	BPL	FRM1CB

	TAY

	LDX	#$FF	;FILL PRED

FRM1CA	INX
	LDA	#0
	STA	BUFF,X

	CPX	MAPTMP
	BCC	FRM1CA

	TYA		;EDGE BYTE
	DTA	$2C	;BIT

FORM1C	LDA	#$FF	;FILL!
	STA	BUFF,X

	LDA	MIX	;LAST SECT?
	CMP	MAPS
	BEQ	FORM1D

FORM1CB	INX
	BPL	FORM1C

	LDA	MSAVE	;NEXT SECT
	JSR	WRRD

	JSR	CLBUF

	INC	MIX
	INC	MSAVE

	LDX	#0
	BEQ	FORM1C
*

FORM1D	CPX	MKONEC
	BNE	FORM1CB

	LDA	#$FC	;LAST BYTE
	STA	BUFF,X

	LDA	MSAVE
	JSR	WRRD

*BOOT SECTOR

	JSR	CLBUF

	LDX	#32

FORM1E	LDA	VZBOOT,X
	STA	BUFF,X
	DEX
	BPL	FORM1E

	LDA	MAPS
	STA	BUFF+15
	CLC
	ADC	#2
	STA	BUFF+9

	LDA	MAXSECT
	STA	BUFF+11
	SEC
	SBC	MAPS
	TAY
	LDA	MAXSECT+1
	STA	BUFF+12
	SBC	#0
	TAX

	TYA
	SEC
	SBC	#4
	STA	BUFF+13
	TXA
	SBC	#0
	STA	BUFF+14

	LDA	$D20A
	STA	BUFF+39

	LDA	#1
	JSR	WRRD
***
	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX formatted."
	DTA	0
***

FORMEND	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX"
	DTA	C" is D"
	DTA	0

	LDA	DNUMB
	ORA	#$30
	JSR	PRT1

	JSR	PRINT
	DTA	C": ("
	DTA	C"Size: "
	DTA	0

* PRINT SIZE

	LDA	#0
	STA	MAXSECT+1
	STA	TEMP2

	LDA	NBANKS
	LDX	#4

PRTSZ1	ASL
	ROL	MAXSECT+1
	DEX
	BPL	PRTSZ1

	STA	MAXSECT

	LDX	#3
	STX	TEMP1

PRTSZ2	LDX	TEMP1
	LDY	#0
	BEQ	PRTSZ4
*
PRTSZ3	LDA	MAXSECT
	SEC
	SBC	PRTSZTL,X
	STA	MAXSECT
	LDA	MAXSECT+1
	SBC	PRTSZTH,X
	STA	MAXSECT+1

	INY

PRTSZ4	LDA	MAXSECT
	CMP	PRTSZTL,X
	LDA	MAXSECT+1
	SBC	PRTSZTH,X
	BCS	PRTSZ3

	TYA
	BEQ	PRTSZ5
	INC	TEMP2

PRTSZ5	ORA	#$20
	LDY	TEMP2
	BEQ	PRTSZ6
	ORA	#$10

PRTSZ6	JSR	PRT1

	DEC	TEMP1
	BPL	PRTSZ2
***

	JSR	PRINT
	DTA	C" kB)"
	DTA	155,0

	JMP	(10)
***

PRTSZTL	DTA	1,10,100,$E8
PRTSZTH	DTA	0,0,0,3

***

VZBOOT	DTA	'S'
	DTA	3,0,48,$E0,7
	JMP $3080
	DTA	A(0,0,0)
	DTA	0,2,0,64,0,4,0
	DTA	C"RAMBOX  "
	DTA	1,128,$22

VZDIR	DTA	23,0,0
	DTA	C"MAIN       "

***

CLBUF	LDX	#127
	LDA	#0

CLBF2	STA	BUFF,X
	DEX
	BPL	CLBF2

	RTS
****

WRRD	LDX	#'W'
	LDY	#128

RWRD	STX	STCMD
	STY	STSTAT
	STA	STAUX

	LDX	#11
RWRD2	LDA	STAB,X
	STA	$300,X
	DEX
	BPL	RWRD2

	LDA	10
	SEC
	SBC	#10
	STA	SIOJMP+1
	LDA	11
	SBC	#0
	STA	SIOJMP+2

	JSR	SIOJMP
	BMI	SIOERR

	RTS
**

SIOJMP	JMP	($FFFF)

**

STAB	DTA	$31
STDRIV	DTA	0
STCMD	DTA	0
STSTAT	DTA	0
	DTA	A(BUFF)
	DTA	7,0
	DTA	A(128)
STAUX	DTA	A(0)

**

SIOERR	JSR	PRINT
	DTA	155,253
	DTA	C"Internal Error!"
	DTA	155,0

	JMP	(10)
****

REMOVE	JSR	SRCHRUT
	BCC	REMNOT
	JMP	REMOV2
**

REMNOT	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX driver not "
	DTA	C"installed!"
	DTA	155,0

REMCANT	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,253,0

	JMP	(10)
**

REM2LST	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX is not the"
	DTA	C" last"
	DTA	155
	DTA	C"installed handler!"
	DTA	155,0

	JMP	REMCANT
**

REMOV2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	REM2LST

	CPY	#RESLEN
	BNE	REM2LST

*REMOVE IT
*SIO
	LDA	#$FF	;CODE #1
	STA	$D57F

	LDY	#1
REM2B	LDA	RDSPUV+1+RESO,Y
	STA	(SIOADR),Y
	DEY
	BPL	REM2B

*RESET
	LDY	#2

REM3	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	REM3
***

	JSR	PRINT
	DTA	155
	DTA	C"RAMBOX driver removed."
	DTA	155,0

	JMP	(10)

****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#TST1-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR2
*
	LDY	#R02-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR3

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS
*****

RTTL	DTA	$7E,$7D,$7B,$77
	DTA	$6F,$5F,$3F,$7F
	DTA	$7F,$7F,$7F,$7F
	DTA	$7F,$7F,$7F

RTTH	DTA	$FF,$FF,$FF,$FF
	DTA	$FF,$FF,$FF,$FE
	DTA	$FD,$FB,$F7,$EF
	DTA	$DF,$BF,$7F
**

RAMTEST	LDA	#0
	STA	NBANKS

RT1	LDY	NBANKS
	LDX	RTTL,Y
	LDA	RTTH,Y
*SAVE OLD
	LDY	#$FF
	STY	$D57F
	LDY	$D587
	STY	TEMP1

	STA	$D500,X
	LDY	$D587
	STY	TEMP2
*SET NEW
	LDY	#$FF
	STY	$D587
	STY	$D57F
	INY
	STY	$D587
*TEST
	DEY
	STA	$D500,X
	CPY	$D587
	BNE	RT2
	STY	$D57F
	INY
	CPY	$D587
	BEQ	RT3

RT2	CLC
RT3	PHP

*RESTORE
	STA	$D500,X
	LDY	TEMP2
	STY	$D587

	LDY	#$FF
	STY	$D57F
	LDY	TEMP1
	STY	$D587
*OK?
	PLP
	BCC	RT4

	INC	NBANKS
	LDA	NBANKS
	CMP	#15
	BCC	RT1
*END

RT4	LDX	NBANKS
	LDY	RTTB,X
	STY	NBANKS

	DEY
	STY	MAXSECT+1
	LDA	#$FE
	STA	MAXSECT

	RTS
*

RTTB	DTA	0
	DTA	0,0,0,0,0,0,0
	DTA	1,2,4,8
	DTA	16,32,64,128

***********

RESID	JSR	$FFFF

R01	LDA	#$22
TST3	STA	743
R02	LDA	#$22
TST2	STA	744

	RTS
***

RDSIO	LDA	#$FF	;CODE #1
	STA	$D57F

	JSR	$D580

	STA	$D500,X

RDS2	LDA	($32),Y
	STA	($34),Y
	DEY
	BNE	RDS2

	INY
TST1	RTS
***

RESLEN	EQU	*-RESID

*******

RES2	JMP	RES20+RESO

R2SWITCH	STA	$D57E	;CODE #2
	NOP
	JMP	$E474
****

RES20	LDA	$300
	CMP	#$31
	BNE	RES2P

	LDA	$301
RR1	CMP	#0
	BEQ	RES2B

RES2P	PLA
	PLA
RDSPUV	JMP	$FFFF

**
*C=1
RES2B	LDA	$304
	SBC	#1
	STA	$34
	STA	$32
	LDA	$305
	SBC	#0
	STA	$35
	STA	$33

	LDA	$309
	BNE	R2NAK
	LDY	$308
	LDX	$303

	LDA	$302
	CMP	#'S'
	BNE	R2RW
*STATUS

	CPX	#$40
	BNE	R2NAK

	CPY	#4
	BNE	R2NAK

	LDA	#$FF
RES2S	STA	($34),Y
	DEY
	BNE	RES2S

	INY
	LDA	#$10
	STA	($34),Y

	DTA	$2C		;BIT
R2NAK	LDY	#139

	PLA
	PLA
	STY	$303
	TYA
	RTS

***

R2RW	LDA	#$FF
	BNE	R2SWITCH	;CODE #2

*****

RESO	EQU	$D580-RES2

RES2LEN	EQU	*-RES2

*******

RES3	JMP	$E474
	STA	$D57E	;CODE #2
	NOP
***

	CPY	#128
	BNE	R3NAK

	LDA	$302
	CMP	#'R'
	BEQ	RR8READ
	CMP	#'W'
	BEQ	RR8WR
	CMP	#'P'
	BNE	R3NAK

RR8WR	CPX	#128
	BNE	R3NAK

	LDX	#2
	BNE	RR8RWS

RR8READ	CPX	#64
	BNE	R3NAK

	LDX	#0

RR8RWS	LDA	#$7F
	STA	$32,X
	LDA	#$D5
	STA	$33,X

	LDA	#1
	STA	$303

	LDA	$30A	;MAXSECT
	CLC
	ADC	#1
	TAX
	LDA	$30B
	ADC	#0
	TAY
	BNE	RR2

	CPX	#2
	BCC	R3NAK

RR2	CPY	#$FF
	BCS	R3NAK

	TXA
	ASL
	PHP
	LSR
	EOR	#$7F
	TAX

	PLP
	TYA
	ROL
	EOR	#$FF

	LDY	#128

	RTS
***

R3NAK	LDY	#139

	PLA
	PLA
	STY	$303
	TYA
	RTS

***

RES3LEN	EQU	*-RES3

