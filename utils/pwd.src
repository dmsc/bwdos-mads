*************************************
*				*
*	 "PWD" for BW-DOS		*
*				*
*************************************

DIRLEN	EQU	128
PATHLEN	EQU	129

DIRBUF	EQU	130

PATH	EQU	$3F00

	ORG	$3000
***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	#4
	STA	DIRLEN

*Get parameter, D*:
GETNAME	JSR	GETNAME

	LDY	#33
	LDA	(10),Y
	CMP	#'D'
	BNE	ERR146
	INY
	LDA	(10),Y
	STA	DIR+1
	LDY	#36
	LDA	(10),Y
	EOR	#$9B
	BEQ	START3

ERR146	LDY	#146
	JMP	ERROR

*Open directory data
*A=0 here
START3	TAY
DIRNAME	INY		;Start by adding a ">" to the path
	STY	PATHLEN
	LDA	#'>'
	STA	PATH-1,Y

	JSR	CLOSE

	LDX	#<DIR
	LDA	#>DIR
	LDY	#3
	JSR	CIOXA	;OPEN DIR

	LDY	#37
	JSR	CIOXA	;POINT

	LDX	#23
	STX	$348+$10
	LDA	#>DIRBUF
	LDX	#<DIRBUF
	LDY	#7
	JSR	CIOXA	;READ

	JSR	CLOSE
***
	LDA	DIRBUF+1
	ORA	DIRBUF+2
	BEQ	PRTD1	;Main DIR


*Convert name to DOS format, and prepend the new directory
*name to the full path

	LDY	PATHLEN
	LDX	#2
	CLC

CVEXT	LDA	DIRBUF+14,X
	STA	PATH,Y
	EOR	#' '
	BEQ	CVEX1
	INY
	SEC
CVEX1	DEX
	BPL	CVEXT

	LDX	#7
	BCC	CVNAM
	LDA	#'.'
	INX
	BNE	CVNM1

CVNAM	LDA	DIRBUF+6,X
CVNM1	STA	PATH,Y
	EOR	#' '
	BEQ	CVNM2
	INY
CVNM2	DEX
	BPL	CVNAM

	CPY	PATHLEN
	BCC	ERRLONG

*Now, go to parent directory
MPATH1	LDX	DIRLEN
	LDA	#'<'
	STA	DIR-1,X
	LDA	#$9B
	STA	DIR,X

	INC	DIRLEN
*And repeat!
	BNE	DIRNAME

***
ERRLONG	JSR	PRINT
	DTA	155
	DTA	C"Path too long!"
	DTA	155,0
	BEQ	ERR5X

*Print full path and exit to DOS
PRTD1	LDY	#253
	LDA	DIR-253,Y
	JSR	PRT1
	INC	PRTD1+1
	BNE	PRTD1
PRTPATH	LDX	PATHLEN
	LDA	PATH-1,X
	JSR	PRT1
	DEC	PATHLEN
	BNE	PRTPATH
	JMP	(10)

***
	ICL	"error.inc"

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474	;XDIVIO

ERR5	JSR	CLOSE

	JMP	(10)
***

CLOSE	LDY	#12

CIOXA	STX	$344+$10
	STA	$345+$10

	LDX	#5
	LDA	#0
CIOP0	STA	$349+$10,X
	DEX
	BPL	CIOP0

	LDA	#$1C
	STA	$34A+$10

CIO10	LDX	#$10
	STY	$342+$10
	JSR	$E456
	BMI	ERROR
	RTS

***
	ICL	"print.inc"
	ICL	"print1.inc"
***
ERRTAB	DTA	100,10,1

DIR	DTA	C"D1:"
	DTA	$9B

