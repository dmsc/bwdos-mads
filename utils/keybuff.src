*************************************
*				*
*   " K E Y B U F F "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128

RELZP	EQU	130

***

	ORG	$3000

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
GETNAME	JSR	PRTEX

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#'N'
	BEQ	ST2ON
	CMP	#'F'
	BNE	SYNTAX
*OFF?
	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	INY
	LDA	(10),Y
	CMP	#$9B
	BEQ	START3
***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: KEYBUFF ON"
	DTA	155
	DTA	C"        KEYBUFF OFF"
	DTA	155,0

	JMP	(10)

***
	ICL	"print.inc"
	ICL	"print1.inc"

*** NENI UZ ?

START3	JSR	SRCHRUT
	BCC	START4
*JE!
	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"already installed!"
	DTA	155,0
	JMP	(10)

***INSTALL

START4	LDA	743
	STA	SRCHADR
	CLC
	ADC	#<RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#>RESLEN
	STA	744
	STA	R02+1

*RELOK

	LDX	#0

RLK1	LDY	RELOKT,X
	BEQ	RLK2

	CLC
	LDA	RELOKT+1,X
	ADC	SRCHADR
	STA	RESID+1,Y

	LDA	#0
	ADC	SRCHADR+1
	STA	RESID+2,Y

	INX
	INX

	JMP	RLK1
***

RLK2	LDX	#5

RLK3	LDY	RELOKT2,X
	LDA	NEWADR,X
	STA	RESID,Y
	DEX
	BPL	RLK3
***

	LDA	$208
	STA	OLDADR
	LDA	$209
	STA	OLDADR+1
***

	LDY	#(KBUFF-RESID)

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDA	SRCHADR
	CLC
	ADC	#3
	STA	INITJP+1
	LDA	SRCHADR+1
	ADC	#0
	STA	INITJP+2

	JSR	INITJP

**
	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"Installed."
	DTA	155,0

	JMP	(10)
***

INITJP	JMP	$E471

***

PRTKBUF	JSR	PRINT
	DTA	155
	DTA	C"Keyboard buffer "
	DTA	0

	RTS
***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"not Installed!"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"is not the last"
	DTA	155
	DTA	C"handler installed!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1

	CMP	#>RESLEN
	BNE	DISCANT
	CPY	#<RESLEN
	BNE	DISCANT
*ODSTRAN

	SEI

	LDY	#(OLDADR-RESID)
	LDA	(SRCHADR),Y
	STA	$208
	INY
	LDA	(SRCHADR),Y
	STA	$209

	LDY	#$5F
	LDX	#$E4
	LDA	#6
	JSR	$E45C

	CLI
***
	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**

	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"Removed."
	DTA	155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#<RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#>RESLEN
	STA	SRCHADR+1
*
SRCHR1	LDX	#2
SRCHR3	LDY	SRCHTAB1,X

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	TYA
	CMP	SRCHTAB2,X
	BNE	SRCHR2

	DEX
	BPL	SRCHR3

	RTS

SRCHTAB1	DTA	R08-RESID,S2-RESID,KRDIX-RESID
SRCHTAB2	DTA	TST1-RESID,TST2-RESID,TST3-RESID

**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$19
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
	STA	744

	SEI

S1	JSR	CLBUF

R03	LDA	#$22
	STA	$208
R04	LDA	#$22
	STA	$209

R05	LDY	#$22
R06	LDX	#$22
TST1	LDA	#6
	JSR	$E45C

	CLI
	RTS

***
KEYIRQ	TXA
	PHA

	LDX	764	; Save current CH value
	LDA	#$FF	; And clear
	STA	764

*Call original Keyboard IRQ
R08	LDA	#>RETIRQ
	PHA
R07	LDA	#<RETIRQ
	PHA
	PHP
	PHA
	JMP	$FFFF
OLDADR	EQU	*-2
TST2

RETIRQ	LDA	764
	STX	764

	CMP	#$E1	;SH+CT+SPC
	BNE	KI3

S2	JSR	CLBUF
S3	JMP	BUFBEL

KI3	CMP	#$FF
	BEQ	KIEX

*Insert key-code into buffer
S7	LDX	KWRIX+1
	CPX	#32
	BEQ	BUFBEL
S9	INC	KWRIX+1
S10	STA	KBUFF,X
TST3
KIEX	PLA
	TAX
	PLA
	RTI

***
BUFBEL	LDX	#0
BUFB2	TXA
	AND	#8
	STA	$D01F
	INX
	BNE	BUFB2
	BEQ	KIEX
***

KEYVBI	LDX	764
	INX
	BNE	KV2

KRDIX	LDX	#0
KWRIX	CPX	#0
	BNE	KV1
S8	JSR	CLBUF
	BNE	KV2

KV1
S11	LDA	KBUFF,X
	STA	764
S12	INC	KRDIX+1

KV2	JMP	$E45F	;immediate VBI

***

CLBUF	LDX	#0
S16	STX	KRDIX+1
S17	STX	KWRIX+1
	DEX
	STX	764

	RTS
***

KBUFF	DTA	0

RESLEN	EQU	(KBUFF+32)-RESID

***

NEWADR	DTA	A(0,0,0)

RELOKT	DTA	S1-RESID,CLBUF-RESID
	DTA	S2-RESID,CLBUF-RESID
	DTA	S3-RESID,BUFBEL-RESID
	DTA	S7-RESID,KWRIX+1-RESID
	DTA	S8-RESID,CLBUF-RESID
	DTA	S9-RESID,KWRIX+1-RESID
	DTA	S10-RESID,KBUFF-RESID
	DTA	S11-RESID,KBUFF-RESID
	DTA	S12-RESID,KRDIX+1-RESID
	DTA	S16-RESID,KRDIX+1-RESID
	DTA	S17-RESID,KWRIX+1-RESID

	DTA	NEWADR-1-RESID,KEYIRQ-RESID
	DTA	NEWADR+1-RESID,KEYVBI-RESID
	DTA	NEWADR+3-RESID,RETIRQ-RESID

	DTA	0

RELOKT2	DTA	R03+1-RESID,R04+1-RESID
	DTA	R05+1-RESID,R06+1-RESID
	DTA	R07+1-RESID,R08+1-RESID

