*************************************
*				*
*   " K E Y B U F F "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128

RELZP	EQU	130

***

	ORG	$6000
	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA	155,253
	DTA	C"Incorrect DOS !"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#'N'
	BEQ	ST2ON
	CMP	#'F'
	BNE	SYNTAX
*OFF?
	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	INY
	LDA	(10),Y
	CMP	#$9B
	BEQ	START3
***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: KEYBUFF ON"
	DTA	155
	DTA	C"        KEYBUFF OFF"
	DTA	155,0

	JMP	(10)

*** NENI UZ ?

START3	JSR	SRCHRUT
	BCC	START4
*JE!
	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"already "
	DTA	C"installed !"
	DTA	155,0
	JMP	(10)

***INSTALL

START4	LDA	743
	STA	SRCHADR
	CLC
	ADC	#<RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#>RESLEN
	STA	744
	STA	R02+1

*RELOK

	LDX	#0

RLK1	LDA	RELOKT,X
	STA	RELZP
	LDA	RELOKT+1,X
	STA	RELZP+1

	ORA	RELZP
	BEQ	INSTAL2

	LDA	RELOKT+2,X
	SEC
	SBC	#<RESID
	TAY
	LDA	RELOKT+3,X
	SBC	#>RESID
	PHA

	TYA
	LDY	#1

	CLC
	ADC	SRCHADR
	STA	(RELZP),Y
	INY
	PLA
	ADC	SRCHADR+1
	STA	(RELZP),Y

	INX
	INX
	INX
	INX

	JMP	RLK1
***

INSTAL2	LDA	NEWADR
	STA	R03+1
	LDA	NEWADR+1
	STA	R04+1

	LDA	NEWADR+2
	STA	R05+1
	LDA	NEWADR+3
	STA	R06+1

***

	LDA	$208
	STA	OLDADR
	LDA	$209
	STA	OLDADR+1
***

	LDY	#(KBUFF-RESID)

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDA	SRCHADR
	CLC
	ADC	#3
	STA	INITJP+1
	LDA	SRCHADR+1
	ADC	#0
	STA	INITJP+2

	JSR	INITJP

**
	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"Installed."
	DTA	155,0

	JMP	(10)
***

INITJP	JMP	$E471

***

PRTKBUF	JSR	PRINT
	DTA	155
	DTA	C"Keyboard buffer "
	DTA	0

	RTS
***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"not Installed !"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"is not the last"
	DTA	155
	DTA	C"installed handler !"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1

	CMP	#>RESLEN
	BNE	DISCANT
	CPY	#<RESLEN
	BNE	DISCANT
*ODSTRAN

	SEI

	LDY	#(OLDADR-RESID)
	LDA	(SRCHADR),Y
	STA	$208
	INY
	LDA	(SRCHADR),Y
	STA	$209

	LDY	#$5F
	LDX	#$E4
	LDA	#6
	JSR	$E45C

	CLI
***
	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**

	JSR	PRTKBUF
	JSR	PRINT
	DTA	C"Removed."
	DTA	155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#<RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#>RESLEN
	STA	SRCHADR+1
*

SRCHR1	LDY	#OLDKEY-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#S2-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	LDY	#S15-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR4

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
	STA	744

	SEI

S1	JSR	CLBUF

R03	LDA	#$22
	STA	$208
R04	LDA	#$22
	STA	$209

R05	LDY	#$22
R06	LDX	#$22
TST1	LDA	#6
	JSR	$E45C

	CLI
	RTS
***

KEYIRQ	TXA
	PHA

	LDA	$D209
OLDKEY	CMP	#$FF
	BNE	KI2

KWAIT	LDX	#0
TST2	BNE	KIEX2

KI2	CMP	#$9F	;CTRL+1
	BNE	KI2B

	LDA	$2FF
	EOR	#$FF
	STA	$2FF
	BCS	KIEX
**

KI2B	CMP	#$E1	;SH+CT+SPC
	BNE	KI3

S2	JSR	CLBUF
S3	JSR	BUFBEL2
	BCC	KIEX
**

KI3	TAX

	AND	#$3F
	CMP	#17	;HELP
	BNE	KI4

	STX	$2DC
	BEQ	KIEX
**

KI4	STX	OLDKEY+1

	TXA
S4	JSR	VLOZ

KIEX	LDA	#3
S5	STA	KWAIT+1
	STA	$4D

KIEX2	LDA	#$20	;REP. WAIT
S6	STA	REPCNT

	PLA
	TAX
	PLA
	RTI
***

VLOZ	PHA

S7	LDX	KWRIX+1
	INX
	TXA
	DEX

	AND	#31
S8	CMP	KRDIX+1
	BEQ	BUFBEL

S9	STA	KWRIX+1

	PLA
S10	STA	KBUFF,X

	SEC
	RTS
***

REPCNT	DTA	0

***

BUFBEL	PLA
BUFBEL2	LDX	#0

BUFB2	LDA	$D20A
	AND	#8
	STA	$D01F
	BEQ	BUFB2

	DEX
	BNE	BUFB2

	CLC
	RTS
***

KEYVBI	LDX	764
	INX
	BNE	KV2

KRDIX	LDX	#0
KWRIX	CPX	#0
	BEQ	KV2

S11	LDA	KBUFF,X
	STA	764

	INX
	TXA
	AND	#31
S12	STA	KRDIX+1

** JESTE?

KV2	LDA	$D20F
	AND	#4
	BEQ	KV3
*NE

S13	LDA	KWAIT+1
	BEQ	KV2B
S14	DEC	KWAIT+1

KV2B	LDA	#0
	SEC
	BCS	KVEX
*ANO

KV3	DEC	REPCNT
TST3	BNE	KVEX2

	LDA	$D209
	CMP	#$9F
	BEQ	KV4
	CMP	#$E1
	BEQ	KV4

S15	JSR	VLOZ

KV4	LDA	#3	;REP SPD

KVEX	STA	REPCNT
	BCS	KVEX2

	JMP	$E462
***

KVEX2	JMP	$E45F

***

OLDADR	DTA	A(0)

***

CLBUF	LDX	#0
S16	STX	KRDIX+1
S17	STX	KWRIX+1
	DEX
	STX	764

	RTS
***

KBUFF	DTA	0

RESLEN	EQU	(KBUFF+32)-RESID

***

NEWADR	DTA	A(0,0)

RELOKT	DTA	A(S1,CLBUF)
	DTA	A(S2,CLBUF)
	DTA	A(S3,BUFBEL2)
	DTA	A(KI4,OLDKEY+1)
	DTA	A(S4,VLOZ)
	DTA	A(S5,KWAIT+1)
	DTA	A(S6,REPCNT)
	DTA	A(S7,KWRIX+1)
	DTA	A(S8,KRDIX+1)
	DTA	A(S9,KWRIX+1)
	DTA	A(S10,KBUFF)
	DTA	A(S11,KBUFF)
	DTA	A(S12,KRDIX+1)
	DTA	A(S13,KWAIT+1)
	DTA	A(S14,KWAIT+1)
	DTA	A(KV3,REPCNT)
	DTA	A(S15,VLOZ)
	DTA	A(KVEX,REPCNT)
	DTA	A(S16,KRDIX+1)
	DTA	A(S17,KWRIX+1)

	DTA	A(NEWADR-1,KEYIRQ)
	DTA	A(NEWADR+1,KEYVBI)

	DTA	A(0,0)

