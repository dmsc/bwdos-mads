	ORG	$5FFD,$A800
	OUT	N

O	EQU	$A800-$5FFD
	JMP	UTAJ+O
***
*   PRED SAVE SPUSTIT OD $A800,
*   PAK ULOZIT OD A803 NA 6000
***

*************************************
*				*
*   " K E Y B U F F "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EPZ	128

RELZP	EPZ	130

***

ODT0	LDA	START

	LDX	#0
	LDY	#6

ODTAJ	LDA	PRINT,X
	EOR	#$55
ODT2	STA	PRINT,X

	INX
	BNE	ODTAJ

	INC	ODTAJ+2
	INC	ODT2+2

	DEY
	BNE	ODTAJ
*
	LDA	#76
	STA	ODT0

	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS !"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#'N'
	BEQ	ST2ON
	CMP	#'F'
	BNE	SYNTAX
*OFF?
	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	INY
	LDA	(10),Y
	CMP	#$9B
	BEQ	START3
***

SYNTAX	JSR	PRINT
	DFB 155
	ASC "Syntax: KEYBUFF ON"
	DFB 155
	ASC "        KEYBUFF OFF"
	DFB 155,0

	JMP	(10)

*** NENI UZ ?

START3	JSR	SRCHRUT
	BCC	START4
*JE!
	JSR	PRTKBUF
	JSR	PRINT
	ASC "already "
	ASC "installed !"
	DFB 155,0
	JMP	(10)

***INSTALL

START4	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN:L
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#RESLEN:H
	STA	744
	STA	R02+1

*RELOK

	LDX	#0

RLK1	LDA	RELOKT,X
	STA	RELZP
	LDA	RELOKT+1,X
	STA	RELZP+1

	ORA	RELZP
	BEQ	INSTAL2

	LDA	RELOKT+2,X
	SEC
	SBC	#RESID:L
	TAY
	LDA	RELOKT+3,X
	SBC	#RESID:H
	PHA

	TYA
	LDY	#1

	CLC
	ADC	SRCHADR
	STA	(RELZP),Y
	INY
	PLA
	ADC	SRCHADR+1
	STA	(RELZP),Y

	INX
	INX
	INX
	INX

	JMP	RLK1
***

INSTAL2	LDA	NEWADR
	STA	R03+1
	LDA	NEWADR+1
	STA	R04+1

	LDA	NEWADR+2
	STA	R05+1
	LDA	NEWADR+3
	STA	R06+1

***

	LDA	$208
	STA	OLDADR
	LDA	$209
	STA	OLDADR+1
***

	LDY	#(KBUFF-RESID)

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
**

	LDA	SRCHADR
	CLC
	ADC	#3
	STA	INITJP+1
	LDA	SRCHADR+1
	ADC	#0
	STA	INITJP+2

	JSR	INITJP

**
	JSR	PRTKBUF
	JSR	PRINT
	ASC "Installed."
	DFB 155,0

	JMP	(10)
***

INITJP	JMP	$E471

***

PRTKBUF	JSR	PRINT
	DFB 155
	ASC "Keyboard buffer "
	DFB 0

	RTS
***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRTKBUF
	JSR	PRINT
	ASC "not Installed !"
	DFB 155,0

	JMP	DISERR2
***

DISCANT	JSR	PRTKBUF
	JSR	PRINT
	ASC "is not the last"
	DFB 155
	ASC "installed handler !"
	DFB 155,0

DISERR2	JSR	PRINT
	ASC "Can't remove."
	DFB 155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1

	CMP	#RESLEN:H
	BNE	DISCANT
	CPY	#RESLEN:L
	BNE	DISCANT
*ODSTRAN

	SEI

	LDY	#(OLDADR-RESID)
	LDA	(SRCHADR),Y
	STA	$208
	INY
	LDA	(SRCHADR),Y
	STA	$209

	LDY	#$5F
	LDX	#$E4
	LDA	#6
	JSR	$E45C

	CLI
***
	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**

	JSR	PRTKBUF
	JSR	PRINT
	ASC "Removed."
	DFB 155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN:L
	STA	SRCHADR
	LDA	744
	SBC	#RESLEN:H
	STA	SRCHADR+1
*

SRCHR1	LDY	#OLDKEY-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#S2-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	LDY	#S15-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR4

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
	STA	744

	SEI

S1	JSR	CLBUF

R03	LDA	#$22
	STA	$208
R04	LDA	#$22
	STA	$209

R05	LDY	#$22
R06	LDX	#$22
TST1	LDA	#6
	JSR	$E45C

	CLI
	RTS
***

KEYIRQ	TXA
	PHA

	LDA	$D209
OLDKEY	CMP	#$FF
	BNE	KI2

KWAIT	LDX	#0
TST2	BNE	KIEX2

KI2	CMP	#$9F	;CTRL+1
	BNE	KI2B

	LDA	$2FF
	EOR	#$FF
	STA	$2FF
	BCS	KIEX
**

KI2B	CMP	#$E1	;SH+CT+SPC
	BNE	KI3

S2	JSR	CLBUF
S3	JSR	BUFBEL2
	BCC	KIEX
**

KI3	TAX

	AND	#$3F
	CMP	#17	;HELP
	BNE	KI4

	STX	$2DC
	BEQ	KIEX
**

KI4	STX	OLDKEY+1

	TXA
S4	JSR	VLOZ

KIEX	LDA	#3
S5	STA	KWAIT+1
	STA	$4D

KIEX2	LDA	#$20	;REP. WAIT
S6	STA	REPCNT

	PLA
	TAX
	PLA
	RTI
***

VLOZ	PHA

S7	LDX	KWRIX+1
	INX
	TXA
	DEX

	AND	#31
S8	CMP	KRDIX+1
	BEQ	BUFBEL

S9	STA	KWRIX+1

	PLA
S10	STA	KBUFF,X

	SEC
	RTS
***

REPCNT	DFB	0

***

BUFBEL	PLA
BUFBEL2	LDX	#0

BUFB2	LDA	$D20A
	AND	#8
	STA	$D01F
	BEQ	BUFB2

	DEX
	BNE	BUFB2

	CLC
	RTS
***

KEYVBI	LDX	764
	INX
	BNE	KV2

KRDIX	LDX	#0
KWRIX	CPX	#0
	BEQ	KV2

S11	LDA	KBUFF,X
	STA	764

	INX
	TXA
	AND	#31
S12	STA	KRDIX+1

** JESTE?

KV2	LDA	$D20F
	AND	#4
	BEQ	KV3
*NE

S13	LDA	KWAIT+1
	BEQ	KV2B
S14	DEC	KWAIT+1

KV2B	LDA	#0
	SEC
	BCS	KVEX
*ANO

KV3	DEC	REPCNT
TST3	BNE	KVEX2

	LDA	$D209
	CMP	#$9F
	BEQ	KV4
	CMP	#$E1
	BEQ	KV4

S15	JSR	VLOZ

KV4	LDA	#3	;REP SPD

KVEX	STA	REPCNT
	BCS	KVEX2

	JMP	$E462
***

KVEX2	JMP	$E45F

***

OLDADR	DFW 0

***

CLBUF	LDX	#0
S16	STX	KRDIX+1
S17	STX	KWRIX+1
	DEX
	STX	764

	RTS
***

KBUFF	DFB 0

RESLEN	EQU	(KBUFF+32)-RESID

***

NEWADR	DFW 0,0

RELOKT	DFW	S1,CLBUF
	DFW	S2,CLBUF
	DFW	S3,BUFBEL2
	DFW	KI4,OLDKEY+1
	DFW	S4,VLOZ
	DFW	S5,KWAIT+1
	DFW	S6,REPCNT
	DFW	S7,KWRIX+1
	DFW	S8,KRDIX+1
	DFW	S9,KWRIX+1
	DFW	S10,KBUFF
	DFW	S11,KBUFF
	DFW	S12,KRDIX+1
	DFW	S13,KWAIT+1
	DFW	S14,KWAIT+1
	DFW	KV3,REPCNT
	DFW	S15,VLOZ
	DFW	KVEX,REPCNT
	DFW	S16,KRDIX+1
	DFW	S17,KWRIX+1

	DFW	NEWADR-1,KEYIRQ
	DFW	NEWADR+1,KEYVBI

	DFW	0,0

**************
*
* KONEC PROGRAMU, NASLEDUJE SLUZEBNI
* RUTINKA
*
**************

UTAJ	LDA	#(PRINT+O):L
	STA	0
	LDA	#(PRINT+O):H
	STA	1

UTAJ2	LDY	#0
	LDA	(0),Y
	EOR	#$55
	STA	(0),Y

	INC	0
	BNE	UTAJ3
	INC	1

UTAJ3	LDA	0
	CMP	#(UTAJ+O):L
	BNE	UTAJ2

	LDA	1
	CMP	#(UTAJ+O):H
	BNE	UTAJ2

	RTS

****

SAVEOD	EQU	$A803
SAVETO	EQU	UTAJ+O
