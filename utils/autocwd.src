*************************************
*				*
*   " A U T O C W D "  FOR BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128

ALINST	EQU	130

***

	ORG	$3000

	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	SYNTAX

	CMP	#'O'
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	START3
*OFF?
	INY
	CMP	(10),Y
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
	JMP	DISABLE
***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: AUTOCWD [Dn:]path"
	DTA	155
	DTA	C"        AUTOCWD OFF"
	DTA	155,0

	JMP	(10)

*Already installed?
START3	JSR	SRCHRUT
	ROR	ALINST
	BMI	START5	;Yes!

*Install
START4	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*Relocate
	LDA	SRCHADR
	CLC
	ADC	#RESNAM-RESID
	STA	R03+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R04+1

	LDA	SRCHADR
	CLC
	ADC	#RESSUB-RESID
	STA	R05+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R05+2
**
	LDY	#RESLEN

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
****

START5	LDY	#33

ST5B	LDA	(10),Y
	STA	RESNAM-33,Y

	INY
	CPY	#61
	BCC	ST5B
*
	LDY	#RESNAM-RESID+27
	LDX	#27

ST5C	LDA	RESNAM,X
	STA	(SRCHADR),Y

	DEY
	DEX
	BPL	ST5C
****
	LDA	ALINST
	BMI	START6
*
	JSR	PRINT
	DTA	C" Installed."
	DTA	155,0

	BEQ	JDOS
**

START6	JSR	PRINT
	DTA	C": Path changed."
	DTA	155,0

	BEQ	JDOS
***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRINT
	DTA	C" not Installed!"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	PRINT
	DTA	C" is not the last handler"
	DTA	155
	DTA	C"installed!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

JDOS	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	CPY	#RESLEN
	BNE	DISCANT
*ODSTRAN

	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**
	JSR	PRINT
	DTA	C" Removed."
	DTA	155,0

	BEQ	JDOS
****

	ICL	"print.inc"
	ICL	"print1.inc"
***

*Print name and search
SRCHRUT	JSR	PRINT
	DTA	155
	DTA	C"AutoCWD"
	DTA	0

	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R05-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#RESNAM-1-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$1A
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744
**
	LDA	#12
R05	JSR	RESSUB

R03	LDA	#<RESNAM
	STA	$354
R04	LDA	#>RESNAM
TST2	STA	$355

	LDA	#0
	STA	$35A
	STA	$35B

	LDA	#44
*
RESSUB	STA	$352
	LDX	#$10

	JMP	$E456
**

RESNAM	DTA	C"There will"
	DTA	C" be specif"
	DTA	C"ied path"
**

RESLEN	EQU	*-RESID

***
