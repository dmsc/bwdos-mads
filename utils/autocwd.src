	ORG	$5FFD,$A800
	OUT	N

O	EQU	$A800-$5FFD
	JMP	UTAJ+O
***
*   PRED SAVE SPUSTIT OD $A800,
*   PAK ULOZIT OD A803 NA 6000
***

*************************************
*				*
*   " A U T O C W D "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EPZ	128

ALINST	EPZ	130

***

ODT0	LDA	START

	LDX	#0
	LDY	#6

ODTAJ	LDA	PRINT,X
	EOR	#$55
ODT2	STA	PRINT,X

	INX
	BNE	ODTAJ

	INC	ODTAJ+2
	INC	ODT2+2

	DEY
	BNE	ODTAJ
*
	LDA	ODT3
	STA	ODT0

ODT3	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS "
	ASC "version !"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	SYNTAX

	CMP	#'O'
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	START3
*OFF?
	INY
	CMP	(10),Y
	BNE	START3

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
	JMP	DISABLE
***

SYNTAX	JSR	PRINT
	DFB 155
	ASC "Syntax: "
	ASC "AUTOCWD [Dn:]path"
	DFB 155
	ASC "        AUTOCWD OFF"
	DFB 155,0

	JMP	(10)

*** NENI UZ ?

START3	LDA	#0
	STA	ALINST

	JSR	SRCHRUT
	BCC	START4
*JE!
	DEC	ALINST
	JMP	START5

***INSTALL

START4	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*RELOK
	LDA	SRCHADR
	CLC
	ADC	#RESNAM-RESID
	STA	R03+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R04+1

	LDA	SRCHADR
	CLC
	ADC	#RESSUB-RESID
	STA	R05+1
	LDA	SRCHADR+1
	ADC	#0
	STA	R05+2
**
	LDY	#RESLEN

INSTL2	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	INSTL2
**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3
****

START5	LDY	#33

ST5B	LDA	(10),Y
	STA	RESNAM-33,Y

	INY
	CPY	#61
	BCC	ST5B
*
	LDY	#RESNAM-RESID
	LDX	#0

ST5C	LDA	RESNAM,X
	STA	(SRCHADR),Y

	INY
	INX
	CPX	#28
	BCC	ST5C
****
	LDA	ALINST
	BMI	START6
*
	JSR	PRINT
	DFB 155
	ASC "AutoCWD "
	ASC "Installed."
	DFB 155,0

	JMP	(10)
**

START6	JSR	PRINT
	DFB 155
	ASC "AutoCWD: Path changed."
	DFB 155,0

	JMP	(10)
***

DISABLE	JSR	SRCHRUT
	BCS	DISABLE2

	JSR	PRINT
	DFB 155
	ASC "AutoCWD"
	ASC " not Installed!"
	DFB 155,0

	JMP	DISERR2
***

DISCANT	JSR	PRINT
	DFB 155
	ASC "AutoCWD is not the"
	ASC " last installed"
	DFB 155
	ASC " handler!"
	DFB 155,0

DISERR2	JSR	PRINT
	ASC "Can't remove."
	DFB 155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	CPY	#RESLEN
	BNE	DISCANT
*ODSTRAN

	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**
	JSR	PRINT
	DFB 155
	ASC "AutoCWD"
	ASC " Removed."
	DFB 155,0

	JMP	(10)
****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R05-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#RESNAM-1-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744
**
	LDA	#12
R05	JSR	RESSUB

R03	LDA	#RESNAM:L
	STA	$354
R04	LDA	#RESNAM:H
TST2	STA	$355

	LDA	#0
	STA	$35A
	STA	$35B

	LDA	#44
*
RESSUB	STA	$352
	LDX	#$10

	JMP	$E456
**

RESNAM	ASC "There will"
	ASC " be specif"
	ASC "ied path"
**

RESLEN	EPZ	*-RESID

***

**************
*
* KONEC PROGRAMU, NASLEDUJE SLUZEBNI
* RUTINKA
*
**************

UTAJ	LDA	#(PRINT+O):L
	STA	0
	LDA	#(PRINT+O):H
	STA	1

UTAJ2	LDY	#0
	LDA	(0),Y
	EOR	#$55
	STA	(0),Y

	INC	0
	BNE	UTAJ3
	INC	1

UTAJ3	LDA	0
	CMP	#(UTAJ+O):L
	BNE	UTAJ2

	LDA	1
	CMP	#(UTAJ+O):H
	BNE	UTAJ2

	RTS

****

SAVEOD	EQU	$A803
SAVETO	EQU	UTAJ+O
