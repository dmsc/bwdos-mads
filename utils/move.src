	ORG	$6000
	JMP	START

*************************************
*				*
*     " M O V E "  PRO BW-DOS	*
*				*
*************************************

NAMOFFS	EQU	128
TMP	EQU	129

EMPTYPOS	EQU	130
STATUS	EQU	132
FILEPOS	EQU	133

SECT1ST	EQU	135
MAINSECT	EQU	137

MAINSTAT	EQU	140
SRCPOS	EQU	141
DSTPOS	EQU	143

SUBD1ST	EQU	145

**VOLNO: 147

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

BADDEV	LDY	#168
	JMP	ERROR

GETPAR	JSR	GETNAME

	LDX	#0
*
	LDY	#34
	LDA	(10),Y
	AND	#15
	STA	RWSDRIV

	DEY
	LDA	(10),Y
	CMP	#'D'
	BNE	BADDEV

GTPAR0	STX	NAMOFFS

GTPAR1	LDA	(10),Y
	STA	SRC,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

	RTS
***

GETFLTR	LDX	NAMOFFS
	LDY	#0

	LDA	#6
	STA	TMP

	DEX

DC1B1	INX

	LDA	SRC,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	CMP	#'?'
	BEQ	DC1BZN

	JSR	JELIPL
	BCS	DC1BNEP
*

DC1BZN	JSR	DC1BPUT
	JMP	DC1B1
**

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP2

DC1BP1	RTS

*

DC1BP2	STA	MSK,Y
	INY
	RTS

**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE
	BCS	DC1B1
**

DC1BNEP	SEC
	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC

	ROR	TMP
	BCC	DC1B1

	RTS

****

ERROR	TYA
	PHA

	JSR	PRINT
	DTA 155
	DTA C"Error "
	DTA 0

	LDX	#2
	PLA

ERR2	LDY	#0

ERR3	CMP	ERRTAB,X
	BCC	ERR4

	SBC	ERRTAB,X
	INY
	BNE	ERR3
**

ERR4	PHA
	TXA
	PHA

	TYA
	ORA	#$30
	JSR	PRT1

	PLA
	TAX
	PLA

	DEX
	BPL	ERR2
**
	LDA	#$9B
	JSR	PRT1

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DTA 1,10,100

***

CLOSE	LDX	#$10

	LDA	#12
	JMP	CIOCMD

***

JELIPL	CMP	#'_'
	BEQ	JLPP

	CMP	#$30
	BCC	JLPM
	CMP	#$3A
	BCC	JLPP
	CMP	#$41
	BCC	JLPM
	CMP	#$5B
	BCS	JLPM

JLPP	CLC
	RTS
*

JLPM	SEC
	RTS
***

OPEN	PHA
	TYA
	PHA
	TXA
	PHA

	JSR	CLOSE

	LDX	#$10
	PLA
	STA	$345,X
	PLA
	STA	$344,X
	PLA
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	LDA	#3

CIOCMD	STA	$342,X

CIO	JSR	$E456
	BPL	GTDRET

CIOERR	JMP	ERROR

***

GETHEAD	JSR	POINT0

GETDIR	LDA	#7
	DTA $2C
PUTDIR	LDA	#11

	LDX	#$10
	STA	$342,X

	LDA	#<DIRBUF
	STA	$344,X
	LDA	#>DIRBUF
	STA	$345,X
	LDA	#23
	STA	$348,X
	LDA	#0
	STA	$349,X

	JSR	$E456
	BPL	GTDRET

	CPY	#136
	BNE	CIOERR

	TYA
GTDRET	RTS

****

NOTE	LDX	#$10
	LDA	#<SRC
	STA	$344,X
	LDA	#>SRC
	STA	$345,X
	LDA	#38

	JSR	CIOCMD

	LDX	#$10
	LDA	$34E,X
	BNE	NOTERR
	LDA	$34D,X
	BMI	NOTERR
	STA	FILEPOS+1
	LDA	$34C,X
	STA	FILEPOS

	RTS
***

NOTERR	LDY	#166
	JMP	ERROR
***

POINT0	LDY	#0
	LDX	#0
	BEQ	POINT

POINTSRC	LDY	SRCPOS
	LDX	SRCPOS+1

POINT	TXA
	LDX	#$10
	STA	$34D,X
	TYA
	STA	$34C,X
	LDA	#0
	STA	$34E,X

	LDA	#<SRC
	STA	$344,X
	LDA	#>SRC
	STA	$345,X

	LDA	#37
	JMP	CIOCMD
***

SEARCH	LDA	#0
	STA	EMPTYPOS
	STA	EMPTYPOS+1

SRCH1	JSR	NOTE

	JSR	GETDIR
	BMI	SRCH2
	LDA	DIRBUF
	BEQ	SRCH2

	AND	#8+16
	CMP	#8
	BNE	SRCH3

	LDX	#10
SRCH1B	LDA	MSK,X
	CMP	#'?'
	BEQ	SRCH1C
	CMP	DIRBUF+6,X
	BNE	SRCH1
SRCH1C	DEX
	BPL	SRCH1B
*NASEL
	LDX	#0
	LDA	DIRBUF
	AND	#32
	BEQ	SRCH1D
	DEX
SRCH1D	STX	STATUS

	LDA	FILEPOS
	STA	EMPTYPOS
	LDA	FILEPOS+1
	STA	EMPTYPOS+1

	LDA	#0
	RTS
**

SRCH3	JSR	CONDEP
	JMP	SRCH1
**

SRCH2	JSR	NOTE
	JSR	CONDEP

	LDA	#255
	RTS
***

CONDEP	LDA	EMPTYPOS
	ORA	EMPTYPOS+1
	BNE	CONDEPX

	LDA	FILEPOS
	STA	EMPTYPOS
	LDA	FILEPOS+1
	STA	EMPTYPOS+1

CONDEPX	RTS

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA 155,253
	DTA C"Incorrect DOS version"
	DTA 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SPSIO+1
	LDA	11
	SBC	#0
	STA	SPSIO+2
***

	JSR	CLOSE
	JSR	GETPAR

	JSR	GETNAME
	LDY	#33
	LDA	(10),Y
	CMP	SRC
	BEQ	STRT2B
	JMP	BADDEV

STRT2B	INY
	LDA	(10),Y
	CMP	SRC+1
	BEQ	STRT2C

*JINY DRIVE

	JSR	PRINT
	DTA 155
	DTA C"Can't move to "
	DTA C"another drive!"
	DTA 155,0
	JMP	ERR5X
***

STRT2C	JSR	GETFLTR

*SEARCH SOURCE

	LDY	#<SRC
	LDX	#>SRC
	LDA	#$14
	JSR	OPEN

	JSR	GETHEAD
	LDX	#22
STRT2CA	LDA	DIRBUF,X
	STA	DIR1HL,X
	DEX
	BPL	STRT2CA

	JSR	SEARCH
	BPL	STRT2D

NOTF	JSR	PRINT
	DTA 155
	DTA C"File not found !"
	DTA 155,0
	JMP	ERR5X

* OK

STRT2D	LDA	STATUS
	STA	MAINSTAT
	LDA	EMPTYPOS
	STA	SRCPOS
	LDA	EMPTYPOS+1
	STA	SRCPOS+1

	LDA	DIRBUF+1
	STA	SUBD1ST
	LDA	DIRBUF+2
	STA	SUBD1ST+1

	LDX	#10
STRT2DB	LDA	DIRBUF+6,X
	STA	MSK,X
	DEX
	BPL	STRT2DB

*** UPRAV DEST.

	JSR	CLOSE

	LDY	#33
	LDX	#0

STRT3A	LDA	(10),Y
	STA	DST,X

	INY
	INX
	CPX	#28
	BCS	STRT3B

	CMP	#155
	BNE	STRT3A

	DEX

STRT3B	LDA	DST-1,X
	CMP	#':'
	BEQ	STRT3C
	CMP	#'<'
	BEQ	STRT3C
	CMP	#'>'
	BEQ	STRT3C

	LDA	#'>'
	JSR	STRT3P

* PRIDEJ FNAME

STRT3C	LDY	#0

STRT3CA	CPY	#8
	BCS	STRT3CB

	LDA	DIRBUF+6,Y
	CMP	#32
	BEQ	STRT3CB

	JSR	STRT3P
	INY
	BNE	STRT3CA
*
STRT3CB	LDA	#'.'
	JSR	STRT3P
	LDY	#8

STRT3CC	CPY	#11
	BCS	STRT3CD

	LDA	DIRBUF+6,Y
	CMP	#32
	BEQ	STRT3CD

	JSR	STRT3P
	INY
	BNE	STRT3CC
*
STRT3CD	LDA	#155
	JSR	STRT3P

	JMP	STRT4
**

STRT3P	STA	DST,X
	INX
	RTS

** SEARCH V DEST DIR

STRT4	LDY	#<DST
	LDX	#>DST
	LDA	#$14
	JSR	OPEN

	JSR	GETHEAD

	LDX	#22
STRT4AX	LDA	DIRBUF,X
	STA	DIR2HL,X
	DEX
	BPL	STRT4AX

	LDX	#22
STRT4AA	LDA	DIRBUF,X
	CMP	DIR1HL,X
	BNE	STRT4AB
	DEX
	BPL	STRT4AA
*SAME DIR
	JSR	PRINT
	DTA 155
	DTA C"Source and destination"
	DTA C" directory"
	DTA 155
	DTA C"is the same."
	DTA 155,0
	JMP	ERR5
**
STRT4AB	JSR	SEARCH
	BMI	STRT5
*UZ JE!
	JSR	PRINT
	DTA 155
	DTA C"May not move - "
	DTA C"already exists"
	DTA 155
	DTA C"in target directory !"
	DTA 155,0
	JMP	ERR5X

*JDE O SUBDIR?

STRT5	LDA	EMPTYPOS
	STA	DSTPOS
	LDA	EMPTYPOS+1
	STA	DSTPOS+1
	BPL	STRT5AA
*
	JSR	PRINT
	DTA 155
	DTA C"Directory full !"
	DTA 155,0
	JMP	ERR5X
**

STRT5AA	JSR	CLOSE

	LDA	MAINSTAT
	BMI	STRT5A
	JMP	STRT6

* SUBDIR -> BUILD PATH!

STRT5A	JSR	SETSIO

	LDA	#0
	TAX
STRT5B	STA	PATHBUF,X
	DEX
	BNE	STRT5B

	LDA	#155
	STA	PATHBUF
*MAIN?
	LDA	DIR2HL+1
	ORA	DIR2HL+2
	BNE	STRT5C
*MAIN!
	LDA	MAINSECT
	STA	SECT1ST
	LDA	MAINSECT+1
	STA	SECT1ST+1
	JMP	STRT6

*NE MAIN

STRT5C	LDX	#22
STRT5CA	LDA	DIR2HL,X
	STA	SECBUF,X
	DEX
	BPL	STRT5CA

	LDX	#10
STRT5CB	LDA	DIR2HL+6,X
	STA	MSK,X
	DEX
	BPL	STRT5CB

*PUT NAME&NUM

STRT5D	LDA	SECBUF+1
	ORA	SECBUF+2
	BNE	STRT5DB
	JMP	STRT5J
*NAME
STRT5DB	LDY	#11
STRT5E	DEY
	CPY	#7
	BEQ	STRT5F

	LDA	SECBUF+6,Y
	CMP	#32
	BEQ	STRT5E

STRT5EB	LDA	SECBUF+6,Y
	JSR	PUTNAME
	DEY
	CPY	#7
	BNE	STRT5EB

	LDA	#'.'
	JSR	PUTNAME

STRT5F	LDY	#8

STRT5G	DEY
	BMI	STRT5H

	LDA	SECBUF+6,Y
	CMP	#32
	BEQ	STRT5G

STRT5GB	LDA	SECBUF+6,Y
	JSR	PUTNAME
	DEY
	BPL	STRT5GB

STRT5H	LDA	#'>'
	JSR	PUTNAME
*NUM
	LDA	SECBUF+1
	CMP	SUBD1ST
	BNE	STRT5I
	LDA	SECBUF+2
	CMP	SUBD1ST+1
	BNE	STRT5I

*NELZE - LOOPED

LOOPED	JSR	PRINT
	DTA 155
	DTA C"May not move - "
	DTA C"loop in directory"
	DTA 155
	DTA C"structure !"
	DTA 155,0
	JMP	ERR5X

*NEXT DIR
STRT5I	LDY	SECBUF+1
	LDX	SECBUF+2
	JSR	RSECT

	LDY	SECBUF+4
	LDX	SECBUF+5
	JSR	RSECT
	JMP	STRT5D

*KONEC PATHU

STRT5J	LDA	#':'
	JSR	PUTNAME
	LDA	SRC+1
	JSR	PUTNAME
	LDA	#'D'
	JSR	PUTNAME

*ZJISTI SECT1ST

	LDY	#<PATHBUF
	LDX	#>PATHBUF
	LDA	#$14
	JSR	OPEN

	JSR	SEARCH
	BPL	STRT5K

*NENASEL - DIVNE...

DIVNE	JSR	PRINT
	DTA 155
	DTA C"Disk corrupted !"
	DTA 155,0
	JMP	ERR5X
**

STRT5K	LDA	STATUS
	BPL	DIVNE
*OK
	LDA	DIRBUF+1
	STA	SECT1ST
	LDA	DIRBUF+2
	STA	SECT1ST+1

	JSR	CLOSE

* NENI PRESUNOVANY SUB-DIR TOTOZNY
* S NOVYM PARENT DIR.?

	LDA	SUBD1ST
	CMP	SECT1ST
	BNE	STRT6
	LDA	SUBD1ST+1
	CMP	SECT1ST+1
	BNE	STRT6
	JMP	LOOPED

** VSE ZJISTENO, LZE PROVEST VLASTNI
** PRESUN!

* READ SOURCE ENTRY

STRT6	LDY	#<SRC
	LDX	#>SRC
	LDA	#$1C
	JSR	OPEN

	JSR	POINTSRC
	JSR	GETDIR

* "DELETE" IT!

	LDA	DIRBUF
	PHA
	LDA	#$10
	STA	DIRBUF

	JSR	POINTSRC
	JSR	PUTDIR
	JSR	CLOSE

	PLA
	STA	DIRBUF

* SAVE DEST ENTRY

	LDY	#<DST
	LDX	#>DST
	LDA	#$1C
	JSR	OPEN

	LDY	DSTPOS
	LDX	DSTPOS+1
	JSR	POINT

	JSR	PUTDIR

	JSR	CLOSE

* SUB-DIR -> UPRAV ODKAZ NA UP-DIR.

	LDA	MAINSTAT
	BMI	STRT6B
	JMP	STRT6C

* USCHOVEJ NAME

STRT6B	LDX	#22
STRT6BA	LDA	DIRBUF,X
	STA	SECBUF,X
	DEX
	BPL	STRT6BA
* OPEN
	LDX	#0
STRT6BAA	LDA	DST,X
	CMP	#$9B
	BEQ	STRT6BAB
	INX
	CPX	#35
	BCC	STRT6BAA
	BCS	STRT6BBE
*
STRT6BAB	STA	DST+4,X
	LDA	#'>'
	STA	DST,X
	LDA	#'*'
	STA	DST+1,X
	STA	DST+3,X
	LDA	#'.'
	STA	DST+2,X

	LDY	#<DST
	LDX	#>DST
	LDA	#$1C
	JSR	OPEN
* READ
	JSR	GETHEAD

* SAME NAME?

	LDX	#10
STRT6BB	LDA	DIRBUF+6,X
	CMP	SECBUF+6,X
	BNE	STRT6BBE
	DEX
	BPL	STRT6BB
	BMI	STRT6BC
*
STRT6BBE	JMP	DIVNE

**

STRT6BC	LDA	SECT1ST
	STA	DIRBUF+1
	LDA	SECT1ST+1
	STA	DIRBUF+2

	JSR	POINT0
	JSR	PUTDIR
	JSR	CLOSE

** HOTOVO!

STRT6C	JSR	PRINT
	DTA 155
	DTA C"O.K."
	DTA 155,0

	JMP	ERR5

***

PUTNAME	PHA

	LDA	PATHBUF+255
	BNE	PTHTOOL

	LDX	#255
PUTNM2	LDA	PATHBUF-1,X
	STA	PATHBUF,X
	DEX
	BNE	PUTNM2

	PLA
	STA	PATHBUF

	RTS
***

PTHTOOL	JSR	PRINT
	DTA 155
	DTA C"Internal buffer for "
	DTA C"dest. path"
	DTA 155
	DTA C"overflow. Sorry!"
	DTA 155,0
	JMP	ERR5X
***

SETSIO	LDA	#128
	STA	SECLEN
	ASL
	STA	SECLEN+1
	TAX
	ROL
	TAY
	JSR	RSECT

	LDA	SECBUF+7
	CMP	#$80
	BNE	SETSIOE

	LDA	SECBUF+31
	STA	SECLEN
	ASL
	BNE	SETSIOE
	ROL
	EOR	#1
	STA	SECLEN+1

	LDA	SECBUF+9
	STA	MAINSECT
	LDA	SECBUF+10
	STA	MAINSECT+1

	RTS
***

SETSIOE	LDY	#148
	JMP	ERROR

***

RSECT	STY	SECNUM
	STX	SECNUM+1

	LDX	#11
RWS2	LDA	RWSIOT,X
	STA	$300,X
	DEX
	BPL	RWS2

	JSR	SPSIO

	TYA
	BMI	ERJP2

	RTS
**

ERJP2	JMP	ERROR

**

RWSIOT	DTA	$31
RWSDRIV	DTA	0,'R'
	DTA	64
	DTA	A(SECBUF)
	DTA 7,0
SECLEN	DTA	A(128)
SECNUM	DTA	A(1)

***

SPSIO	JMP	($FFFF)

***

SRC	EQU	*
MSK	EQU	SRC+40
DST	EQU	MSK+20
DIRBUF	EQU	DST+50
SECBUF	EQU	DIRBUF+40
PATHBUF	EQU	SECBUF+256
DIR1HL	EQU	PATHBUF+256
DIR2HL	EQU	DIR1HL+40

***

