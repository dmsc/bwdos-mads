*************************************
*				*
*     " M O V E "  PRO BW-DOS	*
*				*
*************************************

NAMOFFS	EQU	128
TMP	EQU	129

EMPTYPOS	EQU	130
STATUS	EQU	132
FILEPOS	EQU	133

SECT1ST	EQU	135
MAINSECT	EQU	137

MAINSTAT	EQU	140
SRCPOS	EQU	141
DSTPOS	EQU	143

SUBD1ST	EQU	145

DIRBUF	EQU	147	; 23 bytes
DIR2HL	EQU	170	; 23 bytes

SRC	EQU	193	; 29 bytes
DST	EQU	222	; rest of ZP (and start of stack!)

***
	ORG	$3000
	ICL	"chkdos.inc"
***
	LDA	10
	ADC	#2	; C=1
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SBC	#9	; C=0
	STA	SPSIO+1
	LDA	11
	SBC	#0
	STA	SPSIO+2
***

GETPAR	JSR	GETNAME

	LDX	#0
*
	LDY	#34
	LDA	(10),Y
	AND	#15
	STA	RWSDRIV

	DEY
	LDA	(10),Y
	CMP	#'D'
	BNE	BADDEV

GTPAR0	STX	NAMOFFS

GTPAR1	LDA	(10),Y
	STA	SRC,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

***
	JSR	GETNAME
	LDY	#33
	LDA	(10),Y
	CMP	#'D'
	BEQ	STRT2B

BADDEV	LDY	#168
	JMP	ERROR

STRT2B	INY
	LDA	(10),Y
	CMP	SRC+1
	BEQ	STRT2C

* Drives differ
	JSR	PRINT
	DTA	155
	DTA	C"Can't move to another drive!"
	DTA	155,0
	BEQ	JERR5X
***

STRT2C	JSR	GETFLTR

* Search source by opening directory
	LDY	#SRC
	LDX	#0
	JSR	OPEN14

	JSR	GETHEAD
	LDX	#22
STRT2CA	LDA	DIRBUF,X
	STA	DIR1HL,X
	DEX
	BPL	STRT2CA

	JSR	SEARCH
	BCC	STRT2D

NOTF	JSR	PRINT
	DTA	155
	DTA	C"File not found!"
	DTA	155,0
JERR5X	JMP	ERR5X

* OK

STRT2D	LDA	STATUS
	STA	MAINSTAT
	LDA	EMPTYPOS
	STA	SRCPOS
	LDA	EMPTYPOS+1
	STA	SRCPOS+1

	LDA	DIRBUF+1
	STA	SUBD1ST
	LDA	DIRBUF+2
	STA	SUBD1ST+1

	LDX	#10
STRT2DB	LDA	DIRBUF+6,X
	STA	MSK,X
	DEX
	BPL	STRT2DB

*** UPRAV DEST.

	JSR	CLOSE

	LDY	#33
	LDX	#0

STRT3A	LDA	(10),Y
	STA	DST,X

	INY
	INX
	CPX	#28
	BCS	STRT3B

	CMP	#155
	BNE	STRT3A

	DEX

STRT3B	LDA	DST-1,X
	CMP	#':'
	BEQ	STRT3C
	CMP	#'<'
	BEQ	STRT3C
	CMP	#'>'
	BEQ	STRT3C

	LDA	#'>'
	STA	DST,X
	INX

* PRIDEJ FNAME

STRT3C	LDY	#0

STRT3CA	CPY	#8
	BCS	STRT3CB

	LDA	DIRBUF+6,Y
	CMP	#32
	BEQ	STRT3CB

	STA	DST,X
	INX
	INY
	BNE	STRT3CA
*
STRT3CB	LDA	#'.'
	STA	DST,X
	INX
	LDY	#8

STRT3CC	CPY	#11
	BCS	STRT3CD

	LDA	DIRBUF+6,Y
	CMP	#32
	BEQ	STRT3CD

	STA	DST,X
	INX
	INY
	BNE	STRT3CC
*
STRT3CD	LDA	#155
	STA	DST,X
	INX

** SEARCH V DEST DIR

STRT4	LDY	#DST
	LDX	#0
	JSR	OPEN14

	JSR	GETHEAD

	LDX	#22
STRT4AX	LDA	DIRBUF,X
	STA	DIR2HL,X
	DEX
	BPL	STRT4AX

	LDX	#22
STRT4AA	LDA	DIRBUF,X
	CMP	DIR1HL,X
	BNE	STRT4AB
	DEX
	BPL	STRT4AA
*SAME DIR
	JSR	PRINT
	DTA	155
	DTA	C"Source and destination directory"
	DTA	155
	DTA	C"is the same."
	DTA	155,0
	JMP	ERR5
**
STRT4AB	JSR	SEARCH
	BCS	STRT5
*UZ JE!
	JSR	PRINT
	DTA	155
	DTA	C"May not move - already exists"
	DTA	155
	DTA	C"in target directory!"
	DTA	155,0
	BEQ	JERR5X2

*JDE O SUBDIR?

STRT5	LDA	EMPTYPOS
	STA	DSTPOS
	LDA	EMPTYPOS+1
	STA	DSTPOS+1
	BPL	STRT5AA
*
	JSR	PRINT
	DTA	155
	DTA	C"Directory full!"
	DTA	155,0
JERR5X2	JMP	ERR5X
**

STRT5AA	JSR	CLOSE

	LDA	MAINSTAT
	BMI	STRT5A
	JMP	STRT6

* SUBDIR -> BUILD PATH!

STRT5A	JSR	SETSIO

	LDA	#0
	TAX
STRT5B	STA	PATHBUF,X
	DEX
	BNE	STRT5B

	LDA	#155
	STA	PATHBUF
*MAIN?
	LDA	DIR2HL+1
	ORA	DIR2HL+2
	BNE	STRT5C
*MAIN!
	LDA	MAINSECT
	STA	SECT1ST
	LDA	MAINSECT+1
	STA	SECT1ST+1
	JMP	STRT6

*NE MAIN

STRT5C	LDX	#22
STRT5CA	LDA	DIR2HL,X
	STA	SECBUF,X
	DEX
	BPL	STRT5CA

	LDX	#10
STRT5CB	LDA	DIR2HL+6,X
	STA	MSK,X
	DEX
	BPL	STRT5CB

*PUT NAME&NUM

STRT5D	LDA	SECBUF+1
	ORA	SECBUF+2
	BNE	STRT5DB
	JMP	STRT5J
*NAME
STRT5DB	LDY	#11
STRT5E	DEY
	CPY	#7
	BEQ	STRT5F

	LDA	SECBUF+6,Y
	CMP	#32
	BEQ	STRT5E

STRT5EB	LDA	SECBUF+6,Y
	JSR	PUTNAME
	DEY
	CPY	#7
	BNE	STRT5EB

	LDA	#'.'
	JSR	PUTNAME

STRT5F	LDY	#8

STRT5G	DEY
	BMI	STRT5H

	LDA	SECBUF+6,Y
	CMP	#32
	BEQ	STRT5G

STRT5GB	LDA	SECBUF+6,Y
	JSR	PUTNAME
	DEY
	BPL	STRT5GB

STRT5H	LDA	#'>'
	JSR	PUTNAME
*NUM
	LDA	SECBUF+1
	CMP	SUBD1ST
	BNE	STRT5I
	LDA	SECBUF+2
	CMP	SUBD1ST+1
	BNE	STRT5I

*NELZE - LOOPED

LOOPED	JSR	PRINT
	DTA	155
	DTA	C"May not move - loop in directory"
	DTA	155
	DTA	C"structure!"
	DTA	155,0
	BEQ	JERR5X3

*NEXT DIR
STRT5I	LDY	SECBUF+1
	LDX	SECBUF+2
	JSR	RSECT

	LDY	SECBUF+4
	LDX	SECBUF+5
	JSR	RSECT
	JMP	STRT5D

*KONEC PATHU

STRT5J	LDA	#':'
	JSR	PUTNAME
	LDA	SRC+1
	JSR	PUTNAME
	LDA	#'D'
	JSR	PUTNAME

*ZJISTI SECT1ST

	LDY	#<PATHBUF
	LDX	#>PATHBUF
	JSR	OPEN14

	JSR	SEARCH
	BCC	STRT5K

*NENASEL - DIVNE...

DIVNE	JSR	PRINT
	DTA	155
	DTA	C"Disk corrupted!"
	DTA	155,0
JERR5X3	JMP	ERR5X
**

STRT5K	LDA	STATUS
	BPL	DIVNE
*OK
	LDA	DIRBUF+1
	STA	SECT1ST
	LDA	DIRBUF+2
	STA	SECT1ST+1

	JSR	CLOSE

* NENI PRESUNOVANY SUB-DIR TOTOZNY
* S NOVYM PARENT DIR.?

	LDA	SUBD1ST
	CMP	SECT1ST
	BNE	STRT6
	LDA	SUBD1ST+1
	CMP	SECT1ST+1
	BNE	STRT6
	JMP	LOOPED

** VSE ZJISTENO, LZE PROVEST VLASTNI
** PRESUN!

* READ SOURCE ENTRY

STRT6	LDY	#SRC
	JSR	OPEN01C

	LDA	SRCPOS
	LDX	SRCPOS+1
	JSR	PGETDIR

* "DELETE" IT!

	LDA	DIRBUF
	PHA
	LDA	#$10
	STA	DIRBUF

	LDA	SRCPOS
	LDX	SRCPOS+1
	JSR	PPUTDIR

	PLA
	STA	DIRBUF

* SAVE DEST ENTRY
	JSR	OPEND1C

	LDA	DSTPOS
	LDX	DSTPOS+1
	JSR	PPUTDIR

* SUB-DIR -> UPRAV ODKAZ NA UP-DIR.

	LDA	MAINSTAT
	BMI	STRT6B
	JMP	STRT6C

* USCHOVEJ NAME

STRT6B	LDX	#22
STRT6BA	LDA	DIRBUF,X
	STA	SECBUF,X
	DEX
	BPL	STRT6BA
* OPEN
	LDX	#0
STRT6BAA	LDA	DST,X
	CMP	#$9B
	BEQ	STRT6BAB
	INX
	CPX	#35
	BCC	STRT6BAA
	BCS	STRT6BBE
*
STRT6BAB	STA	DST+4,X
	LDA	#'>'
	STA	DST,X
	LDA	#'*'
	STA	DST+1,X
	STA	DST+3,X
	LDA	#'.'
	STA	DST+2,X

	JSR	OPEND1C
* READ
	JSR	GETHEAD

* SAME NAME?

	LDX	#10
STRT6BB	LDA	DIRBUF+6,X
	CMP	SECBUF+6,X
	BNE	STRT6BBE
	DEX
	BPL	STRT6BB
	BMI	STRT6BC
*
STRT6BBE	JMP	DIVNE

**

STRT6BC	LDA	SECT1ST
	STA	DIRBUF+1
	LDA	SECT1ST+1
	STA	DIRBUF+2

	LDA	#0
	TAX
	JSR	PPUTDIR

** HOTOVO!

STRT6C	JSR	PRINT
	DTA	155
	DTA	C"O.K."
	DTA	155,0
	BEQ	ERR5

***

* Setup SIO
SETSIO	LDX	#0
	LDY	#1
	JSR	RSECT

	LDA	SECBUF+7
	CMP	#$80
	BNE	SETSIOE

	LDA	SECBUF+31
	STA	SECLEN
	ASL		; C=1 from CMP
	BNE	SETSIOE
	BCS	SETSIO1
	INC	SECLEN+1

SETSIO1	LDA	SECBUF+9
	STA	MAINSECT
	LDA	SECBUF+10
	STA	MAINSECT+1

	RTS

***

RSECT	STY	SECNUM
	STX	SECNUM+1

	LDX	#11
RWS2	LDA	RWSIOT,X
	STA	$300,X
	DEX
	BPL	RWS2

	JSR	SPSIO

	TYA
	BMI	ERROR

	RTS

***

SETSIOE	LDY	#148

****
	ICL	"error.inc"

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR5	JSR	CLOSE

	JMP	(10)
***

OPEND1C	LDY	#DST
OPEN01C	LDX	#0
OPEN1C	LDA	#$1C
	DTA	$2C
OPEN14	LDA	#$14
OPEN	PHA
	TYA
	PHA
	TXA
	PHA

	JSR	CLOSE

	PLA
	STA	$355
	PLA
	STA	$354
	PLA
	STA	$35A
	LDA	#0
	STA	$35B

	LDA	#3

CIOCMD	STA	$352
	LDX	#$10
	JSR	$E456
	BPL	CIORET
	CPY	#136
	BNE	ERROR
CIORET	RTS

***
* POINT + WRITE + CLOSE
PPUTDIR	JSR	POINT
	JSR	PUTDIR

CLOSE	LDA	#12
	BNE	CIOCMD

NOTERR	LDY	#166
	BNE	ERROR

***
PTHTOOL	JSR	PRINT
	DTA	155
	DTA	C"Destination path too long, sorry!"
	DTA	155,0
	JMP	ERR5X

***

GETHEAD	LDA	#0
	TAX
PGETDIR	JSR	POINT

GETDIR	LDA	#7
	DTA	$2C
PUTDIR	LDA	#11

	LDX	#DIRBUF
	STX	$354
	LDX	#0
	STX	$355
	STX	$359
	LDX	#23
	STX	$358
	BNE	CIOCMD

****

NOTE	LDA	#38
	JSR	CIOCMD

	LDA	$35E
	BNE	NOTERR
	LDA	$35D
	BMI	NOTERR
	STA	FILEPOS+1
	LDA	$35C
	STA	FILEPOS

	RTS
***

PUTNAME	PHA

	LDA	PATHBUF+255
	BNE	PTHTOOL

	LDX	#255
PUTNM2	LDA	PATHBUF-1,X
	STA	PATHBUF,X
	DEX
	BNE	PUTNM2

	PLA
	STA	PATHBUF

	RTS

* Search for the file, and also for the first empty position
* in the directory.
SEARCH	LDA	#0
	STA	EMPTYPOS
	STA	EMPTYPOS+1

SRCH1	JSR	NOTE

	JSR	GETDIR
	BEQ	SRCH2	; EOF
	LDA	DIRBUF
	BEQ	SRCH2	; End of entries

	AND	#8+16
	CMP	#8
	BNE	SRCH3	; Found an empty or erased position

	LDX	#10
SRCH1B	LDA	MSK,X
	CMP	#'?'
	BEQ	SRCH1C
	CMP	DIRBUF+6,X
	BNE	SRCH1
SRCH1C	DEX
	BPL	SRCH1B

* Found, store if it is a file or directory
	LDX	#0
	LDA	DIRBUF
	AND	#32
	BEQ	SRCH1D
	DEX
SRCH1D	STX	STATUS

	CLC	; C=0: FOUND
STOPOS	LDA	FILEPOS
	STA	EMPTYPOS
	LDA	FILEPOS+1
	STA	EMPTYPOS+1
	RTS
**

SRCH3	JSR	CONDEP
	JMP	SRCH1
**

SRCH2	JSR	NOTE
	SEC	; C=1: NOT FOUND

CONDEP	LDA	EMPTYPOS
	ORA	EMPTYPOS+1
	BEQ	STOPOS
	RTS

***
***
GETFLTR	LDX	NAMOFFS
	LDY	#0

	LDA	#6
	STA	TMP

	DEX

DC1B1	INX

	LDA	SRC,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	CMP	#'?'
	BEQ	DC1BZN

	CMP	#'_'
	BEQ	DC1BZN

	CMP	#'0'
	BCC	DC1BNEP
	CMP	#'9'+1
	BCC	DC1BZN
	CMP	#'A'
	BCC	DC1BNEP
	CMP	#'Z'+1
	BCS	DC1BNEP

DC1BZN	JSR	DC1BPUT
	JMP	DC1B1
**

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP2

DC1BP1	RTS

*

DC1BP2	STA	MSK,Y
	INY
	RTS

**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE
	BCS	DC1B1
**

DC1BNEP	SEC
	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC

	ROR	TMP
	BCC	DC1B1

	RTS

***
POINT	STX	$35D
	STA	$35C
	LDX	#0
	STX	$35E

	LDA	#37
	JMP	CIOCMD
***

	ICL	"print.inc"
	ICL	"print1.inc"

***
GETNAME	JMP	PRTEX
***
SPSIO	JMP	($FFFF)

* SIO command
RWSIOT	DTA	$31
RWSDRIV	DTA	0,'R'
	DTA	64
	DTA	A(SECBUF)
	DTA	7,0
SECLEN	DTA	A(128)
SECNUM	DTA	A(1)

* Binary to decimal conversion table
ERRTAB	DTA	100,10,1

************

MSK	ORG	*+20
SECBUF	ORG	*+256
PATHBUF	ORG	*+256
DIR1HL	ORG	*+40

***
