	ORG	$4000
	JMP	START

*************************************
*				*
*   " U N E R A S E "  PRO BW-DOS	*
*				*
*************************************

NAMOFFS	EQU	128
TMP	EQU	129
SIOADR	EQU	130

DIRPOS	EQU	132
VTOCADR	EQU	134
VTOCMSK	EQU	136
MAPSEC	EQU	137
FFLG	EQU	139
MAPIX	EQU	140

**VOLNO: 141

SRC	EQU	$500
MSK	EQU	$540
DIRBUF	EQU	$5C0

BUFF1	EQU	$5000
MAPBUF	EQU	$5100
PATHBUF	EQU	$5200
VTOCBUF	EQU	$5300

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GETPAR	JSR	GETNAME

	LDX	#0
*
	LDY	#34
	LDA	(10),Y
	AND	#15
	STA	RWSDRIV

	DEY

	LDA	(10),Y
	CMP	#'D'
	BEQ	GTPAR0

	LDY	#146	;NOT "D:"
	JMP	ERROR
**

GTPAR0	STX	NAMOFFS

GTPAR1	LDA	(10),Y
	STA	SRC,X

	INX
	INY

	CMP	#':'
	BEQ	GTPAR0
	CMP	#'>'
	BEQ	GTPAR0
	CMP	#'<'
	BEQ	GTPAR0

	CMP	#$9B
	BNE	GTPAR1

	RTS
***

GETFLTR	LDX	NAMOFFS
	LDY	#0

	LDA	#6
	STA	TMP

	DEX

DC1B1	INX

	LDA	SRC,X
	CMP	#'*'
	BEQ	DC1BHVE

	CMP	#'.'
	BEQ	DC1BTEC

	CMP	#'?'
	BEQ	DC1BZN

	JSR	JELIPL
	BCS	DC1BNEP
*

DC1BZN	JSR	DC1BPUT
	JMP	DC1B1
**

DC1BPUT	CPY	#8
	BCC	DC1BP2

	BIT	TMP
	BPL	DC1BP1

	CPY	#11
	BCC	DC1BP2

DC1BP1	RTS

*

DC1BP2	STA	MSK,Y
	INY
	RTS

**

DC1BHVE	LDA	#'?'
	JSR	DC1BPUT
	BCC	DC1BHVE
	BCS	DC1B1
**

DC1BNEP	SEC
	ROR	TMP

DC1BTEC	LDA	#$20
	JSR	DC1BPUT
	BCC	DC1BTEC

	ROR	TMP
	BCC	DC1B1

	RTS

****
	ICL	"error.inc"

ERR5X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474	;XDIVIO

ERR5	JSR	CLOSE

	JMP	(10)
**

ERRTAB	DTA	100,10,1

***

CLOSE	LDX	#$10

	LDA	#12
	STA	$342,X

	JSR	$E456
	BMI	ERROR

	RTS
***

JELIPL	CMP	#'_'
	BEQ	JLPP

	CMP	#$30
	BCC	JLPM
	CMP	#$3A
	BCC	JLPP
	CMP	#$41
	BCC	JLPM
	CMP	#$5B
	BCS	JLPM

JLPP	CLC
	RTS
*

JLPM	SEC
	RTS
***

CIOSRC	LDA	#<SRC
	STA	$344,X
	LDA	#>SRC
	STA	$345,X
	JMP	$E456

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	STA	SPSIO+1
	LDA	11
	SBC	#0
	STA	SIOADR+1
	STA	SPSIO+2
***

	JSR	PRINT
	DTA 155
	DTA C"UNERASE ver. 1.2 for"
	DTA C" BW-DOS"
	DTA 155,0
***

	JSR	GETPAR
	JSR	GETFLTR
*OPEN

	JSR	CLOSE

	JSR	GETPATH
*

	LDA	#23
	STA	DIRPOS
	LDA	#0
	STA	DIRPOS+1

	JSR	REOPEN

* SEARCH

* NOTE

MAIN2	LDX	#$10
	LDA	#38
	STA	$342,X

	JSR	CIOSRC
	BMI	ERRJP

	LDX	#$10
	LDA	$34C,X
	STA	DIRPOS

	LDY	#$A9
	LDA	$34D,X
	STA	DIRPOS+1
	BMI	ERRJP
	LDA	$34E,X
	BNE	ERRJP
*READ
	LDA	#7
	JSR	RWDIR
	BPL	MAIN2B
*
	CPY	#136
	BNE	ERRJP

	JSR	PRINT
	DTA 155
	DTA C"No more files found."
	DTA 155,0

	JSR	CLOSE
	JMP	(10)

ERRJP	JMP	ERROR

***

MAIN2B	LDX	#10

MAIN2C	LDA	MSK,X
	CMP	#'?'
	BEQ	MAIN2D

	CMP	DIRBUF+6,X
	BNE	MAIN2

MAIN2D	DEX
	BPL	MAIN2C
*STAT
	LDA	DIRBUF
	AND	#$30
	CMP	#$10
	BNE	MAIN2

* NASEL!
	JSR	CLOSE

	LDX	#10
MAIN2E	LDA	DIRBUF+6,X
	STA	MAIN2F,X
	DEX
	BPL	MAIN2E

	JSR	PRINT
	DTA 155
	DTA C"> "
MAIN2F	DTA C"            ..."
	DTA 0

*RD SECT 1

	JSR	RDSEC1
	JMP	UNER1

NOTSP	LDY	#$94
ERJP3	JMP	ERROR

*VTOCs

UNER1	LDA	#<VTOCBUF
	STA	VTOCADR
	STA	RWSADR
	LDA	#>VTOCBUF
	STA	VTOCADR+1
	STA	RWSADR+1

	LDA	#0
	TAY
	LDX	#$20

UNER1B	STA	(VTOCADR),Y
	INY
	BNE	UNER1B

	INC	VTOCADR+1
	DEX
	BNE	UNER1B
*READ
	LDA	BUFF1+15
	BEQ	NOTSP
	STA	VTOCADR

	LDA	BUFF1+16
	STA	$30A
	LDA	BUFF1+17
	STA	$30B

UNER1C	JSR	RDSEC31

	INC	$30A
	BNE	UNER1D
	INC	$30B

UNER1D	LDA	RWSADR
	CLC
	ADC	RWSLEN
	STA	RWSADR

	LDA	RWSADR+1
	ADC	RWSLEN+1
	STA	RWSADR+1

	DEC	VTOCADR
	BNE	UNER1C

** UNERASE IT !!

	LDA	DIRBUF+1
	STA	MAPSEC
	LDA	DIRBUF+2
	STA	MAPSEC+1

	LDA	#0
	STA	FFLG

UNER2	LDA	MAPSEC
	STA	$30A
	LDA	MAPSEC+1
	STA	$30B

	JSR	RDSECMP

	LDA	FFLG
	BNE	UNER2B

	DEC	FFLG

	LDA	MAPBUF+2
	ORA	MAPBUF+3
	BNE	NELZEJ
**

UNER2B	LDY	MAPSEC
	LDX	MAPSEC+1
	JSR	BLOKIT
	BCS	NELZEJ

	LDA	#4
	STA	MAPIX

UNER2C	LDX	MAPIX
	CPX	BUFF1+31
	BEQ	UNER2D

	INC	MAPIX
	INC	MAPIX

	LDA	MAPBUF,X
	TAY
	ORA	MAPBUF+1,X
	BEQ	UNER2C

	LDA	MAPBUF+1,X
	TAX

	JSR	BLOKIT
	BCC	UNER2C

NELZEJ	JMP	NELZE

**

UNER2D	LDA	MAPBUF
	STA	MAPSEC
	LDA	MAPBUF+1
	STA	MAPSEC+1

	ORA	MAPSEC
	BNE	UNER2

** WR SECTORY

	INC	BUFF1+38

	LDA	#<BUFF1
	STA	RWSADR
	LDA	#>BUFF1
	STA	RWSADR+1

	LDX	#1
	STX	$30A
	DEX
	STX	$30B

	LDX	#$80
	JSR	WRSEC

*VTOCs
	LDA	#<VTOCBUF
	STA	RWSADR
	LDA	#>VTOCBUF
	STA	RWSADR+1

	LDA	BUFF1+15
	STA	VTOCADR

	LDA	BUFF1+16
	STA	$30A
	LDA	BUFF1+17
	STA	$30B

UNER3C	LDX	BUFF1+31
	JSR	WRSEC

	INC	$30A
	BNE	UNER3D
	INC	$30B

UNER3D	LDA	RWSADR
	CLC
	ADC	RWSLEN
	STA	RWSADR

	LDA	RWSADR+1
	ADC	RWSLEN+1
	STA	RWSADR+1

	DEC	VTOCADR
	BNE	UNER3C

** ZAPIS DO DIR.

	JSR	REOPEN

*MODIF. STAT.

	LDA	DIRBUF
	AND	#(255-26)
	ORA	#8
	STA	DIRBUF
*WRITE
	LDA	#11
	JSR	RWDIR
	BMI	ERRJP5

** 1 FILE UNERASED

	JSR	PRINT
	DTA C" Done."
	DTA 0

	JMP	MAIN2

*** NELZE!

NELZE	JSR	PRINT
	DTA C" Can't restore !"
	DTA 0

	JSR	REOPEN

	LDA	#7
	JSR	RWDIR

	JMP	MAIN2
***

RWDIR	LDX	#$10
	STA	$342,X

	LDA	#<DIRBUF
	STA	$344,X
	LDA	#>DIRBUF
	STA	$345,X

	LDA	#23
	STA	$348,X
	LDA	#0
	STA	$349,X

	JMP	$E456

***

REOPEN	LDX	#$10

	LDA	#44	;CWD
	STA	$342,X

	LDA	#0
	STA	$34A,X
	STA	$34B,X

	LDA	#<PATHBUF
	STA	$344,X
	LDA	#>PATHBUF
	STA	$345,X

	JSR	$E456
	BMI	ERRJP5
*
REOPNB	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#$1C
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIOSRC
	BPL	UNER4

ERRJP5	JMP	ERROR

*POINT

UNER4	LDX	#$10
	LDA	#37
	STA	$342,X

	LDA	DIRPOS
	STA	$34C,X
	LDA	DIRPOS+1
	STA	$34D,X
	LDA	#0
	STA	$34E,X

	JSR	CIOSRC
	BMI	ERRJP5

	RTS
***

BLKITTB	DTA 128,64,32,16
	DTA 8,4,2,1
**

BLOKIT	CPX	BUFF1+12
	BCC	BLKIT0
	BNE	BLKITER

	CPY	BUFF1+11
	BCC	BLKIT0
	BEQ	BLKIT0

BLKITER	SEC
	RTS

**

BLKIT0	TYA
	PHA

	AND	#7
	TAY
	LDA	BLKITTB,Y
	STA	VTOCMSK

	PLA

	LDY	#3

BLKIT1	PHA
	TXA
	LSR
	TAX
	PLA
	ROR

	DEY
	BNE	BLKIT1

	CLC
	ADC	#<VTOCBUF
	STA	VTOCADR

	TXA
	ADC	#>VTOCBUF
	STA	VTOCADR+1

***
	LDY	#0
	LDA	(VTOCADR),Y
	AND	VTOCMSK
	BEQ	BLKITER

	LDA	(VTOCADR),Y
	EOR	VTOCMSK
	STA	(VTOCADR),Y

*FREE SECTORS

	LDA	BUFF1+13
	BNE	BLKIT2
	DEC	BUFF1+14
BLKIT2	DEC	BUFF1+13

	CLC
	RTS
***

RDSECMP	LDA	#<MAPBUF
	STA	RWSADR
	LDA	#>MAPBUF
	STA	RWSADR+1

RDSEC31	LDX	BUFF1+31

RDSEC	LDA	#'R'
	LDY	#64
	BNE	RWS1
*

WRSEC	LDY	#8	;WRTCMD
	LDA	(SIOADR),Y
	LDY	#128
*

RWS1	STA	RWSIOT+2
	STY	RWSIOT+3

	TXA
	STA	RWSLEN
	ASL
	ROL
	EOR	#1
	STA	RWSLEN+1

	TXA
	ASL
	BNE	RWSER

	LDX	#9
RWS2	LDA	RWSIOT,X
	STA	$300,X
	DEX
	BPL	RWS2

	JSR	SPSIO

	TYA
	BMI	ERJP2

	RTS
**

RWSER	LDY	#$8F

ERJP2	JMP	ERROR

**

RWSIOT	DTA	$31
RWSDRIV	DTA	0,0,0
RWSADR	DTA	A(SRC)
	DTA 7,0
RWSLEN	DTA	A(128)

***

SPSIO	JMP	($FFFF)

***

GETPATH	LDX	#0
	LDA	#$20	;SPC
GPT1	STA	PATHBUF,X
	INX
	BNE	GPT1

	LDX	#4
GPT2	LDA	SRC,X
	STA	PATHBUF,X
	DEX
	BPL	GPT2

*CURRENT DIR.

	LDA	#'*'
	STA	SRC+3
	LDA	#$9B
	STA	SRC+4

	LDA	#0
	STA	DIRPOS
	STA	DIRPOS+1

	JSR	REOPNB	;OPEN+POINT

	LDA	#7
	JSR	RWDIR
	BPL	GPT3A

	JMP	ERROR

GPT3A	LDX	#22
GPT3	LDA	DIRBUF,X
	STA	MAPBUF,X
	DEX
	BPL	GPT3

	JSR	CLOSE

	LDA	PATHBUF+3
	STA	SRC+3
	LDA	PATHBUF+4
	STA	SRC+4
*
	LDA	#'>'
	STA	PATHBUF+3
	LDA	#$9B
	STA	PATHBUF+4
*
	JSR	RDSEC1

* DALSI UROVEN

GPT4	LDA	MAPBUF+1
	STA	$30A
	LDA	MAPBUF+2
	STA	$30B
	ORA	$30A
	BNE	GPT5
*MAIN!
	RTS

*CONVERT FNAME

GPT5	LDX	#0
	LDY	#1

	LDA	#'>'
	STA	DIRBUF

GPT6	LDA	MAPBUF+6,X
	CMP	#$20
	BEQ	GPT7

	STA	DIRBUF,Y
	INY

	INX
	CPX	#8
	BCC	GPT6

GPT7	LDA	#'.'
	STA	DIRBUF,Y
	INY

	LDX	#8

GPT8	LDA	MAPBUF+6,X
	CMP	#$20
	BEQ	GPT9

	STA	DIRBUF,Y
	INY

	INX
	CPX	#11
	BCC	GPT8
	BCS	GPT9

* VLOZ

GPT9B	LDA	DIRBUF,Y
	JSR	PTHINS

GPT9	DEY
	BPL	GPT9B

* READ NEXT LEVEL

	JSR	RDSECMP

	LDA	MAPBUF+2
	ORA	MAPBUF+3
	BNE	GPTERR

	LDA	MAPBUF+4
	STA	$30A
	LDA	MAPBUF+5
	STA	$30B
	ORA	$30A
	BEQ	GPTERR

	JSR	RDSECMP
	JMP	GPT4
***

GPTERR	JSR	PRINT
	DTA 155
	DTA C"Can't find path to "
	DTA C"current directory!"
	DTA 155,0

	JMP	ERR5X
***

PTHINS	LDX	PATHBUF+$FF
	BMI	GPTERR

	LDX	#$FF
	STA	PATHBUF+2

PTHI2	LDA	PATHBUF-1,X
	STA	PATHBUF,X

	DEX
	CPX	#3
	BCS	PTHI2

	LDA	#':'
	STA	PATHBUF+2

RDS1RTS	RTS

***

RDSEC1	LDA	#<BUFF1
	STA	RWSADR
	LDA	#>BUFF1
	STA	RWSADR+1

	LDX	#1
	STX	$30A
	DEX
	STX	$30B

	LDX	#$80
	JSR	RDSEC

	LDA	BUFF1+7
	CMP	#$80
	BEQ	RDS1RTS

	JMP	NOTSP
***
