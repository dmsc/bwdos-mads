*************************************
*				*
*      " X B A T "  FRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

NAME	EQU	$3000

	ORG	$3100

START	LDA	$700
	CMP	#'S'
	BEQ	START2
	LDA	#<ERRDOS
	LDX	#ERRTAB-ERRDOS

	JMP	ERRX

START2
***
	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2

GETNAME	JSR	GETNAME

***
	LDY	#33
	LDX	#256-28

ST3	LDA	(10),Y

	CMP	#$9B
	BNE	ST6

ST4	LDX	#0
ST5	LDA	COMNAM,X

ST6	STA	NAME-33,Y
	INY
	INX
	BMI	ST3
	CPX	#5
	BNE	ST5

*XDIV
	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

*Check for exe file
	JSR	CLOSE

	LDY	#3
	JSR	CIONAME
	JSR	TESTFF
	JSR	TESTFF
	JSR	CLOSE
*LOAD
	LDY	#40

CIONAME	LDX	#7
CCPY	LDA	CIOTAB,X
	STA	$344+$10,X
	DEX
	BPL	CCPY

	JSR	CIO10
	BMI	ERROR
CRTS	RTS

CIOTAB	DTA	<NAME,>NAME,0,0,0,0,4,0

*CLOSE
CLOSE	LDY	#12
CIO10	LDX	#$10
	STY	$342+$10
	JMP	$E456
***

TESTFF	LDY	#7
	JSR	CIONAME

	CMP	#$FF
	BEQ	CRTS

TFFER	LDY	#152

*Show ERROR and number
ERROR	TYA
	LDX	#$FD

ERR2	SEC

ERR4	SBC	ERRTAB-$FD,X
	INC	ERR4N-$FD,X
	BCS	ERR4
**
	ADC	ERRTAB-$FD,X

	INX
	BNE	ERR2

	LDA	#<ERRMSG
	LDX	#ERRDOS-ERRMSG+1	;Add 1 for the last CR

ERRX	STA	$344
	LDA	#>ERRMSG
	STA	$345
	STX	$348
	LDX	#0
	STX	$349
	LDA	#11
	STA	$342

	JSR	$E456

ERR5	JSR	CLOSE

	JMP	(10)
**

COMNAM	DTA	C".COM"
ERRMSG	DTA	155
	DTA	C"Error "
ERR4N	DTA	C"///"
ERRDOS	DTA	155,253
	DTA	C"Incorrect DOS version"
	DTA	155

***
ERRTAB	DTA	100,10,1


