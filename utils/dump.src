*************************************
*				*
*     " D U M P "   PRO BW-DOS	*
*				*
*************************************

HEX	EQU	128

POS	EQU	131
LEN	EQU	134

ASCII	EQU	137

LLEN	EQU	138
LPOS	EQU	139

SAVE82	EQU	140

STATUS	EQU	141
LIX	EQU	142

* VOLNO: 143

	ORG	$3000

NAME	EQU	*+$800
***
	ICL	"chkdos.inc"
***
	LDA	82
	STA	SAVE82
	LDA	#0
	STA	82

	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***

	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***

	LDX	#0
	STX	POS
	STX	POS+1
	STX	POS+2
	STX	ASCII

	DEX
	STX	LEN
	STX	LEN+1
	STX	LEN+2
***

	JSR	GETHEX
	BEQ	ST6

**

ST5	LDX	#2
ST5B	LDA	HEX,X
	STA	POS,X
	DEX
	BPL	ST5B
**

ST6	JSR	GETHEX
	BEQ	ST6D

**

ST6B	LDX	#2
ST6C	LDA	HEX,X
	STA	LEN,X
	DEX
	BPL	ST6C
**

ST6D	JSR	GETHEX

*** OPEN

ST8	JSR	CLOSE

	LDA	#4
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDA	#3
	JSR	CIONAME
	BPL	ST9
**
	JSR	VRAT82
	JSR	PRINT
	DTA C"Can't open file!"
	DTA 0
	JMP	ERR2X

*** POINT

ST9	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDA	POS
	STA	$34C+$10
	LDA	POS+1
	STA	$34D+$10
	LDA	POS+2
	STA	$34E+$10

	LDA	#37
	JSR	CIONAME
	BPL	ST10
***
	JSR	VRAT82
	JSR	PRINT
	DTA C"Can't set "
	DTA C"file-position!"
	DTA 0
	JMP	ERR2X

*** DUMP IT !

ST10	LDA	#155
	JSR	PRT1

*GET

DUMP1	LDA	#8
	STA	$348+$10
	LDA	#0
	STA	LPOS
	STA	$349+$10

	LDA	#7
	JSR	CIONAME
	STY	STATUS

	LDA	$348+$10
	STA	LLEN
*LEN
	LDA	LEN
	TAX
	SEC
	SBC	LLEN
	STA	LEN
	LDA	LEN+1
	SBC	#0
	STA	LEN+1
	LDA	LEN+2
	SBC	#0
	STA	LEN+2
	BCS	DUMP1A

	STX	LLEN
	LDA	#136
	STA	STATUS

DUMP1A	LDA	LLEN
	BNE	DUMP1B
	JMP	DUMPX
*POS

DUMP1B	LDA	POS+2
	JSR	DPRTHX
	LDA	POS+1
	JSR	DPRTHX
	LDA	POS
	JSR	DPRTHX

	JSR	DSPAC

	LDA	LLEN
	CLC
	ADC	POS
	STA	POS
	BCC	DUMP2
	INC	POS+1
	BNE	DUMP2
	INC	POS+2

*** DATA

DUMP2	LDX	#0
DUMP2A	STX	LIX
	LDA	NAME,X
	JSR	DPRTHX

	JSR	DSPAC

	LDX	LIX
	INX
	CPX	LLEN
	BCC	DUMP2A

	BCS	DUMP3

*** MOVE

DUMP3A	JSR	DSPAC

DUMP3	LDA	LPOS
	CMP	#31
	BCC	DUMP3A

*** CHARS
	LDX	#0
DUMP4	STX	LIX
	LDA	NAME,X

	LDX	ASCII
	BNE	DUMP4A

	CMP	#$9B
	BNE	DUMP4AA
	LDA	#$20

DUMP4AA	PHA
	LDA	#27
	JSR	PRT1
	PLA
	JMP	DUMP4D
**

DUMP4A	AND	#127
	CMP	#125
	BCC	DUMP4B
	LDA	#'.'

DUMP4B	CMP	#32
	BCS	DUMP4D
	ORA	#64

DUMP4D	JSR	DPRT1

	LDX	LIX
	INX
	CPX	LLEN
	BCC	DUMP4
*** EOL

	JSR	PRTEOL

*** NEXT?

DUMPX	LDA	STATUS
	BMI	DUMPX2

	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1
***

DUMPX2	CMP	#136
	BEQ	DUMPX3

	JSR	VRAT82
	JSR	PRINT
	DTA C"Error while reading!"
	DTA 0

	JMP	ERR2X

***

DESC	LDA	#$FF
	STA	764

	JSR	VRAT82
	JSR	PRINT
	DTA C"<Aborted>"
	DTA 0
DUMPX3	JMP	ERR2

*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DTA $2C
DSPAC	LDA	#' '

DPRT1	INC	LPOS
	JMP	PRT1

***
	ICL	"print.inc"
***

VRAT82	LDA	SAVE82
	STA	82

PRTEOL	LDA	#$9B

**
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#1
GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEX
	STA	HEX+1
	STA	HEX+2

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXOPT

GETHX2	CPY	#43
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	BEQ	GETHX2
***

GHXOPT	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	GHXERR
	DEC	ASCII
	LDY	#0
	RTS

GHXERR	JSR	VRAT82
	JSR	PRINT
	DTA C"Bad parameter!"
	DTA 0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	VRAT82

	JSR	CLOSE
	JMP	(10)

***

CLOSE	LDA	#12
***
CIONAME	LDX	#$10
	STA	$342,X
	LDA	#<NAME
	STA	$344,X
	LDA	#>NAME
	STA	$345,X

	JMP	$E456
***
