*************************************
*				*
*     " D U M P "   FOR BW-DOS	*
*				*
*************************************

HEX	EQU	128
POS	EQU	131
LEN	EQU	134
ASCII	EQU	137
SAVE82	EQU	138

	ORG	$3000
***
START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<PBADVER
	LDX	#PEND-PBADVER+1
	JMP	PRINT

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2
*Store old right-margin value, and set to 0
	LDA	82
	STA	SAVE82
	LDA	#0
	STA	82
	STA	ASCII

*Read file name
	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3

*Clear POS to 0 and LEN to $FFFFFF
	LDY	#2
CLRV	LDX	#0
	STX	POS,Y
	DEX
	STX	LEN,Y
	DEY
	BPL	CLRV

*Get POS
	LDY	#<POS
	JSR	GETHEX
*Get LEN
	LDY	#<LEN
	JSR	GETHEX
*Get optional '/A'
	LDY	#<HEX
	JSR	GETHEX

*Open file
	JSR	CLOSE
	LDA	#3
	JSR	CIONAME
	BPL	ST9
*Open ERROR
	LDA	#<PEOPEN
	LDX	#PEPOINT-PEOPEN+1
	JMP	PERROR

*Set file position
ST9	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	DUMP1

	LDX	#2

ST10	LDA	POS,X
	STA	$34C+$10,X
	DEX
	BPL	ST10

	LDA	#37
	JSR	CIONAME
	BPL	DUMP1
*Point ERROR
	LDA	#<PEPOINT
	LDX	#PBADPAR-PEPOINT+1
	JMP	PERROR

*Load data for the line
DUMP1	LDA	#7
	JSR	CIONAME
	BPL	DUMP2
	CPY	#136
	BEQ	DUMP2
*Read ERROR
	LDX	#PEOPEN-PEREAD+1
	LDA	#<PEREAD
	JSR	PERROR

*Clear line and write position
DUMP2	LDX	#39
	LDA	#$20
CBUF	STA	BUFF,X
	DEX
	BNE	CBUF

	LDY	#3
PRTPOS	LDA	POS-1,Y
	JSR	PRTHEX
	DEY
	BNE	PRTPOS
	INX

*Copy bytes to line buffer
*Y=0

DUMP1A	CPY	$348+$10
	BEQ	DUMP1D

*Decrement LEN and test if reaches 0
	LDA	LEN
	BNE	DUMP1B
	LDA	LEN+1
	BNE	DUMP1C
	LDA	LEN+2
	BEQ	DUMP1D
	DEC	LEN+2
DUMP1C	DEC	LEN+1
DUMP1B	DEC	LEN

*Print HEX byte
	LDA	NAME,Y
	PHA
	JSR	PRTHEX
	INX

*Print ASCII byte
	PLA
	BIT	ASCII
	BMI	DUMP4A

*Replace EOL with SPC
	CMP	#$9B
	BNE	DUMP4D
	LDA	#$20

*Replace all INV and CTRL chars
DUMP4A	AND	#127
	CMP	#125
	BCC	DUMP4B
	LDA	#'.'

DUMP4B	CMP	#32
	BCS	DUMP4D
	ORA	#64

DUMP4D	STA	BUFF+32,Y

*Increment POS
	INC	POS
	BNE	DUMP1X
	INC	POS+1
	BNE	DUMP1X
	INC	POS+2

DUMP1X	INY
	BNE	DUMP1A

DUMP1D	LDX	#1
	TYA
	BEQ	DUMPX2

*Write line
	STX	$2FE
	LDA	#<BUFF
	LDX	#40
	JSR	PRINTX
	DEC	$2FE

*Check for ESC key
	LDA	764
	CMP	#$1C
	BEQ	DESC
	JMP	DUMP1

*ESC pressed
DESC	LDA	#$FF
	STA	764

	LDX	#PEREAD-PABORT+1
DUMPX2	LDA	#<PABORT
	JSR	PRINT
	JMP	EXIT

*Put 1 byte HEX into buffer
PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	PRTHX1

	ADC	#6
**
PRTHX1	STA	BUFF+1,X
	INX
	RTS

*Restore margin and print
PRINT	LDY	SAVE82
	STY	82

***
PRINTX	STA	$344
	STX	$348
	LDX	#0
	STX	$349
	LDY	#>PABORT
	STY	$345
	LDY	#11
	STY	$342
	JMP	$E456

*Error messages used with PRINT above
*Must be all in the same memory page
PABORT	DTA	155
	DTA	C"<Aborted>"
PEREAD	DTA	155
	DTA	C"Error while reading!"
PEOPEN	DTA	155
	DTA	C"Can't open file!"
PEPOINT	DTA	155
	DTA	C"Can't set file-position!"
PBADPAR	DTA	155
	DTA	C"Bad parameter!"
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
PEND	DTA	155
*Reuse last three messages for buffer
BUFF	EQU	PEPOINT

***

GETNAME	JMP	GETNAME

***
GHXOPT	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	GHXERR
	DEC	ASCII
	RTS

GHXRTS	CPY	#36
	BEQ	GHXR2

	LDX	#2
GHXR1	LDA	HEX,X
GHXSTO	EQU	*+1
	STA	POS,X
	DEX
	BPL	GHXR1

GHXR2	RTS

**

GETHEX	STY	GHXSTO
	JSR	GETNAME

	LDA	#0
	STA	HEX
	STA	HEX+1
	STA	HEX+2

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BEQ	GHXOPT

GETHX2	LDA	(10),Y

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	INY
	CPY	#43
	BCC	GETHX2
***

GHXERR	LDX	#PBADVER-PBADPAR+1
	LDA	#<PBADPAR
PERROR	JSR	PRINT

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

EXIT	JSR	CLOSE
	JMP	(10)

***
CLOSE	LDA	#12
***
CIONAME	LDX	#8
*Copy $342, $344, $345 and $346 to $34A
CPCIO	STA	$342+$10-8,X
	INX
	LDA	CIODTA-10,X
	BMI	CPCIO
	CPX	#$10
	BNE	CPCIO
*X=$10 here
	JMP	$E456

CIODTA	DTA	<NAME,>NAME,$80,$80,8,0,4,0

***
*File name and read buffer - low part must be < $80
NAME	EQU	$3300

