	ORG	$6000
	JMP	START

*************************************
*				*
*     " D U M P "   PRO BW-DOS	*
*				*
*************************************

HEX	EQU	128

POS	EQU	131
LEN	EQU	134

ASCII	EQU	137

LLEN	EQU	138
LPOS	EQU	139

SAVE82	EQU	140

STATUS	EQU	141
LIX	EQU	142

* VOLNO: 143

NAME	EQU	$6800

***
	ICL	"print.inc"
***

VRAT82	LDA	SAVE82
	STA	82

PRTEOL	LDA	#$9B

**
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#1
GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEX
	STA	HEX+1
	STA	HEX+2

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXRTS2

GETHX2	CPY	#43
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEX
	ROL	HEX+1
	ROL	HEX+2
	DEX
	BNE	GETHX4

	PLA
	ORA	HEX
	STA	HEX

	JMP	GETHX2
***

GHXERR	JSR	VRAT82
	JSR	PRINT
	DTA C"Bad parameter !"
	DTA 0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	VRAT82

	JSR	CLOSE
	JMP	(10)

***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

CIONAME	LDA	#<NAME
	STA	$344,X
	LDA	#>NAME
	STA	$345,X

	JMP	$E456
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA 155,253
	DTA C"Incorrect DOS !"
	DTA 155,0

	JMP	(10)
***

START2	LDA	82
	STA	SAVE82
	LDA	#0
	STA	82

	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***

	JSR	GETNAME

	LDY	#33
ST3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	ST3
***

	LDX	#0
	STX	POS
	STX	POS+1
	STX	POS+2
	STX	ASCII

	DEX
	STX	LEN
	STX	LEN+1
	STX	LEN+2
***

	JSR	GETHEX
	BNE	ST5

	CMP	#'/'
	BEQ	ST7
	BNE	ST6
**

ST5	LDX	#2
ST5B	LDA	HEX,X
	STA	POS,X
	DEX
	BPL	ST5B
**

ST6	JSR	GETHEX
	BNE	ST6B

	CMP	#'/'
	BEQ	ST7
	BNE	ST6D
**

ST6B	LDX	#2
ST6C	LDA	HEX,X
	STA	LEN,X
	DEX
	BPL	ST6C
**

ST6D	JSR	GETNAME

ST7	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	ST8

	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	ST8

	DEC	ASCII

*** OPEN

ST8	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	CIONAME
	BPL	ST9
**
	JSR	VRAT82
	JSR	PRINT
	DTA C"Can't open file !"
	DTA 0
	JMP	ERR2X

*** POINT

ST9	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	ST10

	LDX	#$10
	LDA	#37
	STA	$342,X

	LDA	POS
	STA	$34C,X
	LDA	POS+1
	STA	$34D,X
	LDA	POS+2
	STA	$34E,X

	JSR	CIONAME
	BPL	ST10
***
	JSR	VRAT82
	JSR	PRINT
	DTA C"Can't set "
	DTA C"file-position !"
	DTA 0
	JMP	ERR2X

*** DUMP IT !

ST10	LDA	#155
	JSR	PRT1

*GET

DUMP1	LDX	#$10
	LDA	#7
	STA	$342,X

	LDA	#8
	STA	$348,X
	LDA	#0
	STA	LPOS
	STA	LIX
	STA	$349,X

	JSR	CIONAME
	STY	STATUS

	LDX	#$10
	LDA	$348,X
	STA	LLEN
*LEN
	LDA	LEN
	TAX
	SEC
	SBC	LLEN
	STA	LEN
	LDA	LEN+1
	SBC	#0
	STA	LEN+1
	LDA	LEN+2
	SBC	#0
	STA	LEN+2
	BCS	DUMP1A

	STX	LLEN
	LDA	#136
	STA	STATUS

DUMP1A	LDA	LLEN
	BNE	DUMP1B
	JMP	DUMPX
*POS

DUMP1B	LDA	POS+2
	JSR	DPRTHX
	LDA	POS+1
	JSR	DPRTHX
	LDA	POS
	JSR	DPRTHX

	JSR	DSPAC

	LDA	LLEN
	CLC
	ADC	POS
	STA	POS
	BCC	DUMP2
	INC	POS+1
	BNE	DUMP2
	INC	POS+2

*** DATA

DUMP2	LDX	LIX
	LDA	NAME,X
	JSR	DPRTHX

	JSR	DSPAC

	INC	LIX
	LDA	LIX
	CMP	LLEN
	BCC	DUMP2

	LDA	#0
	STA	LIX

*** MOVE

DUMP3	LDA	LPOS
	CMP	#31
	BCS	DUMP4

	JSR	DSPAC

	JMP	DUMP3

*** CHARS

DUMP4	LDX	LIX
	LDA	NAME,X

	LDX	ASCII
	BNE	DUMP4A

	CMP	#$9B
	BNE	DUMP4AA
	LDA	#$20

DUMP4AA	PHA
	LDA	#27
	JSR	PRT1
	PLA
	JMP	DUMP4D
**

DUMP4A	AND	#127
	CMP	#125
	BCC	DUMP4B
	LDA	#'.'

DUMP4B	CMP	#32
	BCS	DUMP4D
	ORA	#64

DUMP4D	JSR	DPRT1

	INC	LIX
	LDA	LIX
	CMP	LLEN
	BCC	DUMP4
*** EOL

	JSR	PRTEOL

*** NEXT?

DUMPX	LDA	STATUS
	BMI	DUMPX2

	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1
***

DUMPX2	CMP	#136
	BEQ	DUMPX3

	JSR	VRAT82
	JSR	PRINT
	DTA C"Error while reading !"
	DTA 0

DUMPX3	JMP	ERR2X

***

DESC	LDA	#$FF
	STA	764

	JSR	VRAT82
	JSR	PRINT
	DTA C"<Aborted>"
	DTA 0
	JMP	ERR2

*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DTA $2C
DSPAC	LDA	#' '

DPRT1	INC	LPOS
	JMP	PRT1

***
