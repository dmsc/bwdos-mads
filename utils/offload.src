*************************************
*				*
*  " O F F L O A D "   FOR BW-DOS	*
*				*
*************************************

	ORG	$3000

***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

*File name
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3A

*Show syntax
	JSR	PRINT
	DTA	155
	DTA	C"Syntax:"
	DTA	155
	DTA	C"OFFLOAD fname [offs.] [/QL]"
	DTA	155,0
	JMP	ERR2
***

START3A	LDY	#33

START3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	START3

	LDA	NAME
	STA	DNAM
	LDA	NAME+1
	STA	DNAM+1

*Already initialized inside code
;	LDA	#0
;	STA	LDIT
;	STA	NOQUE
;	STA	OFFSL
;	STA	OFFSH

*Get load offset
	JSR	GETOFF

	CMP	#'/'
	BEQ	STRT3D

**/QL

STRT3C	JSR	GETNAME

STRT3D	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	STRT4

STRT3E	INY
	LDA	(10),Y
	CMP	#$9B
	BEQ	STRT4

	CMP	#'Q'
	BEQ	STRT3F

	CMP	#'L'
	BNE	STRT3ER
	STA	NOQUE
	LDA	#'!'
	STA	ASKS

STRT3F	STA	LDIT
	BCS	STRT3E
**

STRT3ER	JMP	GHXERR

**OPEN

STRT4	JSR	CLOSE

	LDA	#<NAME
	STA	$344+$10
	LDA	#>NAME
	STA	$345+$10

	LDA	#4
	STA	$34A+$10
	LDA	#0
	STA	$34B+$10

	LDA	#3
	JSR	CIO10
	BPL	STRT5
**

	JSR	PRINT
	DTA	155
	DTA	C"Can't open file!"
	DTA	155,0
	JMP	ERR2X
***

STRT5	JSR	GET1
	STA	STRT5B+1
	JSR	GET1
STRT5B	AND	#0
	CMP	#$FF
	BNE	STRT5ER

	JMP	LOAD1
**

STRT5ER	JSR	PRINT
	DTA	155
	DTA	C"Not binary load file!"
	DTA	155,0
	JMP	ERR2X
***

GETNAME	JMP	GETNAME

***

GHXRTS2	RTS

**

GETOFF	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXRTS2

GETHX2	CPY	#41
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS2

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	OFFSL
	ROL	OFFSH
	DEX
	BNE	GETHX4

	BEQ	GETHX2
***

GHXERR	JSR	PRINT
	DTA	155
	DTA	C"Bad parameter!"
	DTA	155,0
	JMP	ERR2X

**********
* End of part 1
**********

NAME	EQU	*

O1 EQU *
END1 EQU *
O2 EQU *
*
**********
* Second part: low-memory loader
**********

*Force inclusion of a FFFF header between sections
	OPT	H-
	ORG	0
	DTA	255,255
	OPT	H+
	ORG	$500

***
	ICL	"print.inc"
***
GET1	LDX	#$10
	LDA	#7
	JSR	GEPU1
	BPL	PRTEX

	CPY	#136
	BNE	ERROR

	JSR	PRINT
	DTA	155
	DTA	C"<EOF>"
	DTA	155,0
	BEQ	ERR2

**
ERROR	JSR	PRINT
	DTA	155
	DTA	C"I/O Error!"
	DTA	155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JSR	CLOSE
	JMP	(10)
***

NOTPOI	LDX	#<DNAM
	STX	$344+$10
	LDX	#>DNAM
	STX	$345+$10

	JSR	CIO10
	BMI	ERROR

	RTS
*

DNAM	DTA	C"D1:"
	DTA	155

***
PRTAX	PHA
	TXA
	JSR	PRTHEX
	PLA

PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6
**

PRT1	TAY
	LDA	#11
	LDX	#0

GEPU1	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	TYA
	JMP	$E456

***

CLOSE	LDA	#12
CIO10	LDX	#$10
CIOX	STA	$342,X
	JMP	$E456

*****

LOAD1	JSR	PRINT
	DTA	155,0

LOAD1A	JSR	GET1
	STA	ADRL
	JSR	GET1
	STA	ADRH
	TAX

	AND	ADRL
	CMP	#$FF
	BEQ	LOAD1A

*Print start address
	LDA	ADRL
	JSR	PRTAX

	LDA	#'-'
	JSR	PRT1

	JSR	GET1
	STA	ENDL
	PHA
	JSR	GET1
	STA	ENDH

*Print end address
	TAX
	PLA
	JSR	PRTAX

*Print position
	JSR	PRINT
	DTA	C" at "
	DTA	0
*NOTE
	LDA	#38
	JSR	NOTPOI

	LDA	$34D+$10
	STA	POSM
	LDX	$34E+$10
	STX	POSH

	JSR	PRTAX

	LDA	$34C+$10
	STA	POSL
	JSR	PRTHEX

*Get length

LN0	LDA	#0
**
ENDL	EQU	LN0+1
**
	SEC
	SBC	ADRL
	TAY

LN1	LDA	#0
**
ENDH	EQU	LN1+1
**
	SBC	ADRH
	TAX

	INY
	BNE	LN2
	INX

LN2	STY	LENL
	STX	LENH

**LOAD?

LN3	LDA	#0
**
LDIT	EQU	LN3+1
**
	BNE	LOAD2

*Don't load (skip)
LOADNE
LN4	LDA	#0
**
POSL	EQU	LN4+1
**
	CLC
LN5	ADC	#0
**
LENL	EQU	LN5+1
**
	STA	$34C+$10

LN6	LDA	#0
**
POSM	EQU	LN6+1
**
LN7	ADC	#0
**
LENH	EQU	LN7+1
**
	STA	$34D+$10

LN8	LDA	#0
**
POSH	EQU	LN8+1
**
	ADC	#0
	STA	$34E+$10

	LDA	#37
	JSR	NOTPOI

JLOAD1	JMP	LOAD1

*Show new load address
LOAD2	JSR	PRINT
	DTA	C". Load at "
	DTA	0

	LDA	ADRL
	CLC
**
	ADC	#0
OFFSL	EQU	*-1
**
	STA	ADRL
	PHA

	LDA	ADRH
**
	ADC	#0
OFFSH	EQU	*-1
**
	STA	ADRH

	TAX
	PLA
	JSR	PRTAX

	JSR	PRINT
ASKS	DTA	C"?"
	DTA	0

*Ask?
	LDA	#0
NOQUE	EQU	*-1
**
	BNE	LOAD3
**

LD25	LDX	764
	INX
	BEQ	LD25

	LDA	#$FF
	STA	764

	CPX	#$1D
	BEQ	LD25ESC
	CPX	#$2C
	BEQ	LD25Y
	CPX	#$24
	BNE	LD25

	LDA	#'N'
	JSR	PRT1
	JMP	LOADNE
**

LD25Y	LDA	#'Y'
	JSR	PRT1
***

LOAD3	LDA	#0
ADRL	EQU	*-1
**
	STA	$344+$10
	LDA	#0
ADRH	EQU	*-1
**
	STA	$345+$10

	LDA	LENL
	STA	$348+$10
	LDA	LENH
	STA	$349+$10

	LDA	#7
	JSR	CIO10
	BPL	JLOAD1
	JMP	ERROR
**
***
LD25ESC	JSR	PRINT
	DTA	155
	DTA	C"<Aborted>"
	DTA	155,0
	JMP	ERR2X
**

