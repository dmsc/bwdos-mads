	ORG	$6000,$A800
	JMP	START
	OUT	N

*************************************
*				*
*  " O F F L O A D "    PRO BW-DOS	*
*				*
*************************************

***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#0
	LDX	#0
**
HEXL	EQU	GHXRTS+1
HEXH	EQU	GHXRTS+3
**
	LDA	#1

GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEXL
	STA	HEXH

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXRTS2

GETHX2	CPY	#41
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	PLA
	ORA	HEXL
	STA	HEXL

	JMP	GETHX2
***

GHXERR	JSR	PRINT
	DFB 155
	ASC "Bad parameter !"
	DFB 155,0
	JMP	ERR2X
***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS !"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
**NAME

	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3A

**SYNTAX

	JSR	PRINT
	DFB 155
	ASC "Syntax:"
	DFB 155
	ASC "OFFLOAD fname [offs.] "
	ASC "[/QL]"
	DFB 155,0
	JMP	ERR2
***

START3A	LDY	#33

START3	LDA	(10),Y
	STA	NAME-33,Y
	INY
	CPY	#61
	BCC	START3

	LDA	NAME
	STA	DNAM
	LDA	NAME+1
	STA	DNAM+1

**OFFS
	LDA	#0
	STA	OFFSL
	STA	OFFSH
	STA	LDIT
	STA	NOQUE

	JSR	GETHEX
	BNE	STRT3B

	CMP	#'/'
	BEQ	STRT3D
	BNE	STRT3C
**

STRT3B	STY	OFFSL
	STX	OFFSH

**/QL

STRT3C	JSR	GETNAME

STRT3D	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	STRT4

STRT3E	INY
	LDA	(10),Y
	CMP	#$9B
	BEQ	STRT4

	CMP	#'Q'
	BEQ	STRT3F

	CMP	#'L'
	BNE	STRT3ER
	STA	NOQUE

STRT3F	STA	LDIT
	BEQ	STRT3E
**

STRT3ER	JMP	GHXERR

**OPEN

STRT4	JSR	CLOSE

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	#NAME:L
	STA	$344,X
	LDA	#NAME:H
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BPL	STRT5
**

	JSR	PRINT
	DFB 155
	ASC "Can't open file !"
	DFB 155,0
	JMP	ERR2X
***

STRT5	JSR	GET1
	STA	STRT5B+1
	JSR	GET1
STRT5B	AND	#0
	CMP	#$FF
	BNE	STRT5ER

	JMP	LOAD1
**

STRT5ER	JSR	PRINT
	DFB 155
	ASC "Not binary load file !"
	DFB 155,0
	JMP	ERR2X
*
**********
*
* KONEC PRVNI CASTI
*
**********
*
NAME	EQU	*

O1 EQU *
END1 EQU *
O2 EQU *
*
**********
*
* DRUHA CAST
*
**********
*
	ORG	$500,$B000

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

GET1	LDX	#$10
	LDA	#7
	JSR	GEPU1
	BPL	PRTEX

	CPY	#136
	BNE	ERROR

	JSR	PRINT
	DFB 155
	ASC "<EOF>"
	DFB 155,0

ERR2	JSR	CLOSE
	JMP	(10)
**

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474
	JMP	ERR2
***

ERROR	JSR	PRINT
	DFB 155
	ASC "I/O Error !"
	DFB 155,0
	JMP	ERR2X
***

NOTPOI	STA	$342,X

	LDA	#DNAM:L
	STA	$344,X
	LDA	#DNAM:H
	STA	$345,X

	JSR	$E456
	BMI	ERROR

	RTS
*

DNAM	ASC "D1:"
	DFB 155

***

PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	PRT1

	ADC	#6
**

PRT1	TAY
	LDA	#11
	LDX	#0

GEPU1	STA	$342,X

	LDA	#0
	STA	$348,X
	STA	$349,X

	TYA
	JMP	$E456

***

CLOSE	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456

*****

LOAD1	JSR	PRINT
	DFB 155,0

LOAD1A	JSR	GET1
	STA	ADRL
	JSR	GET1
	STA	ADRH

	AND	ADRL
	CMP	#$FF
	BEQ	LOAD1A

	JSR	GET1
	STA	ENDL
	JSR	GET1
	STA	ENDH

**PRT ADRESY

	LDA	ADRH
	JSR	PRTHEX
	LDA	ADRL
	JSR	PRTHEX

	LDA	#'-'
	JSR	PRT1

	LDA	ENDH
	JSR	PRTHEX
	LDA	ENDL
	JSR	PRTHEX

**POZICE

	JSR	PRINT
	ASC " Pos."
	DFB 0
*NOTE
	LDX	#$10
	LDA	#38
	JSR	NOTPOI

	LDX	#$10
	LDA	$34C,X
	STA	POSL
	LDA	$34D,X
	STA	POSM
	LDA	$34E,X
	STA	POSH

	JSR	PRTHEX
	LDA	POSM
	JSR	PRTHEX
	LDA	POSL
	JSR	PRTHEX
**LEN

LN0	LDA	#0
**
ENDL	EQU	LN0+1
**
	SEC
	SBC	ADRL
	TAY

LN1	LDA	#0
**
ENDH	EQU	LN1+1
**
	SBC	ADRH
	TAX

	INY
	BNE	LN2
	INX

LN2	STY	LENL
	STX	LENH

**LOAD?

LN3	LDA	#0
**
LDIT	EQU	LN3+1
**
	BNE	LOAD2
*NE

LOADNE	LDX	#$10
LN4	LDA	#0
**
POSL	EQU	LN4+1
**
	CLC
LN5	ADC	#0
**
LENL	EQU	LN5+1
**
	STA	$34C,X

LN6	LDA	#0
**
POSM	EQU	LN6+1
**
LN7	ADC	#0
**
LENH	EQU	LN7+1
**
	STA	$34D,X

LN8	LDA	#0
**
POSH	EQU	LN8+1
**
	ADC	#0
	STA	$34E,X

	LDA	#37
	JSR	NOTPOI

	JMP	LOAD1
**LOAD!

LOAD2	JSR	PRINT
	ASC " Load at "
	DFB 0

	LDA	ADRL
	CLC
LD21	ADC	#0
**
OFFSL	EQU	LD21+1
**
	STA	ADRL

	LDA	ADRH
LD22	ADC	#0
**
OFFSH	EQU	LD22+1
**
	STA	ADRH

	JSR	PRTHEX
	LDA	ADRL
	JSR	PRTHEX
**QUE?

LD23	LDA	#0
**
NOQUE	EQU	LD23+1
**
	BEQ	LD24

	JSR	PRINT
	ASC " ! "
	DFB 0
	JMP	LOAD3
**

LD24	JSR	PRINT
	ASC " ? "
	DFB 0
***

LD25	LDX	764
	INX
	BEQ	LD25

	LDA	#$FF
	STA	764

	CPX	#$1D
	BEQ	LD25ESC
	CPX	#$2C
	BEQ	LD25Y
	CPX	#$24
	BNE	LD25

	LDA	#'N'
	JSR	PRT1
	JMP	LOADNE
**

LD25ESC	JSR	PRINT
	DFB 155
	ASC "<Aborted>"
	DFB 155,0
	JMP	ERR2X
**

LD25Y	LDA	#'Y'
	JSR	PRT1
***

LOAD3	LDX	#$10
	LDA	#7
	STA	$342,X

LD31	LDA	#0
**
ADRL	EQU	LD31+1
**
	STA	$344,X
LD32	LDA	#0
**
ADRH	EQU	LD32+1
**
	STA	$345,X

	LDA	LENL
	STA	$348,X
	LDA	LENH
	STA	$349,X

	JSR	$E456
	BPL	LOAD4
	JMP	ERROR
**

LOAD4	JMP	LOAD1

***
