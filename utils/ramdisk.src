	ORG	$36FD,$A800
	OUT	N

O	EQU	$A800-$36FD
	JMP	UTAJ+O
***
*   PRED SAVE SPUSTIT OD $A800,
*   PAK ULOZIT OD A803 NA 3700
***

*************************************
*				*
*	RAM-DISK PRO BW-DOS	*
*				*
*************************************

SRCHADR	EPZ	128
SIOADR	EPZ	130

DNUMB	EPZ	132
XFLAG	EPZ	133
FFLAG	EPZ	134

NBANKS	EPZ	135

LDFLG	EPZ	136

MAXSECT	EPZ	137

MAPS	EPZ	139

MSAVE	EPZ	140
MIX	EPZ	141
MKONEC	EPZ	142

*** VOLNO: 143

BUFF	EQU	$500

SWTAB	EQU	$5C0

***

ODT0	LDA	START

	LDX	#0
	LDY	#9

ODTAJ	LDA	PRINT,X
	EOR	#$55
ODT2	STA	PRINT,X

	INX
	BNE	ODTAJ

	INC	ODTAJ+2
	INC	ODT2+2

	DEY
	BNE	ODTAJ
*
	LDA	#76
	STA	ODT0

	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DFB 155,253
	ASC "Incorrect DOS version"
	DFB 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	LDA	11
	SBC	#0
	STA	SIOADR+1
**
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
*HELP
	JSR	PRINT
	DFB 155
	ASC "  Syntax:"
	DFB 155
	ASC "RAMDISK "
	ASC "drive#[N][E] "
	ASC "[config_file]"
	DFB 155
	ASC "RAMDISK OFF"
	DFB 155,0

	JMP	(10)
***

START3	CMP	#'O'
	BNE	START4

	INY
	LDA	#'F'
	CMP	(10),Y
	BNE	START4
	INY
	CMP	(10),Y
	BNE	START4

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START4

	JMP	REMOVE
***

START4	LDA	#0
	STA	XFLAG
	STA	FFLAG

	LDY	#36

STRT4A	STA	DNUMB
	STA	STDRIV

STRT4B	LDA	(10),Y
	INY
	CMP	#$9B
	BEQ	START5

	CMP	#'N'
	BNE	STRT4C
	STA	FFLAG
	BEQ	STRT4B

STRT4C	CMP	#'E'
	BNE	STRT4D
	STA	XFLAG
	BEQ	STRT4B

STRT4D	SEC
	SBC	#'0'
	BEQ	ST4ER
	CMP	#10
	BCC	STRT4A
**

ST4ER	JSR	PRINT
	DFB 155,253
	ASC "Syntax Error!"
	DFB 155,0

	JMP	(10)
***

START5	LDA	DNUMB
	BEQ	ST4ER

**CONFIG

	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BEQ	ST5A

	JMP	CONFIG1

*CONFIG NENI

ST5A	LDX	#32

ST5B	LDA	SWTABVZ,X
	STA	SWTAB,X

	DEX
	BPL	ST5B

	JSR	RAMTEST

	LDA	NBANKS
	BEQ	ST5NO

	LDX	XFLAG
	BEQ	START6J

	SEC
	SBC	#4
	STA	NBANKS
	BEQ	ST5MALO
	BCS	START6J
*

ST5MALO	JSR	PRINT
	DFB 155
	ASC "Only 130XE banks"
	ASC " available!"
	DFB 155,0

ST5AB	JSR	PRINT
	ASC "Operation aborted."
	DFB 155,253,0

	JMP	(10)
**

START6J	JMP	START6

*
ST5NO	JSR	PRINT
	DFB 155
	ASC "No Extra memory"
	ASC " available!"
	DFB 155,0
	JMP	ST5AB
***

CLOSE1	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

CONFIG1	LDA	#$FF
	LDX	#32

CONF1B	STA	SWTAB,X
	DEX
	BPL	CONF1B

	INX
	STX	LDFLG

	JSR	CLOSE1

*OPEN CONFIG

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	10
	CLC
	ADC	#33
	STA	$344,X
	LDA	11
	ADC	#0
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BPL	CONFIG2
**

CONFERR	JSR	CLOSE1

	JSR	PRINT
	DFB 155
	ASC "Can't read"
	DFB 0

CFGE2	JSR	PRINT
	ASC " configuration file!"
	DFB 155,0

	JMP	ST5AB
**

CONFIG2	LDX	#$10

	LDA	#7
	STA	$342,X
	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456
	BPL	CONF2B

	CPY	#136
	BNE	CONFERR
	BEQ	CONFIG3
*

CONF2B	INC	LDFLG
	LDX	LDFLG
	CPX	#33
	BCS	CONFBAD

	STA	SWTAB,X
	BCC	CONFIG2
**

CONFBAD	JSR	CLOSE1

	JSR	PRINT
	DFB 155
	ASC "Bad"
	DFB 0

	JMP	CFGE2
**

CONFIG3	JSR	CLOSE1

	LDA	LDFLG
	BEQ	CONFBAD

	JSR	RAMTEST

	LDA	LDFLG
	CMP	NBANKS
	BNE	CONFBAD

*PAMET TESTOVANA, NBANKS A RBTAB JSOU
*AKTUALNI

START6	JSR	SRCHRUT
	BCC	STRT6B
	JMP	START7

*INSTALUJ REZIDENT

STRT6B	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*SIO
	LDY	#1
	LDA	(SIOADR),Y
	STA	RDSPUV+2
	DEY
	LDA	(SIOADR),Y
	STA	RDSPUV+1

	LDA	#(RDSIO-RESID)
	CLC
	ADC	SRCHADR
	STA	(SIOADR),Y
	INY
	LDA	#0
	ADC	SRCHADR+1
	STA	(SIOADR),Y

*COPY
	LDY	#RESLEN

STRT6C	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	STRT6C
*RESET
	LDY	#2

STRT6D	LDA	11,Y
	STA	(SRCHADR),Y
	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	STRT6D

*REZIDENT EXISTUJE

START7	LDA	DNUMB
	STA	RR1+1
*RELOK
	LDA	#(R05-RESID)+1
	CLC
	ADC	SRCHADR
	STA	RR2+1
	STA	RR8+1
	STA	RR10+1
	LDA	#0
	ADC	SRCHADR+1
	STA	RR2+2
	STA	RR8+2
	STA	RR10+2

	LDA	#(R04-RESID)+1
	CLC
	ADC	SRCHADR
	STA	RR7+1
	STA	RR9+1
	STA	RR3+1
	LDA	#0
	ADC	SRCHADR+1
	STA	RR7+2
	STA	RR9+2
	STA	RR3+2
*MAXSECT
	LDA	NBANKS
	LSR
	STA	MAXSECT+1
	LDA	#0
	ROR
	STA	MAXSECT

	JSR	DECMAXS

	LDA	MAXSECT
	STA	RR5+1
	LDA	MAXSECT+1
	STA	RR6+1

	JSR	DECMAXS

*BANK0
	JSR	DISINT

	LDA	$D301
	PHA

	LDA	RBTAB
	LDY	#(R03-RESID)+1
	STA	(SRCHADR),Y

	STA	$D301

	LDY	#0

STRT7C	LDA	RES2,Y
	STA	$4000,Y

	INY
	BNE	STRT7C
*
	PLA
	STA	$D301

	JSR	ENINT

* FORMATOVANI

	LDA	FFLAG
	BEQ	FORM1

	LDA	#1
	LDX	#'R'
	LDY	#64
	JSR	RWRD

	LDA	BUFF+11
	CMP	MAXSECT
	BNE	FORM0
	LDA	BUFF+12
	CMP	MAXSECT+1
	BNE	FORM0

	LDA	#$80
	CMP	BUFF+7
	BNE	FORM0
	CMP	BUFF+31
	BNE	FORM0

	LDA	BUFF+32
	CMP	#$22
	BNE	FORM0
	JMP	FORMEND
***

FORM0	JSR	PRINT
	DFB 155,253
	ASC "RAM-Disk format "
	ASC "was different!"
	DFB 155,0
*#MAPS

FORM1	LDA	MAXSECT+1
	LSR
	LSR
	TAX
	INX
	STX	MAPS
*DIR
	JSR	CLBUF

	LDX	MAPS
	INX
	INX
	TXA
	INX
	STX	BUFF+4

	JSR	WRRD

	JSR	CLBUF

	LDX	#13
FORM1B	LDA	VZDIR,X
	STA	BUFF+3,X
	DEX
	BPL	FORM1B

	LDA	MAPS
	CLC
	ADC	#3
	JSR	WRRD

*BITMAPS
	JSR	CLBUF

	LDA	MAXSECT
	STA	MKONEC
	LDA	MAXSECT+1
	LSR
	ROR	MKONEC
	LSR
	ROR	MKONEC
	LSR	MKONEC

	LDX	#2
	STX	MSAVE
	DEX
	STX	MIX
	DEX

	LDY	MAPS
	LDA	MPSTB,Y
	DFB	$2C	;BIT

FORM1C	LDA	#$FF
	STA	BUFF,X

	LDA	MIX
	CMP	MAPS
	BEQ	FORM1D

FORM1CB	INX
	BPL	FORM1C

	LDA	MSAVE
	JSR	WRRD

	JSR	CLBUF

	INC	MIX
	INC	MSAVE

	LDX	#0
	BEQ	FORM1C
*

FORM1D	CPX	MKONEC
	BNE	FORM1CB

	LDA	#$FE
	STA	BUFF,X

	LDA	MSAVE
	JSR	WRRD

*BOOT SECTOR

	JSR	CLBUF

	LDX	#32

FORM1E	LDA	VZBOOT,X
	STA	BUFF,X
	DEX
	BPL	FORM1E

	LDA	MAPS
	STA	BUFF+15
	CLC
	ADC	#2
	STA	BUFF+9

	LDA	MAXSECT
	STA	BUFF+11
	SEC
	SBC	MAPS
	TAY
	LDA	MAXSECT+1
	STA	BUFF+12
	SBC	#0
	TAX

	TYA
	SEC
	SBC	#3
	STA	BUFF+13
	TXA
	SBC	#0
	STA	BUFF+14

	LDA	$D20A
	STA	BUFF+39

	LDA	#1
	JSR	WRRD
***
	JSR	PRINT
	DFB 155
	ASC "RAM-Disk formatted."
	DFB 0
***

FORMEND	JSR	PRINT
	DFB 155
	ASC "RAM-Disk"
	ASC " is D"
	DFB 0

	LDA	DNUMB
	CLC
	ADC	#$30
	JSR	PRT1

	JSR	PRINT
	ASC ": ("
	ASC "Size: "
	DFB 0

	LDA	NBANKS
	ASL
	CLC
	ADC	NBANKS

	PHA
	JSR	FSP
	PLA
	PHA
	CLC
	ADC	#1
	JSR	FSP
	PLA
	CLC
	ADC	#2
	JSR	FSP

	JSR	PRINT
	ASC " kB)"
	DFB 155,0

	JMP	(10)
***

FSP	TAY
	LDA	FST,Y
	JMP	PRT1
**

FST	ASC "  0"
	ASC " 16"
	ASC " 32"
	ASC " 48"
	ASC " 64"
	ASC " 80"
	ASC " 96"
	ASC "112"
	ASC "128"
	ASC "144"
	ASC "160"
	ASC "176"
	ASC "192"
	ASC "208"
	ASC "224"
	ASC "240"
	ASC "256"
	ASC "272"
	ASC "288"
	ASC "304"
	ASC "320"
	ASC "336"
	ASC "352"
	ASC "368"
	ASC "384"
	ASC "400"
	ASC "416"
	ASC "432"
	ASC "448"
	ASC "464"
	ASC "480"
	ASC "496"
	ASC "512"

**

MPSTB	DFB $0F,$07,$03,$01
	DFB 0,0

VZBOOT	DFB 0,3,0,48,$E0,7
	JMP $3080
	DFW 0,0,0
	DFB 0,2,0,32,0,4,0
	ASC "RAM-DISK"
	DFB 0,128,$22

VZDIR	DFB 23,0,0
	ASC "MAIN       "

***

CLBUF	LDX	#127
	LDA	#0

CLBF2	STA	BUFF,X
	DEX
	BPL	CLBF2

	RTS
****

WRRD	LDX	#'W'
	LDY	#128

RWRD	STX	STCMD
	STY	STSTAT
	STA	STAUX

	LDX	#11
RWRD2	LDA	STAB,X
	STA	$300,X
	DEX
	BPL	RWRD2

	LDA	10
	SEC
	SBC	#10
	STA	SIOJMP+1
	LDA	11
	SBC	#0
	STA	SIOJMP+2

	JSR	SIOJMP
	BMI	SIOERR

	RTS
**

SIOJMP	JMP	($FFFF)

**

STAB	DFB	$31
STDRIV	DFB	0
STCMD	DFB	0
STSTAT	DFB	0
	DFW	BUFF
	DFB 7,0
	DFW	128
STAUX	DFW	0

**

SIOERR	JSR	PRINT
	DFB 155,253
	ASC "Internal Error!"
	DFB 155,0

	JMP	(10)
***

DECMAXS	LDA	MAXSECT
	BNE	STRT7B
	DEC	MAXSECT+1
STRT7B	DEC	MAXSECT

	RTS

****

REMOVE	JSR	SRCHRUT
	BCC	REMNOT
	JMP	REMOV2
**

REMNOT	JSR	PRINT
	DFB 155
	ASC "RAM-Disk not "
	ASC "installed!"
	DFB 155,0

REMCANT	JSR	PRINT
	ASC "Can't remove."
	DFB 155,253,0

	JMP	(10)
**

REM2LST	JSR	PRINT
	DFB 155
	ASC "RAM-Disk is not the"
	ASC " last"
	DFB 155
	ASC "installed handler!"
	DFB 155,0

	JMP	REMCANT
**

REMOV2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	REM2LST

	CPY	#RESLEN
	BNE	REM2LST

*REMOVE IT

	LDY	#2

REM3	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	REM3
*
	LDY	#(RDSPUV-RESID)+1
	LDA	(SRCHADR),Y
	TAX
	INY
	LDA	(SRCHADR),Y

	LDY	#1
	STA	(SIOADR),Y
	DEY
	TXA
	STA	(SIOADR),Y
***

	JSR	PRINT
	DFB 155
	ASC "RAM-Disk removed."
	DFB 155,0

	JMP	(10)

****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R03-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#R04-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	LDY	#RDSPUV-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR4

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS
*****

RAMTEST	JSR	DISINT

	LDA	$D301
	PHA

*SAVE OLD

	LDX	#32

RT0	JSR	RSWITCH

	LDA	$4080
	STA	BUFF,X
	LDA	$5032
	STA	BUFF+33,X
	LDA	$7FFE
	STA	BUFF+66,X

	DEX
	BPL	RT0

*SET NEW

	LDX	#32

RT1	JSR	RSWITCH

	STX	$4080
	STX	$5032
	STX	$7FFE

	DEX
	BPL	RT1

*TEST

	LDY	#0
	LDX	#32

RT2	JSR	RSWITCH
	CPX	$4080
	BNE	RT3
	CPX	$5032
	BNE	RT3
	CPX	$7FFE
	BNE	RT3

	LDA	SWTAB,X
	STA	RBTAB,Y
	INY

RT3	DEX
	BNE	RT2

	STY	NBANKS

*OBNOV OLD

	LDX	#32

RT4	JSR	RSWITCH

	LDA	BUFF,X
	STA	$4080
	LDA	BUFF+33,X
	STA	$5032
	LDA	BUFF+66,X
	STA	$7FFE

	DEX
	BPL	RT4

*KONEC

	PLA
	STA	$D301
***

ENINT	LDA	#$40
	STA	54286
	CLI

	RTS
***

DISINT	LDA	#0
	STA	54286
	SEI

	RTS
***

RSWITCH	LDA	SWTAB,X
	STA	$D301

	RTS
**

SWTABVZ	DFB $FF
	DFB $EF,$EB,$E7,$E3 ;130XE
	DFB $AF,$AB,$A7,$A3 ;128k
	DFB $CF,$CB,$C7,$C3 ;RAMBO
	DFB $8F,$8B,$87,$83 ;RAMBO
	DFB $6F,$6B,$67,$63 ;COMPY
	DFB $2F,$2B,$27,$23 ;COMPY

	DFB 255,255,255,255
	DFB 255,255,255,255

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***

RDSIO	LDA	$D301
	PHA

	LDX	#0
	STX	54286
	SEI

R03	LDX	#$FF
TST2	STX	$D301

	JSR	$4000

	DEY
	BMI	RDSIO3

R04	LDX	#$FF
	STX	$D301

	LDA	($32),Y

R05	LDX	#$FF
TST3	STX	$D301

	STA	($34),Y

	DEY
	BPL	R04

	INY

RDSIO3	PLA
	STA	$D301

	LDA	#$40
	STA	54286
	CLI

	INY
	BEQ	RDSPUV

	STY	$303
	RTS
**

RDSPUV	JMP	$FFFF

***

RESLEN	EPZ	*-RESID

*******

RES2	LDA	$300
	CMP	#$31
	BNE	RES2P

	LDA	$301
RR1	CMP	#0
	BEQ	RES2B

RES2P	LDY	#0
	RTS
**

RES2B	LDA	$304
	STA	$34
	LDA	$305
	STA	$35

	LDA	$309
	BNE	R2NAK
	LDY	$308

	LDA	$302
	CMP	#'S'
	BNE	R2RW
*STATUS

	LDA	#$FF
RR2	STA	R05+1

	LDX	$303
	CPX	#$40
	BNE	R2NAK

	CPY	#4
	BNE	R2NAK

	LDA	#(RDSTAT+RESO):L
	STA	$32
	LDA	#(RDSTAT+RESO):H
	STA	$33

	LDA	RBTAB+RESO
RR3	STA	R04+1

	RTS
***

R2NAK	LDY	#139
	RTS

***

R2RW	CPY	#128
	BNE	R2NAK

	LDA	$30A	;MAXSECT
RR5	CMP	#2
	LDA	$30B
RR6	SBC	#1
	BCS	R2NAK

	LDA	$30A
	ORA	$30B
	BEQ	R2NAK
*
	LDA	$30A
	CLC
	ADC	#1
	TAY
	LDA	$30B
	ADC	#0
	PHA
	TYA

	LSR
	STA	$33
	LDA	#0
	ROR
	STA	$32

	TYA
	ASL
	PLA
	ROL
	TAY

	LDA	RBTAB+RESO,Y
RR7	STA	R04+1
RR8	STA	R05+1

	LDA	$33
	AND	#$3F
	ORA	#$40
	STA	$33
**
	LDY	#128
	LDX	$303

	LDA	$302
	CMP	#'R'
	BEQ	RR8READ
	CMP	#'W'
	BEQ	RR8WR
	CMP	#'P'
	BNE	R2NAK

RR8WR	CPX	#128
	BNE	R2NAK

	LDA	$32
	LDX	$34
	STA	$34
	STX	$32

	LDA	$33
	LDX	$35
	STA	$35
	STX	$33

	LDA	#$FF
RR9	STA	R04+1

	RTS
**

RR8READ	CPX	#$40
	BNE	R2NAK

	LDA	#$FF
RR10	STA	R05+1

	RTS
***

RDSTAT	DFB $10,$FF,$FF,$FF

***

RBTAB	DFW 0,0,0,0
	DFW 0,0,0,0
	DFW 0,0,0,0
	DFW 0,0,0,0
***

RESO	EQU	$4000-RES2

RES2LEN	EQU	*-RES2

**************
*
* KONEC PROGRAMU, NASLEDUJE SLUZEBNI
* RUTINKA
*
**************

UTAJ	LDA	#(PRINT+O):L
	STA	0
	LDA	#(PRINT+O):H
	STA	1

UTAJ2	LDY	#0
	LDA	(0),Y
	EOR	#$55
	STA	(0),Y

	INC	0
	BNE	UTAJ3
	INC	1

UTAJ3	LDA	0
	CMP	#(UTAJ+O):L
	BNE	UTAJ2

	LDA	1
	CMP	#(UTAJ+O):H
	BNE	UTAJ2

	RTS

****

SAVEOD	EQU	$A803
SAVETO	EQU	UTAJ+O
