*************************************
*				*
*	RAM-DISK PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128
SIOADR	EQU	130

DNUMB	EQU	132
XFLAG	EQU	133
FFLAG	EQU	134

NBANKS	EQU	135

LDFLG	EQU	136

MAXSECT	EQU	137

MAPS	EQU	139

MSAVE	EQU	140
MIX	EQU	141
MKONEC	EQU	142

TEMP1	EQU	144
TEMP2	EQU	145

*** VOLNO: 146

BUFF	EQU	$500

SWTAB	EQU	$480

***

	ORG	$3700
	JMP	START

***
	ICL	"print.inc"
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA	155,253
	DTA	C"Incorrect DOS version"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2

	LDA	10
	SEC
	SBC	#10
	STA	SIOADR
	LDA	11
	SBC	#0
	STA	SIOADR+1
**
	JSR	PRINT
	DTA	155
	DTA	C"BW-DOS Ramdisk ver. "
	DTA	C"1.2 by BEWESOFT"
	DTA	155,0
**
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BNE	START3
*HELP

STRTHLP	JSR	PRINT
	DTA	155
	DTA	C"  Syntax:"
	DTA	155
	DTA	C"RAMDISK "
	DTA	C"drive#[F][E] "
	DTA	C"[config_file]"
	DTA	155
	DTA	C"RAMDISK OFF"
	DTA	155,0

	JMP	(10)
***

START3	CMP	#'O'
	BNE	START4

	INY
	LDA	#'F'
	CMP	(10),Y
	BNE	START4
	INY
	CMP	(10),Y
	BNE	START4

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	START4

	JMP	REMOVE
***

START4	LDA	#0
	STA	XFLAG
	STA	FFLAG

	LDY	#36

STRT4A	STA	DNUMB
	STA	STDRIV

STRT4B	LDA	(10),Y
	INY
	CMP	#$9B
	BEQ	START5

	CMP	#'N'
	BEQ	STRT4B

	CMP	#'F'
	BNE	STRT4C
	STA	FFLAG
	BEQ	STRT4B

STRT4C	CMP	#'E'
	BNE	STRT4D
	STA	XFLAG
	BEQ	STRT4B

STRT4D	SEC
	SBC	#'0'
	BEQ	ST4ER
	CMP	#10
	BCC	STRT4A
**

ST4ER	JMP	STRTHLP

***

START5	LDA	DNUMB
	BEQ	ST4ER

**CONFIG

	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#$9B
	BEQ	ST5A

	JMP	CONFIG1

*CONFIG NENI - VYTVOR DEFAULT TABLE

ST5A	LDX	#63

ST5B	TXA
	EOR	#63

	PHA
	AND	#3
	STA	TEMP1

	PLA
	ASL
	ASL
	ASL
	ROL	TEMP1
	SEC
	ROL	TEMP1

	AND	#$E0
	ORA	TEMP1

	STA	SWTAB+1,X

	DEX
	BPL	ST5B

	STX	SWTAB
**
	JSR	RAMTEST

	LDA	NBANKS
	BEQ	ST5NO

	LDX	XFLAG
	BEQ	START6J

	SEC
	SBC	#4
	STA	NBANKS
	BEQ	ST5MALO
	BCS	START6J
*

ST5MALO	JSR	PRINT
	DTA	155
	DTA	C"Only 130XE banks"
	DTA	C" available!"
	DTA	155,0

ST5AB	JSR	PRINT
	DTA	C"Operation aborted."
	DTA	155,253,0

	JMP	(10)
**

START6J	JMP	START6

*
ST5NO	JSR	PRINT
	DTA	155
	DTA	C"No Extra memory"
	DTA	C" available!"
	DTA	155,0
	JMP	ST5AB
***

CLOSE1	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
***

CONFIG1	LDA	#$FF
	LDX	#64

CONF1B	STA	SWTAB,X
	DEX
	BPL	CONF1B

	INX
	STX	LDFLG	;#0

	JSR	CLOSE1

*OPEN CONFIG

	LDX	#$10
	LDA	#3
	STA	$342,X

	LDA	10
	CLC
	ADC	#33
	STA	$344,X
	LDA	11
	ADC	#0
	STA	$345,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	$E456
	BPL	CONFIG2
**

CONFERR	JSR	CLOSE1

	JSR	PRINT
	DTA	155
	DTA	C"Can't read"
	DTA	0

CFGE2	JSR	PRINT
	DTA	C" configuration file!"
	DTA	155,0

	JMP	ST5AB
**

CONFIG2	LDX	#$10

	LDA	#7
	STA	$342,X
	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456
	BPL	CONF2B

	CPY	#136
	BNE	CONFERR
	BEQ	CONFIG3
*

CONF2B	INC	LDFLG
	LDX	LDFLG
	CPX	#65
	BCS	CONFBAD

	STA	SWTAB,X
	BCC	CONFIG2
**

CONFBAD	JSR	CLOSE1

	JSR	PRINT
	DTA	155
	DTA	C"Bad"
	DTA	0

	JMP	CFGE2
**

CONFIG3	JSR	CLOSE1

	LDA	LDFLG
	BEQ	CONFBAD

	JSR	RAMTEST

	LDA	LDFLG
	CMP	NBANKS
	BNE	CONFBAD

*PAMET TESTOVANA, NBANKS A RBTAB JSOU
*AKTUALNI

START6	JSR	SRCHRUT
	BCC	STRT6B
	JMP	START7

*INSTALUJ REZIDENT

STRT6B	LDA	743
	STA	SRCHADR
	CLC
	ADC	#RESLEN
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*SIO
	LDY	#1
	LDA	(SIOADR),Y
	STA	RDSPUV+2
	DEY
	LDA	(SIOADR),Y
	STA	RDSPUV+1

	LDA	#(RDSIO-RESID)
	CLC
	ADC	SRCHADR
	STA	(SIOADR),Y
	INY
	LDA	#0
	ADC	SRCHADR+1
	STA	(SIOADR),Y

*COPY
	LDY	#RESLEN

STRT6C	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y

	TYA
	BNE	STRT6C
*RESET
	LDY	#2

STRT6D	LDA	11,Y
	STA	(SRCHADR),Y
	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	STRT6D

*REZIDENT EXISTUJE

START7	LDA	DNUMB
	STA	RR1+1

*RELOK
	LDA	#(R05-RESID)+1
	CLC
	ADC	SRCHADR
	STA	RR8+1
	STA	RR10+1
	LDA	#0
	ADC	SRCHADR+1
	STA	RR8+2
	STA	RR10+2

	LDA	#(R04-RESID)+1
	CLC
	ADC	SRCHADR
	STA	RR7+1
	STA	RR9+1
	STA	RR3+1
	LDA	#0
	ADC	SRCHADR+1
	STA	RR7+2
	STA	RR9+2
	STA	RR3+2
*MAXSECT
	LDA	NBANKS
	LSR
	STA	MAXSECT+1
	LDA	#0
	ROR
	STA	MAXSECT

	JSR	DECMAXS

	LDA	MAXSECT
	STA	RR5+1
	LDA	MAXSECT+1
	STA	RR6+1

	JSR	DECMAXS
*SPARTA?
	LDY	#$FE

	LDA	$703
	CMP	#'B'
	BNE	SPRTA1
	LDA	$704
	CMP	#'W'
	BNE	SPRTA1

	INY

SPRTA1	STY	SPSW2+1
	STY	SPSW3+1

*BANK0
	JSR	DISINT

	LDA	$D301
	PHA

	LDA	RBTAB
	LDY	#(R03-RESID)+1
	STA	(SRCHADR),Y

	STA	$D301

	LDY	#0

STRT7C	LDA	RES2,Y
	STA	$4000,Y

	INY
	BNE	STRT7C
*
	PLA
	STA	$D301

	JSR	ENINT

* FORMATOVANI

	LDA	FFLAG
	BNE	FORM1

	LDA	#1
	LDX	#'R'
	LDY	#64
	JSR	RWRD

	LDA	BUFF+11
	CMP	MAXSECT
	BNE	FORM1
	LDA	BUFF+12
	CMP	MAXSECT+1
	BNE	FORM1

	LDA	#$80
	CMP	BUFF+7
	BNE	FORM1
	CMP	BUFF+31
	BNE	FORM1

	LDA	BUFF+32
	CMP	#$22
	BNE	FORM1
	JMP	FORMEND

*#MAPS

FORM1	LDA	MAXSECT+1
	LSR
	LSR
	TAX
	INX
	STX	MAPS
*DIR
	JSR	CLBUF

	LDX	MAPS
	INX
	INX
	TXA
	INX
	STX	BUFF+4

	JSR	WRRD

	JSR	CLBUF

	LDX	#13
FORM1B	LDA	VZDIR,X
	STA	BUFF+3,X
	DEX
	BPL	FORM1B

	LDA	MAPS
	CLC
	ADC	#3
	JSR	WRRD

*BITMAPS
	JSR	CLBUF

	LDA	MAXSECT
	STA	MKONEC
	LDA	MAXSECT+1
	LSR
	ROR	MKONEC
	LSR
	ROR	MKONEC
	LSR	MKONEC

	LDX	#2
	STX	MSAVE
	DEX
	STX	MIX

*X=1
	LDA	MAPS
	ASL
	TAY
	LDA	MPSTB,Y
	STA	BUFF-1,X
	LDA	MPSTB+1,Y

	DTA	$2C	;BIT

FORM1C	LDA	#$FF
	STA	BUFF,X

	LDA	MIX
	CMP	MAPS
	BEQ	FORM1D

FORM1CB	INX
	BPL	FORM1C

	LDA	MSAVE
	JSR	WRRD

	JSR	CLBUF

	INC	MIX
	INC	MSAVE

	LDX	#0
	BEQ	FORM1C
*

FORM1D	CPX	MKONEC
	BNE	FORM1CB

	LDA	#$FC
	STA	BUFF,X

	LDA	MSAVE
	JSR	WRRD

*BOOT SECTOR

	JSR	CLBUF

	LDX	#32

FORM1E	LDA	VZBOOT,X
	STA	BUFF,X
	DEX
	BPL	FORM1E

	LDA	MAPS
	STA	BUFF+15
	CLC
	ADC	#2
	STA	BUFF+9

	LDA	MAXSECT
	STA	BUFF+11
	SEC
	SBC	MAPS
	TAY
	LDA	MAXSECT+1
	STA	BUFF+12
	SBC	#0
	TAX

	TYA
	SEC
	SBC	#4
	STA	BUFF+13
	TXA
	SBC	#0
	STA	BUFF+14

	LDA	$D20A
	STA	BUFF+39

	LDA	#1
	JSR	WRRD
***
	JSR	PRINT
	DTA	155
	DTA	C"RAM-Disk formatted."
	DTA	0
***

FORMEND	JSR	PRINT
	DTA	155
	DTA	C"RAM-Disk"
	DTA	C" is D"
	DTA	0

	LDA	DNUMB
	ORA	#$30
	JSR	PRT1

	JSR	PRINT
	DTA	C": ("
	DTA	C"Size: "
	DTA	0

* PRINT SIZE

	LDA	#0
	STA	MAXSECT+1
	STA	TEMP2

	LDA	NBANKS
	LDX	#3
	STX	TEMP1

PRTSZ1	ASL
	ROL	MAXSECT+1
	DEX
	BPL	PRTSZ1

	STA	MAXSECT

PRTSZ2	LDX	TEMP1
	LDY	#0
	BEQ	PRTSZ4
*
PRTSZ3	LDA	MAXSECT
	SEC
	SBC	PRTSZTL,X
	STA	MAXSECT
	LDA	MAXSECT+1
	SBC	PRTSZTH,X
	STA	MAXSECT+1

	INY

PRTSZ4	LDA	MAXSECT
	CMP	PRTSZTL,X
	LDA	MAXSECT+1
	SBC	PRTSZTH,X
	BCS	PRTSZ3

	TYA
	BEQ	PRTSZ5
	INC	TEMP2

PRTSZ5	ORA	#$20
	LDY	TEMP2
	BEQ	PRTSZ6
	ORA	#$10

PRTSZ6	JSR	PRT1

	DEC	TEMP1
	BPL	PRTSZ2
***

	JSR	PRINT
	DTA	C" kB)"
	DTA	155,0

	JMP	(10)
***

PRTSZTL	DTA	1,10,100,$E8
PRTSZTH	DTA	0,0,0,3

***

MPSTB	DTA	A($FF0F,$FF07,$FF03,$FF01)
	DTA	A($FF00,$7F00,$3F00,$1F00)
	DTA	A($0F00)

VZBOOT	DTA	'S'
	DTA	3,0,48,$E0,7
	JMP $3080
	DTA	A(0,0,0)
	DTA	0,2,0,32,0,4,0
	DTA	C"RAM-DISK"
	DTA	0,128,$22

VZDIR	DTA	23,0,0
	DTA	C"MAIN       "

***

CLBUF	LDX	#127
	LDA	#0

CLBF2	STA	BUFF,X
	DEX
	BPL	CLBF2

	RTS
****

WRRD	LDX	#'W'
	LDY	#128

RWRD	STX	STCMD
	STY	STSTAT
	STA	STAUX

	LDX	#11
RWRD2	LDA	STAB,X
	STA	$300,X
	DEX
	BPL	RWRD2

	LDA	10
	SEC
	SBC	#10
	STA	SIOJMP+1
	LDA	11
	SBC	#0
	STA	SIOJMP+2

	JSR	SIOJMP
	BMI	SIOERR

	RTS
**

SIOJMP	JMP	($FFFF)

**

STAB	DTA	$31
STDRIV	DTA	0
STCMD	DTA	0
STSTAT	DTA	0
	DTA	A(BUFF)
	DTA	7,0
	DTA	A(128)
STAUX	DTA	A(0)

**

SIOERR	JSR	PRINT
	DTA	155,253
	DTA	C"Internal Error!"
	DTA	155,0

	JMP	(10)
***

DECMAXS	LDA	MAXSECT
	BNE	STRT7B
	DEC	MAXSECT+1
STRT7B	DEC	MAXSECT

	RTS

****

REMOVE	JSR	SRCHRUT
	BCC	REMNOT
	JMP	REMOV2
**

REMNOT	JSR	PRINT
	DTA	155
	DTA	C"RAM-Disk not "
	DTA	C"installed!"
	DTA	155,0

REMCANT	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,253,0

	JMP	(10)
**

REM2LST	JSR	PRINT
	DTA	155
	DTA	C"RAM-Disk is not the"
	DTA	C" last"
	DTA	155
	DTA	C"installed handler!"
	DTA	155,0

	JMP	REMCANT
**

REMOV2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	REM2LST

	CPY	#RESLEN
	BNE	REM2LST

*REMOVE IT

	LDY	#2

REM3	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	REM3
*
	LDY	#(RDSPUV-RESID)+1
	LDA	(SRCHADR),Y
	TAX
	INY
	LDA	(SRCHADR),Y

	LDY	#1
	STA	(SIOADR),Y
	DEY
	TXA
	STA	(SIOADR),Y
***

	JSR	PRINT
	DTA	155
	DTA	C"RAM-Disk removed."
	DTA	155,0

	JMP	(10)

****

SRCHRUT	LDA	743
	SEC
	SBC	#RESLEN
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R03-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#R04-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST2-RESID
	BNE	SRCHR3
*
	LDY	#RDSPUV-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR4

	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS
*****

RAMTEST	LDX	#63
	LDA	#$FF
RT0A	STA	RBTAB,X
	DEX
	BPL	RT0A
*
	JSR	DISINT

	LDA	$D301
	PHA

*SAVE OLD

	LDX	#64

RT0	JSR	RSWITCH

	LDA	$4080
	STA	BUFF,X
	LDA	$5032
	STA	BUFF+65,X
	LDA	$7FFE
	STA	BUFF+130,X

	DEX
	BPL	RT0

*SET NEW

	LDX	#64

RT1	JSR	RSWITCH

	STX	$4080
	STX	$5032
	STX	$7FFE

	DEX
	BPL	RT1

*TEST

	LDY	#0
	LDX	#64

RT2	JSR	RSWITCH
	CPX	$4080
	BNE	RT3
	CPX	$5032
	BNE	RT3
	CPX	$7FFE
	BNE	RT3

	LDA	SWTAB,X
	STA	RBTAB,Y
	INY

RT3	DEX
	BNE	RT2

	STY	NBANKS

*OBNOV OLD

	LDX	#64

RT4	JSR	RSWITCH

	LDA	BUFF,X
	STA	$4080
	LDA	BUFF+65,X
	STA	$5032
	LDA	BUFF+130,X
	STA	$7FFE

	DEX
	BPL	RT4

*KONEC

	PLA
	STA	$D301
***

ENINT	LDA	#$40
	STA	54286
	CLI

	RTS
***

DISINT	LDA	#0
	STA	54286
	SEI

	RTS
***

RSWITCH	LDA	SWTAB,X
	STA	$D301

	RTS

***********

RESID	JSR	$FFFF

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	RTS
***

RDSIO	LDA	$D301
	PHA

	LDX	#0
	STX	54286
	SEI

R03	LDX	#$FF
TST2	STX	$D301

	JSR	$4000

	DEY
	BMI	RDSIO3

R04	LDX	#$FF
	STX	$D301

	LDA	($32),Y

R05	LDX	#$FF
TST3	STX	$D301

	STA	($34),Y

	DEY
	BPL	R04

	INY

RDSIO3	PLA
	STA	$D301

	LDA	#$40
	STA	54286
	CLI

	INY
	BEQ	RDSPUV

	STY	$303
	RTS
**

RDSPUV	JMP	$FFFF

***

RESLEN	EQU	*-RESID

*******

RES2	LDA	$300
	CMP	#$31
	BNE	RES2P

	LDA	$301
RR1	CMP	#0
	BEQ	RES2B

RES2P	LDY	#0
	RTS
**

RES2B	LDA	$304
	STA	$34
	LDA	$305
	STA	$35

	LDA	$309
	BNE	R2NAK
	LDY	$308

	LDA	$302
	CMP	#'S'
	BNE	R2RW
*STATUS

	LDX	$303
	CPX	#$40
	BNE	R2NAK

	CPY	#4
	BNE	R2NAK

	LDA	#<((RDSTAT+RESO))
	STA	$32
	LDA	#>((RDSTAT+RESO))
	STA	$33

	LDA	RBTAB+RESO
RR3	STA	R04+1

**

SPSW3	LDA	#$FF
RR10	STA	R05+1

	RTS

***

RR8READ	CPX	#$40
	BEQ	SPSW3
**

R2NAK	LDY	#139
	RTS

***

R2RW	CPY	#128
	BNE	R2NAK

	LDY	$30A	;MAXSECT
RR5	CPY	#2
	LDA	$30B
	TAX
RR6	SBC	#1
	BCS	R2NAK

	TYA
	ORA	$30B
	BEQ	R2NAK
*
	INY
	BNE	RR6B
	INX

RR6B	TYA

	LSR
	PHA
	LDA	#0
	ROR
	STA	$32

	TYA
	ASL
	TXA
	ROL
	TAY

	LDA	RBTAB+RESO,Y
RR7	STA	R04+1
RR8	STA	R05+1

	PLA
	AND	#$3F
	ORA	#$40
	STA	$33
**
	LDY	#128
	LDX	$303

	LDA	$302
	CMP	#'R'
	BEQ	RR8READ
	CMP	#'W'
	BEQ	RR8WR
	CMP	#'P'
	BNE	R2NAK

RR8WR	CPX	#128
	BNE	R2NAK

	LDA	$32
	LDX	$34
	STA	$34
	STX	$32

	LDA	$33
	LDX	$35
	STA	$35
	STX	$33

SPSW2	LDA	#$FF
RR9	STA	R04+1

	RTS
***

RDSTAT	DTA	$10,$FF,$FF,$FF

***

RBTAB	DTA	0

***

RESO	EQU	$4000-RES2

RES2LEN	EQU	RBTAB+64-RES2

