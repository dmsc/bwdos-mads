	ORG	$5FFD,$A800

O	EQU	$A800-$5FFD
	JMP	UTAJ+O
***
*   PRED SAVE SPUSTIT OD $A800,
*   PAK ULOZIT OD A803 NA 6000
***

*************************************
*				*
*   " A R G S P R N "  PRO BW-DOS	*
*				*
*************************************

SRCHADR	EQU	128
RELZP	EQU	130
REDFLG	EQU	132
TCNT	EQU	134
TTMP	EQU	135
MODFLG	EQU	137
CLOSTMP	EQU	138
CLOSIX	EQU	139
CLOSAD	EQU	140

*** PRO REZIDENT

PORTA	EQU	$D504
PORTB	EQU	$D505
PACTL	EQU	$D506
PBCTL	EQU	$D507

***

ODT0	LDA	START

	LDX	#0
	LDY	#10

ODTAJ	LDA	PRINT,X
	EOR	#$55
ODT2	STA	PRINT,X

	INX
	BNE	ODTAJ

	INC	ODTAJ+2
	INC	ODT2+2

	DEY
	BNE	ODTAJ
*
	LDA	#76
	STA	ODT0

	JMP	START

***

PRINT	PLA
	STA	PRINT3+1
	PLA
	STA	PRINT3+2

PRINT2	INC	PRINT3+1
	BNE	PRINT3
	INC	PRINT3+2

PRINT3	LDA	$FFFF
	BEQ	PRINT4

	JSR	PRT1
	JMP	PRINT2
**

PRINT4	LDA	PRINT3+2
	PHA
	LDA	PRINT3+1
	PHA

PRTEX	RTS

***

PRT1	TAY

	LDA	#0
	TAX
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	TYA
	JMP	$E456
***

GETNAME	JMP	PRTEX

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA	155,253
	DTA	C"Bad DOS version !"
	DTA	155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'O'
	BNE	ST2ON
*OFF?
	INY
	LDA	(10),Y
	CMP	#'F'
	BNE	SYNTAX

	INY
	CMP	(10),Y
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#$9B
	BNE	SYNTAX
	JMP	DISABLE
*ON?

ST2ON	LDX	#0

	CMP	#'/'
	BNE	ST2O2

	INY
	LDA	(10),Y
	CMP	#'L'
	BNE	ST2O0

	INY
	LDA	(10),Y
	CMP	#'C'
	BEQ	BOTH
	BNE	ST2O2

ST2O0	INX
	CMP	#'N'
	BEQ	ST2O1

	INX
	CMP	#'C'
	BNE	SYNTAX

	INY
	LDA	(10),Y
	CMP	#'L'
	BNE	ST2O2

BOTH	LDX	#3

ST2O1	INY
	LDA	(10),Y

ST2O2	CMP	#$9B
	BEQ	START3

***

SYNTAX	JSR	PRINT
	DTA	155
	DTA	C"Syntax: ARGSPRN [/(N|C|L|CL)]"
	DTA	155
	DTA	C"        ARGSPRN OFF"
	DTA	155,0

	JMP	(10)

*** NENI UZ ?

START3	STX	MODFLG
	LDA	ST3T,X
	STA	REDFLG

	JSR	SRCHRUT
	BCC	START4
*JE!
	JMP	CHGCON

***INSTALL

START4	JSR	ARGSTST
	BCS	START4A

*RTC/P NEEX.

	JSR	PRINT
	DTA	155
	DTA	C"ARGS RTC/P cartridge "
	DTA	C"not installed !"
	DTA	155,0
	JMP	(10)
***

START4A	JSR	CLOSEP

	LDX	REDFLG

	LDA	743
	STA	SRCHADR
	CLC
	ADC	RESLENT,X
	STA	743
	STA	R01+1

	LDA	744
	STA	SRCHADR+1
	ADC	#0
	STA	744
	STA	R02+1
*RELOK

RLK0	LDX	#0

RLK1	LDA	RELOKT,X
	STA	RELZP
	LDA	RELOKT+1,X
	STA	RELZP+1

	ORA	RELZP
	BEQ	INSTAL2

	LDA	RELOKT+2,X
	SEC
	SBC	#<RESID
	TAY
	LDA	RELOKT+3,X
	SBC	#>RESID
	PHA

	TYA
	LDY	#1

	CLC
	ADC	SRCHADR
	STA	(RELZP),Y
	INY
	PLA
	ADC	SRCHADR+1
	STA	(RELZP),Y

	INX
	INX
	INX
	INX

	JMP	RLK1

*** MODIFIKUJ DLE REZIMU

INS2T1	DTA	$A9,$EA,$A9,$F0
INS2T2	DTA	$0A,$EA,$0D,PUTCRLF-PUT2
**

INSTAL2	LDX	MODFLG
	LDA	INS2T1,X
	STA	PUTCHG
	LDA	INS2T2,X
	STA	PUTCHG+1

*** DOKONCI RELOKACI

	LDA	RELTB
	STA	R03+1
	LDA	RELTB+1
	STA	R04+1

*** ULOZ PUV. ADRESU

	JSR	SRCHHATB
	BCC	IRI3

	LDA	$31B,X
	STA	OLDADR
	LDA	$31C,X
	STA	OLDADR+1
**

IRI3	LDX	REDFLG
	LDY	RESLENT,X

INST2C	DEY
	LDA	RESID,Y
	STA	(SRCHADR),Y
	TYA
	BNE	INST2C

**

	LDY	#2
INSTL3	LDA	11,Y
	STA	(SRCHADR),Y

	LDA	SRCHADR-1,Y
	STA	11,Y

	DEY
	BNE	INSTL3

** INIT

	LDA	SRCHADR
	CLC
	ADC	#3
	STA	INSTL4J+1
	LDA	SRCHADR+1
	ADC	#0
	STA	INSTL4J+2

INSTL4J	JSR	$E474

**

	JSR	ARGSDRV
	JSR	PRINT
	DTA	155
	DTA	C"installed."
	DTA	155,0

INSTL5	LDX	MODFLG
	BEQ	INST50
	DEX
	BEQ	INST51
	DEX
	BEQ	INST52

INST53	JSR	PRINT
	DTA	C"(EOL -> CR/LF)"
	DTA	155,0
	JMP	(10)

INST50	JSR	PRINT
	DTA	C"(EOL -> LF)"
	DTA	155,0
	JMP	(10)

INST51	JSR	PRINT
	DTA	C"(No EOL conversion.)"
	DTA	155,0
	JMP	(10)

INST52	JSR	PRINT
	DTA	C"(EOL -> CR)"
	DTA	155,0
	JMP	(10)

***

DISABLE	JSR	SRCHRUT
	STA	MODFLG
	TAX
	LDA	ST3T,X
	STA	REDFLG
	BCS	DISABLE2

	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"not"
	DTA	155
	DTA	C"installed!"
	DTA	155,0

	JMP	DISERR2
***

DISCANT	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"is not"
	DTA	155
	DTA	C"the last installed"
	DTA	C" handler!"
	DTA	155,0

DISERR2	JSR	PRINT
	DTA	C"Can't remove."
	DTA	155,0

	JMP	(10)
***

DISABLE2	LDA	743
	SEC
	SBC	SRCHADR
	TAY
	LDA	744
	SBC	SRCHADR+1
	BNE	DISCANT

	TYA
	LDX	REDFLG
	CMP	RESLENT,X
	BNE	DISCANT

** ZAVRI

	JSR	CLOSEP

** VRAT DO HATABS

	JSR	SRCHHATB
	BCC	RRI3

	LDY	#OLDADR-RESID
	LDA	(SRCHADR),Y
	STA	CLOSAD
	INY
	LDA	(SRCHADR),Y
	STA	CLOSAD+1

	ORA	CLOSAD
	BNE	DIS2A

	STA	$31A,X

DIS2A	LDA	CLOSAD
	STA	$31B,X
	LDA	CLOSAD+1
	STA	$31C,X

*ODSTRAN
RRI3	LDY	#2

DISBL2	LDA	(SRCHADR),Y
	STA	11,Y

	LDA	SRCHADR-1,Y
	STA	743-1,Y

	DEY
	BNE	DISBL2
**
	JSR	ARGSDRV
	JSR	PRINT
	DTA	C"removed."
	DTA	155,0

	JMP	(10)
****

ARGSDRV	JSR	PRINT
	DTA	155
	DTA	C"Printer driver for"
	DTA	C" ARGS RTC/P "
	DTA	0
	RTS
****

ST3T	DTA	1,1,1,0

**** ZMENA KONFIGURACE

CHGCNE	JSR	ARGSDRV
	JSR	PRINT
	DTA	C":"
	DTA	155
	DTA	C"May not change confi"
	DTA	C"guration from/to"
	DTA	155
	DTA	C"the CR/LF mode! (Dri"
	DTA	C"ver is different)"
	DTA	155,0
	JMP	(10)
**

CHGCON	TAX
	LDA	ST3T,X
	BEQ	CHGCNE
	LDA	REDFLG
	BEQ	CHGCNE

	LDX	MODFLG
	LDY	#PUTCHG-RESID

	LDA	INS2T1,X
	STA	(SRCHADR),Y
	INY
	LDA	INS2T2,X
	STA	(SRCHADR),Y
*
	JSR	ARGSDRV
	JSR	PRINT
	DTA	C":"
	DTA	155
	DTA	C"Configuration "
	DTA	C"changed."
	DTA	155,0

	JMP	INSTL5

*** ZAVRI VSECHNO, KDE JE "P:"

*SEARCH IN HATABS

CLOSEP	JSR	SRCHHATB
	BCC	CLP7

	STX	CLOSIX
	LDA	#$70

CLP4	STA	CLOSTMP
	TAX
	LDA	$340,X
	CMP	CLOSIX
	BNE	CLP5

	LDA	#12
	STA	$342,X
	JSR	$E456

CLP5	LDA	CLOSTMP
	SEC
	SBC	#$10
	BCS	CLP4

* BATCH/HARDCOPY

	LDA	10
	SEC
	SBC	#8
	STA	CLOSAD
	LDA	11
	SBC	#0
	STA	CLOSAD+1

	LDA	10
	CLC
	ADC	#8
	STA	XDIVJ+1
	LDA	11
	ADC	#0
	STA	XDIVJ+2

	LDY	#0
	JSR	CLP6

	LDY	#1
**

CLP6	LDA	(CLOSAD),Y
	CMP	CLOSIX
	BNE	CLP7

XDIVJ	JMP	($FFFF)

CLP7	RTS

***

SRCHHATB	LDX	#0
SRCHH2	LDA	$31A,X
	CMP	#'P'
	BEQ	SRCHH3
	INX
	INX
	INX
	CPX	#33
	BCC	SRCHH2
	CLC
SRCHH3	RTS

****

SRCHRUT	LDA	743
	SEC
	SBC	RESLENT+1
	STA	SRCHADR
	LDA	744
	SBC	#0
	STA	SRCHADR+1
*

SRCHR1	LDY	#R03-RESID

SRCHR2	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST1-RESID
	BNE	SRCHR2
*
	LDY	#TST2-RESID

SRCHR3	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#INI-RESID
	BNE	SRCHR3
*
	LDY	#TST4-RESID

SRCHR4	LDA	(SRCHADR),Y
	CMP	RESID,Y
	BNE	SRCHNXT

	DEY
	CPY	#TST3-RESID
	BNE	SRCHR4
**REZIM
	LDY	#PUTCHG+1-RESID
	LDA	(SRCHADR),Y
	LDX	#3

SRCHR5	CMP	INS2T2,X
	BEQ	SRCHR6

	DEX
	BPL	SRCHR5
	BMI	SRCHNXT

SRCHR6	TXA
*C=1
	RTS
**

SRCHNXT	LDA	SRCHADR
	BNE	SNXT2
	DEC	SRCHADR+1
SNXT2	DEC	SRCHADR

	LDA	SRCHADR+1
	CMP	#$10
	BCS	SRCHR1

	RTS

**** TEST PRITOMNOSTI RTC/P MODULU

ARGSTST	LDA	#$3C
	STA	PACTL
	LDX	#$55
	STX	PORTA

	LDA	#$38
	STA	PACTL

	LDY	#$FF
	STY	PORTA

	CPY	PORTA
	BNE	TSTNE

	LDA	#$3C
	STA	PACTL
	CPX	PORTA
	BNE	TSTNE

	INY
	LDA	#$30
	STA	PBCTL

	ASL
	STA	PORTB	;$60
	CMP	PORTB
	BNE	TSTNE

	LDA	#$34
	STA	PBCTL
	STY	PORTB

	SEC
	RTS

TSTNE	CLC
	RTS

***********REZIDENT

RESID	JSR	$E474

R01	LDA	#$22
	STA	743
R02	LDA	#$22
TST1	STA	744

	LDY	#$30
	LDX	#$FF

RI1	LDA	$31A,Y
	BNE	RI2
	TYA
	TAX

RI2	CMP	#'P'
	BEQ	RI4

	DEY
	DEY
	DEY
	BPL	RI1

	TXA
	TAY
	BMI	INI

RI4	LDA	#'P'
	STA	$31A,Y
R03	LDA	#<PTAB
	STA	$31B,Y
R04	LDA	#>PTAB
	STA	$31C,Y
	BNE	INI
****

OLDADR	DTA	A(0)

****

PTAB	DTA	A(INI-1)
S01	DTA	A(INI-1)
S02	DTA	A(NOTSUP-1)
S03	DTA	A(PUT-1)
S04	DTA	A(INI-1)
S05	DTA	A(NOTSUP-1)

INI	LDA	#$38
INI2	STA	PACTL
	LDY	#$FF
	STY	PORTA
	LDA	#$3C
	STA	PACTL

	INY
	LDA	#$30
	STA	PBCTL
	ASL
	STA	PORTB	;$60
	LDA	#$34
	STA	PBCTL
	STY	PORTB

	INY
	RTS
***

BREAK	LDY	#128
	STY	17
	RTS
NOTSUP	LDY	#146
	RTS
TIMOT	LDY	#138
PUTRET	RTS
****

PUT	CMP	#155
TST2	BNE	PUT2
PUTCHG	LDA	#$0D

PUT2	TAX
S06	JSR	INI
TST3	LDY	20
	DEY

PUT3	LDA	17
	BEQ	BREAK
	CPY	20
	BEQ	TIMOT
	LDA	PORTB
	BMI	PUT3

	STX	PORTA

	LDA	#$30
TST4	BNE	INI2

**** DALE POUZE PRO CR/LF

PUTCRLF	LDA	#$0D
S07	JSR	PUT2
	BMI	PUTRET
	LDA	#$0A
	BNE	PUT2

*****

RESLEN1	EQU	PUTCRLF-RESID
RESLEN2	EQU	*-RESID

RESLENT	DTA	RESLEN2,RESLEN1

***

RELTB	DTA	A(PTAB)

RELOKT	DTA	A(PTAB-1,INI-1)
	DTA	A(S01-1,INI-1)
	DTA	A(S02-1,NOTSUP-1)
	DTA	A(S03-1,PUT-1)
	DTA	A(S04-1,INI-1)
	DTA	A(S05-1,NOTSUP-1)
	DTA	A(S06,INI)
	DTA	A(S07,PUT2)

	DTA	A(RELTB-1,PTAB)

	DTA	A(0,0)

**************
*
* KONEC PROGRAMU, NASLEDUJE SLUZEBNI
* RUTINKA
*
**************

UTAJ	LDA	#<((PRINT+O))
	STA	0
	LDA	#>((PRINT+O))
	STA	1

UTAJ2	LDY	#0
	LDA	(0),Y
	EOR	#$55
	STA	(0),Y

	INC	0
	BNE	UTAJ3
	INC	1

UTAJ3	LDA	0
	CMP	#<((UTAJ+O))
	BNE	UTAJ2

	LDA	1
	CMP	#>((UTAJ+O))
	BNE	UTAJ2

	RTS

****

SAVEOD	EQU	$A803
SAVETO	EQU	UTAJ+O
