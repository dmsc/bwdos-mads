*************************************
*				*
*   " D I R M A S T "  FOR BW-DOS	*
*				*
*************************************

COUNT	EQU	130
ZPG1	EQU	132
SCRPOS	EQU	134
KURPOS	EQU	136
KURFLG	EQU	138
ZPG2	EQU	140
KURTYP	EQU	152
ORIPO	EQU	154

SCRCNT	EQU	139
AKTPOS	EQU	142
ZOBZ	EQU	144
ZIF	EQU	146
KAFE	EQU	147
ODDEL	EQU	150	;Must be ZP
TIX	EQU	151

CONVPR	EQU	156

PUTY1B	EQU	159
PUTY1C	EQU	160
FNAME	EQU	161

*** VOLNO: 190


***
	ORG	$2BC0

START	LDA	$700
	CMP	#'S'
	BEQ	INI00

	LDY	#<STERR
	DTA	$2C
SYNTER	LDY	#<SYNTTX
	JSR	PRINT
	JMP	(10)
***
INI00	LDX	#39
	LDA	#160
INI1	STA	PRAL,X
	DEX
	BPL	INI1

* End of initialization
	JSR	CLOSE

* Check parameters

	LDA	10
	CLC
	ADC	#3
	STA	GETNM+1
	LDA	11
	ADC	#0
	STA	GETNM+2

	JSR	GETNM
	BNE	L0BA

	LDA	#155
	STA	(10),Y
	JSR	DEJNAM

	LDY	#36
	BNE	L0BAB
*

L0BA	JSR	DEJNAM
	JSR	GETNM
	BNE	LOOP2P

L0BAB	INY
	LDA	(10),Y
	CMP	#'Q'
	BNE	SYNTER
	INY
	LDA	(10),Y
	CMP	#155
	BNE	SYNTER

* Ask for pressing a key to continue
	LDY	#<QTEXT
	JSR	PRINT
	JSR	GETK
LOOP2P	JMP	LOOP2

* Messages:
STERR	DTA	C"Incorrect DOS version!"
	DTA	253,155
***
SYNTTX	DTA	C"Syntax: DIRMAST [Dn:][path] [/Q]"
	DTA	253,155
***
QTEXT	DTA	C"Insert disk, and press any key..."
	DTA	155
***
CLST	DTA	125,155
***
ERTX	DTA	125
	DTA	C"Error "
ERNM	DTA	C"???!"
	DTA	253,155
***
TOOLONG	DTA	125,253
	DTA	C"Directory too long!"
EOLT	DTA	155
***

* Check that the texts are at the same page
	ERT	>STERR <> >EOLT

***
GETNM	JSR	GETNM
	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	RTS
**

DEJNAM	LDY	#33

L0B	LDA	(10),Y
	STA	FNAME-33,Y

	INY
	CPY	#61
	BCC	L0B

	RTS

STARS	DTA	C"*.*"
	DTA	155
****
LOOP2	LDA	#155
	STA	FNAME+28

	LDA	#0
	STA	$35A
	STA	$35B

	LDA	#44
	JSR	CIONAME

* Append "*.*" to command line
	LDX	#4
CPSTAR	LDA	STARS-1,X
	STA	FNAME+3-1,X
	DEX
	BNE	CPSTAR
	TXA
* Init COUNT and ZPG2
	STA	COUNT
	STA	COUNT+1
	JSR	POSCNT

	LDA	#$14
	JSR	OPENFNM

LOOP3	LDA	ZPG2
	STA	$354
	LDA	ZPG2+1
	STA	$355

	LDA	#23
	STA	$358
	LDA	#0
	STA	$359

	LDA	#7
	JSR	CIOC2

	BPL	LOOP4

	CPY	#136
	BEQ	LOOP5

	JMP	ERROR
*
LOOP4	LDY	#0
	LDA	(ZPG2),Y
	BEQ	LOOP5

	INC	COUNT
	BNE	LOOP4A
	INC	COUNT+1

LOOP4A	JSR	ADZ2

	LDA	#255-23
	JSR	TSTSIZE
	BCS	LOOP3

	LDY	#<TOOLONG
	JMP	DOSPRT

LOOP5	JSR	CLOSE

*** CHKDSK
LOOP6	LDX	#>CHK1
	LDY	#<CHK1
	JSR	CHKDSK

* Setup screen
LOOP6B	LDA	COUNT
	STA	ORIPO
	LDA	COUNT+1
	STA	ORIPO+1

	JSR	CUROFF

	LDA	#$F2
	STA	712
	LDA	#$F4
	STA	710
	LDA	#$0A
	STA	709

	LDA	#0
	STA	SCRPOS
	STA	SCRPOS+1
	STA	KURPOS
	STA	KURPOS+1
MLFLAG	STA	KURFLG

***
MAINLOOP	JSR	PHEADER

	LDA	#(OL1-MSGS)/2
	JSR	ZOBR

	LDA	#(OL2-MSGS)/2
	JSR	ZOBR

* Build screen for directory display, 21 rows
	LDA	#21
	STA	SCRCNT

* Init AKTPOS and ZPG2 to first entry to display, SCRPOS.
	LDA	SCRPOS
	LDX	SCRPOS+1
	JSR	POSAX

* Clear text row buffer
ML4	LDY	#39
	LDA	#32

ML5	STA	BUFFT,Y
	DEY
	BPL	ML5

* Check if the cursor is in this row
	LDA	#0
	STA	KAFE

	LDA	KURPOS
	CMP	AKTPOS
	BNE	ML6

	LDA	KURPOS+1
	CMP	AKTPOS+1
	BNE	ML6
*C=1
	ROR	KAFE

* Skip if we have no entry selected
	LDA	KURFLG
	BEQ	ML6

* Show selected entry at left
	LDX	#11

ML5B	LDA	BUFFF+6-1,X
	EOR	#$80
	STA	BUFFT-1,X
	DEX
	BNE	ML5B

	STX	KAFE

	LDA	#$88	;"up triangle"
	STA	BUFFT+11

* Build file row, only if we have files left
ML6	LDA	AKTPOS+1
	CMP	COUNT+1
	BCC	ML6A
	BNE	ML8

	LDA	AKTPOS
	CMP	COUNT
	BCS	ML8

* Show entry type
ML6A	JSR	FLAG

	STA	BUFFT+12

	LDX	KAFE
	BPL	ML6C

* Store type of selected entry
	STA	KURTYP

* Show filename
ML6C	LDY	#16
	LDX	#11

ML7	LDA	(ZPG2),Y
ML7S	EOR	KAFE
	STA	BUFFT+13,X

	CPX	#9
	BNE	ML7Y

	DEX
	LDA	#32
	BNE	ML7S

ML7Y	DEY
	DEX
	BPL	ML7

	LDA	BUFFT+12	; Skip datt/time if entry is comment
	CMP	#34	;"
	BEQ	ML8

* Show DATE
	LDX	#0
	LDY	#17

	JSR	PUTY	; 2 digits
	JSR	PUTYPOM	; '-' and 2 digits
	JSR	PUTYPOM	; '-' and 2 digits

* And TIME
	LDA	#32
	JSR	PUTYCH	; ' ' and 2 digits
	LDA	#':'
	JSR	PUTYCH	; ':' and 2 digits

* Print line to screen
ML8	LDA	#(BUFFT-MSGS)/2
	JSR	ZOBR

* Go to next  entry
	JSR	ADZ2

	INC	AKTPOS
	BNE	ML9
	INC	AKTPOS+1

ML9	DEC	SCRCNT
	BEQ	MENU

	JMP	ML4

* Process MENU keys
MENU	JSR	GETK

; Always valid
	CMP	#27
	BEQ	ESCRUT

	CMP	#$1C
	BEQ	UPJ
	CMP	#'-'
	BEQ	UPJ
	CMP	#'='
	BEQ	DOWJ
	CMP	#$1D
	BEQ	DOWJ
	CMP	#$1F
	BEQ	RIGHT
	CMP	#'*'
	BNE	NORI

* Put a picked entry into the list
RIGHT	LDA	KURFLG
	BEQ	MENU
	JSR	INSER
	JMP	MAINLOOP

UPJ	JMP	UP
DOWJ	JMP	DOWN
LEFJ	JMP	LEFT
QUIT	JMP	Q2
TEXTJ	JMP	TEXT
**

; Only valid without a selected file
NORI	LDX	KURFLG
	BNE	MENU

	CMP	#$1E
	BEQ	LEFJ
	CMP	#'+'
	BEQ	LEFJ

; Allow inverse video for letters
	AND	#$5F
	CMP	#'E'
	BEQ	ERASE
	CMP	#'D'
	BEQ	DATE
	CMP	#'C'
	BEQ	TEXTJ
	CMP	#'Q'
	BEQ	QUIT
	CMP	#'T'
	BNE	MENU

* Change TIME
TIME	LDX	#':'
	LDY	#(TIMLIN-MSGS)/2
	LDA	#20
	BNE	TIME2J

* Change DATE
DATE	LDX	#'-'
	LDY	#(DATLIN-MSGS)/2
	LDA	#17
TIME2J	JMP	TIME2

*** ESC
ESCRUT	LDA	#(ESCLIN-MSGS)/2
	JSR	HORNI

	JSR	GETK
	AND	#$5F

	CMP	#'Y'
	BNE	MAINJ
*
DOSCLS	LDY	#<CLST
	JMP	DOSPRT

* Erase entry - only for comments or deleted files
ERASE	JSR	ZTST

	LDA	KURTYP
	CMP	#'='
	BEQ	ERA2

	CMP	#34	;"
	BNE	MAINJ
*
ERA2	JSR	DELET

	LDA	KURPOS
	CMP	COUNT
	BNE	MAINJ

	LDA	KURPOS+1
	CMP	COUNT+1
	BNE	MAINJ

	LDA	KURPOS
	ORA	KURPOS+1
	BEQ	MAINJ

	JSR	DKPO

MAINJ	JMP	MAINLOOP

* Add comment entry
TEXT	LDX	#11
	LDA	#32
T2	STA	TBU,X
	DEX
	BNE	T2

	LDA	#'|'
	STA	TBU+8

	LDX	#0
TLOPK0	STX	TIX

TLOPKU	LDA	#126
	STA	TBU,X

TLOP	LDA	#(TEXTT-MSGS)/2
	JSR	HORNI

	JSR	GETK

	CMP	#155
	BEQ	TEXTEOL
	CMP	#126
	BEQ	TEXTDEL

	LDX	TIX
	CPX	#12
	BCS	TLOP

	STA	TBU,X
	INX
	CPX	#8
	BNE	TLOPK0

	INX
	BNE	TLOPK0
**

TEXTDEL	LDX	TIX
	BEQ	TLOP

	LDA	#32
	STA	TBU,X

	DEX
	CPX	#8
	BNE	TLOPK0

	DEX
	BPL	TLOPK0
**

TEXTEOL	LDA	#32
	LDX	TIX
	STA	TBU,X

	LDX	#22
TEO2	LDA	COMLIN,X
	STA	BUFFF,X
	DEX
	BPL	TEO2

	LDX	#11
	LDY	#10
TEO3	LDA	TBU,X
	STA	BUFFF+6,Y
	DEX
	CPX	#8
	BNE	NO83

	DEX

NO83	DEY
	BPL	TEO3

MLJKF1	LDA	#1
	JMP	MLFLAG

* Pick an entry from list
LEFT	JSR	ZTST
	JSR	POSKU

	LDY	#22
LFT2	LDA	(ZPG2),Y
	STA	BUFFF,Y
	DEY
	BPL	LFT2

	JSR	DELET
	JMP	MLJKF1

***
UP	LDA	KURPOS
	ORA	KURPOS+1
	BEQ	NAKON

	JSR	DKPO

*MOVE SCR?

	LDA	KURPOS
	SEC
	SBC	SCRPOS
	TAY
	LDA	KURPOS+1
	SBC	SCRPOS+1
	BNE	UPX

	CPY	#2
	BCS	UPX

UPRO	LDA	SCRPOS
	ORA	SCRPOS+1
	BEQ	UPX

	LDA	SCRPOS
	BNE	UPRO2

	DEC	SCRPOS+1
UPRO2	DEC	SCRPOS

UPX	JMP	MAINLOOP

***

NAKON	LDA	COUNT
	SEC
	SBC	#18
	STA	SCRPOS

	LDA	COUNT+1
	SBC	#0
	STA	SCRPOS+1

	BCS	NAKON4

	LDA	#0
	STA	SCRPOS
	STA	SCRPOS+1

NAKON4	LDA	KURFLG
	CMP	#1

	LDA	COUNT
	SBC	#0
	STA	KURPOS
	LDA	COUNT+1
	SBC	#0
	STA	KURPOS+1

	BCC	NAZAC
	JMP	MAINLOOP
***

DOWN	LDA	COUNT
	SEC
	SBC	KURPOS
	TAY
	LDA	COUNT+1
	SBC	KURPOS+1
	BNE	DWN2

	TYA
	BEQ	NAZAC

	DEY
	BNE	DWN2

	LDA	KURFLG
	BEQ	NAZAC

DWN2	INC	KURPOS
	BNE	DWN3
	INC	KURPOS+1

DWN3	LDA	KURPOS
	SEC
	SBC	SCRPOS
	CMP	#19
	BCC	DWX

	INC	SCRPOS
	BNE	DWX
	INC	SCRPOS+1

DWX	JMP	MAINLOOP

***

NAZAC	LDA	#0
	STA	KURPOS
	STA	KURPOS+1
	STA	SCRPOS
	STA	SCRPOS+1
	JMP	MAINLOOP

***
Q5	LDY	#150
	JMP	ERROR
**

Q2	LDA	#(QTX-MSGS)/2
	JSR	HORNI

	JSR	GETK
	AND	#$5F

	CMP	#'Y'
	BNE	DWX

Q3	LDA	#(QTX2-MSGS)/2
	JSR	HORNI

	JSR	CLOSE

* Check if DISK was changed:
	LDX	#>CHK2
	LDY	#<CHK2
	JSR	CHKDSK

	LDX	#16
Q4	LDA	CHK1,X
	CMP	CHK2,X
	BNE	Q5	; Yes, error 150

	DEX
	BPL	Q4

* Open directory and save the full list
	LDA	#$18
	JSR	OPENFNM

	JSR	POSCNT

	SEC
	SBC	#<KONEC
	STA	$358

	TXA
	SBC	#>KONEC
	TAY

	ORA	$358	; Skip if no data to save
	BEQ	ORIDOP0

	LDA	#<KONEC
	LDX	#>KONEC
SAVEENT	STA	$354
	STX	$355
	STY	$359

	LDA	#11
	JSR	CIOCMD

* Check if we need to pad directory with zeros?
SAVEEND	LDA	COUNT
	CMP	ORIPO
	LDA	COUNT+1
	SBC	ORIPO+1
	BCS	ORIDOP0	; No, end.

* Increment COUNT
	INC	COUNT
	BNE	FILLZ
	INC	COUNT+1

* Fill one buffer with zeros
FILLZ	LDX	#23
	LDA	#0
ORIDNL	STA	BUFFF,X
	DEX
	BPL	ORIDNL

* Write buffer and retry
	LDA	#23
	STA	$358
	LDA	#<BUFFF
	LDX	#>BUFFF
	LDY	#0
	BEQ	SAVEENT

*
ORIDOP0	JSR	CLOSE
	BMI	ORID0ER

	JMP	DOSCLS

ORID0ER	JMP	ERROR

***
* Insert new entry
INSER	JSR	POSCNT

* Check if we have enough space
	LDA	#255-46
	JSR	TSTSIZE
	BCC	INSERERR

* Move remaining entries, one at a time
INSER2	LDA	AKTPOS
	CMP	KURPOS
	BNE	INSER4

	LDA	AKTPOS+1
	CMP	KURPOS+1
	BEQ	INSER5
*
INSER4	LDA	ZPG2
	SEC
	SBC	#23
	STA	ZPG1

	LDA	ZPG2+1
	SBC	#0
	STA	ZPG1+1
*
	JSR	COPYENT

	LDA	AKTPOS
	BNE	INSER4C
	DEC	AKTPOS+1
INSER4C	DEC	AKTPOS

	JMP	INSER2

* Now copy into place
INSER5	LDY	#22

INSER6	LDA	BUFFF,Y
	STA	(ZPG2),Y
	DEY
	BPL	INSER6

	INC	COUNT
	BNE	INSERERR
	INC	COUNT+1

INSERERR	LDA	#0
	STA	KURFLG

	LDA	KURPOS
	CMP	COUNT
	BNE	INSER7

	LDA	KURPOS+1
	CMP	COUNT+1
	BNE	INSER7

DKPO	LDA	KURPOS
	BNE	INSER7A
	DEC	KURPOS+1
INSER7A	DEC	KURPOS

INSER7	RTS

***

DELET	JSR	POSKU

DELET2	LDA	ZPG2
	CLC
	ADC	#23
	STA	ZPG1

	LDA	ZPG2+1
	ADC	#0
	STA	ZPG1+1

	JSR	COPYENT

	INC	AKTPOS
	BNE	DELET3
	INC	AKTPOS+1

DELET3	LDA	AKTPOS
	CMP	COUNT
	BNE	DELET2

	LDA	AKTPOS+1
	CMP	COUNT+1
	BNE	DELET2

	LDA	COUNT
	BNE	DELET4
	DEC	COUNT+1
DELET4	DEC	COUNT

	RTS

***

TIME2	STX	ODDEL
	STA	TIX
	JSR	ZTST

	LDX	KURTYP
	CPX	#34	;"
	BNE	TIME2Z

	JMP	MAINLOOP
*
TIME2Z	TYA
	PHA
	JSR	POSKU
	PLA
	JSR	HORNI

	LDY	#1
	STY	752
	STY	84
	DEY
	STY	86
	LDA	#29
	STA	85

	LDA	#3
	STA	SCRCNT

	; Clear cursor
	LDA	(94),Y
	STA	93

	LDA	#160
	DTA	$2C
TIME3	LDA	ODDEL

	JSR	PRT1

	JSR	TIMGET
*10
	STA	KAFE
	ASL
	ASL
	ADC	KAFE
	ASL

	STA	KAFE

	JSR	TIMGET

	CLC
	ADC	KAFE

	LDY	TIX
	STA	(ZPG2),Y

	INC	TIX

	DEC	SCRCNT
	BNE	TIME3

NOTD	JSR	CUROFF

	JMP	MAINLOOP

*

TIMGET	JSR	GETK
	AND	#127

	CMP	#$30
	BCC	TIMGET
	CMP	#$3A
	BCS	TIMGET

	PHA
	JSR	PRT1
	PLA

	AND	#15

	RTS

***

PUTYPOM	LDA	#'-'
PUTYCH	STA	BUFFT+26,X
	INX

PUTY	STY	PUTY1C
	STX	PUTY1B

	LDA	(ZPG2),Y
	JSR	CONVDC

	LDX	PUTY1B
	STA	BUFFT+26,X
	TYA
	STA	BUFFT+27,X
	INX
	INX

	LDY	PUTY1C
	INY
	RTS
**

FLAG	LDY	#0

	LDA	(ZPG2),Y
	BEQ	NODIR
	EOR	#9
	BEQ	CHKCOM
	CMP	#1
	BNE	NOFIL
	LDA	#' '	; Standard file
	RTS
NOFIL	AND	#$D6
	CMP	#$10
	BNE	NOERA
	LDA	#'='	; Erased entry
	RTS
NOERA	LDA	(ZPG2),Y
	AND	#$FE
	CMP	#$28
	BNE	NODIR
	LDA	#'>'	; Directory
	RTS
NODIR	LDA	#'?'	; Unknown
	RTS

CHKCOM	LDY	#5
FPOR1	ORA	(ZPG2),Y
	DEY
	BNE	FPOR1

	LDY	#17
FPOR2	ORA	(ZPG2),Y
	INY
	CPY	#23
	BCC	FPOR2

	TAY
	BNE	FTXN

	LDA	#34	;" - Comment
	RTS
FTXN	LDA	#'*'	; Protected file
	RTS

***
COPYENT	LDY	#22

CPENT1	LDA	(ZPG1),Y
	STA	(ZPG2),Y
	DEY
	BPL	CPENT1

	LDA	ZPG1
	STA	ZPG2
	LDA	ZPG1+1
	STA	ZPG2+1
	RTS
***

ADZ2	LDA	ZPG2
	CLC
	ADC	#23
	STA	ZPG2

	BCC	ADZ22
	INC	ZPG2+1

ADZ22	RTS

***
* Print screen header
PHEADER	LDA	88
	STA	ZPG1
	LDA	89
	STA	ZPG1+1
	LDA	#(BWLN-MSGS)/2

* Print screen row in A
ZOBR	LDX	#>MSGS
	ASL
	BCC	ZOBR1A
	INX
ZOBR1A	STA	ZOBZ
	STX	ZOBZ+1

	LDY	#39

ZOBR2	LDA	(ZOBZ),Y
	ASL
	PHP
	SBC	#63
	BPL	ZOBR4
	EOR	#64
ZOBR4	PLP
	ROR
	STA	(ZPG1),Y

	DEY
	BPL	ZOBR2

	LDA	ZPG1
	CLC
	ADC	#40
	STA	ZPG1

	BCC	ZOBR5
	INC	ZPG1+1

ZOBR5	RTS

***
CHKDSK	STX	$359
	STY	$358
	LDA	#47
	BNE	CIONAME

OPENFNM	STA	$35A
	LDA	#0
	STA	$35B

	LDA	#3

CIONAME	LDX	#>FNAME
	STX	$355
	LDX	#<FNAME
	STX	$354

CIOCMD	JSR	CIOC2
	BMI	ERROR
	RTS
**

ERROR	TYA
	PHA

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	PLA
	JSR	CONVDC
	STY	ERNM+2
	STA	ERNM+1
	STX	ERNM

	LDY	#<ERTX

DOSPRT	JSR	PRINT

	JSR	DOSCLO
	JMP	(10)

***

DOSCLO	LDA	#0
	STA	712
	STA	752
	LDA	#$94
	STA	710
	LDA	#$CA
	STA	709

CLOSE	LDA	#12
CIOC2	LDX	#$10
	STA	$352
	JMP	$E456

***
GETK
	LDA	$E420+5
	PHA
	LDA	$E420+4
	PHA
	LDX	#0
	STX	$2A     ;Clears ICAX1Z
	RTS
***

HORNI	PHA
	JSR	PHEADER
	PLA
	JSR	ZOBR
	LDA	#(PRAL-MSGS)/2
	JMP	ZOBR

***
CUROFF	LDY	#1
	STY	752
	STY	84
	STY	85

	DEY
	STY	86

	LDA	(94),Y
	STA	93

	LDY	#<EOLT
**

PRINT	LDA	#>STERR
	STA	$345
	STY	$344
	LDY	#9
	LDX	#$FF
	STX	$348
	INX
CIOPRT	STY	$342
	STX	$349
	JMP	$E456

***

PRT1	LDY	#11
	LDX	#0
	STX	$348
	BEQ	CIOPRT
***
POSCNT	LDA	COUNT
	LDX	COUNT+1
	JMP	POSAX

POSKU	LDX	KURPOS+1
	LDA	KURPOS

POSAX	STX	AKTPOS+1
	STA	AKTPOS

	LDA	#<KONEC
	LDX	#>KONEC

	LDY	#23
POSK1	CLC
	ADC	AKTPOS
	PHA
	TXA
	ADC	AKTPOS+1
	TAX
	PLA
	DEY
	BNE	POSK1

	STA	ZPG2
	STX	ZPG2+1
	RTS

***
* Test if there is space for A more bytes in the buffer
TSTSIZE	;EOR	#255	; Already done in caller
	SEC
	ADC	741
	TAY
	LDA	742
	SBC	#0
;	BCC	TSIZEX	; Only needed if 741/742 was less than A

	CPY	ZPG2
	SBC	ZPG2+1
TSIZEX	RTS

***
* Directory entry for a comment line:
COMLIN	DTA	9,0,0,0,0,0
	DTA	C"           "
	DTA	0,0,0
	DTA	0,0,0

***
* Binary to decimal conversion
CONVDC	LDX	#2

COND2	LDY	#'/'
	SEC

COND1	SBC	CONT,X
	INY
	BCS	COND1

	STY	CONVPR,X
	ADC	CONT,X
	DEX
	BPL	COND2

	LDA	CONVPR+1
	LDX	CONVPR+2

ZEXI	RTS

**
CONT	DTA	1,10,100

****
ZTST	LDA	COUNT
	ORA	COUNT+1
	BNE	ZEXI
	PLA
	PLA
	JMP	MAINLOOP
****
	.ALIGN	2
MSGS	EQU	(*/256)*256
ESCLIN	DTA	C" Exit to DOS  - Are you sure (Y or N) ? "*
**
DATLIN	DTA	C"  Enter new date (dd-mm-yy) : "*
	DTA	C"        "
	DTA	C"  "*
**
TIMLIN	DTA	C"  Enter new time (hh-mm-ss) : "*
	DTA	C"        "
	DTA	C"  "*
**
QTX	DTA	C" Save changes - are you sure (Y or N) ? "*
**
QTX2	DTA	C"        Writing new directory ...       "*
**
TEXTT	DTA	C"      Comment line : "*
TBU	DTA	C"             "
	DTA	C"        "*
***
BWLN	DTA	C"BW-DOS Dir. Master V1.3    BEWESOFT 1995"
***
OL1	DTA	160,156,157,158,159
	DTA	C"=Move  C=Comment line  Q=Quit+Save "*
***
OL2	DTA	C" E=Erase  ESC=Abort  T,D=New time, date "*
***

BUFFT	ORG	*+42
PRAL	ORG	*+42
CHK1	ORG	*+17
CHK2	ORG	*+17
BUFFF	ORG	*+24

KONEC

***

