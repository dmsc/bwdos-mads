	ORG	$3800,$A800
	JMP	START
*************************************
*				*
*   " D I R M A S T "  PRO BW-DOS	*
*				*
*************************************

POCET	EPZ	130
ZPG1	EPZ	132
SCRPOS	EPZ	134
KURPOS	EPZ	136
KURFLG	EPZ	138
ZPG2	EPZ	140
KURTYP	EPZ	152
ORIPO	EPZ	154

SCRCNT	EPZ	139
AKTPOS	EPZ	142
ZOBZ	EPZ	144
ZIF	EPZ	146
KAFE	EPZ	147
FLAGZ	EPZ	148
ODDEL	EPZ	150	;MUSI ZPG
TIX	EPZ	151
NASTYP	EPZ	153

CONVPR	EPZ	156

OLDDEV	EPZ	159

*** VOLNO: 161

L0ODS	LDX	#27

L0OD2	LDA	FNAME-1,X
	STA	FNAME,X

	DEX
	BNE	L0OD2

	RTS
***

STERR	ASC "Incorrect DOS "
	ASC "version !"
	DFB 253,155
***

START	LDA	$700
	CMP	#'S'
	BEQ	INI00

	JSR	EOL

	LDX	#STERR:H
	LDY	#STERR:L
	JSR	PRINT

	JMP	(10)
***

INI00	LDX	#39
	LDA	#160
INI1	STA	PRAL,X
	DEX
	BPL	INI1

*** KONEC INICIALIZACE KONSTANT

	LDX	#$10
	JSR	CLOSE
	LDX	#$20
	JSR	CLOSE

	LDX	#$20
	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	LDY	#KNAME:L
	LDA	#KNAME:H

	JSR	OPEN

*** PARAMETR?

	LDA	10
	CLC
	ADC	#3
	STA	GETNM+1
	LDA	11
	ADC	#0
	STA	GETNM+2

GETNM	JSR	INSER7

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	L0BA
	INY
	LDA	(10),Y
	CMP	#'Q'
	BNE	L0BA
	INY
	LDA	(10),Y
	CMP	#155
	BEQ	L0A
*
L0BA	LDY	#33
L0B	LDA	(10),Y
	STA	FNAME-33,Y

	INY
	CPY	#61
	BCC	L0B

	JMP	LOOP2

** PARAMETR NEBYL

L0A	LDY	#33
L0A0	LDA	(10),Y
	STA	OLDDEV-33,Y
	INY
	CPY	#35
	BCC	L0A0

	LDA	#0
	STA	FNAME+1
	STA	FNAME+2

	JSR	EOL

	LDX	#PATTX:H
	LDY	#PATTX:L
	JSR	PRINT

LOOP0	LDX	#0
	JSR	ADFNAME

	TXA
	STA	$349,X
	LDA	#28
	STA	$348,X

	LDA	#5
	STA	$342,X

	JSR	$E456
	BPL	L0BB

	JMP	DOSEXI
***

L0BB	LDA	FNAME+1
	CMP	#':'
	BEQ	L0D

	LDA	FNAME+2
	CMP	#':'
	BEQ	LOOP2

	JSR	L0ODS
	JSR	L0ODS
	JSR	L0ODS

	LDA	OLDDEV
	STA	FNAME
	LDA	OLDDEV+1
	STA	FNAME+1

	LDA	#':'
	STA	FNAME+2
	BNE	LOOP2
*
L0D	JSR	L0ODS
	LDA	#'1'
	STA	FNAME+1
***

LOOP2	LDA	#155
	STA	FNAME+28

	LDX	#$10
	JSR	ADFNAME

	LDA	#0
	STA	$34A,X
	STA	$34B,X

	LDA	#44
	JSR	CIOCMD

***
	LDA	#'*'
	STA	FNAME+3
	STA	FNAME+5
	LDA	#'.'
	STA	FNAME+4
	LDA	#155
	STA	FNAME+6
**
	LDX	#$10
	LDA	#$14
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	STA	POCET
	STA	POCET+1

	JSR	OPENFNM

	LDA	#KONEC:L
	STA	ZPG2
	LDA	#KONEC:H
	STA	ZPG2+1

LOOP3	LDX	#$10
	LDA	ZPG2
	STA	$344,X
	LDA	ZPG2+1
	STA	$345,X

	LDA	#23
	STA	$348,X
	LDA	#0
	STA	$349,X

	LDA	#7
	STA	$342,X

	JSR	$E456

	BPL	LOOP4

	CPY	#136
	BEQ	LOOP5

	JMP	ERROR
*
LOOP4	LDY	#0
	LDA	(ZPG2),Y
	BEQ	LOOP5

	INC	POCET
	BNE	LOOP4A
	INC	POCET+1

LOOP4A	JSR	ADZ2

	LDA	741
	SEC
	SBC	ZPG2
	TAY
	LDA	742
	SBC	ZPG2+1
	BNE	LOOP3

	CPY	#23
	BCS	LOOP3

	LDX	#TOOLONG:H
	LDY	#TOOLONG:L
	JMP	DOSPRT

LOOP5	LDX	#$10
	JSR	CLOSE

*** CHKDSK

LOOP6	LDX	#CHK1:H
	LDY	#CHK1:L
	JSR	CHKDSK

*** NASAD SVUJ OBRAZ

LOOP6B	LDA	POCET
	STA	ORIPO
	LDA	POCET+1
	STA	ORIPO+1

	JSR	CUROFF

	LDA	#$F2
	STA	712
	LDA	#$F4
	STA	710
	LDA	#$0A
	STA	709

	LDA	#0
	STA	SCRPOS
	STA	SCRPOS+1
	STA	KURPOS
	STA	KURPOS+1
	STA	KURFLG

***

MAINLOOP	LDA	88
	STA	ZPG1
	LDA	89
	STA	ZPG1+1

*** NADPIS

	LDX	#BWLN:H
	LDY	#BWLN:L
	JSR	ZOBR

	LDX	#OL1:H
	LDY	#OL1:L
	JSR	ZOBR

	LDX	#OL2:H
	LDY	#OL2:L
	JSR	ZOBR

*** INIT PRO ZOBRAZENI DIR.

	LDA	#21
	STA	SCRCNT

	LDA	#KONEC:L
	STA	ZPG2
	LDA	#KONEC:H
	STA	ZPG2+1

	LDA	#0
	STA	AKTPOS
	STA	AKTPOS+1

*** HLEDEJ ZACATEK OBRAZU

ML2	LDA	AKTPOS
	CMP	SCRPOS
	BNE	ML3

	LDA	AKTPOS+1
	CMP	SCRPOS+1
	BEQ	ML4

ML3	JSR	ADZ2
	JSR	API
	JMP	ML2

*** CLR. BUFFER (TEXTOVY)

ML4	LDY	#39
	LDA	#32

ML5	STA	BUFFT,Y
	DEY
	BPL	ML5

*** JE TU KURZOR ?

	LDA	#0
	STA	KAFE

	LDA	KURPOS
	CMP	AKTPOS
	BNE	ML6

	LDA	KURPOS+1
	CMP	AKTPOS+1
	BNE	ML6
*C=1
	ROR	KAFE

	LDA	KURFLG
	BEQ	ML6

*** ZOBRAZ VEDLE

	LDX	#11

ML5B	LDA	BUFFF+6-1,X
	EOR	#$80
	STA	BUFFT-1,X
	DEX
	BNE	ML5B

	STX	KAFE

	LDA	#$88	;"ROH"
	STA	BUFFT+11

*** ZOBRAZ 1 RADEK

ML6	LDA	AKTPOS+1
	CMP	POCET+1
	BCC	ML6A
	BNE	ML8

	LDA	AKTPOS
	CMP	POCET
	BCS	ML8

*** PRIZNAK

ML6A	LDX	ZPG2+1
	LDY	ZPG2

	JSR	FLAG

	STA	BUFFT+12
	STA	NASTYP

	LDX	KAFE
	BPL	ML6C

	STA	KURTYP

*** FILENAME

ML6C	LDY	#16
	LDX	#11

ML7	LDA	(ZPG2),Y
	EOR	KAFE
	STA	BUFFT+13,X

	DEY
	CPY	#13
	BNE	ML7Y

	DEX
	LDA	#32
	EOR	KAFE
	STA	BUFFT+13,X

ML7Y	DEX
	BPL	ML7

	LDA	NASTYP
	CMP	#34	;"
	BEQ	ML8

*** DATUM

	LDX	#0
	LDY	#17

	JSR	PUTY
	JSR	PUTYPOM
	JSR	PUTYPOM

*** CAS

	LDA	#32
	JSR	PUTYCH
	LDA	#':'
	JSR	PUTYCH

*** VLASTNI VYPIS

ML8	LDX	#BUFFT:H
	LDY	#BUFFT:L
	JSR	ZOBR

	JSR	ADZ2
	JSR	API

	DEC	SCRCNT
	BEQ	MENU

	JMP	ML4

*** VLASTNI MENU

MENU	JSR	GETK

	CMP	#27
	BEQ	ESCRUT

	CMP	#$1C
	BEQ	UPJ
	CMP	#$1D
	BEQ	DOWJ
	CMP	#$1E
	BEQ	LEFJ
	CMP	#$1F
	BEQ	RIGJ

	AND	#127

	CMP	#'-'
	BEQ	UPJ
	CMP	#'='
	BEQ	DOWJ
	CMP	#'+'
	BEQ	LEFJ
	CMP	#'*'
	BNE	NORI

RIGJ	JMP	RIGHT
UPJ	JMP	UP
DOWJ	JMP	DOWN
LEFJ	JMP	LEFT
QUIT	JMP	Q2
TEXTJ	JMP	TEXT
**

NORI	AND	#$5F
	CMP	#'E
	BEQ	ERASE
	CMP	#'D
	BEQ	DATUM
	CMP	#'C
	BEQ	TEXTJ
	CMP	#'Q
	BEQ	QUIT
	CMP	#'T
	BNE	MENU

*** NOVY CAS

CAS	LDA	#':'
	STA	ODDEL

	LDA	#20
	LDX	#TIMLIN:H
	LDY	#TIMLIN:L

TIME2J	JMP	TIME2

*** NOVE DATUM

DATUM	LDA	#'-'
	STA	ODDEL

	LDX	#DATLIN:H
	LDY	#DATLIN:L
	LDA	#17
	BNE	TIME2J

*** ESC

ESCRUT	JSR	HORNI

	LDX	#ESCLIN:H
	LDY	#ESCLIN:L
	JSR	ZOBR

	JSR	GETK
	AND	#$5F

	CMP	#'Y'
	BNE	MAINJ
*
DOSCLS	JSR	DOSCLO
	JSR	CLS
	JMP	(10)

***

ERASE	JSR	ZTST

	LDA	KURTYP
	CMP	#'='
	BEQ	ERA2

	CMP	#34	;"
	BNE	MAINJ
*
ERA2	JSR	DELET

	LDA	KURPOS
	CMP	POCET
	BNE	MAINJ

	LDA	KURPOS+1
	CMP	POCET+1
	BNE	MAINJ

	LDA	KURPOS
	ORA	KURPOS+1
	BEQ	MAINJ

	JSR	DKPO

MAINJ	JMP	MAINLOOP

***

TEXT	LDA	KURFLG
	BNE	MAINJ

	JSR	HORNI

	LDX	#11
	LDA	#32
T2	STA	TBU,X
	DEX
	BNE	T2

	LDA	#'|'
	STA	TBU+8

	LDX	#0
TLOPK0	STX	TIX

TLOPKU	LDA	#126
	STA	TBU,X

TLOP	JSR	HORREP
	LDX	#TEXTT:H
	LDY	#TEXTT:L
	JSR	ZOBR

	JSR	GETK

	CMP	#155
	BEQ	TEXTEOL
	CMP	#126
	BEQ	TEXTDEL

	LDX	TIX
	CPX	#12
	BCS	TLOP

	STA	TBU,X
	INX
	CPX	#8
	BNE	TLOPK0

	INX
	BNE	TLOPK0
**

TEXTDEL	LDX	TIX
	BEQ	TLOP

	LDA	#32
	STA	TBU,X

	DEX
	CPX	#8
	BNE	TLOPK0

	DEX
	BPL	TLOPK0
**

TEXTEOL	LDA	#32
	LDX	TIX
	STA	TBU,X

	LDX	#22
TEO2	LDA	COMLIN,X
	STA	BUFFF,X
	DEX
	BPL	TEO2

	LDX	#11
	LDY	#10
TEO3	LDA	TBU,X
	STA	BUFFF+6,Y
	DEX
	CPX	#8
	BNE	NO83

	DEX

NO83	DEY
	BPL	TEO3

MLJKF1	LDA	#1
	STA	KURFLG

MLJ2	JMP	MAINLOOP

***

LEFT	JSR	ZTST

	JSR	POSKU

	LDY	#22
LFT2	LDA	(ZPG2),Y
	STA	BUFFF,Y
	DEY
	BPL	LFT2

	JSR	DELET
	JMP	MLJKF1

***

RIGHT	LDA	KURFLG
	BEQ	MLJ2

	JSR	INSER

	JMP	MAINLOOP

***

UP	LDA	KURPOS
	ORA	KURPOS+1
	BEQ	NAKON

	JSR	DKPO

*MOVE SCR?

	LDA	KURPOS
	SEC
	SBC	SCRPOS
	TAY
	LDA	KURPOS+1
	SBC	SCRPOS+1
	BNE	UPX

	CPY	#2
	BCS	UPX

UPRO	LDA	SCRPOS
	ORA	SCRPOS+1
	BEQ	UPX

	LDA	SCRPOS
	BNE	UPRO2

	DEC	SCRPOS+1
UPRO2	DEC	SCRPOS

UPX	JMP	MAINLOOP

***

NAKON	LDA	POCET
	SEC
	SBC	#18
	STA	SCRPOS

	LDA	POCET+1
	SBC	#0
	STA	SCRPOS+1

	BCS	NAKON4

	LDA	#0
	STA	SCRPOS
	STA	SCRPOS+1

NAKON4	LDA	KURFLG
	CMP	#1

	LDA	POCET
	SBC	#0
	STA	KURPOS
	LDA	POCET+1
	SBC	#0
	STA	KURPOS+1

	BCC	NAZAC
	JMP	MAINLOOP
***

DOWN	LDA	POCET
	SEC
	SBC	KURPOS
	TAY
	LDA	POCET+1
	SBC	KURPOS+1
	BNE	DWN2

	TYA
	BEQ	NAZAC

	DEY
	BNE	DWN2

	LDA	KURFLG
	BEQ	NAZAC

DWN2	INC	KURPOS
	BNE	DWN3
	INC	KURPOS+1

DWN3	LDA	KURPOS
	SEC
	SBC	SCRPOS
	CMP	#19
	BCC	DWX

	INC	SCRPOS
	BNE	DWX
	INC	SCRPOS+1

DWX	JMP	MAINLOOP

***

NAZAC	LDA	#0
	STA	KURPOS
	STA	KURPOS+1
	STA	SCRPOS
	STA	SCRPOS+1
	JMP	MAINLOOP

***
Q5	LDY	#150
	JMP	ERROR
**

Q2	LDA	KURFLG
	BNE	DWX

	JSR	HORNI
	LDX	#QTX:H
	LDY	#QTX:L
	JSR	ZOBR

	JSR	GETK
	AND	#$5F

	CMP	#'Y'
	BNE	DWX

Q3	JSR	HORREP
	LDX	#QTX2:H
	LDY	#QTX2:L
	JSR	ZOBR

	LDX	#$10
	JSR	CLOSE

*** NEVYMENIL NEKDO DISKETU ???

	LDX	#CHK2:H
	LDY	#CHK2:L
	JSR	CHKDSK

	LDX	#16
Q4	LDA	CHK1,X
	CMP	CHK2,X
	BNE	Q5

	DEX
	BPL	Q4
*
	LDX	#$10
	LDA	#$1C
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	JSR	OPENFNM

	LDA	POCET
	STA	KURPOS
	LDA	POCET+1
	STA	KURPOS+1

	JSR	POSKU

	LDX	#$10
	LDA	#KONEC:L
	STA	$344,X
	LDA	#KONEC:H
	STA	$345,X

	LDA	ZPG2
	SEC
	SBC	#KONEC:L
	STA	$348,X

	LDA	ZPG2+1
	SBC	#KONEC:H
	STA	$349,X

	ORA	$348,X
	BEQ	Q5X

	LDA	#11
	JSR	CIOCMD
*

Q5X	LDA	ORIPO
	SEC
	SBC	POCET
	STA	POCET

	LDA	ORIPO+1
	SBC	POCET+1
	STA	POCET+1

	BCC	ORIDOP0

	LDX	#23
	LDA	#0
ORIDNL	STA	BUFFF,X
	DEX
	BPL	ORIDNL
*DOPLN

ORIDOP	LDA	POCET
	ORA	POCET+1
	BEQ	ORIDOP0

	LDX	#$10
	LDA	#BUFFF:L
	STA	$344,X
	LDA	#BUFFF:H
	STA	$345,X

	LDA	#23
	STA	$348,X
	LDA	#0
	STA	$349,X

	LDA	#11
	JSR	CIOCMD

	LDA	POCET
	BNE	ORIDO2

	DEC	POCET+1
ORIDO2	DEC	POCET

	JMP	ORIDOP
**

ORIDOP0	LDX	#$10
	JSR	CLOSE
	BMI	ORID0ER

	JMP	DOSCLS

ORID0ER	JMP	ERROR

***

INSER	LDA	KURPOS
	PHA
	LDA	KURPOS+1
	PHA

	LDA	POCET
	STA	KURPOS
	LDA	POCET+1
	STA	KURPOS+1

	JSR	POSKU

	PLA
	STA	KURPOS+1
	PLA
	STA	KURPOS

** JE DOST MISTA ???

	LDA	741
	SEC
	SBC	ZPG2
	TAY
	LDA	742
	SBC	ZPG2+1
	BNE	INSER2

	CPY	#46
	BCC	INSERERR

** ODSUN ZBYTKU

INSER2	LDA	AKTPOS
	CMP	KURPOS
	BNE	INSER4

	LDA	AKTPOS+1
	CMP	KURPOS+1
	BEQ	INSER5
*
INSER4	LDA	ZPG2
	SEC
	SBC	#23
	STA	ZPG1

	LDA	ZPG2+1
	SBC	#0
	STA	ZPG1+1
*
	LDY	#22

INSER4B	LDA	(ZPG1),Y
	STA	(ZPG2),Y
	DEY
	BPL	INSER4B

	LDA	ZPG1
	STA	ZPG2
	LDA	ZPG1+1
	STA	ZPG2+1

	LDA	AKTPOS
	BNE	INSER4C
	DEC	AKTPOS+1
INSER4C	DEC	AKTPOS

	JMP	INSER2

** VLASTNI VLOZENI

INSER5	LDY	#22

INSER6	LDA	BUFFF,Y
	STA	(ZPG2),Y
	DEY
	BPL	INSER6

	INC	POCET
	BNE	INSERERR
	INC	POCET+1

INSERERR	LDA	#0
	STA	KURFLG

	LDA	KURPOS
	CMP	POCET
	BNE	INSER7

	LDA	KURPOS+1
	CMP	POCET+1
	BNE	INSER7

DKPO	LDA	KURPOS
	BNE	INSER7A
	DEC	KURPOS+1
INSER7A	DEC	KURPOS

INSER7	RTS

***

DELET	JSR	POSKU

DELET2	LDA	ZPG2
	CLC
	ADC	#23
	STA	ZPG1

	LDA	ZPG2+1
	ADC	#0
	STA	ZPG1+1

	LDY	#22

DELET3	LDA	(ZPG1),Y
	STA	(ZPG2),Y
	DEY
	BPL	DELET3

	LDA	ZPG1
	STA	ZPG2
	LDA	ZPG1+1
	STA	ZPG2+1

	JSR	API

	LDA	AKTPOS
	CMP	POCET
	BNE	DELET2

	LDA	AKTPOS+1
	CMP	POCET+1
	BNE	DELET2

	LDA	POCET
	BNE	DELET4
	DEC	POCET+1
DELET4	DEC	POCET

	RTS

***

TIME2	STA	TIX

	JSR	ZTST

	LDA	KURTYP
	CMP	#34	;"
	BNE	TIME2Z

	JMP	MAINLOOP
*
TIME2Z	TYA
	PHA
	TXA
	PHA

	JSR	POSKU

	JSR	HORNI

	PLA
	TAX
	PLA
	TAY
	JSR	ZOBR

	LDX	#1
	STX	752
	STX	84
	DEX
	STX	86
	LDA	#29
	STA	85

	LDA	#3
	STA	SCRCNT

	JSR	OBNKU

	LDA	#160
	DFB $2C
TIME3	LDA	ODDEL

	JSR	PRT1

	JSR	TIMGET
*10
	STA	KAFE
	ASL
	ASL
	CLC
	ADC	KAFE
	ASL

	STA	KAFE

	JSR	TIMGET

	CLC
	ADC	KAFE

	LDY	TIX
	STA	(ZPG2),Y

	INC	TIX

	DEC	SCRCNT
	BNE	TIME3

NOTD	JSR	CUROFF

	JMP	MAINLOOP

*

TIMGET	JSR	GETK
	AND	#127

	CMP	#$30
	BCC	TIMGET
	CMP	#$3A
	BCS	TIMGET

	PHA
	JSR	PRT1
	PLA

	AND	#15

	RTS

***

PUTYPOM	LDA	#'-'
PUTYCH	JSR	PUTCH

PUTY	TYA
	PHA
	STX	PUTY1B+1

	LDA	(ZPG2),Y
	JSR	CONVDC

	TXA

PUTY1B	LDX	#0
	JSR	PUTCH
	TYA
	JSR	PUTCH

	PLA
	TAY
	INY
	RTS
**

PUTCH	STA	BUFFT+26,X
	INX
	RTS

***

FLAG	STY	FLAGZ
	STX	FLAGZ+1

	LDY	#0
	LDX	#9	;TAB LEN

F2	LDA	FTB1,X
	CMP	(FLAGZ),Y
	BEQ	FOK

	DEX
	BNE	F2
*
FOK	LDA	FTB2,X

FSPO	CMP	#'*'
	BNE	FEND

	LDA	#0

	LDY	#5
FPOR1	ORA	(FLAGZ),Y
	DEY
	BNE	FPOR1

	LDY	#17
FPOR2	ORA	(FLAGZ),Y
	INY
	CPY	#23
	BCC	FPOR2

	TAY
	BNE	FTXN

	LDA	#34	;"
	DFB $2C
FTXN	LDA	#'*'

FEND	RTS

**

FTB1	DFB 0,$28,$29,$18,$19
	DFB $38,$39,$10,9,8

FTB2	ASC "?>>=="
	ASC "===* "

***

API	INC	AKTPOS
	BNE	API2
	INC	AKTPOS+1

API2	RTS

***

ADZ2	LDA	ZPG2
	CLC
	ADC	#23
	STA	ZPG2

	BCC	ADZ22
	INC	ZPG2+1

ADZ22	RTS

***

ZOBR	STY	ZOBZ
	STX	ZOBZ+1

	LDY	#39

ZOBR2	LDA	(ZOBZ),Y
	ASL
	PHP

	CMP	#64
	BCS	ZOBR3

	CLC
	ADC	#128
	BNE	ZOBR4

ZOBR3	CMP	#192
	BCS	ZOBR4

	SEC
	SBC	#64

ZOBR4	PLP
	ROR
	STA	(ZPG1),Y

	DEY
	BPL	ZOBR2

	LDA	ZPG1
	CLC
	ADC	#40
	STA	ZPG1

	BCC	ZOBR5
	INC	ZPG1+1

ZOBR5	RTS

***

OPENFNM	LDY	#FNAME:L
	LDA	#FNAME:H

OPEN	STA	$345,X
	TYA
	STA	$344,X

	LDA	#3

CIOCMD	STA	$342,X

CIO	JSR	$E456
	BMI	ERROR
	RTS
**

ERROR	TYA
	PHA

	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	PLA
	JSR	CONVDC
	STY	ERNM+2
	STX	ERNM+1
	STA	ERNM

	LDX	#ERTX:H
	LDY	#ERTX:L

DOSPRT	JSR	PRINT

DOSEXI	JSR	DOSCLO
	JMP	(10)

***

DOSCLO	LDX	#$10
	JSR	CLOSE
	LDX	#$20
	JSR	CLOSE

	LDA	#0
	STA	712
	STA	752
	LDA	#$94
	STA	710
	LDA	#$CA
	STA	709

	RTS

***

ERTX	DFB	125
	ASC	"Error "
ERNM	ASC	"??? !"
	DFB	253,155

***

KNAME	ASC	"K:"
	DFB	$9B

***

PATTX	ASC	"Insert disk and"
	ASC " enter [Dn:]path :"
EOLT	DFB	$9B

***

TOOLONG	DFB	125,253
	ASC	"Directory too "
	ASC	"long !"
	DFB	$9B

***

BWLN	ASC	/BW-DOS Dir/
	ASC	/ectory Mas/
	ASC	/ter    BEW/
	ASC	/ESOFT 1994/

***

OL1	DFB	160,156,157,158,159
	ASC	/=Move/
	ASC	/  C=Commen/
	ASC	/t line  Q=/
	ASC	/Quit+Save /

***

OL2	ASC	/ E=Erase  /
	ASC	/ESC=Abort /
	ASC	/ T,D=New t/
	ASC	/ime, date /

***
GETK	LDX	#$20

	LDA	#0
	STA	$348,X
	STA	$349,X

	LDA	#7
	STA	$342,X

	JSR	$E456
	BMI	GETK

	RTS

***

HORNI	LDA	88
	CLC
	ADC	#80
	STA	ZPG1

	LDA	89
	ADC	#0
	STA	ZPG1+1

	LDX	#PRAL:H
	LDY	#PRAL:L
	JSR	ZOBR

HORREP	LDA	88
	CLC
	ADC	#40
	STA	ZPG1

	LDA	89
	ADC	#0
	STA	ZPG1+1

	RTS

**

ESCLIN	ASC	/ Exit to D/
	ASC	/OS  - Are /
	ASC	/you sure (/
	ASC	/Y or N) ? /

**

DATLIN	ASC	/  Enter ne/
	ASC	/w date (dd/
	ASC	/-mm-yy) : /
	ASC	"        "
	ASC	/  /

**

TIMLIN	ASC	/  Enter ne/
	ASC	/w time (hh/
	ASC	/-mm-ss) : /
	ASC	"        "
	ASC	/  /

***

CLS	LDA	#125
	DFB $2C
EOL	LDA	#155

PRT1	PHA

	LDX	#0
	TXA
	STA	$348,X
	STA	$349,X

	LDA	#11
	STA	$342,X

	PLA
	JMP	$E456
***

CUROFF	LDX	#0
	STX	86

	INX
	STX	752
	STX	84
	STX	85

	JSR	OBNKU

	LDX	#EOLT:H
	LDY	#EOLT:L
**

PRINT	TXA
	LDX	#0
	STA	$345,X
	TYA
	STA	$344,X

	LDA	#$FF
	STA	$348,X

	TXA
	STA	$349,X

	LDA	#9
	DFB $2C
CLOSE	LDA	#12

	STA	$342,X

	JMP	$E456

***

POSKU	LDA	#KONEC:L
	STA	ZPG2
	LDA	#KONEC:H
	STA	ZPG2+1

	LDA	#0
	STA	AKTPOS
	STA	AKTPOS+1

POSKU2	LDA	AKTPOS
	CMP	KURPOS
	BNE	POSKU3

	LDA	AKTPOS+1
	CMP	KURPOS+1
	BEQ	POSKUE

POSKU3	JSR	ADZ2
	JSR	API
	JMP	POSKU2

POSKUE	RTS

***

TEXTT	ASC	/      Comm/
	ASC	/ent line :/
	ASC	/ /
TBU	ASC	"          "
	ASC	"   "
	ASC	/        /

***

COMLIN	DFB	9,0,0,0,0,0
	ASC	"           "
	DFB	0,0,0
	DFB	0,0,0

***

QTX	ASC	/ Save chan/
	ASC	/ges - are /
	ASC	/you sure (/
	ASC	/Y or N) ? /

***

QTX2	ASC	/       Wri/
	ASC	/tting new /
	ASC	/directory /
	ASC	/...       /

***

OBNKU	LDY	#0
	LDA	(94),Y
	STA	93
	RTS

***

CHKDSK	TXA

	LDX	#$10
	STA	$349,X
	TYA
	STA	$348,X

	JSR	ADFNAME

	LDA	#47
	JMP	CIOCMD
***

ADFNAME	LDA	#FNAME:L
	STA	$344,X
	LDA	#FNAME:H
	STA	$345,X

	RTS
***

CONVDC	LDX	#'0'
	STX	CONVPR
	STX	CONVPR+1
	STX	CONVPR+2

	LDX	#2

COND1	CMP	CONT,X
	BCC	COND2

	INC	CONVPR,X
	SBC	CONT,X
	BCS	COND1
*
COND2	DEX
	BPL	COND1

	LDY	CONVPR
	LDX	CONVPR+1
	LDA	CONVPR+2

ZEXI	RTS

**

CONT	DFB 1,10,100

****

ZTST	LDA	POCET
	ORA	POCET+1
	BEQ	ZTSTE

	LDA	KURFLG
	BEQ	ZEXI

ZTSTE	PLA
	PLA
	JMP	MAINLOOP
****

CHK1	EQU	*
CHK2	EQU	CHK1+17
FNAME	EQU	CHK2+17
BUFFF	EQU	FNAME+29
BUFFT	EQU	BUFFF+24
PRAL	EQU	BUFFT+41

KONEC	EQU	PRAL+41

***

