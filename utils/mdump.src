*************************************
*				*
*    " M D U M P "   PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

NAME	EQU	$6F8

	ORG	$4C0
***
	ICL	"chkdos.inc"
***
	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***
	JSR	GETHEX
	BEQ	ST6

**

ST5	STY	POS
	STX	POS+1
**

ST6	JSR	GETHEX
	BEQ	ST6D

**

ST6B	STY	LENL
	STX	LENH
**

ST6D	JSR	GETHEX

*** DUMP IT !

ST10	JSR	PRTEOL

*GET

DUMP1	LDX	#8
	STX	LLEN

	DEX

DUMP1AA	DTA	$BD	; LDA ABS,X
POS	DTA	A(0)
	STA	NAME,X
	DEX
	BPL	DUMP1AA

	INX
	STX	LPOS
*LEN

D1AALO	LDA	#$FF
	TAX
	SEC
	SBC	#8
	STA	LENL
D1AAHI	LDA	#$FF
	SBC	#0
	STA	LENH
	BCS	DUMP1A
**
LENL	EQU	D1AALO+1
LENH	EQU	D1AAHI+1
**

	STX	LLEN
	DEC	STATUS

DUMP1A	LDA	#0
	BNE	DUMP1B
	JMP	DUMPX
**
LLEN	EQU	DUMP1A+1
**

*POS

DUMP1B	LDA	POS+1
	JSR	DPRTHX
	LDA	POS
	JSR	DPRTHX

	JSR	DSPAC

	LDA	LLEN
	CLC
	ADC	POS
	STA	POS
	BCC	DUMP2
	INC	POS+1

*** DATA

DUMP2	LDX	#0
DUMP2A	STX	LIX
	LDA	NAME,X
	JSR	DPRTHX

	JSR	DSPAC

**
LIX	EQU	*+1
**
	LDX	#0
	INX
	CPX	LLEN
	BCC	DUMP2A

	BCS	DUMP3

*** MOVE

DUMP3A	JSR	DSPAC

DUMP3	LDA	#0
	CMP	#29
	BCC	DUMP3A
**
LPOS	EQU	DUMP3+1
**

*** CHARS
	LDX	#0
DUMP4	STX	LIXA
	LDA	NAME,X

D4ASC	LDX	#0
	BNE	DUMP4A
**
ASCII	EQU	D4ASC+1
**

	CMP	#$9B
	BNE	DUMP4AA
	LDA	#$20

DUMP4AA	PHA
	LDA	#27
	JSR	PRT1
	PLA
	JMP	DUMP4D
**

DUMP4A	AND	#127
	CMP	#125
	BCC	DUMP4B
	LDA	#'.'

DUMP4B	CMP	#32
	BCS	DUMP4D
	ORA	#64

DUMP4D	JSR	DPRT1

**
LIXA	EQU	*+1
**
	LDX	#0
	INX
	CPX	LLEN
	BCC	DUMP4
*** EOL

	JSR	PRTEOL

*** NEXT?

DUMPX	LDA	#0
	BMI	DUMPX2
**
STATUS	EQU	DUMPX+1
**

	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1
***

DESC	LDA	#$FF
	STA	764

	JSR	PRINT
	DTA 155
	DTA C"<Aborted>"
	DTA 155,0

DUMPX2	JMP	ERR2

*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DTA $2C
DSPAC	LDA	#' '

DPRT1	INC	LPOS
	JMP	PRT1

***
	ICL	"print.inc"
***

PRTEOL	LDA	#$9B

**
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#0
	LDX	#0
**
HEXL	EQU	GHXRTS+1
HEXH	EQU	GHXRTS+3
**
	LDA	#1

GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEXL
	STA	HEXH

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXOPT

GETHX2	CPY	#41
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	BEQ	GETHX2
***

GHXOPT	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	GHXERR
	DEC	ASCII
	LDY	#0
	RTS

GHXERR	JSR	PRINT
	DTA 155
	DTA C"Bad parameter!"
	DTA 155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JMP	(10)

ENDEND	EQU	*

***

