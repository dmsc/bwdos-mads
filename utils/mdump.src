*************************************
*				*
*    " M D U M P "   FOR BW-DOS	*
*				*
*************************************

* VOLNO: 128

HEXL	EQU	$20
HEXH	EQU	$21

	ORG	$480

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	LDA	#<PBADVER
	LDX	#PEND-PBADVER+1
	JMP	PRINT

START2	LDY	#4
	LDA	(10),Y
	STA	GETNAME+1
	INY
	LDA	(10),Y
	STA	GETNAME+2

*Address, default $0000
	JSR	GETHEX
	STA	POS
	STX	POS+1
*Length, default $FFFF
	JSR	GETHEX
	BEQ	GOPT
	STA	LENL
	STX	LENH
*Options
GOPT	JSR	GETHEX

*Start dump

*Clear line and write position
DUMP1	LDX	#37
	LDA	#$20
CBUF	STA	BUFF,X
	DEX
	BNE	CBUF

	LDA	POS+1
	JSR	PRTHEX
	LDA	POS
	JSR	PRTHEX
	INX

*Copy up to 8 bytes to line buffer
	LDY	#0

DUMP1A

LENL	EQU	*+1
	LDA	#$FF
	BNE	DUMP1B
LENH	EQU	*+1
	LDA	#$FF
	BEQ	DUMP1C
	DEC	LENH
DUMP1B	DEC	LENL


POS	EQU	*+1
	LDA	$FFFF

	PHA
	JSR	PRTHEX
	INX


ASCII	EQU	*+1
	LDA	#0

	BNE	DUMP4A
**
	PLA
	CMP	#$9B
	BNE	DUMP4D
	LDA	#$20
	BNE	DUMP4D

DUMP4A	PLA
	AND	#127
	CMP	#125
	BCC	DUMP4B
	LDA	#'.'

DUMP4B	CMP	#32
	BCS	DUMP4D
	ORA	#64

DUMP4D	STA	BUFF+30,Y

	INC	POS
	BNE	DUMP1X
	INC	POS+1

DUMP1X	INY
	CPY	#8
	BNE	DUMP1A

DUMP1C	LDX	#1
	TYA
	BEQ	DUMPX2

*Write line
	STX	$2FE

	LDA	#<BUFF
	LDX	#38
	JSR	PRINT
	DEC	$2FE

*Check for ESC key
	LDA	764
	CMP	#$1C
	BNE	DUMP1

***
DESC	LDA	#$FF
	STA	764

	LDX	#PBADPAR-PABORT+1
DUMPX2	LDA	#<PABORT
	JMP	PEXIT

*****

PRTHEX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	PRTHX2
	PLA

PRTHX2	AND	#15
	ORA	#$30
	CMP	#$3A
	BCC	PRTHX1

	ADC	#6
**
PRTHX1	STA	BUFF+1,X
	INX
	RTS

***
PRINT	STA	$344
	STX	$348
PRT0	LDX	#0
	STX	$349
	LDY	#>PABORT
	STY	$345
	LDY	#11
	STY	$342
	JMP	$E456

***
GHXOPT	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	GHXERR
	DEC	ASCII
	LDY	#36

***
GHXRTS	LDA	HEXL
	LDX	HEXH
	CPY	#36
	RTS

**

GETHEX	LDA	#0
	STA	HEXL
	STA	HEXH

GETNAME	JSR	GETNAME

	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BEQ	GHXOPT

GETHX2	LDA	(10),Y

	CMP	#$9B
	BEQ	GHXRTS

	EOR	#'0'
	CMP	#$0A
	BCC	GETHX3

	SBC	#$77
	CMP	#$FA
	BCC	GHXERR

GETHX3	ASL
	ASL
	ASL
	ASL

	LDX	#4
GETHX4	ASL
	ROL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	INY
	CPY	#41
	BCC	GETHX2

***
GHXERR	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

	LDX	#PBADVER-PBADPAR+1
	LDA	#<PBADPAR
PEXIT	JSR	PRINT
	JMP	(10)

PABORT	DTA	155
	DTA	C"<Aborted>"
PBADPAR	DTA	155
	DTA	C"Bad parameter!"
PBADVER	DTA	155,253
	DTA	C"Incorrect DOS version"
PEND	DTA	155

BUFF	EQU	PBADPAR	;Reuse last two messages for buffer


