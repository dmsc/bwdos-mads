	ORG	$4D0
	JMP	START
*************************************
*				*
*    " M D U M P "   PRO BW-DOS	*
*				*
*************************************

* VOLNO: 128

NAME	EQU	$6F8

***
	ICL	"print.inc"
***

PRTEOL	LDA	#$9B

**
	ICL	"print1.inc"
***

GETNAME	JMP	PRTEX

***

GHXRTS	LDY	#0
	LDX	#0
**
HEXL	EQU	GHXRTS+1
HEXH	EQU	GHXRTS+3
**
	LDA	#1

GHXRTS2	RTS

**

GETHEX	JSR	GETNAME

	LDA	#0
	STA	HEXL
	STA	HEXH

	LDY	#36
	LDA	(10),Y
	CMP	#155
	BEQ	GHXRTS2
	CMP	#'/'
	BEQ	GHXRTS2

GETHX2	CPY	#41
	BCS	GHXERR

	LDA	(10),Y
	INY

	CMP	#$9B
	BEQ	GHXRTS

	SEC
	SBC	#$30
	CMP	#$0A
	BCC	GETHX3

	SBC	#7
	CMP	#$0A
	BCC	GHXERR
	CMP	#$10
	BCS	GHXERR

GETHX3	PHA

	LDX	#4
GETHX4	ASL	HEXL
	ROL	HEXH
	DEX
	BNE	GETHX4

	PLA
	ORA	HEXL
	STA	HEXL

	JMP	GETHX2
***

GHXERR	JSR	PRINT
	DTA 155
	DTA C"Bad parameter!"
	DTA 155,0

ERR2X	LDY	#8
	LDA	(10),Y
	STA	ERR5Y+1
	INY
	LDA	(10),Y
	STA	ERR5Y+2
	LDY	#1
ERR5Y	JSR	$E474

ERR2	JMP	(10)

***

START	LDA	$700
	CMP	#'S'
	BEQ	START2

	JSR	PRINT
	DTA 155,253
	DTA C"Bad DOS !"
	DTA 155,0

	JMP	(10)
***

START2	LDA	10
	CLC
	ADC	#3
	STA	GETNAME+1
	LDA	11
	ADC	#0
	STA	GETNAME+2
***

	LDX	#0
	STX	POS
	STX	POS+1
	STX	ASCII
	STX	STATUS

	DEX
	STX	LENL
	STX	LENH
***

	JSR	GETHEX
	BNE	ST5

	CMP	#'/'
	BEQ	ST7
	BNE	ST6
**

ST5	STY	POS
	STX	POS+1
**

ST6	JSR	GETHEX
	BNE	ST6B

	CMP	#'/'
	BEQ	ST7
	BNE	ST6D
**

ST6B	STY	LENL
	STX	LENH
**

ST6D	JSR	GETNAME

ST7	LDY	#36
	LDA	(10),Y
	CMP	#'/'
	BNE	ST10

	INY
	LDA	(10),Y
	CMP	#'A'
	BNE	ST10

	DEC	ASCII

*** DUMP IT !

ST10	JSR	PRTEOL

*GET

DUMP1	LDX	#8
	STX	LLEN

	DEX

DUMP1AA	LDA	$FFFF,X
	STA	NAME,X
	DEX
	BPL	DUMP1AA
**
POS	EQU	DUMP1AA+1
**

	INX
	STX	LIX
	STX	LPOS
*LEN

D1AALO	LDA	#0
	TAX
	SEC
	SBC	#8
	STA	LENL
D1AAHI	LDA	#0
	SBC	#0
	STA	LENH
	BCS	DUMP1A
**
LENL	EQU	D1AALO+1
LENH	EQU	D1AAHI+1
**

	STX	LLEN
	DEC	STATUS

DUMP1A	LDA	#0
	BNE	DUMP1B
	JMP	DUMPX
**
LLEN	EQU	DUMP1A+1
**

*POS

DUMP1B	LDA	POS+1
	JSR	DPRTHX
	LDA	POS
	JSR	DPRTHX

	JSR	DSPAC

	LDA	LLEN
	CLC
	ADC	POS
	STA	POS
	BCC	DUMP2
	INC	POS+1

*** DATA

DUMP2	LDX	#0
	LDA	NAME,X
	JSR	DPRTHX
**
LIX	EQU	DUMP2+1
**

	JSR	DSPAC

	INC	LIX
	LDA	LIX
	CMP	LLEN
	BCC	DUMP2

	LDA	#0
	STA	LIX
	BEQ	DUMP3

*** MOVE

DUMP3A	JSR	DSPAC

DUMP3	LDA	#0
	CMP	#29
	BCC	DUMP3A
**
LPOS	EQU	DUMP3+1
**

*** CHARS

DUMP4	LDX	LIX
	LDA	NAME,X

D4ASC	LDX	#0
	BNE	DUMP4A
**
ASCII	EQU	D4ASC+1
**

	CMP	#$9B
	BNE	DUMP4AA
	LDA	#$20

DUMP4AA	PHA
	LDA	#27
	JSR	PRT1
	PLA
	JMP	DUMP4D
**

DUMP4A	AND	#127
	CMP	#125
	BCC	DUMP4B
	LDA	#'.'

DUMP4B	CMP	#32
	BCS	DUMP4D
	ORA	#64

DUMP4D	JSR	DPRT1

	INC	LIX
	LDA	LIX
	CMP	LLEN
	BCC	DUMP4
*** EOL

	JSR	PRTEOL

*** NEXT?

DUMPX	LDA	#0
	BMI	DUMPX2
**
STATUS	EQU	DUMPX+1
**

	LDA	764
	CMP	#$1C
	BEQ	DESC

	JMP	DUMP1
***

DESC	LDA	#$FF
	STA	764

	JSR	PRINT
	DTA 155
	DTA C"<Aborted>"
	DTA 155,0

DUMPX2	JMP	ERR2

*****

DPRTHX	PHA
	LSR
	LSR
	LSR
	LSR
	JSR	DPRTHX2
	PLA
*

DPRTHX2	AND	#15
	CLC
	ADC	#$30
	CMP	#$3A
	BCC	DPRT1

	ADC	#6
**
	DTA $2C
DSPAC	LDA	#' '

DPRT1	INC	LPOS
	JMP	PRT1

***

ENDEND	EQU	*

***

