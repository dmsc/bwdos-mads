*				*
*  " M E N U " PRO BW-DOS - CAST 1  *
*				*

*PRENOS

SIOADR	EQU	128
WRTCMD	EQU	130

DRIVNUM	EQU	131
SECLEN	EQU	132
SL	EQU	132
MEDFLG	EQU	134

DOFF	EQU	135
DPOS	EQU	136
DSECT	EQU	137

RWA	EQU	138
RWL	EQU	140

T	EQU	142
SSAVE	EQU	145

DTS	EQU	146
POS	EQU	148
DTAIX	EQU	151

*

BSS	EQU	152
PRTADR	EQU	153
PADR	EQU	153
PSAD	EQU	155
PRTMSK	EQU	157
PMSK	EQU	157

EDPOS	EQU	158
EDMAX	EQU	159
EDX	EQU	160
EDY	EQU	161

*

MEM2	EQU	162

DIRPOC	EQU	164
MDP	EQU	165
DIRSPOS	EQU	166
DSP	EQU	166

CMDPOS	EQU	167
CMDSPOS	EQU	168

DRIVE	EQU	169

MT1	EQU	170
PTTY	EQU	172
PTTP	EQU	173
PTTI	EQU	174

SORTA	EQU	175
MT2	EQU	177
SORTM	EQU	179

EXP	EQU	180
EXF	EQU	181

INFLG	EQU	182
OUTFLG	EQU	183
INPOS	EQU	184
OUTPOS	EQU	187
COPIX	EQU	190
COPTOP	EQU	191
COPDSTD	EQU	192
COPSAME	EQU	193
IN1ST	EQU	194
OUT1ST	EQU	195
CRA	EQU	196
COPENDF	EQU	198
COPNEWF	EQU	199

BELLFLG	EQU	200
COPPDRV	EQU	201

EDSPOS	EQU	202
QFLG	EQU	203
MBF	EQU	204

CERRF	EQU	205


JSRS	.MACRO	A1,A2,A3
	JSR	:A1
	JSR	:A2
	JSR	:A3
	.MEND

EOL	.MACRO
	DTA	155
	.MEND

BIT3	.MACRO
	DTA	$2C
	.MEND

LDSTA	.MACRO	BYT,ADR
	LDA	#:BYT
	STA	:ADR
	.MEND

LDXY	.MACRO	A1
	LDX	#>(:A1)
	LDY	#<(:A1)
	.MEND

DSTA	.MACRO	A1,A2
	LDA	#<:A1
	STA	:A2
	LDA	#>:A1
	STA	:A2+1
	.MEND

**

	JMP	START0
WAITKB	JMP	WAITKBA
INIT25B	JMP	INIT25
RWSECTXB	JMP	RWSECTX
READDIR	JMP	READD
PROTECTB	JMP	PROTECT
UNPROTB	JMP	UNPROT
RENAMEB	JMP	RENAME
ERASEB	JMP	ERASE
CHKDSKB	JMP	CHKDSK
READB	JMP	READ
WRI	JMP	WRITE
FORMATB	JMP	FORMAT
RDDNXTB	JMP	RDDNXT
AUXS0	JMP	AUXS0A
CD	JMP	CIODAB
CLOSE	JMP	CLOSEA
REOPNS	JMP	REOPNSA
GETKEYB	JMP	GETKEY
CONVDCB	JMP	CONVDC
WAITKEY	JMP	WAITKEYA
GETKBB	JMP	GETKB
CONVFNM	JMP	CONVFNMA
	JMP	$E477
*

RWST1	DTA	'"'
	DTA	C"!ONSWRP"

RWST2	DTA	64,64,128,64
	DTA	64,128,64,128

RWST3	DTA	0,0,12,12
	DTA	4,0,0,0

RWST4	DTA	255,255,7,7
	DTA	7,7,7,7

**

RWSECTX	STY	$304
	STX	$305

	LDY	#$31
	STY	$300
	LDY	DRIVNUM
	STY	$301

	STA	$302

	LDX	#7

RWS1	CMP	RWST1,X
	BEQ	RWS2

	DEX
	BPL	RWS1

	LDY	#168
	RTS
*

RWS2	LDA	RWST2,X
	STA	$303

	LDA	RWST4,X
	STA	$306

	LDY	#0
	LDA	RWST3,X
	BNE	RWS3

	LDA	#128
	LDX	$30B
	BNE	RWS2B
	LDX	$30A
	CPX	#4
	BCC	RWS3

RWS2B	LDA	SL
	LDY	SL+1
	BNE	RWS3
	CMP	#0
	BNE	RWS3

	STA	$303

RWS3	STA	$308
	STY	$309

	JMP	(SIOADR)
*

RWSECT	JSR	RWSECTX
	BMI	RWSER
	RTS

RWSER	JMP	ERROR

*

INIT25	LDA	10
	STA	SL
	LDA	11
	STA	SL+1
	DEC	SL+1

	LDY	#(256-10)
	LDA	(SL),Y
	STA	SIOADR
	INY
	LDA	(SL),Y
	STA	SIOADR+1

	LDY	#(256-2)
	LDA	(SL),Y
	STA	WRTCMD

	LDSTA	255,DSECT
	STA	DTS
	STA	DTS+1

	RTS

*

DENSTST	LDSTA	0,SL
	STA	SL+1
	STA	$30B
	LDSTA	4,$30A

	LDA	#'R'
	LDXY	DAB
	JSR	RWSECTX

	LDY	#$10
DNST3	STA	$D40A
	DEX
	BNE	DNST3
	DEY
	BNE	DNST3

	LDA	#'S'
	LDXY	DAB
	JSR	RWSECT

	LDA	DAB
	AND	#32
	EOR	#32
	ASL
	ASL
	STA	SL
	ASL
	ROL
	EOR	#1
	STA	SL+1

	LDSTA	255,DSECT

	RTS
*

CLMAPB	LDA	#0
	TAX

RDVT1	STA	MAPB,X
	DEX
	BNE	RDVT1

	RTS
*

RDVTOC	JSR	CLMAPB

	LDX	#0
	STX	MEDFLG
*
	INX
	LDY	#$68
	LDA	#'R'
	JSR	RWMAPB

	LDA	SL
	BPL	RDVTX

	LDA	MAPB+2
	CMP	#3
	BCC	RDVTX
*
	LDSTA	4,$30B
	LDSTA	0,$30A

	LDA	#'R'
	LDXY	(MAPB+128)
	JSR	RWSECT

	LDX	#128+$54

RDVT2	LDA	MAPB,X
	STA	MAPB-$70,X

	INX
	BNE	RDVT2

	DEC	MEDFLG
*

RDVTX	RTS

*

WRVTOC	LDA	MEDFLG
	BPL	WRVT2
*
	LDSTA	4,$30B
	LDSTA	0,$30A

	LDA	WRTCMD
	LDXY	(MAPB+$10)
	JSR	RWSECT

	LDA	#0
	LDX	#$64

WRVT1	STA	MAPB,X

	INX
	BNE	WRVT1
*

WRVT2	LDX	#1
	LDY	#$68

WRMAPB	LDA	WRTCMD

RWMAPB	STY	$30A
	STX	$30B

	LDXY	MAPB
	JMP	RWSECT
*

SETDP	LDA	DPOS
	TAY

	AND	#7
	ASL
	ASL
	ASL
	ASL
	STA	DOFF

	TYA
	LSR
	LSR
	LSR

	CMP	DSECT
	BEQ	SDRPX
*
	STA	DSECT

	LDX	#'R'
	BNE	RWDS

SDRPX	RTS

*

WRDER	LDY	#168
	JMP	ERROR
*

WRD	LDA	DSECT
	BMI	WRDER
	LDX	WRTCMD
*

RWDS	CLC
	ADC	#$69
	STA	$30A
	LDSTA	1,$30B

	TXA
	LDXY	DIRB
	JMP	RWSECT
*

SRCHD	LDSTA	255,DPOS

SRDNEXT	LDA	#255
	STA	T

SRD1	INC	DPOS
	LDA	DPOS
	CMP	#64
	BCS	SRD1B

	JSR	SETDP

	LDX	DOFF
	LDA	DIRB,X
	BNE	SRD2
*
	CLC

SRD1B	LDA	T
	BMI	SRD1C

	STA	DPOS
	JSR	SETDP

	CLC

SRD1C	LDY	#255
	RTS
*

SRD2	CMP	#$80
	BNE	SRD2B
*
	LDA	T
	BPL	SRD1

	LDA	DPOS
	STA	T
	JMP	SRD1
*

SRD2B	AND	#(255-32)

	CMP	#$42
	BEQ	SRD3
	CMP	#3
	BNE	SRD1

SRD3	LDY	#0

SRD3B	LDA	F,Y
	CMP	#'?'
	BEQ	SRD3C

	CMP	DIRB+5,X
	BNE	SRD1

SRD3C	INX
	INY
	CPY	#11
	BCC	SRD3B
*
	LDY	#1
	CLC
	RTS
*

NAMTST	LDY	#11
	JSR	NAMTST2
	LDY	#8

NAMTST2	DEY
	BMI	NAMTSX
	CPY	#7
	BEQ	NAMTSX

	LDA	F,Y
	CMP	#32
	BEQ	NAMTST2

NAMTS3	LDA	F,Y

	CMP	#'*'
	BEQ	NAMWIL
	CMP	#'?'
	BEQ	NAMWIL

	CMP	#'_'
	BEQ	NAMTS4

	CMP	#$30
	BCC	NAMBAD
	CMP	#$3A
	BCC	NAMTS4

	CMP	#'A'
	BCC	NAMBAD
	CMP	#'['
	BCS	NAMBAD

NAMTS4	DEY
	BMI	NAMTSX
	CPY	#7
	BNE	NAMTS3

NAMTSX	RTS

*

NAMWIL	LDY	#163
	BIT3
NAMBAD	LDY	#165
	JMP	ERROR
*

READD	TSX
	STX	SSAVE

	LDSTA	0,RWL
	STA	RWL+1

	JSR	INIT25
	JSR	DENSTST

	LDY	#10
	LDA	#'?'
RDD2A	STA	F,Y
	DEY
	BPL	RDD2A

	JSR	SRCHD
	BMI	RDDX

*

RDD3	LDX	DOFF
	LDY	#0
*
	LDA	DIRB,X
	AND	#32
	CMP	#32
	LDA	#4
	ROL

	STA	(RWA),Y
	INY
*
	LDA	#0
	STA	(RWA),Y
	INY
	STA	(RWA),Y
	INY
*
	STA	T
	LDA	DIRB+1,X
	STA	T+1
	LDA	DIRB+2,X
	STA	T+2

	LDA	SL
	BPL	RDD3B

	LSR	T+2
	ROR	T+1
	ROR	T

RDD3B	JSRS RDD3BSB,RDD3BSB,RDD3BSB

RDD3C	LDA	T-3,Y
	STA	(RWA),Y
	INY
	CPY	#6
	BCC	RDD3C
*

RDD3D	LDA	DIRB+5,X
	STA	(RWA),Y
	INX
	INY
	CPY	#17
	BCC	RDD3D
*
	LDA	#0
RDD3E	STA	(RWA),Y
	INY
	CPY	#23
	BCC	RDD3E
*
	JSR	RDDNXT
*
	JSR	SRDNEXT
	BPL	RDD3

RDDX	LDY	#1
	RTS

*

RDD3BSB	LDA	T
	SEC
	SBC	DIRB+1,X
	STA	T

	LDA	T+1
	SBC	DIRB+2,X
	STA	T+1

	BCS	RDD3BS2
	DEC	T+2

RDD3BS2	RTS

*

RDDNXT	LDA	#23
	CLC
	ADC	RWA
	STA	RWA
	BCC	RDD3F
	INC	RWA+1

RDD3F	LDA	#23
	CLC
	ADC	RWL
	STA	RWL
	BCC	RDD3G
	INC	RWL+1

RDD3G	RTS

*

ERROR	LDX	SSAVE
	TXS

	TYA
	RTS
*

PROTECT	SEC
	DTA	$24
UNPROT	CLC

	TSX
	STX	SSAVE

	PHP

	JSRS INIT25,NAMTST,DENSTST
	JSR	SRCHD
	BPL	UNPR2
*

UNPNOTF	LDY	#170
	JMP	ERROR
*

UNPR2	LDX	DOFF
	LDA	DIRB,X

	PLP
	BCC	UNPR2B

	ORA	#32
	BNE	UNPR3
*
UNPR2B	AND	#(255-32)

UNPR3	STA	DIRB,X

	JSR	WRD

	LDY	#1
	RTS
*

RENAME	TSX
	STX	SSAVE

	JSRS INIT25,NAMTST,DENSTST
	JSR	SRCHD
	BMI	UNPNOTF

	LDX	#10
RENM1	LDA	F2,X
	STA	F,X
	DEX
	BPL	RENM1

	JSR	NAMTST

	LDX	DOFF
	LDY	#0

RENM2	LDA	F,Y
	STA	DIRB+5,X

	INX
	INY
	CPY	#11
	BCC	RENM2

	JSR	WRD

	LDY	#1
	RTS
*

CHKDSK	TSX
	STX	SSAVE

	JSRS INIT25,DENSTST,RDVTOC

	LDA	#0
	LDY	#16
CHKD2	STA	(RWA),Y
	DEY
	BPL	CHKD2
*
	LDY	#1
	LDA	SL
	STA	(RWA),Y
*
	INY
	LDA	MAPB+1
	STA	(RWA),Y
	INY
	LDA	MAPB+2
	STA	(RWA),Y
*
	INY
	LDX	MAPB+4
	LDA	MAPB+3

	BIT	MEDFLG
	BPL	CHKD3

	CLC
	ADC	MAPB+$8A
	PHA
	TXA
	ADC	MAPB+$8B
	TAX
	PLA

CHKD3	STA	(RWA),Y
	INY
	TXA
	STA	(RWA),Y
*
	LDX	#0

CHKD4	INY
	LDA	CHKDNAM,X
	STA	(RWA),Y

	INX
	CPX	#8
	BCC	CHKD4

	LDY	#1
	RTS
*

CHKDNAM	DTA	C"AtariDOS"

*

RDDATS	STY	$30A
	STX	$30B
	STY	DTS
	STX	DTS+1

	LDXY	DAB
	LDA	#'R'
	JSR	RWSECT

	LDX	SL
	DEX
	DEX
	DEX

	LDA	DAB,X
	LSR
	LSR
	CMP	DPOS
	BNE	RDDTSE

	RTS
*

RDDTSE	LDY	#166
	JMP	ERROR

*

ERADTA	JSR	RDVTOC

	LDX	DOFF
	LDA	DIRB+3,X
	TAY
	LDA	DIRB+4,X
	TAX
*

ERADT2	TYA
	BNE	ERADT3
	TXA
	BNE	ERADT3

	JMP	WRVTOC
*

ERADT3	LSR	T+2

	CPY	#$D0
	TXA
	SBC	#2
	BCC	ERADT4

	LDA	MEDFLG
	BPL	RDDTSE

	CPX	#4
	BCS	RDDTSE

	SEC
	ROR	T+2
*

ERADT4	TYA
	PHA
	TXA
	PHA

	JSR	RDDATS
*
	PLA
	STA	T+1
	PLA
	STA	T
	PHA

	LDY	#3
ERADT5	LSR	T+1
	ROR	T
	DEY
	BNE	ERADT5

	LDA	T
	CLC
	ADC	#<(MAPB+10)
	STA	T
	LDA	T+1
	ADC	#>(MAPB+10)
	STA	T+1

	PLA
	AND	#7
	TAY
	LDA	#0
	SEC
ERADT6	ROR
	DEY
	BPL	ERADT6

	INY
	ORA	(T),Y
	CMP	(T),Y
	STA	(T),Y

	BEQ	ERADT8
*
	LDX	#3
	LDA	T+2
	BPL	ERADT7
	LDX	#$8A

ERADT7	INC	MAPB,X
	BNE	ERADT8
	INC	MAPB+1,X
*

ERADT8	LDX	SL
	DEX
	DEX
	LDA	DAB,X
	TAY
	DEX
	LDA	DAB,X
	AND	#3
	TAX

	JMP	ERADT2
*

ERASE	TSX
	STX	SSAVE

	JSRS INIT25,NAMTST,DENSTST
	JSR	SRCHD
	BPL	ERASE2

	LDY	#170
	BIT3
ERAPROT	LDY	#164
	JMP	ERROR
*

ERASE2	LDX	DOFF
	LDA	DIRB,X
	AND	#32
	BNE	ERAPROT

	JSR	ERADTA

	LDX	DOFF
	LDA	#$80
	STA	DIRB,X

	JSR	WRD

	LDY	#1
	RTS
*

PRIDEL	LSR	T+2
	LDX	#0

PRID2	LDA	MAPB+10,X
	BNE	PRID3

	INX
	CPX	#$5A
	BCC	PRID2

	LDA	MEDFLG
	BPL	PRID2E
	STA	T+2

	CPX	#$80
	BCC	PRID2

PRID2E	LDA	#0
	TAX
	TAY
	RTS
*

PRID3	LDY	#0

PRID3B	ASL
	BCS	PRID4
	INY
	BCC	PRID3B
*

PRID4	STY	T

*
	LDA	#255
	CLC
PRID4A	ROR
	DEY
	BPL	PRID4A

	AND	MAPB+10,X
	STA	MAPB+10,X
*
	INY
	STY	T+1

	TXA
	LDY	#3
PRID4B	ASL
	ROL	T+1
	DEY
	BNE	PRID4B

	ORA	T
	STA	T
*
	LDX	#3
	LDA	T+2
	BPL	PRID5
*
	TXA
	LDX	DOFF
	STA	DIRB,X
*
	LDX	#$8A

PRID5	LDA	MAPB,X
	BNE	PRID5B
	LDA	MAPB+1,X
	BEQ	PRID6

	DEC	MAPB+1,X
PRID5B	DEC	MAPB,X

*

PRID6	LDX	DOFF
	INC	DIRB+1,X
	BNE	PRID7
	INC	DIRB+2,X

PRID7	LDY	T
	LDX	T+1
	LDA	#1
	RTS
*

CLB	LDA	#0
	TAX
CLB2	STA	DAB,X
	DEX
	BNE	CLB2

	RTS
*

WRDATS	LDX	SL
	DEX
	DEX
	DEX
	LDA	DAB,X
	AND	#3
	STA	DAB,X

	LDA	DPOS
	ASL
	ASL
	ORA	DAB,X
	STA	DAB,X

	LDA	DTS
	STA	$30A
	LDA	DTS+1
	STA	$30B

	LDXY	DAB
	LDA	WRTCMD
	JMP	RWSECT
*

READ	TSX
	STX	SSAVE

	LDA	RWA+1
	PHA
	LDA	RWA
	PHA
*
	JSRS INIT25,NAMTST,DENSTST
	JSR	SRCHD
	BPL	R2

	LDY	#170
	JMP	ERROR
*

R2	LDX	DOFF
	LDA	DIRB+3,X
	TAY
	LDA	DIRB+4,X
	TAX

	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	R2B

	LDA	POS
	LDY	POS+1
	LDX	POS+2

R2B	STA	DTAIX

	JSR	RDDATS
*

R3	LDA	RWL
	ORA	RWL+1
	BNE	R3B

	LDY	#1
	BIT3
R3X	LDY	#136

	PLA
	STA	T
	PLA
	STA	T+1

	LDA	RWA
	SEC
	SBC	T
	STA	RWL

	LDA	RWA+1
	SBC	T+1
	STA	RWL+1

	LDA	DTAIX
	STA	POS
	LDA	DTS
	STA	POS+1
	LDA	DTS+1
	STA	POS+2

	TYA
	RTS

*

R3B	LDX	SL
	DEX
	LDA	DAB,X
	SEC
	SBC	DTAIX
	BNE	R3C

	LDA	DAB-1,X
	STA	T
	TAY
	LDA	DAB-2,X
	AND	#3
	TAX
	ORA	T
	BEQ	R3X

	JSR	RDDATS

	LDSTA	0,DTAIX
	BEQ	R3

*

R3C	LDX	RWL+1
	BNE	R4

	CMP	RWL
	BCC	R4

	LDA	RWL
*

R4	STA	T
	LDY	#0
	LDX	DTAIX

R4B	LDA	DAB,X
	STA	(RWA),Y

	INX
	INY
	CPY	T
	BCC	R4B

	STX	DTAIX
*
	JSR	RWNEXT
	JMP	R3
*

RWNEXT	LDA	RWA
	CLC
	ADC	T
	STA	RWA
	BCC	R5A
	INC	RWA+1

R5A	LDA	RWL
	SEC
	SBC	T
	STA	RWL
	BCS	R5B
	DEC	RWL+1

R5B	RTS

*

WRITE	TSX
	STX	SSAVE
*
	JSRS INIT25,NAMTST,DENSTST
	JSR	SRCHD
	BPL	WE2

	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BNE	WRNOTF

	BCC	WE2B

	LDY	#169
	BIT3
WRPROT	LDY	#164
	BIT3
WRNOTF	LDY	#170
	BIT3
WRBADP	LDY	#166
	JMP	ERROR
*

WE2	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BNE	WE2C

	LDX	DOFF
	LDA	DIRB,X
	AND	#32
	BNE	WRPROT

	JSR	ERADTA
*

WE2B	LDX	DOFF
	LDA	#$42
	STA	DIRB,X

	INX
	LDA	#0
	LDY	#4
W2BB	STA	DIRB,X
	INX
	DEY
	BNE	W2BB

	LDY	#0
W2BC	LDA	F,Y
	STA	DIRB,X
	INX
	INY
	CPY	#11
	BCC	W2BC

*

	JSR	RDVTOC
	JSR	PRIDEL
	BEQ	ER1ST

	TXA
	LDX	DOFF

	STA	DTS+1
	STA	DIRB+4,X
	TYA
	STA	DTS
	STA	DIRB+3,X

CLBWR3	JSR	CLB
	BEQ	WE3
*
ER1ST	LDY	#162
	JMP	ERROR

*

WE2C	LDY	POS+1
	LDX	POS+2
	JSR	RDDATS

	LDX	SL
	DEX
	LDA	POS
	CMP	DAB,X
	BNE	WRBADP

	JSR	RDVTOC

*

WE3	LDA	RWL
	ORA	RWL+1
	BNE	WE3B

	LDA	DTS
	STA	POS+1
	LDA	DTS+1
	STA	POS+2

	LDX	SL
	DEX
	LDA	DAB,X
	STA	POS

	JSR	WRDATS
	LDA	#1

ER2ND	PHA
	JSR	WRD
	JSR	WRVTOC
	PLA
	TAY
	RTS

*

WE3B	LDX	SL
	DEX
	DEX
	DEX
	TXA
	SEC
	SBC	DAB+2,X
	BNE	WE3C

	JSR	PRIDEL
	PHP

	TYA
	PHA
	TXA
	PHA

	LDX	SL
	DEX
	DEX
	STA	DAB-1,X
	TYA
	STA	DAB,X

	JSR	WRDATS

	PLA
	STA	DTS+1
	PLA
	STA	DTS

	LDA	#162
	PLP
	BEQ	ER2ND
	BNE	CLBWR3

*

WE3C	LDX	RWL+1
	BNE	WE4

	CMP	RWL
	BCC	WE4

	LDA	RWL
*

WE4	STA	T
	LDY	#0
	LDX	SL
	DEX
	LDA	DAB,X
	TAX

WE4B	LDA	(RWA),Y
	STA	DAB,X

	INX
	INY
	CPY	T
	BCC	WE4B

	TXA
	LDX	SL
	DEX
	STA	DAB,X
*
	JSR	RWNEXT
	JMP	WE3

*

FMER1	LDY	#168
	JMP	ERROR
*

FORMAT	TSX
	STX	SSAVE

	JSR	INIT25

	LDX	RWL
	CPX	#3
	BCS	FMER1
*
	LDA	FT1,X
	STA	SL
	LDA	FT2,X
	STA	SL+1
	LDA	FT3,X
	STA	MEDFLG

*

	LDSTA	4,$30A
	LDSTA	0,$30B
	LDXY	DAB
	LDA	#'N'
	JSR	RWSECTX
	BMI	FM2

	LDX	RWL

	LDSTA	40,DAB
	LDSTA	0,DAB+2
	STA	DAB+4
	LDA	FT4,X
	STA	DAB+3
	LDA	FT5,X
	STA	DAB+5
	LDA	FT2,X
	STA	DAB+6
	LDA	FT1,X
	STA	DAB+7

	LDXY	DAB
	LDA	#'O'
	JSR	RWSECT

*

FM2	LDSTA	4,$30A
	LDSTA	0,$30B

	LDA	MEDFLG
	ASL
	LDA	#'!'
	ADC	#0

	LDXY	DAB
	JSR	RWSECT

*

	JSR	CLMAPB

	LDSTA	2,MAPB
	STA	MAPB+2
	STA	MAPB+4
	LDSTA	$C3,MAPB+1
	STA	MAPB+3

	LDX	#$5A
	LDA	#$FF
FM3A	STA	MAPB+9,X
	DEX
	BNE	FM3A

	LSR
	STA	MAPB+$38
	STX	MAPB+$37
	LDSTA	$0F,MAPB+$0A
*
	LDA	MEDFLG
	BPL	FM4

	LDX	#$25
	LDA	#$FF
FM3B	STA	MAPB+$64,X
	DEX
	BNE	FM3B

	LSR
	STA	MAPB+$64

	INX
	STX	MAPB+$8B
	LDSTA	$2F,MAPB+$8A

	LDSTA	3,MAPB+2
	LDSTA	$F2,MAPB+1
*

FM4	JSR	WRVTOC

*
	JSR	CLMAPB

	LDA	#$69
	STA	T

FM4B	LDX	#1
	LDY	T
	JSR	WRMAPB

	INC	T
	LDA	T
	CMP	#$71
	BCC	FM4B
*
	LDX	#7
FM4C	LDA	FALBOT-1,X
	STA	MAPB,X
	DEX
	BNE	FM4C

	LDA	$D20A
	STA	MAPB
*X=0
	LDY	#1
	JSR	WRMAPB
*
	LDY	#1
	RTS
*

FT1	DTA	128,128,0
FT2	DTA	0,0,1
FT3	DTA	0,255,0
FT4	DTA	18,26,18
FT5	DTA	0,4,4

FALBOT	DTA	1,0,32,6,32
	SEC
	RTS

****

START0	LDA	$700
	CMP	#'S'
	BNE	START0E
	JMP	START3
*

START0E	LDX	#0
	LDA	#11
	STA	$342,X

	LDA	#<STERT
	STA	$344,X
	LDA	#>STERT
	STA	$345,X

	LDA	#<STERL
	STA	$348,X
	LDA	#>STERL
	STA	$349,X

	JSR	$E456

	JMP	(10)
*

STERT	DTA	155,155
	DTA	C"Incorrect DOS "
	DTA	C"version !!!"
	DTA	253,253,253,253,155

STERL	EQU	*-STERT

*
LOOPTX	DTA	C"      BW-DOS MENU 1."
	DTA	C"2  By BEWESOFT      "
	EOL

ERRTX
	DTA	C"Error    !"
	EOL

SRTM
	DTA	5,0,20
	DTA	9,16,24,29,37
	DTA	6,7,4,7,2
	DTA	C"ENSD0"
	DTA	C"Sorting: Ext+N. Name"
	DTA	C"+E. Size Date+T. No"
	EOL

NDSKT
	DTA	C"New disk: Drive:"
	EOL

NDSKM
	DTA	5,17,20
	DTA	0,3,6,9,12
	DTA	3,3,3,3,3
	DTA	C"12348"
	DTA	C" 1  2  3  4  8 "
PRESSK
	EOL

WKTX	DTA	C"Press a key..."
	DTA	160,155

SELTX
	DTA	C"S"
	EOL
DESTX
	DTA	C"Des"
	EOL
SDTX
	DTA	C"elect group:"
	EOL

DIT1
	DTA	C"Volume:"
DIT2
	DTA	C"Bytes/sector:"
DIT3
	DTA	C"Total bytes:"
DIT4
	DTA	C"Bytes free:"

DI1
	DTA	D"1222222222222222"
	DTA	D"222222222222222"
	DTA	5,155

DI2
	DTA	C"|          Disk "
	DTA	C"info:          |"
	EOL

DI3
	DTA	D"!222222222222222"
	DTA	D"222222222222222$"
	EOL

DI4
	DTA	D":222222222222222"
	DTA	D"222222222222222#"
	EOL

NOTSAM
	DTA	C"Disk changed!"
	EOL

MDTX
	DTA	C"Directory name:"
	EOL

RNTX
	DTA	C"Rename to:"
	EOL

AINM
	DTA	4,0,20
	DTA	26,29,32,35
	DTA	3,3,3,3
	DTA	C"1234"
	DTA	C"Format Atari disk: "
	DTA	C"Drive: "
	DTA	C" 1  2  3  4 "
	EOL

AINS
	DTA	2,19,20
	DTA	14,17
	DTA	2,3
	DTA	C"NY"
	DTA	C"Are you sure? "
	DTA	C"No Yes"
	EOL

AIND
	DTA	3,19,20
	DTA	0,7,14
	DTA	6,6,6
	DTA	C"SMD"
	DTA	C"Single Medium Double"
	EOL

FRMTNG
	DTA	C"Formatting..."
	DTA	160,155

MTTX
	DTA	C"Filename:"
	EOL

COPTX1
	DTA	C"  Copy to drive:"
	EOL

COPMEN
	DTA	2,0,20
	DTA	11,15
	DTA	3,2
	DTA	C"YN"
	DTA	C"Same disk? Yes No"
	EOL

COPTX2
	DTA	C"Dest. path:"
	EOL

SRCTX
	DTA	C"Insert source disk..."
	DTA	160,155

DSTTX
	DTA	C"Insert destination"
	DTA	C" disk..."
	DTA	160,155

BELLMEN
	DTA	2,0,20
	DTA	6,10
	DTA	3,2
	DTA	C"YN"
	DTA	C"Bell: Yes No"
	EOL

CLTBT
	DTA	A(CTN,CTM,CTP,CTP)
	DTA	A(CTP,CTP,CTP,CTP)
	DTA	A(CTP,CTP,CTP,CTP)
	DTA	A(CTP,CTP,CTP,CTP)
	DTA	A(CTP,CTP,CTD)

CTN	DTA	C"|  Filename  |P|  Si"
	DTA	C"ze  |  Date  |Time |"
	EOL

CTM	DTA	D"!2222222272223232222"
	DTA	D"2222322222222322222$"
	EOL

CTP	DTA	C"|        |   | |    "
	DTA	C"    |        |     |"
	EOL

CTD	DTA	D":2222222282228282222"
	DTA	D"2222822222222822222#"
	EOL

CMDS
	DTA	C"New disk "
	EOL
	DTA	C"Disk info"
	EOL
	DTA	C"Sub dir  "
	EOL
	DTA	C"Up dir   "
	EOL
	DTA	C"Make dir "
	EOL
	DTA	C"Del. dir "
	EOL
	DTA	C"Copy     "
	EOL
	DTA	C"Erase    "
	EOL
	DTA	C"Rename   "
	EOL
	DTA	C"Protect  "
	EOL
	DTA	C"Unprotect"
	EOL
	DTA	C"View text"
	EOL
	DTA	C"Make text"
	EOL
	DTA	C"Ainit    "
	EOL
	DTA	C"Exit     "
	EOL
	DTA	C"Setup    "
	EOL
	DTA	C"Select   "
	EOL
	DTA	C"Deselect "
	EOL
*

RAMPH
	DTA	D"22222222227222222222"
	DTA	D"72222222227222222222"
	EOL

RAMPM
	DTA	C"          |         "
	DTA	C"|         |         "
	EOL

RAMPL
	DTA	D"22222222228222222222"
	DTA	D"82222222228222222222"
	EOL

RDD3T
	DTA	A(SRTB0,SRTB1,SRTB2,SRTB3)

SRTB0	DTA	14,15,16
	DTA	6,7,8,9,10,11,12,13
	DTA	255

SRTB1	DTA	6,7,8,9,10,11,12,13
	DTA	14,15,16
	DTA	255

SRTB2	DTA	5,4,3
	DTA	255

SRTB3	DTA	19,18,17,20,21,22
	DTA	255

QMEN
	DTA	2,0,20
	DTA	24,28
	DTA	3,2
	DTA	C"YN"
	DTA	C"Question for overwrite"
	DTA	C": Yes No"
	EOL

* PRO B

AUXS0A	LDX	#$10
	LDA	#0
	STA	$34A,X
	STA	$34B,X
	RTS
*

CIODAB	STA	$342,X
	LDA	#<DAB
	STA	$344,X
	LDA	#>DAB
	STA	$345,X

	JMP	$E456
*

CLOSE2	LDX	#$20
	BIT3
CLOSE0	LDX	#0
	BIT3
CLOSEA	LDX	#$10
	LDA	#12
	STA	$342,X
	JMP	$E456
*

REOPNSA	JSR	CLOSE0

	LDX	#0
	STX	$2FE
	LDA	#3
	STA	$342,X

	LDA	#<ENAM
	STA	$344,X
	LDA	#>ENAM
	STA	$345,X

	LDA	#12
	STA	$34A,X
	TXA
	STA	$34B,X

	JSR	$E456
	BMI	REOPER
	RTS
*
REOPER	JMP	$E474
*
ENAM	DTA	C"E:"
	DTA	155

KNAM	DTA	C"K:"
	DTA	155
*

WAITKEYA	LDA	#0
	BIT3
WAITKBA	LDA	#$FF
	PHA

	LDSTA	<WKTX,PADR
	LDSTA	>WKTX,PADR+1
	LDY	#20
	LDA	#0
	JSR	PRINTA

	PLA
	BIT3
GETKB	LDA	#$FF
	ASL
	BPL	GETKEY
	LDA	BELLFLG
	BNE	GETKEY
*BELL!
	LDX	#15

	LDSTA	3,$D20F
	LDSTA	0,$D208

	LDSTA	72,$D200

GKB1A	TXA
	ORA	#$A0
	STA	$D201

	LDY	20
	INY
	INY
	INY

GKB1B	LDA	764
	CMP	#$FF
	BNE	GKB1C
	CPY	20
	BNE	GKB1B

	DEX
	BNE	GKB1A

GKB1C	LDSTA	0,$D201
**

GETKEY	JSR	CLOSE2
*
	LDX	#$20
	LDA	#3
	STA	$342,X

	LDA	#4
	STA	$34A,X
	LDA	#0
	STA	$34B,X

	LDA	#<KNAM
	STA	$344,X
	LDA	#>KNAM
	STA	$345,X

	JSR	$E456

	LDSTA	0,$2B6
	LDSTA	$40,$2BE

GETK2	LDX	#$20
	LDA	#7
	STA	$342,X
	LDA	#0
	STA	$348,X
	STA	$349,X

	JSR	$E456
	BMI	GETK2

	PHA
	JSR	CLOSE2
	PLA

	RTS
*

CONVDC	STY	T
	STX	T+1
	STA	T+2

	LDA	#0
	LDX	#8
COND1	STA	DAB-1,X
	DEX
	BNE	COND1
*
COND2	LDA	T
	SEC
	SBC	CDT3,X
	STA	PRTMSK

	LDA	T+1
	SBC	CDT2,X
	TAY

	LDA	T+2
	SBC	CDT1,X
	BCC	COND3
*
	STA	T+2
	STY	T+1
	LDA	PRTMSK
	STA	T

	INC	DAB,X
	BNE	COND2
*

COND3	INX
	CPX	#8
	BCC	COND2
*
	LDXY	$20

COND4	CPX	#7
	BEQ	COND4B
	LDA	DAB,X
	BEQ	COND5
COND4B	LDY	#$30

COND5	TYA
	ORA	DAB,X
	STA	DAB,X

	INX
	CPX	#8
	BCC	COND4

	RTS
*

CDT1	DTA	$98,$0F,$01,0,0,0,0,0
CDT2	DTA	$96,$42,$86,$27,3,0,0,0
CDT3	DTA	$80,$40,$A0,$10,$E8,100,10,1

**

CONVFNMA	LDXY	0
	LDA	#8
	JSR	CF2

	CMP	#'.'
	BEQ	CF1

	CMP	#155
	BNE	CFE
	DEX

CF1	LDA	#11
	JSR	CF2

	CMP	#155
	BNE	CFE
	RTS
**
CFE	LDY	#165
	JMP	JERR
**
CF2	STA	MT1

CF3	LDA	DAB,X
	INX
	CPY	MT1
	BEQ	CF3C

	CMP	#'*'
	BEQ	CF3H

	CMP	#'?'
	BEQ	CF3B
	CMP	#'_'
	BEQ	CF3B
	CMP	#$30
	BCC	CF3C
	CMP	#$3A
	BCC	CF3B
	CMP	#$41
	BCC	CF3C
	CMP	#$5B
	BCS	CF3C

CF3B	STA	F,Y
	INY
	BNE	CF3
*
CF3H	LDA	DAB,X
	INX

	PHA
	LDA	#'?'
	BNE	CF3D
**

CF3C	PHA
	LDA	#32

CF3D	CPY	MT1
	BCC	CF3E

	PLA
	RTS
*
CF3E	STA	F,Y
	INY
	BNE	CF3D

