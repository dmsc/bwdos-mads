	ORG	$4000,$A800
	OUT	N
*************************************
*				*
*  " M E N U " PRO BW-DOS - CAST 1  *
*				*
*************************************

*PRENOS

DATBUF	EQU	$61AB
START	EQU	$576C

*DOMYSLENO

MAPBUF	EQU	DATBUF+256
DIRBUF	EQU	MAPBUF+256
FNAME	EQU	DIRBUF+256
FNAME2	EQU	FNAME+11

***

SIOADR	EPZ	128
WRTCMD	EPZ	130

DRIVNUM	EPZ	131
SECLEN	EPZ	132
MEDFLG	EPZ	134

DIROFF	EPZ	135
DIRPOS	EPZ	136
DIRSECT	EPZ	137

RWADR	EPZ	138
RWLEN	EPZ	140

TEMP25	EPZ	142
SSAVE	EPZ	145

DATSEC	EPZ	146
POS	EPZ	148
DTAIX	EPZ	151

**

BSSAVE	EPZ	152
BTMP	EPZ	153

* VOLNO: 154

	JMP	START0
***

RWST1	DFB '"'
	ASC "!ONSWRP"

RWST2	DFB 64,64,128,64
	DFB 64,128,64,128

RWST3	DFB 0,0,12,12
	DFB 4,0,0,0

RWST4	DFB 255,255,7,7
	DFB 7,7,7,7

***

RWSECTX	STY	$304
	STX	$305

	LDY	#$31
	STY	$300
	LDY	DRIVNUM
	STY	$301

	STA	$302

	LDX	#7

RWS1	CMP	RWST1,X
	BEQ	RWS2

	DEX
	BPL	RWS1

	LDY	#168
	RTS
**

RWS2	LDA	RWST2,X
	STA	$303

	LDA	RWST4,X
	STA	$306

	LDY	#0
	LDA	RWST3,X
	BNE	RWS3

	LDA	#128
	LDX	$30B
	BNE	RWS2B
	LDX	$30A
	CPX	#4
	BCC	RWS3

RWS2B	LDA	SECLEN
	LDY	SECLEN+1
	BNE	RWS3
	CMP	#0
	BNE	RWS3

	STA	$303

RWS3	STA	$308
	STY	$309

	JMP	(SIOADR)
***

RWSECT	JSR	RWSECTX
	BMI	RWSER
	RTS

RWSER	JMP	ERROR

***

INIT25	LDA	10
	STA	SECLEN
	LDA	11
	STA	SECLEN+1
	DEC	SECLEN+1

	LDY	#(256-10)
	LDA	(SECLEN),Y
	STA	SIOADR
	INY
	LDA	(SECLEN),Y
	STA	SIOADR+1

	LDY	#(256-2)
	LDA	(SECLEN),Y
	STA	WRTCMD

	LDA	#255
	STA	DIRSECT
	STA	DATSEC
	STA	DATSEC+1

	RTS

***

DENSTST	LDA	#0
	STA	SECLEN
	STA	SECLEN+1
	STA	$30B
	LDA	#4
	STA	$30A

	LDA	#'R'
	LDY	#DATBUF:L
	LDX	#DATBUF:H
	JSR	RWSECTX

	LDY	#$10
DNST3	STA	$D40A
	DEX
	BNE	DNST3
	DEY
	BNE	DNST3

	LDA	#'S'
	LDY	#DATBUF:L
	LDX	#DATBUF:H
	JSR	RWSECT

	LDA	DATBUF
	AND	#32
	EOR	#32
	ASL
	ASL
	STA	SECLEN
	ASL
	ROL
	EOR	#1
	STA	SECLEN+1

	LDA	#255
	STA	DIRSECT

	RTS
***

CLMAPB	LDA	#0
	TAX

RDVT1	STA	MAPBUF,X
	DEX
	BNE	RDVT1

	RTS
***

RDVTOC	JSR	CLMAPB

	LDX	#0
	STX	MEDFLG
*
	INX
	LDY	#$68
	LDA	#'R'
	JSR	RWMAPB

	LDA	SECLEN
	BPL	RDVTX

	LDA	MAPBUF+2
	CMP	#3
	BCC	RDVTX
*ENH.
	LDA	#4
	STA	$30B
	LDA	#0
	STA	$30A

	LDA	#'R'
	LDY	#(MAPBUF+128):L
	LDX	#(MAPBUF+128):H
	JSR	RWSECT

	LDX	#128+$54

RDVT2	LDA	MAPBUF,X
	STA	MAPBUF-$70,X

	INX
	BNE	RDVT2

	DEC	MEDFLG
*

RDVTX	RTS

***

WRVTOC	LDA	MEDFLG
	BPL	WRVT2
*ED
	LDA	#4
	STA	$30B
	LDA	#0
	STA	$30A

	LDA	WRTCMD
	LDY	#(MAPBUF+$10):L
	LDX	#(MAPBUF+$10):H
	JSR	RWSECT

	LDA	#0
	LDX	#$64

WRVT1	STA	MAPBUF,X

	INX
	BNE	WRVT1
*

WRVT2	LDX	#1
	LDY	#$68

WRMAPB	LDA	WRTCMD

RWMAPB	STY	$30A
	STX	$30B

	LDY	#MAPBUF:L
	LDX	#MAPBUF:H
	JMP	RWSECT
***

SETDIRP	LDA	DIRPOS
	TAY

	AND	#7
	ASL
	ASL
	ASL
	ASL
	STA	DIROFF

	TYA
	LSR
	LSR
	LSR

	CMP	DIRSECT
	BEQ	SDRPX
*READ
	STA	DIRSECT

	LDX	#'R'
	BNE	RWDIRS

SDRPX	RTS

**

WRDER	LDY	#168
	JMP	ERROR
**

WRDIR	LDA	DIRSECT
	BMI	WRDER
	LDX	WRTCMD
*

RWDIRS	CLC
	ADC	#$69
	STA	$30A
	LDA	#1
	STA	$30B

	TXA
	LDY	#DIRBUF:L
	LDX	#DIRBUF:H
	JMP	RWSECT
**

SRCHDIR	LDA	#255
	STA	DIRPOS

SRDNEXT	LDA	#255
	STA	TEMP25

SRD1	INC	DIRPOS
	LDA	DIRPOS
	CMP	#64
	BCS	SRD1B

	JSR	SETDIRP

	LDX	DIROFF
	LDA	DIRBUF,X
	BNE	SRD2
*STAT0
	CLC

SRD1B	LDA	TEMP25
	BMI	SRD1C

	STA	DIRPOS
	JSR	SETDIRP

	CLC

SRD1C	LDY	#255
	RTS
**

SRD2	CMP	#$80
	BNE	SRD2B
*DELETED
	LDA	DIRPOS
	STA	TEMP25
	JMP	SRD1
**

SRD2B	AND	#(255-32)

	CMP	#$42
	BEQ	SRD3
	CMP	#3
	BNE	SRD1

SRD3	LDY	#0

SRD3B	LDA	FNAME,Y
	CMP	#'?'
	BEQ	SRD3C

	CMP	DIRBUF+5,X
	BNE	SRD1

SRD3C	INX
	INY
	CPY	#11
	BCC	SRD3B
*NASEL!
	LDY	#1
	CLC
	RTS
***

NAMTST	LDY	#11
	JSR	NAMTST2
	LDY	#8

NAMTST2	DEY
	BMI	NAMTSX
	CPY	#7
	BEQ	NAMTSX

	LDA	FNAME,Y
	CMP	#32
	BEQ	NAMTST2

NAMTS3	LDA	FNAME,Y

	CMP	#'*'
	BEQ	NAMWIL
	CMP	#'?'
	BEQ	NAMWIL

	CMP	#'_'
	BEQ	NAMTS4

	CMP	#$30
	BCC	NAMBAD
	CMP	#$3A
	BCC	NAMTS4

	CMP	#'A'
	BCC	NAMBAD
	CMP	#'['
	BCS	NAMBAD

NAMTS4	DEY
	BMI	NAMTSX
	CPY	#7
	BNE	NAMTS3

NAMTSX	RTS

**

NAMWIL	LDY	#163
	DFB $2C
NAMBAD	LDY	#165
	JMP	ERROR
***

READDIR	TSX
	STX	SSAVE

	LDA	#0
	STA	RWLEN
	STA	RWLEN+1

	JSR	INIT25
	JSR	DENSTST

	LDY	#10
	LDA	#'?'
RDDIR2	STA	FNAME,Y
	DEY
	BPL	RDDIR2

	JSR	SRCHDIR
	BMI	RDDIRX

*KONVERTUJ

RDDIR3	LDX	DIROFF
	LDY	#0
*STAT
	LDA	DIRBUF,X
	AND	#32
	CMP	#32
	LDA	#4
	ROL

	STA	(RWADR),Y
	INY
*1ST MAP
	LDA	#0
	STA	(RWADR),Y
	INY
	STA	(RWADR),Y
	INY
*LEN
	STA	TEMP25
	LDA	DIRBUF+1,X
	STA	TEMP25+1
	LDA	DIRBUF+2,X
	STA	TEMP25+2

	LDA	SECLEN
	BPL	RDDIR3B

	LSR	TEMP25+2
	ROR	TEMP25+1
	ROR	TEMP25

RDDIR3B	JSR	RDD3BSB
	JSR	RDD3BSB
	JSR	RDD3BSB

RDDIR3C	LDA	TEMP25-3,Y
	STA	(RWADR),Y
	INY
	CPY	#6
	BCC	RDDIR3C
*NAME

RDDIR3D	LDA	DIRBUF+5,X
	STA	(RWADR),Y
	INX
	INY
	CPY	#17
	BCC	RDDIR3D
*TD
	LDA	#0
RDDIR3E	STA	(RWADR),Y
	INY
	CPY	#23
	BCC	RDDIR3E
*
	JSR	RDDNXT
**

RDDIR4	JSR	SRDNEXT
	BPL	RDDIR3

RDDIRX	LDY	#1
	RTS

**

RDD3BSB	LDA	TEMP25
	SEC
	SBC	DIRBUF+1,X
	STA	TEMP25

	LDA	TEMP25+1
	SBC	DIRBUF+2,X
	STA	TEMP25+1

	BCS	RDD3BS2
	DEC	TEMP25+2

RDD3BS2	RTS

***

RDDNXT	LDA	#23
	CLC
	ADC	RWADR
	STA	RWADR
	BCC	RDDIR3F
	INC	RWADR+1

RDDIR3F	LDA	#23
	CLC
	ADC	RWLEN
	STA	RWLEN
	BCC	RDDIR3G
	INC	RWLEN+1

RDDIR3G	RTS

***

ERROR	LDX	SSAVE
	TXS

	TYA
	RTS
***

PROTECT	SEC
	DFB $24
UNPROT	CLC

	TSX
	STX	SSAVE

	PHP

	JSR	INIT25
	JSR	NAMTST
	JSR	DENSTST
	JSR	SRCHDIR
	BPL	UNPR2
*NEEX.

UNPNOTF	LDY	#170
	JMP	ERROR
**

UNPR2	LDX	DIROFF
	LDA	DIRBUF,X

	PLP
	BCC	UNPR2B

	ORA	#32
	BNE	UNPR3
*
UNPR2B	AND	#(255-32)

UNPR3	STA	DIRBUF,X

	JSR	WRDIR

	LDY	#1
	RTS
***

RENAME	TSX
	STX	SSAVE

	JSR	INIT25
	JSR	NAMTST
	JSR	DENSTST
	JSR	SRCHDIR
	BMI	UNPNOTF

	LDX	#10
RENM1	LDA	FNAME2,X
	STA	FNAME,X
	DEX
	BPL	RENM1

	JSR	NAMTST

	LDX	DIROFF
	LDY	#0

RENM2	LDA	FNAME,Y
	STA	DIRBUF+5,X

	INX
	INY
	CPY	#11
	BCC	RENM2

	JSR	WRDIR

	LDY	#1
	RTS
***

CHKDSK	TSX
	STX	SSAVE

	JSR	INIT25
	JSR	DENSTST

	JSR	RDVTOC

	LDA	#0
	LDY	#16
CHKD2	STA	(RWADR),Y
	DEY
	BPL	CHKD2
*BYTSECT
	LDY	#1
	LDA	SECLEN
	STA	(RWADR),Y
*TOTAL
	INY
	LDA	MAPBUF+1
	STA	(RWADR),Y
	INY
	LDA	MAPBUF+2
	STA	(RWADR),Y
*FREE
	INY
	LDX	MAPBUF+4
	LDA	MAPBUF+3

	BIT	MEDFLG
	BPL	CHKD3

	CLC
	ADC	MAPBUF+$8A
	PHA
	TXA
	ADC	MAPBUF+$8B
	TAX
	PLA

CHKD3	STA	(RWADR),Y
	INY
	TXA
	STA	(RWADR),Y
*VOLUME
	LDX	#0

CHKD4	INY
	LDA	CHKDNAM,X
	STA	(RWADR),Y

	INX
	CPX	#8
	BCC	CHKD4

	LDY	#1
	RTS
**

CHKDNAM	ASC "AtariDOS"

***

RDDATS	STY	$30A
	STX	$30B
	STY	DATSEC
	STX	DATSEC+1

	LDY	#DATBUF:L
	LDX	#DATBUF:H
	LDA	#'R'
	JSR	RWSECT

	LDX	SECLEN
	DEX
	DEX
	DEX

	LDA	DATBUF,X
	LSR
	LSR
	CMP	DIRPOS
	BNE	RDDTSE

	RTS
**

RDDTSE	LDY	#166
	JMP	ERROR

***

ERADTA	JSR	RDVTOC

	LDX	DIROFF
	LDA	DIRBUF+3,X
	TAY
	LDA	DIRBUF+4,X
	TAX
*END?

ERADT2	TYA
	BNE	ERADT3
	TXA
	BNE	ERADT3

	JMP	WRVTOC
*MAXSEC?

ERADT3	LSR	TEMP25+2

	CPY	#$D0
	TXA
	SBC	#2
	BCC	ERADT4

	LDA	MEDFLG
	BPL	RDDTSE

	CPX	#4
	BCS	RDDTSE

	SEC
	ROR	TEMP25+2
*RDIT

ERADT4	TYA
	PHA
	TXA
	PHA

	JSR	RDDATS
*UVOLNI
	PLA
	STA	TEMP25+1
	PLA
	STA	TEMP25
	PHA

	LDY	#3
ERADT5	LSR	TEMP25+1
	ROR	TEMP25
	DEY
	BNE	ERADT5

	LDA	TEMP25
	CLC
	ADC	#MAPBUF+10:L
	STA	TEMP25
	LDA	TEMP25+1
	ADC	#MAPBUF+10:H
	STA	TEMP25+1

	PLA
	AND	#7
	TAY
	LDA	#0
	SEC
ERADT6	ROR
	DEY
	BPL	ERADT6

	INY
	ORA	(TEMP25),Y
	CMP	(TEMP25),Y
	STA	(TEMP25),Y

	BEQ	ERADT8
*FREE+1
	LDX	#3
	LDA	TEMP25+2
	BPL	ERADT7
	LDX	#$8A

ERADT7	INC	MAPBUF,X
	BNE	ERADT8
	INC	MAPBUF+1,X
*NEXT

ERADT8	LDX	SECLEN
	DEX
	DEX
	LDA	DATBUF,X
	TAY
	DEX
	LDA	DATBUF,X
	AND	#3
	TAX

	JMP	ERADT2
***

ERASE	TSX
	STX	SSAVE

	JSR	INIT25
	JSR	NAMTST
	JSR	DENSTST
	JSR	SRCHDIR
	BPL	ERASE2

	LDY	#170
	DFB $2C
ERAPROT	LDY	#164
	JMP	ERROR
**

ERASE2	LDX	DIROFF
	LDA	DIRBUF,X
	AND	#32
	BNE	ERAPROT

	JSR	ERADTA

	LDX	DIROFF
	LDA	#$80
	STA	DIRBUF,X

	JSR	WRDIR

	LDY	#1
	RTS
***

PRIDEL	LSR	TEMP25+2
	LDX	#0

PRID2	LDA	MAPBUF+10,X
	BNE	PRID3

	INX
	CPX	#$5A
	BCC	PRID2

	LDA	MEDFLG
	BPL	PRID2E
	STA	TEMP25+2

	CPX	#$80
	BCC	PRID2

PRID2E	LDA	#0
	TAX
	TAY
	RTS
**

PRID3	LDY	#0

PRID3B	ASL
	BCS	PRID4
	INY
	BCC	PRID3B
**

PRID4	STY	TEMP25

*VTOC
	LDA	#255
	CLC
PRID4A	ROR
	DEY
	BPL	PRID4A

	AND	MAPBUF+10,X
	STA	MAPBUF+10,X
*SECT#
	INY
	STY	TEMP25+1

	TXA
	LDY	#3
PRID4B	ASL
	ROL	TEMP25+1
	DEY
	BNE	PRID4B

	ORA	TEMP25
	STA	TEMP25
*FREE
	LDX	#3
	LDA	TEMP25+2
	BPL	PRID5
*FSTAT
	TXA
	LDX	DIROFF
	STA	DIRBUF,X
*
	LDX	#$8A

PRID5	LDA	MAPBUF,X
	BNE	PRID5B
	LDA	MAPBUF+1,X
	BEQ	PRID6

	DEC	MAPBUF+1,X
PRID5B	DEC	MAPBUF,X

*FLEN

PRID6	LDX	DIROFF
	INC	DIRBUF+1,X
	BNE	PRID7
	INC	DIRBUF+2,X

PRID7	LDY	TEMP25
	LDX	TEMP25+1
	LDA	#1
	RTS
***

CLBUF	LDA	#0
	TAX
CLB2	STA	DATBUF,X
	DEX
	BNE	CLB2

	RTS
***

WRDATS	LDX	SECLEN
	DEX
	DEX
	DEX
	LDA	DATBUF,X
	AND	#3
	STA	DATBUF,X

	LDA	DIRPOS
	ASL
	ASL
	ORA	DATBUF,X
	STA	DATBUF,X

	LDA	DATSEC
	STA	$30A
	LDA	DATSEC+1
	STA	$30B

	LDY	#DATBUF:L
	LDX	#DATBUF:H
	LDA	WRTCMD
	JMP	RWSECT
***

READ	TSX
	STX	SSAVE

	LDA	RWADR+1
	PHA
	LDA	RWADR
	PHA
*
	JSR	INIT25
	JSR	NAMTST
	JSR	DENSTST
	JSR	SRCHDIR
	BPL	READ2

	LDY	#170
	JMP	ERROR
*POINT?

READ2	LDX	DIROFF
	LDA	DIRBUF+3,X
	TAY
	LDA	DIRBUF+4,X
	TAX

	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BEQ	READ2B

	LDA	POS
	LDY	POS+1
	LDX	POS+2

READ2B	STA	DTAIX

	JSR	RDDATS
*READ!
*END?

READ3	LDA	RWLEN
	ORA	RWLEN+1
	BNE	READ3B
*END
	LDY	#1
	DFB $2C
READ3X	LDY	#136

	PLA
	STA	TEMP25
	PLA
	STA	TEMP25+1

	LDA	RWADR
	SEC
	SBC	TEMP25
	STA	RWLEN

	LDA	RWADR+1
	SBC	TEMP25+1
	STA	RWLEN+1

	LDA	DTAIX
	STA	POS
	LDA	DATSEC
	STA	POS+1
	LDA	DATSEC+1
	STA	POS+2

	TYA
	RTS

*NEXT SECT?

READ3B	LDX	SECLEN
	DEX
	LDA	DATBUF,X
	SEC
	SBC	DTAIX
	BNE	READ3C

	LDA	DATBUF-1,X
	STA	TEMP25
	TAY
	LDA	DATBUF-2,X
	AND	#3
	TAX
	ORA	TEMP25
	BEQ	READ3X

	JSR	RDDATS

	LDA	#0
	STA	DTAIX
	BEQ	READ3

*LEN IN SECT.

READ3C	LDX	RWLEN+1
	BNE	READ4

	CMP	RWLEN
	BCC	READ4

	LDA	RWLEN
*READ!

READ4	STA	TEMP25
	LDY	#0
	LDX	DTAIX

READ4B	LDA	DATBUF,X
	STA	(RWADR),Y

	INX
	INY
	CPY	TEMP25
	BCC	READ4B

	STX	DTAIX
*NEXT
	JSR	RWNEXT
	JMP	READ3
***

RWNEXT	LDA	RWADR
	CLC
	ADC	TEMP25
	STA	RWADR
	BCC	READ5A
	INC	RWADR+1

READ5A	LDA	RWLEN
	SEC
	SBC	TEMP25
	STA	RWLEN
	BCS	READ5B
	DEC	RWLEN+1

READ5B	RTS

***

WRITE	TSX
	STX	SSAVE
*
	JSR	INIT25
	JSR	NAMTST
	JSR	DENSTST
	JSR	SRCHDIR
	BPL	WRITE2

	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BNE	WRNOTF

	BCC	WRITE2B

	LDY	#169
	DFB $2C
WRPROT	LDY	#164
	DFB $2C
WRNOTF	LDY	#170
	DFB $2C
WRBADP	LDY	#166
	JMP	ERROR
*JE

WRITE2	LDA	POS
	ORA	POS+1
	ORA	POS+2
	BNE	WRITE2C

	LDX	DIROFF
	LDA	DIRBUF,X
	AND	#32
	BNE	WRPROT

	JSR	ERADTA
*ZALOZ

WRITE2B	LDX	DIROFF
	LDA	#$42
	STA	DIRBUF,X

	INX
	LDA	#0
	LDY	#4
WRIT2BB	STA	DIRBUF,X
	INX
	DEY
	BNE	WRIT2BB

	LDY	#0
WRIT2BC	LDA	FNAME,Y
	STA	DIRBUF,X
	INX
	INY
	CPY	#11
	BCC	WRIT2BC

*PRIDEL PRVNI SEKTOR

	JSR	RDVTOC
	JSR	PRIDEL
	BEQ	ER1ST

	TXA
	LDX	DIROFF

	STA	DATSEC+1
	STA	DIRBUF+4,X
	TYA
	STA	DATSEC
	STA	DIRBUF+3,X

CLBWR3	JSR	CLBUF
	BEQ	WRITE3
*
ER1ST	LDY	#162
	JMP	ERROR

*** POINT

WRITE2C	LDY	POS+1
	LDX	POS+2
	JSR	RDDATS

	LDX	SECLEN
	DEX
	LDA	POS
	CMP	DATBUF,X
	BNE	WRBADP

	JSR	RDVTOC

**WR IT!
*END?

WRITE3	LDA	RWLEN
	ORA	RWLEN+1
	BNE	WRITE3B
*END
	LDA	DATSEC
	STA	POS+1
	LDA	DATSEC+1
	STA	POS+2

	LDX	SECLEN
	DEX
	LDA	DATBUF,X
	STA	POS

	JSR	WRDATS
	LDA	#1

ER2ND	PHA
	JSR	WRDIR
	JSR	WRVTOC
	PLA
	TAY
	RTS

*NEXT SECT?

WRITE3B	LDX	SECLEN
	DEX
	DEX
	DEX
	TXA
	SEC
	SBC	DATBUF+2,X
	BNE	WRITE3C

*PRIDEL DALSI

	JSR	PRIDEL
	PHP

	TYA
	PHA
	TXA
	PHA

	LDX	SECLEN
	DEX
	DEX
	STA	DATBUF-1,X
	TYA
	STA	DATBUF,X

	JSR	WRDATS

	PLA
	STA	DATSEC+1
	PLA
	STA	DATSEC

	LDA	#162
	PLP
	BEQ	ER2ND
	BNE	CLBWR3

*LEN IN SECT.

WRITE3C	LDX	RWLEN+1
	BNE	WRITE4

	CMP	RWLEN
	BCC	WRITE4

	LDA	RWLEN
*WRITE!

WRITE4	STA	TEMP25
	LDY	#0
	LDX	SECLEN
	DEX
	LDA	DATBUF,X
	TAX

WRITE4B	LDA	(RWADR),Y
	STA	DATBUF,X

	INX
	INY
	CPY	TEMP25
	BCC	WRITE4B

	TXA
	LDX	SECLEN
	DEX
	STA	DATBUF,X
*NEXT
	JSR	RWNEXT
	JMP	WRITE3

***

FORMER1	LDY	#168
	JMP	ERROR
**

FORMAT	TSX
	STX	SSAVE

	JSR	INIT25

	LDX	RWLEN
	CPX	#3
	BCS	FORMER1
*
	LDA	FORT1,X
	STA	SECLEN
	LDA	FORT2,X
	STA	SECLEN+1
	LDA	FORT3,X
	STA	MEDFLG

*SET DRIVE CONFIG

	LDA	#4
	STA	$30A
	LDA	#0
	STA	$30B
	LDY	#DATBUF:L
	LDX	#DATBUF:H
	LDA	#'N'
	JSR	RWSECTX
	BMI	FORM2

	LDX	RWLEN

	LDA	#40
	STA	DATBUF
	LDA	#0
	STA	DATBUF+2
	STA	DATBUF+4
	LDA	FORT4,X
	STA	DATBUF+3
	LDA	FORT5,X
	STA	DATBUF+5
	LDA	FORT2,X
	STA	DATBUF+6
	LDA	FORT1,X
	STA	DATBUF+7

	LDY	#DATBUF:L
	LDX	#DATBUF:H
	LDA	#'O'
	JSR	RWSECT

*FORMATUJ

FORM2	LDA	#4
	STA	$30A
	LDA	#0
	STA	$30B

	LDA	MEDFLG
	ASL
	LDA	#'!'
	ADC	#0

	LDY	#DATBUF:L
	LDX	#DATBUF:H
	JSR	RWSECT

*VYROB VTOC

	JSR	CLMAPB

	LDA	#2
	STA	MAPBUF
	STA	MAPBUF+2
	STA	MAPBUF+4
	LDA	#$C3
	STA	MAPBUF+1
	STA	MAPBUF+3

	LDX	#$5A
	LDA	#$FF
FORM3A	STA	MAPBUF+9,X
	DEX
	BNE	FORM3A

	LSR
	STA	MAPBUF+$38
	STX	MAPBUF+$37
	LDA	#$0F
	STA	MAPBUF+$0A
*ED?
	LDA	MEDFLG
	BPL	FORM4

	LDX	#$25
	LDA	#$FF
FORM3B	STA	MAPBUF+$64,X
	DEX
	BNE	FORM3B

	LSR
	STA	MAPBUF+$64

	INX
	STX	MAPBUF+$8B
	LDA	#$2F
	STA	MAPBUF+$8A

	LDA	#3
	STA	MAPBUF+2
	LDA	#$F2
	STA	MAPBUF+1
*WR VTOC

FORM4	JSR	WRVTOC

*CLR DIR
	JSR	CLMAPB

	LDA	#$69
	STA	TEMP25

FORM4B	LDX	#1
	LDY	TEMP25
	JSR	WRMAPB

	INC	TEMP25
	LDA	TEMP25
	CMP	#$71
	BCC	FORM4B

*CLR BOOT

	LDX	#7
FORM4C	LDA	FALBOT-1,X
	STA	MAPBUF,X
	DEX
	BNE	FORM4C

	LDA	$D20A
	STA	MAPBUF
*X JE 0
	LDY	#1
	JSR	WRMAPB
*
	LDY	#1
	RTS
**

FORT1	DFB 128,128,0
FORT2	DFB 0,0,1
FORT3	DFB 0,255,0
FORT4	DFB 18,26,18
FORT5	DFB 0,4,4

FALBOT	DFB 1,0,32,6,32
	SEC
	RTS

******** TEXTY PRO CAST 3 *******

EOL	MACRO
	DFB 155
	MEND
****

START0	LDA	$700
	CMP	#'S'
	BNE	START0E
	JMP	START
**

START0E	LDX	#0
	LDA	#11
	STA	$342,X

	LDA	#STERT:L
	STA	$344,X
	LDA	#STERT:H
	STA	$345,X

	LDA	#STERL:L
	STA	$348,X
	LDA	#STERL:H
	STA	$349,X

	JSR	$E456

	JMP	(10)
***

STERT	DFB 155,155
	ASC "Incorrect DOS "
	ASC "version !!!"
	DFB 253,253,253,253,155

STERL	EQU	*-STERT

****

LOOPTX	ASC "        BW-DOS MENU "
	ASC " By BEWESOFT        "
	EOL

ERRTX	ASC "Error    !"
	EOL

SRTM	DFB 5,2,20
	DFB 0,9,18,23,33
	DFB 8,8,4,9,2
	ASC "ENSD0"
	ASC "Ext+Name Name+Ext "
	ASC "Size Date+Time No"
	EOL

NDSKT	ASC "New disk: Drive:"
	EOL

NDSKM	DFB 5,17,20
	DFB 0,3,6,9,12
	DFB 3,3,3,3,3
	ASC "12348"
	ASC " 1  2  3  4  8 "
PRESSK	EOL
WKTX	ASC "Press a key..."
	DFB 160,155

SELTX	ASC "S"
	EOL
DESTX	ASC "Des"
	EOL
SDTX	ASC "elect group:"
	EOL

DIT1	ASC "Volume:"
DIT2	ASC "Bytes/sector:"
DIT3	ASC "Total bytes:"
DIT4	ASC "Bytes free:"

DI1	ASC %1222222222222222%
	ASC %222222222222222%
	DFB 5,155

DI2	ASC "|          Disk "
	ASC "info:          |"
	EOL

DI3	ASC %!222222222222222%
	ASC %222222222222222$%
	EOL

DI4	ASC %:222222222222222%
	ASC %222222222222222#%
	EOL

NOTSAM	ASC "Disk changed!"
	EOL

MDTX	ASC "Directory name:"
	EOL

RNTX	ASC "Rename to:"
	EOL

AINM	DFB 4,0,20
	DFB 26,29,32,35
	DFB 3,3,3,3
	ASC "1234"
	ASC "Format Atari disk: "
	ASC "Drive: "
	ASC " 1  2  3  4 "
	EOL

AINS	DFB 2,19,20
	DFB 14,17
	DFB 2,3
	ASC "NY"
	ASC "Are you sure? "
	ASC "No Yes"
	EOL

AIND	DFB 3,19,20
	DFB 0,7,14
	DFB 6,6,6
	ASC "SMD"
	ASC "Single Medium Double"
	EOL

FRMTNG	ASC "Formatting..."
	DFB 160,155

MTTX	ASC "Filename:"
	EOL

COPTX1	ASC "  Copy to drive:"
	EOL

COPMEN	DFB 2,0,20
	DFB 11,15
	DFB 3,2
	ASC "YN"
	ASC "Same disk? Yes No"
	EOL

COPTX2	ASC "Dest. path:"
	EOL

SRCTX	ASC "Insert source disk..."
	DFB 160,155

DSTTX	ASC "Insert destination"
	ASC " disk..."
	DFB 160,155

* PRENOS

START2	EQU	*
PRDDIR	EQU	READDIR
PPROT	EQU	PROTECT
PUNPR	EQU	UNPROT
PRENAM	EQU	RENAME
PERASE	EQU	ERASE
PCHKD	EQU	CHKDSK
PREAD	EQU	READ
PWRITE	EQU	WRITE
PFORM	EQU	FORMAT
PRDDNXT	EQU	RDDNXT

PLOPTX	EQU	LOOPTX

***
